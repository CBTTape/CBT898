/* REXX       Set Jobcard suffix.                                     */
/* By default a unique userid suffix is returned.                     */
/* Use parameter of '/U' to return a unique suffix (executing or      */
/* held).  If all used then the 1st. character passed will be used.   */
/*--------------------------------------------------------------------*/
$alkujbs:ver='1.02'
    Arg parm#
    match='EXECUTING WAITING'
    If Wordpos('/U',Translate(parm#))>0 Then Do
        parm#=Delword(parm#,Wordpos('/U',Translate(parm#)),1)
        match=match 'ON'
    End
    #m1=Msg('On');x=Outtrap('line.',500,'NOCONCAT')
    Address TSO "STATUS"
    x=Outtrap('Off');x=Msg(#m1);list=
    Parse VAR line.1 w1 .;If Left(w1,3)='IKJ' Then m_id=1;Else m_id=0
    Do i=1 To line.0
        If m_id Then Parse VAR line.i . . id act .
        Else         Parse VAR line.i . id act .
        If Wordpos(act,match)>0 Then Do
            Interpret "Parse VAR id '"Userid()"' suf '('"
            list=list suf
        End
    End
    nat='ABCDEFGHIJKLMNOPQRSTUVWXYZ01234567890@#$'
    id=Substr(parm#,1,1);If id='' Then id='A'
    Do i=1 to Length(nat)
        suf=Substr(nat,i,1)
        If Wordpos(suf,list)>0 Then Iterate
        id=suf;Leave
    End
    Return id
