/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* $APPEND:                                                           */
/* Append second dataset/dataset(member) to first dataset passed.     */
/* For uncataloged datasets pass dataset in format:                   */
/*     dataset=volser                                                 */
/* If the destination is to a member, then the copy will be performed */
/* under the EDITor to ensure stats are retained and updated unless   */
/* a 3rd. parameter of 'NOSTATS' is passed.                           */
/* Pass 4th.parameter of non-blank (eg. 'Q') to indicate that 'ds2'   */
/* data is from the "stack" (Queued). If this option is used, the     */
/* second dataset is ignored (if passed).                             */
/*                                                                    */
/* Invocation:                                                        */
/*     x1=$append(dsn1,dsn2)                                          */
/*     x2=$append(dsn1,,,'Q')                                         */
/*     x3=$append(dsn1(memb1),dsn2(memb2))                            */
/*     x4=$append(dsn1(memb1)=volsr1,dsn2(memb2)=volsr2)              */
/*--------------------------------------------------------------------*/
$Append:Arg ds1,ds2,stats,qud;$append_ver='1.03'
    Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If ds1/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS";If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    If ima_macro Then Signal macro_01
    xx=Time('s');dd1='A1'xx;dd2='A2'xx
    Address TSO
    Parse VAR ds1 ds1 '=' vol1 1 '(' memb ')'
    If memb/='' & stats='NOSTATS' Then memb=
    If vol1/='' Then vol1='VOLUME('vol1') UNIT(SYSALLDA)'
    If qud='' Then Do
        Parse VAR ds2 ds2 '=' vol2
        If vol2/='' Then vol2='VOLUME('vol2') UNIT(SYSALLDA)'
        "ALLOC F("dd2") DSN('"ds2"') SHR REU" vol2;If rc/=0 Then Return rc
    End
    If memb='' Then Do
        "ALLOC F("dd1") DSN('"ds1"') SHR REU" vol1;If rc/=0 Then Return rc
        "EXECIO * DISKR "dd1" (STEM line1. FINIS)"
        If qud='' Then "EXECIO * DISKR "dd2" (STEM line2. FINIS)"
        Else Do
            i=0
            Do Queued();i=i+1;Parse Pull line2.i;End
            line2.0=i
        End
        "EXECIO * DISKW "dd1" (STEM line1.)"
        "EXECIO * DISKW "dd1" (STEM line2. FINIS)"
        "FREE F("dd1")"
    End
    Else Do
        Parse SOURCE . . xcmd .
        If qud='' Then Do
            "NEWSTACK";"EXECIO * DISKR "dd2" (FINIS)"
        End
        Address ISPEXEC "EDIT DATASET('"ds1"') MACRO("xcmd")" Word(vol1,1)
        If rc/=0 Then "DELSTACK"
    End
    If qud='' Then "FREE F("dd2")"
    Return 0

macro_01:
    Do Queued()
        Parse Pull line
        "ISREDIT LINE_AFTER .ZLAST=(line)"
    End
    "ISREDIT END"
    Return rc
