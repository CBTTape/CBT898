/*--------------------------------------------------------------------*/
/* Invoke SuperC (uses $ZTEMPF).                                      */
/*--------------------------------------------------------------------*/
$compare:$compare_ver='1.09'
superC:
    Arg ds1,ds2
    #adr=Address();#m1=Msg('Off')
    Parse VAR ds1 ds1 '=' v1
    If v1/='' Then Do
        x=Listdsi("'SYS1.LINKLIB' VOLUME("v1")")
        If sysreason=22 Then Do
           zedlmsg="Volume '"v1"' not mounted or "||,
                   "defined to the system."
           "ISPEXEC SETMSG MSG(ISRZ001)"
           Return 12
        End
        Else v1='VOLUME('v1')'
    End
    Parse VAR ds2 ds2 '=' v2
    If v2/='' Then Do
        x=Listdsi("'SYS1.LINKLIB' VOLUME("v2")")
        If sysreason=22 Then Do
           zedlmsg="Volume '"v2"' not mounted or "||,
                   "defined to the system."
           "ISPEXEC SETMSG MSG(ISRZ001)"
           Return 12
        End
        Else v2='VOLUME('v2')'
    End
    Address TSO
    ds1=Strip(ds1,,"'");ds2=Strip(ds2,,"'")
    "ALLOC F(NEWDD) DSN('"ds1"') SHR REU UNIT(SYSALLDA)" v1
    "ALLOC F(OLDDD) DSN('"ds2"') SHR REU UNIT(SYSALLDA)" v2
    ztempf=$Ztempf();unitvol=$alkuvol('$ZTEMPF')
    "ALLOC F(OUTDD) DSN('"ztempf"') SHR REU"
    sysin = Sysvar(Syspref)'.'userid'.CHANGE.TEMP.SYSIN'
    "ALLOC F(SYSIN) DSN('"sysin"') NEW CATALOG TRACKS",
                        unitvol "SPACE(1,0) REU"
    x=Outtrap('ll.',5,'NOCONCAT');#m2=Msg('On')
    Address TSO PROFILE
    x=Outtrap('Off');x=Msg(#m2)
    If Substr(ll.1,1,3)='IKJ' Then wt=Word(ll.1,9);Else wt=Word(ll.1,8)
    Address TSO PROFILE 'NOWTPMSG'
    "CALL *(ISRSUPC) 'DELTAL,LINECMP'"               /* Call Supre-CE */
    Address TSO PROFILE wt
    "ISPEXEC LMINIT DATAID(DATAID) DDNAME(OUTDD)"
    "ISPEXEC VIEW DATAID(&DATAID)"
    "ISPEXEC LMFREE DATAID(&DATAID)"
    "FREE  F(NEWDD,OLDDD,OUTDD,SYSIN)"
    x=$ztempf(ztempf,'/D')
    "DELETE '"sysin"'"
    x=Msg(#m1)
    Address(#adr)

    Return 0
