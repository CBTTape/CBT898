/******************************** REXX ********************************/
/* $DDALLOC: V1.4                                                     */
/*   Returns all the datasets allocated to a DD concatenation or      */
/*   'NULLS' of no match.                                             */
/*                                                                    */
/* Parameters (comma seperated):                                      */
/*   ddlist - DDNAME list (comma or space seperated).                 */
/*            To get EXEC or CLIST altlib concatenations, use         */
/*            +SYSEXEC and/or +SYSPROC respectively (prefixed by +).  */
/*            Note: If a list is provided, datasets are returned in   */
/*                  physical order of "LISTA ST" display and not      */
/*                  in DD list order (including altlib requests.)     */
/*   funct  - Any non-blank character(s) ('All') to indicate return   */
/*            all datasets in the returned list otherwise duplicates  */
/*            will be ignored.                                        */
/*            Use 'L' to list altlib allocations. (This will also     */
/*            activate the return all datasets feature.)              */
/*   member - Member name to look for. Returns the first dsn in the   */
/*            DD concatenation (or list) the member is found in. If   */
/*            the "funct" option (non blank) is used, all dataset     */
/*            the member is found in will be returned. Duplicated     */
/*            will be returned.  If member is not found or has        */
/*            invalid format (wilcarding is NOT permitted) then       */
/*            NULLs is returned.                                      */
/*                                                                    */
/* Examples:                                                          */
/*      xx=$ddalloc('SYSPROC SYSEXEC',A)                              */
/*      xx=$ddalloc('+SYSPROC SYSEXEC')                               */
/*      xx=$ddalloc('SYSPROC,SYSEXEC',,'MYREXX')                      */
/*                                                                    */
/*    Sometimes to get the correct results tou may need               */
/*    to make multiple passes. To locate the executable routine 'AAA' */
/*    instead of,                                                     */
/*      xx=$ddalloc('+SYSPROC,SYSPROC,+SYSEXEC,SYSEXEC',,'AAA')       */
/*    code,                                                           */
/*      xx=$ddalloc('+SYSPROC',,'AAA')                                */
/*      If xx='' Then xx=$ddalloc('SYSPROC',,'AAA')                   */
/*      If xx='' Then xx=$ddalloc('+SYSEXEC',,'AAA')                  */
/*      If xx='' Then xx=$ddalloc('SYSEXEC',,'AAA')                   */
/*                                                                    */
/* Requires:                                                          */
/*   $DDALLOC  - This driver.                                         */
/*   $VERNAME  - Verify member name.                                  */
/*                                                                    */
/*--------------------------------------------------------------------*/
$ddalloc: Arg dd_string,funct,member,altlib
    If member/='' Then Do
        If $vername(member,'N')='OK' Then find_memb=1;Else Return ''
    End;Else find_memb=0
    i=0;xxx=;dd_string=Translate(dd_string,' ',',')
    If Index(dd_string,'+') > 0 Then x=Get_Altlibs()
    If dd_string='' Then Return xxx
    x=Outtrap('line.',5000,'NOCONCAT');#msg=MSG('On')
    Address TSO "LISTALC STATUS"
    x=Outtrap('Off');x=Msg(#msg)
    #tr=Trace(o)
    Do While i<line.0
        i=i+1
        If Wordpos(Word(line.i,1),dd_string)=0 Then Iterate
        dd=Word(line.i,1)
        j=i-1
        dsn=Word(line.j,1)
        If find_memb Then Do
            If Sysdsn("'"dsn"("member")'")='OK' Then Do
                If funct='' Then Return dsn
                Else If Wordpos(dsn,xxx)=0 Then xxx=xxx dsn
            End
        End
        Else Do
            If funct/='' Then xxx=xxx dsn
            Else If Wordpos(dsn,xxx)=0 Then xxx=xxx dsn
        End
        Do While i<line.0
            i=i+2
            If Substr(line.i,3,1)/='' Then Do;i=i-1;Leave;End
            j=i-1;dsn=Word(line.j,1)
            If find_memb Then Do
                If Sysdsn("'"dsn"("member")'")='OK' Then Do
                    If funct='' Then Return dsn
                    Else If Wordpos(dsn,xxx)=0 Then xxx=xxx dsn
                End
            End
            Else Do
                If funct/='' Then xxx=xxx dsn
                Else If Wordpos(dsn,xxx)=0 Then xxx=xxx dsn
            End
        End
    End
    Return Strip(xxx)
Get_Altlibs:
    If Wordpos('+SYSEXEC',dd_string)=0 & Wordpos('+SYSPROC',dd_string)=0 Then,
        Return 4
    #m1=Msg('On');x=Outtrap('l.',500,'NOCONCAT')
    Address TSO "ALTLIB DISPLAY";x=Outtrap('Off');x=Msg(#m1)
    If funct='L' Then Do;Do i=1 To l.0;Say i l.i;End;funct=;End
    yyy=dd_string;dd_string=;string=('+SYSEXEC EXEC +SYSPROC CLIST')
    Do while yyy/=''
        Parse VAR yyy yy yyy
        xx=Wordpos(yy,string)
        If xx//2>0 Then yy=get_Specific_altlib(Word(string,xx+1))
        dd_string=dd_string yy
    End
    dd_string=Space(dd_string)
    Return 0

get_Specific_altlib: Arg type
    Do i=1 To l.0
        If Wordpos(type,l.i)=0 Then Iterate
        Parse VAR l.i 'DDNAME=' altdd .
        If type='EXEC' & altdd/='SYSEXEC' Then Leave
        If type='CLIST' & altdd/='SYSPROC' Then Leave
    End
    If i>l.0 Then Return ''
    Do i=i+1 To l.0
        If Wordpos('Stacked',l.i)=0 Then Return altdd
        Parse VAR l.i 'DDNAME=' dd .;altdd=altdd dd
    End
    Return altdd
