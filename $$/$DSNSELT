/******************************** REXX ********************************/
/* Sample $DSNSEL launcher                                            */
/**********************************************************************/
    panel=;dstype='NONVSAM'
    table='FRED'
    level='SYS1.**'
    x=$dsnsel(level,dstype,panel,table)
    level='SYS2.*';dstype=
    x=$dsnsel(level,dstype,panel,table'(OLD)')
    Address ISPEXEC
    "TBTOP &TABLE"
    mig=0;bad=0;tracks=0;seq=0
Say 'Seq  Volume Type   ' Substr('Dsname',1,40) 'Size   Total'
Say '---- ------ -------' Copies('-',40) '------ --------'
    Do While rc=0
        "TBSKIP &TABLE"
        If rc/=0 Then Leave
        If Wordpos(dt,'ALIAS DATA INDEX')>0 Then Iterate
        xx=Listdsi("'"dsn"' NORECALL")
        Size='?'
        Select
            When (sysreason=9 | sysreason=25) Then mig=mig+1
            When (xx<5 & sysreason=12) Then x=get_VSAM_info(dsn)
            When xx<5 Then xx=get_NONVSAM_info()
            Otherwise bad=bad+1
        End
        seq=seq+1
Say Right(seq,4) vol dt Substr(dsn,1,40) Right(size,6) Right(tracks,8)
    End
    "TBEND &TABLE"

    Exit


get_VSAM_info:
Say xx Arg(1)
    x=Outtrap('linei.',500,'NOCONCAT');#msg=MSG('On')
    Address TSO "LISTC ENT('"Arg(1)"') ALL"
    x=Outtrap('Off');x=MSG(#msg)
    If rc/=0 Then Return rc
    stype=;dat=0;idx=0
    Do i=1 To linei.0
        If dat/=0 & idx/=0 Then Leave
        w1=Word(linei.i,1)
        If w1='DATA' Then stype='DAT'
        If w1='INDEX' Then stype='IDX'
        If stype='' Then Iterate
        If Index(linei.i,'TRACKS-')>0 Then Do
            Parse VAR linei.i 'TRACKS-' size .
            size=Strip(TRanslate(size,' ','-'))
            Interpret stype"=Strip(TRanslate(size,' ','-'))"
        End
    End
    size=dat+idx;tracks=tracks+size
    Return 0

get_NONVSAM_info:
    Select
        When sysunits='BLOCK' & sysblkstrk='N/A' Then Do
            size='?';Say 'Next dataset is a "PDS/E" in "BLOCK"',
                         'allocation. Calculate manually.'
            Return 4
        End
        When sysunits='CYLINDER' & sysblkstrk='N/A' Then Do
            size='?';Say 'Next dataset is a "PDS/E" in "CYLINDER"',
                         'allocation. Calculate manually.'
            Return 4
        End
        When sysunits='BLOCK' Then size=sysalloc/sysblkstrk
        When sysunits='CYLINDER' Then size=sysalloc*systrkscyl
        Otherwise size=sysalloc
    End
    tracks=tracks+size
    Return 0
