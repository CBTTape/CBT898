/*------------------------------ REXX --------------------------------*/
/* $EVALSYV:                                                          */
/* ---------                                                          */
/* Evaluate/substitute system defined symbolic variables.             */
/* If the text (usually dataset name) contains a "&" pass it the full */
/* text.  If the symbol is a valid system defined symbol then its     */
/* value will be substituted otherwise the "variable" will remain in  */
/* tact and returned unchanged.                                       */
/*                                                                    */
/* If the variable has a trailing "." it will be removed.             */
/*                                                                    */
/* Example:                                                           */
/*   If Index(dsn,'&')>0 Then dsn=$evalsyv(dsn)                       */
/*                                                                    */
/*--------------------------------------------------------------------*/
$EVALSYV:
$evalsysv: Arg $_edsn
    $_edsn=Strip($_edsn)' ';$_rdsn=
    Do While $_edsn/=''
        Parse VAR $_edsn $_p1 '&' $_edsn;$_rdsn=$_rdsn||$_p1
        If $_edsn='' Then Leave
        x=Verify($_edsn,'ABCDEFGHIJKLMNOPQRSTUVWXYZ@#$0123456789')
        $_var=Substr($_edsn,1,x-1);$_edsn=Substr($_edsn,x)
        If Substr($_edsn,1,1)='.' Then Do
            $_edsn=Substr($_edsn,2);$_term='.'
        End
        Else $_term=
        $_val=Mvsvar(Symdef,$_var)
        If $_val='' Then $_val='&'$_var||$_term
        $_rdsn=$_rdsn||$_val
    End
    Return Strip($_rdsn)
