/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* $PROGLIN: Returns the complete parmlib PROG member "line" details  */
/*           from the line number passed.  Will parse both backwards  */
/*           and forwards to build the full value of either the APF   */
/*           or LINK line value.  Any "/*..*/" comments will be       */
/*           ignored as will line comments be.                        */
/*                                                                    */
/* Parameter(s):                                                      */
/*  nnn - Line number.                                                */
/* Returns (example):                                                 */
/*   dsname volser                                                    */
/*   Error : error_message_text                                       */
/*  12  - Not an edit session.                                        */
/* Usage:                                                             */
/*   1.   line=$proglin(56)                                           */
/*        Parse VAR line dsname volser                                */
/*                                                                    */
/*   2.   Address ISREDIT "$proglin 56"                               */
/*        If rc=0 Then Do                                             */
/*            Address ISPEXEC "VGET ($proglin) SHARED"                */
/*            Parse VAR line dsname volser                            */
/*            volume='VOLUME('volser')'                               */
/*        End                                                         */
/*        Else ....                                                   */
/*--------------------------------------------------------------------*/
$PROGLIN:$PROGLIN_ver='1.02'
    Arg parm#;Address ISPEXEC "CONTROL ERRORS RETURN"
    "ISREDIT MACRO (parm#) NOPROCESS";If rc/=0 Then Signal exit12
    Address ISREDIT
byp1:
    If Arg(2)/='' Then parm#=parm#','Arg(2)
    Parse Upper Value Translate(parm#,' ',',') With lin# type
    If lin#='' Then "(lin#)=LINENUM .ZCSR"
    Parse VAR type ch1 +1
    Select
        When ch1='L' Then type='LNKLST'
        When ch1='A' Then type='APF'
        Otherwise type='APF LNKLST'
    End
    proglist='APF LNKLST EXIT SYSLIB LPA'
    "(line)=LINE" lin#;If rc/=0 Then Signal exit08
    Do While Wordpos(Word(line,1),proglist)=0
        lin#=lin#-1;"(line)=LINE" lin#;If rc/=0 Then Signal exit8
    End
    If Wordpos(Word(line,1),type)=0 Then Return 8
    line=Remove_comments(line)
    Do Forever
        lin#=lin#+1;"(linex)=LINE" lin#;If rc/=0 Then Leave
        If Wordpos(Word(linex,1),proglist)>0 Then Leave
        linex=Remove_comments(linex)
        line=line linex
    End
    Parse VAR line w1 w2 . 1 'DSNAME(' dsname . 1 'VOLUME(' volser,
              ')' . 1 'NAME(' name .
    If dsname='' Then Parse VAR line . . 'DSN(' dsname .
    If dsname='' Then Parse VAR line . . 'LIB(' dsname .
    If dsname='' Then Parse VAR line . . 'LIBRARY(' dsname .
    If volser='' Then Parse VAR line . . 'VOL(' volser .
    If Wordpos(w2,'ADD DELETE')=0 Then Do
        $proglin="Error : Missing 'ADD' or 'DELETE' keyword.";Signal Exit
    End
    Parse VAR dsname . ')' dt
    Parse VAR volser . ')' vt
    Parse VAR name   . ')' nt
    If volser='' & Wordpos('SMS',line)>0 Then volser='SMS'
    Select
        When dsname='' Then $proglin="Error : Missing 'DSName(...)' format."
        When volser='' & w1='APF' Then,
            $proglin="Error : Missing 'VOLUME(...)' format."
        When name='' & w1='LNKLST' Then,
            $proglin="Error : Missing 'NAME(...)' format."
        When dt   /='' Then $proglin="Error : Invalid 'DSName(...)' format."
        When vt   /='' Then $proglin="Error : Invalid 'VOLUME(...)' format."
        When nt   /='' Then $proglin="Error : Invalid 'NAME(...)' format."
        Otherwise Do
            Parse VAR dsname dsname ')'
            Parse VAR volser volser ')'
            $proglin=dsname volser
        End
    End
Exit:Parse source . env .
    If env='FUNCTION' Then Return $proglin
    Else Address ISPEXEC "VPUT ($proglin) SHARED"
    Exit
Exit08:$proglin="Error : Invalid line number passed.";Signal Exit
Exit12:$proglin="Error : Not an edit macro.";Signal Exit
Remove_comments:Arg linei
    If Index(linei,'/*')=0 Then Do
        If Index(linei,'*/')=0 Then Return linei
        $proglin="Error : Missing opening '/*'.";Signal Exit
    End
    If Index(linei,'*/')=0 Then Do
        $proglin="Error : Missing closing '*/'.";Signal Exit
    End
    Parse VAR line p1 '/*' . '*/' p2
    Return Space(p1 p2)
