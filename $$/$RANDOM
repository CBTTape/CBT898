/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*  $RANDOM:                                                          */
/*  This routine will return a range of numbers in a random order.    */
/*  The routine will generate a full set of reandom numbers ranging   */
/*  from poarameter 1 to parameter 2.  Digits only please.            */
/*                                                                    */
/*  Parameters:                                                       */
/*  1. Minimum value.                                                 */
/*  2. Maximum value.                                                 */
/*                                                                    */
/*  Eg. Yahtze_6_dice_roll=$Random(1,6)                               */
/*      card_deck_52=$Random(1,52)                                    */
/*                                                                    */
/*      Card_Deck_With_Joker:                                         */
/*      xxx=$Random(1,53);cards='A 2 3 4 5 6 7 8 9 0 J Q K';deck=     */
/*      Do While xxx/=''                                              */
/*          Parse VAR xxx xx xxx                                      */
/*          suit=xx%13;no=xx//13;If no=0 Then Do;no=13;suit=suit-1;End*/
/*          deck=deck $Ttrunc(suit,,'0,S 1,C 2,D 3,H *,Joker')||,     */
/*               Subword(cards,no,1)                                  */
/*      End                                                           */
/*      card_deck_52_Joker=deck                                       */
/*      Say card_deck_52_Joker                                        */
/*                                                                    */
/*--------------------------------------------------------------------*/
$Random:Arg min,max                 ;$rdm_ver='1.02'
    If max='' Then Parse VAR min min ',' max
    Parse SOURCE op_sys op_mode xcmd_name dd_name dsn_name .
/*--------------------------------------------------------------------*/
    Numeric Digits 20
    divisor  = 2147483648                   /* Fixed for Algorythm    */
    buff     = 200                          /* Force attempts for all */
    If min = '' Then min = 1                /* Minimum number         */
    If max = '' Then max = 52               /* Maximum number (cards) */
    seed = Time('s')                        /* Seed for uniqueness    */
    Parse Value Time('l') With w1 ':' w2 ':' w3 '.' w4
    seed =  w1 || w2 || w3 || w4
                                            /*                        */
/*--------------------------------------------------------------------*/
/* Dummy first seed                                                   */
/*--------------------------------------------------------------------*/
    seed = (15625 * seed) + 22221

    f.       = ''
    v.       = ''
    at  = 0                                 /* Number of attempts     */
    i   = min
    lim = ((max-min)+1)*4
    Do ((max-min)+1)*4 + buff
        at = at + 1
        r4 = (15625 * seed) + 22221
        seed = r4 // divisor      /*   remainder new seed */
        r4 = (MAX-MIN+1)*seed
        r5 = r4 % divisor
        If r5 < 0 Then r5 = r5 * -1
        r5 = r5 + min
        If r5 > max Then Iterate
        If r5 < min Then Iterate
        If f.r5 = 'Y' Then Iterate
        f.r5 = 'Y'
        v.i = r5
        If i = max Then Leave
        i = i + 1
    End
    If trace = 'Y' Then Say i'/'at '- returned'
    string = ''
    string = ''
    missing = ''
    Do i=min To max
        If f.i /= 'Y' Then missing = missing i
        If v.i /= ''  Then string = string v.i
    End
    missing = Strip(missing)
    If trace = 'Y' Then Do
        Say 'Returned='string
        Say 'Missed  ='missing
    End
    j = Words(missing)
    Do i=j By -1 To 1
        string = string Word(missing,i)
    End
    If op_mode = 'COMMAND' Then Say 'Return ->'string
    Else Return Strip(string)
