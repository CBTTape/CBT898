/*Rexx Exec*************************************************************
*                                                                      *
*    Created   : CL  16/08/2001                                        *
*    Modified  : CL  10/09/2003                                        *
*    Modified  : AK  17/08/2004  (added order and HEX sort functions). *
*    Modified  : AK  29/04/2005  (added string sort feature).          *
*    Mod-V1.5  : AK  06/10/2005  (fixed premature exit if in order     *
*                                 problem. Also fixed HEX sort error   *
*                                 with shady data).                    *
*                                                                      *
*    Description                                          Created by   *
*    ----------------------------------------------------------------- *
*    Driven by : C.Lim                                       C.Lim     *
*    Purpose   : Function to sort a string in either ascending(default)*
*                or descending order.                                  *
*                                                                      *
*    Eg. string=R$sort(instring,order,type)                            *
*      instring - blank delimited words                                *
*      order    - A or D                                               *
*      type     - HEX if string consists of displayable hex values     *
*                 and A,B,C,D,D and F cone after numerics.             *
*      length   - Length of each sring to sort. Without this parameter *
*                 (numeric), a word sort is performed.                 *
*                                                                      *
***********************************************************************/
$sort:$sortv='1.5'
Parse arg instring,order,type,strlen
If Substr(type,1,1)='H' Then hex=1;Else hex=0
If Datatype(strlen,'W') Then Do
  vso.0=Length(instring)%strlen;sep=
  Do c1=1 to vso.0
    Interpret "Parse VAR instring vso.c1 +"strlen" instring"
  End
End
Else Do
  vso.0=Words(instring);sep=' '
  Do c1=1 to vso.0;vso.c1=Word(instring,c1);End
End
do c1=1 to vso.0-1
   sorted=1
   do c2=1 TO vso.0-c1
     c3=c2+1
     If hex Then Do;v1=x2d(vso.c2);v2=x2d(vso.c3);End
     Else  Do;v1=vso.c2;v2=vso.c3;End
     If v1>v2 Then Do
        temp=vso.c3;vso.c3=vso.c2;vso.c2=temp
        sorted=0
     End
   End
   If sorted Then Leave
End
string=
If order='D' Then Do;i=vso.0;j=-1;k=1;End;Else Do;i=1;j=1;k=vso.0;End
Do c1=i By j To k;string=string||vso.c1||sep;End
Return Strip(string)
