/*--------------------------ooo00 REXX 00ooo---------------------------*
   $SYSMEMB: Verify existance of a member even in uncataloged dataset
             if a volser is passed.

   Input (positional)
    1. dataset(member) - quoted or unquoted, syspref added if unquoted.
    2. VOLUME(volser)  - for uncatalogued dataset.
   Output:
    OK                 - Member found in dataset.
    MEMBER NOT FOUND   - Member not in dataset.
    DATASET NOT FOUND  - If Dataset not on volser supplied.
*---------------------------------------------------------------------*/
$SYSMEMB:ver='1.01'
    Address ISPEXEC;"CONTROL ERRORS RETURN"
    Parse Upper ARG dsn,volume
    If volume='' Then Parse VAR dsn dsn volume
    If Substr(dsn,1,1)/="'" Then dsn="'"Sysvar(Syspref)"."dsn"'"
    Parse Value Strip(dsn,,"'") With dsn '(' member ')'
    "LMINIT DATAID($VERMEMB) DATASET('"dsn"') ENQ(SHR) "Strip(volume,,"'")
    If rc/=0 Then Return 'DATASET NOT FOUND'
    "LMOPEN DATAID("$vermemb") OPTION(INPUT)"
    "LMMFIND DATAID("$vermemb") MEMBER("MEMBER")"
    rc1=rc
    "LMCLOSE DATAID("$vermemb")"
    "LMFREE DATAID("$vermemb")"
    If rc1=0 Then Return 'OK';Else Return 'MEMBER NOT FOUND'
