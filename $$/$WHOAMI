/******************************** REXX ********************************/
/*                                                                    */
/* $WHOAMI:                                                           */
/*                                                                    */
/* This routine will check the ACEE control block to get persons name.*/
/* Change variable "cname1st=" to name format according to site:      */
/*    1 -Christian name first.                                        */
/*    0 -Surname followed by christian name.                          */
/*                                                                    */
/* This routine wil convert names to lower case (starting with        */
/* capital) and return what asked for, either Christian, surname or   */
/* both.                                                              */
/* It is also used by $WHOAMIC to return "CNAME1ST" setting.          */
/*                                                                    */
/* This routine can be automatically configured using $WHOAMIC.       */
/*                                                                    */
/* DO NOT CHANGE THE 1ST. LINE AFTER THESE COMMENTS AS IT IS USED FOR */
/* CUSTOMISATION by "$WHOAMIC" (automatically invoked by "$CUSTOM").  */
/*--------------------------------------------------------------------*/
    cname1st=0                           /* 1=ChrisSurn, 0=SurnChris  */
/*                                              REXX sample start     */
/*
    Signal On Syntax Name errproc_1
    retlab=end_err_1
    If $whoami('CNAME1ST') Then Say             Christian name First
    Else                        Say             Surname First
    Say Substr($whoami('C'),1,30)               Christian name
    Say Substr($whoami('S'),1,30)               Surname
    Say Substr($whoami(),1,30)                  Both names
end_err_1:
    Signal Off Syntax
    Exit

errproc_1:
    Say "Error: Hello...?"
    Signal Value(retlab)
*/

$Whoami:
    Parse Upper ARG type .;Parse Source . op_mode .
    If type='CNAME1ST' Then Return cname1st
    name=getname()
    name=Translate(name,'  ','.,') /* Cater for '.' or ',' seperator */
    If cname1st Then Parse var name cname sname .
    Else Parse var name sname cname .
    Select
        When type=''  Then name=cname sname
        When type='C' Then name=cname
        Otherwise          name=sname
    End
    If op_mode/='COMMAND' Then Return $U2Lower(name,2,'*')
    Else Do
        ispf=Sysvar(Sysispf)
        If ispf='ACTIVE' Then Do
            zedsmsg=;zedlmsg=Userid() '-' $U2Lower(name,2,'*')
            Address ISPEXEC "SETMSG MSG(ISRZ001)"
        End
        Else Say Userid() '-' U2Lower(name,2,'*')
        Exit
    End

/*--------------------------------------------------------------------*/
/* Get name from ACEE control block (imported from REXX '$getname' for*/
/* self containment).                                                 */
/*--------------------------------------------------------------------*/
getname:
    ascb=Storage('224',4)
    asxb=Storage(d2x(c2d(ascb)+x2d('6c')),4)
    acee=Storage(d2x(c2d(asxb)+x2d('c8')),4)
    nam@=Storage(d2x(c2d(acee)+x2d('64')),4)
    name=Storage(c2x(nam@),30)
    len =c2d(Substr(name,1,1))
    name=Substr(name,2,len-1)
    Return name

/*--------------------------------------------------------------------*/
/* Convert upper to lower (imported from REXX routine '$lower' for    */
/* self containment).                                                 */
/*--------------------------------------------------------------------*/
$U2Lower:
    ARG text,pos,rep
    lc='abcdefghijklmnopqrstuvwxyz';uc='ABCDEFGHIJKLMNOPQRSTUVWXYZ'
    If Datatype(rep,'W') Then Nop;Else rep=99999
    If Datatype(pos,'W') Then Nop;Else pos=2
    rname=
    Do i=rep by -1 To 1
        Parse VAR text name text
        If pos>1 Then Do
            pref=Substr(name,1,pos-1);suff=Substr(name,pos);End
        Else Do;pref=;suff=name;End
        rname=rname pref||Translate(suff,lc,uc)
        If text='' Then Leave
    End
    Return Strip(rname text)
