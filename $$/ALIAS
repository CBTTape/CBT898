/******************************** REXX ********************************/
n_st=mark(2)    /* Mark Start */                                  /*QH*/
/*                                                                /*QH*/
/*--------------------------ooo000OOOO000ooo--------------------------*/
/* ALIAS:                                                             */
/*                                                                    */
/* This macro will create an alias for the member being               */
/* browsed/edited/viewed, pass the alias name as the first parameter. */
/* If the alias is not for the current member, or you are in a member */
/* selection screen pass the member and the alias as the two          */
/* respective parameters.                                             */
/* If the alias exists, pass the keyword patrameter of "/D" to delete */
/* the alias.                                                         */
/*                                                                    */
/* Parameters:                                                        */
/*   /R or /D   - R(eplace) alias if it exists. (Delete alias if it   */
/*                exixts.)                                            */
/*                                                                    */
/* The following members are required to package this routine:        */
/*   REXX:                                                            */
/*     ALIAS    - Driver.                                             */
/*     KM       o Kill member routine to delete existing alias.       */
/*                                                                    */
/*--------------------------------------------------------------------*/
*/                                                                /*QH*/
n_en=mark(-2)   /* Mark End   */                                  /*QH*/
    Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    Arg parm#;If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#)";If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
If Word(parm#,1)="??" Then Do                                     /*QH*/
  Parse Source . . xcmd .;x=$BR(xcmd ?);Return;End                /*QH*/
If Word(parm#,1)="?" Then Signal Help_me;Else Signal No_help_#1   /*QH*/
Mark:If Datatype(Arg(1))/="NUM" Then Arg(1)=1;Return sigl+Arg(1)  /*QH*/
Help_me:"ISREDIT (m)=MEMBER";rcm=rc;#tr=Trace("o")                /*QH*/
Do i=n_st To n_en;nl=Sourceline(i)                                /*QH*/
If rcm=0 Then "ISREDIT LINE_BEFORE 1=NOTELINE (nl)"               /*QH*/
Else Say Substr(nl,1,79);End;Return 0                             /*QH*/
                                                                  /*QH*/
No_help_#1:                                                       /*QH*/
    zedsmsg=
    alias=;member=
    Call parsparm

    If ima_macro then Do
        Address ISREDIT
        "(dsname)=DATASET";If member='' Then "(member)=MEMBER"
        "(changed)=DATA_CHANGED";If changed='YES' Then "SAVE";Address
    End
    Else Do
        "VGET (zscreeni)";screeni=zscreeni
        #msg=Msg('Off');dataset=
        Do i=1 To 50
            xx=Strip(Word(screeni,i),,"'")
            If Index(xx,'.')=0 Then xx=hlq'.'xx
            Parse Var xx yy '('
            If Sysdsn("'"yy"'")/='OK' Then Iterate
            dataset=xx;Leave
        End
        If dataset='' Then Do
            zedlmsg="Could not extract a valid dataset name from screen."
            Signal ext1
        End
        If Index(dataset,'(')>0 & Index(dataset,')')=0 Then Do
            zedlmsg="Incomplete dataset(member) combination in headline."
            Signal ext1
        End
        Parse VAR dataset dsname '(' zz ')' .
        If member='' Then member=zz;
    End

    If Sysdsn("'"dsname"("alias")'")='OK' & delalias Then Do
        yy="As you used '/R', the ""KM"" routine is not available and",
           "you will have to manually delete it first or pick another",
           "alias."
        Signal On Syntax Name syntax_err_1
        x=KM('/NP /DELALIAS('alias')')
    End
    Else yy="If it already exits then pass parameter of '/R' to replace it."
syntax_err_1: Signal Off Syntax
    x=Outtrap('linei.',500,'NOCONCAT');#m=Msg('On')
    Address TSO "RENAME '"dsname"("member")' '"dsname"("alias")' ALIAS"
    x=Outtrap('Off');x=Msg(#m)
    If rc=0 Then zedlmsg="Alias '"alias"' created for member '"member"'."
    Else zedlmsg=linei.1". Alias creation failed, RC="rc"." yy

ext1:"SETMSG MSG(ISRZ001)";Exit

parsparm:
/*--------------------------------------------------------------------*/
/* Parse parameters...                                                */
/*--------------------------------------------------------------------*/
    Parse Upper VAR parm# parm#
    If Wordpos('/D',parm#)>0 Then Do
        delalias=1;parm#=Delword(parm#,Wordpos('/D',parm#),1)
    End;Else delalias=0
    If delalias=0 Then Do
        If Wordpos('/R',parm#)>0 Then Do
            delalias=1;parm#=Delword(parm#,Wordpos('/R',parm#),1)
        End;Else delalias=0
    End
    Parse VAR parm# parm1 parm2 parm3
    If parm1='' Then Do
        zedlmsg='Missing alias name.';Signal ext1
    End
    If $Vername(parm1)/='OK' Then Do
        zedlmsg = 'Invalid alias name.' $Vername(Parm1);Signal ext1
    End
    alias=parm1
    If parm2/='' Then Do
        If $Vername(parm2)/='OK' Then Do
            zedlmsg = 'Invalid alias name.' $Vername(Parm2);Signal ext1
        End
        member=alias;alias=parm2
    End

    Return 0
