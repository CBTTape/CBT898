/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*                                                                    */
/* AP: APpend text.                                                   */
/*                                                                    */
/* Macro to append text at the end of a line or specified column.     */
/* To include spaces in the appendage, put quotes around it. If the   */
/* appendage uses an appostrophe, use double quotes.                  */
/*                                                                    */
/* Can use parameter of /C=cc where 'cc' is the column number to      */
/* append at. If 'cc' is longer than record length, it will be        */
/* append at the end (no space).                                      */
/*                                                                    */
/*--------------------------------------------------------------------*/
AP:ap_ver='1.02'
    Arg parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS";If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    all=;Parse Upper VAR parm# parm@
    Address ISREDIT;"(status)=USER_STATE";all=

    xx=Index(' 'parm@,' /C=')
    If xx>0 Then Do
        xx=$Wordat(parm@,xx)
        Parse VAR xx . '/C=' col .
        If Datatype(col,'W') Then parm#=Space(Delword(parm#,Wordpos(xx,parm@)))
    End;Else col=0

    If parm#='' Then Do
        zedsmsg=;zedlmsg='Missing append parameter.'
        Address ISPEXEC "SETMSG MSG(ISRZ001)";Exit
    End
    If Substr(parm#,1,1)="'" Then parm#=Strip(parm#,,"'")
    Else If Substr(parm#,1,1)='"' Then parm#=Strip(parm#,,'"')

    "(lev)=MACRO_LEVEL"
    If lev>1 Then Do
        Parse VAR parm# fline ',' tline ',' lcmd parm#
    End
    Else Do
        If all='ALL' Then Do;fline=1;"(tline)=LINENUM .ZLAST";End
        Else Do
            "PROCESS RANGE C";"(cmd)=RANGE_CMD"
            If cmd='C' Then Do
                "(fline)=LINENUM .ZFRANGE";"(tline)=LINENUM .ZLRANGE"
            End
            Else Do
                "(fline)=LINENUM .ZFIRST";"(tline)=LINENUM .ZLAST"
            End
        End
    End
    "(caps)=CAPS";"CAPS=OFF"
    Do i=fline To tline
        "(linei)=LINE" i
        If col=0 Then lineo=Strip(linei,'t')||parm#
        Else Do
            lineo=Strip(linei,'t')
            If Length(lineo)<col Then lineo=Substr(lineo,1,col-1)
            lineo=lineo||parm#
        End
        "LINE" i"= (lineo)"
    End
    "CAPS=(caps)";"USER_STATE=(status)"
    Exit
