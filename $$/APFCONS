/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*                                                                    */
/* APFCONS: APF authorisation using the CONSole command.              */
/*                                                                    */
/* Add or delete a dataset to the APF list from an ISPF options 3.4   */
/* screen. Under ISPF option 3.4 ISPF will pass the dataset name and  */
/* the volume is available from the shared profile in variable        */
/* "zdlvol".                                                          */
/* Alternatively invoke from a command line as:                       */
/*    APFCONS 'dsname' V(volser)                                      */
/* where "volser" should be set to '******' for the IPL pack.         */
/*                                                                    */
/*--------------------------------------------------------------------*/
    ARG parm#
    Parse Upper VAR parm# p1 'V(' zdlvol ')' p2;parm#=Space(p1 p2)
    If zdlvol='' Then Address ISPEXEC "VGET (ZDLVOL) SHARED"
    If Wordpos('/D',parm#)>0 Then Do
        act='DELETE';parm#=Strip(Delword(parm#,Wordpos('/D',parm#),1))
    End
    Else act=ADD
    Parse VAR parm# dsn;dsn=Strip(dsn,,"'")
    If zdlvol/='******' Then Do
        If zdlvol='' Then Do
           zedlmsg="Missing volume parameter on volume """zdlvol'".'
           Signal msgx1
        End
        xx=Listdsi("'"dsn"' VOLUME("zdlvol")")
        If xx<5 | sysreason=9 | sysreason=25 Then Nop
        Else Do
           zedlmsg="Dataset '"dsn"' not found on volume """zdlvol'".'
           Signal msgx1
        End
    End
/*--------------------------------------------------------------------*/
/* Activate an unique CONSOLE facility                                */
/*--------------------------------------------------------------------*/
    Address TSO
    sol=Sysvar('SOLDISP');unsol=Sysvar('UNSDISP')
    uid=Userid();alfabet='ABCDEFGHIJKLMNOPQRSTUVWXYZ'
    #msg=MSG('Off');"CONSOLE DEACTIVATE";x=MSG(#msg)
    Do i=1 To 26
        cn=uid||Substr(alfabet,i,1)
        x=Outtrap('line.',1,'NOCONCAT')
        "CONSOLE ACTIVATE NAME("cn")"   /* ACTIVATE CONSOLE           */
        If rc=0 Then Leave
    End
    If rc/=0 Then Do
        zedlmsg='Could not establist CONSOLE'
        Signal msgx1
    End
    "CONSPROF SOLDISP(NO) UNSOLDISP(NO)" /* Set profile to no displays*/
/*--------------------------------------------------------------------*/
/* Issue command                                                      */
/*--------------------------------------------------------------------*/
    syscmd="SETPROG APF,"act",DSNAME="dsn",VOLUME="zdlvol
    "CONSOLE SYSCMD("syscmd") CART('ALK')";rc1=rc
    msg=getmsg('CONSMSG.',,'ALK',,10)
/*--------------------------------------------------------------------*/
/* Clean up                                                           */
/*--------------------------------------------------------------------*/
    'CONSPROF SOLDISP('sol') UNSOLDISP('unsol')' /* Restore profile   */
    'CONSOLE DEACTIVATE'                         /* Deactivate 1st.   */
    If msgÂ¬=0 Then Do
        zedlmsg='No output from console command to process'
        Signal msgx1
    End
/*--------------------------------------------------------------------*/
/* Output result for browsing                                         */
/*--------------------------------------------------------------------*/
    dd='AF'Time('s');ztempf=Userid()'.'dd'.TEMP'
    "ALLOC F("dd") DSN('"ztempf"') NEW CATALOG TRACKS SPACE(15,5)",
        "UNIT(SYSDA) RECFM(V B) LRECL(133) BLKSIZE(27930) DSORG(PS)"
    "NEWSTACK";Queue '*'Centre(' Input ',78,'-')'*';Queue ' '
    Queue ' 'syscmd;Queue ' '
    Queue '*'Centre(' Output ',78,'-')'*';Queue ' '
    Do i=1 To consmsg.0;Queue consmsg.i;End
    Queue ' ';Queue ''
    "EXECIO * DISKW "dd" (FINIS)"
    Address ISPEXEC;"LMINIT DATAID(DATAID) DDNAME("dd")"
    "BROWSE DATAID(&DATAID)";"LMFREE DATAID(&DATAID)"
    Address TSO;"FREE F("dd")";"DELETE '"ztempf"'"
    Return 0

msgx1:Address ISPEXEC "SETMSG MSG(ISRZ001)";Exit
