/******************************** REXX ********************************/
/**********************************************************************/
/* TRACE r                                                            */
/*--------------------------ooo000OOOO000ooo--------------------------*/
/*                                                                    */
/* Macro to convert assembler data to COBOL format:                   */
/*                                                                    */
/*--------------------------------------------------------------------*/
    Address ISPEXEC
    ARG parm#
    Parse SOURCE . . xcmd .
    If parm# /= "" Then Signal byp1
    "ISPEXEC CONTROL ERRORS RETURN"
    "ISPEXEC ISREDIT MACRO (parm#) NOPROCESS"
    If rc > 0 Then SIGNAL byp1

byp1:
    cactn = "RETURN"
/*  cactn = "CANCEL"                                                  */
    "ISPEXEC CONTROL ERRORS" cactn

ini1:
    "ISREDIT (status) = USER_STATE"
    "ISREDIT RESET"
    "ISREDIT (last) = LINENUM .ZLAST"
    "ISPEXEC VGET (a2cmacro,a2clen) SHARED"
    If a2cmacro = 'PERM' Then Do
        a2cmacro = ''
        "ISPEXEC VPUT (a2cmacro) SHARED"
        where = "LINE_AFTER .ZLAST = (ntl)"
        delo  = 'Y'
    End
    Else Do
        where = "LINE_BEFORE .ZFIRST = NOTELINE (ntl)"
        delo  = 'N'
    End

    nlev = '  03'
    If Datatype(a2clen) = 'NUM' Then
        xx = nl('01  DUMP-'a2clen+0'.')
    Else,
        xx = nl('01  RECORD-#1.')
    Do i=1 To last
        "ISREDIT (line) = LINE" i
        If Substr(line,1,1) = '*' Then Iterate
        Parse VAR line lab ' DC ' len "'" val "'" .
        If len = '' Then Iterate
        lab = Strip(lab)
        len = Strip(len)
        Select
            When Index(len,'CL') > 0 Then Do
                Parse VAR len occur 'CL' len
                dt = ''
            End
            When Index(len,'C') > 0 Then Do
                Parse VAR len occur 'C' len
                dt = ''
            End
            When Index(len,'XL') > 0 Then Do
                Parse VAR len occur 'XL' len
                dt = 'X'
            End
            When Index(len,'X') > 0 Then Do
                Parse VAR len occur 'X' len
                dt = 'X'
            End
            Otherwise Do
                xx = nl('* "len" not catered for. Enter manually.')
                Iterate
            End
        End
        If len = '' Then Do
          If dt = 'X' Then len = Length(val) % 2
          Else             len = Length(val)
        End
        If occur = 0 Then Do
            xx = nl('  03  ' || lab'.')
            nlev = '    05'
            Iterate
        End
        If occur = 1 Then occur = ''
        If lab /= '' Then nlev = '  03'
        Else lab = 'FILLER'
        If occur /= '' Then Do
            occurs = ' OCCURS' occur
            all    = ' ALL'
        End
        Else Do
            occurs = ''
            all    = ''
        End
        xx = nl(Substr(nlev'  ' || lab,1,40),
                'PIC X('Right(len,3,'0')')'occurs)
        xx = nl("        VALUE"all dt"'"val"'.")

    End
    If delo  = 'Y' Then Do
        "ISREDIT CURSOR =" last 0
        "ISREDIT DEL ALL NX .ZFIRST .ZCSR"
    End

get_out:
    "ISREDIT USER_STATE = (status)"
    Exit

nl: Parse Arg ntl
    ntl = '      'ntl
    "ISREDIT &WHERE"
    Return 0

