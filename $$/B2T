/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*                                                                    */
/* B2T: Blocks to Tracks.                                             */
/*                                                                    */
/* Macro to process JCL and replace blocks with TRK rounded up based  */
/* on a 3390 capacity of 56,664 bytes per track.                      */
/*                                                                    */
/* New capacity will be placed as a noteline above the original line. */
/*                                                                    */
/* Parameters:                                                        */
/*   /R  - Replace the original line with new value and use the       */
/*         original value as the noteline.                            */
/*                                                                    */
/*--------------------------------------------------------------------*/
B2T:b2t_ver='1.02'
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS";If rc=0 Then ima_macro=1
byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    trkcap=56664;Numeric Digits 12
    Parse Upper VAR parm# parm#
    If Wordpos('/A',parm#)>0 Then Do
        noteline=;parm#=Delword(parm#,Wordpos('/A',parm#),1)
    End
    If Wordpos('/R',parm#)>0 Then Do
        rel=',RLSE';parm#=Delword(parm#,Wordpos('/R',parm#),1)
    End;Else rel=
    Address ISREDIT;"(status)=USER_STATE";"CURSOR=1 0";"X ALL"
    #rel=
    Do Forever
        "F 'SPACE=('"
        If rc/=0 Then Leave
        "(lin#)=LINENUM .ZCSR";"(linei)=LINE .ZCSR"
        If Index(linei,'SPACE=(')=0 Then Iterate
        Parse VAR linei line +72 p3
        Parse VAR line p1 'SPACE=(' type ',(' prim ',' sec ',' p2
        If \Datatype(type,'W') Then Iterate
        prim#=Format((type*prim)/trkcap,,0)
        sec#=Format((prim#/prim)*sec,,0)
        If rel/='' Then Do
            If Index(line,',RLSE')=0 Then #rel=rel;Else #rel=
        End
        linex=p1'SPACE=(TRK,('prim#','sec#','p2')'#rel||p3
        If noteline='' Then Do
            "LINE .ZCSR=(linex)";"LINE_BEFORE .ZCSR=NOTELINE (line)"
        End
        Else "LINE_BEFORE .ZCSR=NOTELINE (linex)"
    End
    If noteline/='' Then iSay('Use parameter of "/A" to apply changes.',
        'Use parameter of "/R" to release unused space.')
    "F 'SPACE=(' ALL"
    Signal get_out

Get_out:"USER_STATE=(status)";Exit
