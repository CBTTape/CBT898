/******************************** REXX ********************************/
/**********************************************************************/
/*--------------------------ooo000OOOO000ooo--------------------------*/
/* BFIPSYS: Dedicated BFI extension for PaySys P/L....                */
/*                                                                    */
/* Called by BFI when expanding COBOL names or may be invoked stand   */
/* alone.                                                             */
/*                                                                    */
/* This routine is for use on PaySys BMS maps where an input field    */
/* is enclosed in brackets. When an input DFHMDF entry with a COBOL   */
/* name (commented) is encountered, the preceeding and following      */
/* DFHMDF entries are checked for corresponding brackets. If they     */
/* are found, their respective entries are updated with the COBOL     */
/* name using the appropriate '-LP' and '-RP' suffixed comments.      */
/*--------------------------------------------------------------------*/
    Arg parm#
    "ISREDIT MACRO (parm#)"

ini1:
    "ISREDIT (status) = USER_STATE"
/*  Address ISPEXEC                                                   */

    "ISREDIT CURSOR = 1 0"
    Do Forever
        "ISREDIT F ' DFHMDF ' NEXT"
        If rc /= 0 Then Leave
        "ISREDIT (line) = LINE .ZCSR"
        If Substr(line,1,1) = '*' Then Iterate
        Parse VAR line v1 . 40 cn +30 .
        cn = Strip(cn)
        If cn = '' Then Iterate
        "ISREDIT (lin#o) = LINENUM .ZCSR"
        "ISREDIT (line) = LINE" lin#o+1
        If Index(line,'UNPROT') = 0 Then Iterate
        If Length(cn'-LP') > 30 Then Do
            linex = '"'cn'-LP" > 30 bytes. Shorten input field name.'
            "ISREDIT LINE_BEFORE .ZFIRST = NOTELINE (linex)"
            Iterate
        End
        "ISREDIT LABEL .ZCSR = .LINEO"
        "ISREDIT F ' DFHMDF ' PREV"
        "ISREDIT (lin#l) = LINENUM .ZCSR"
        "ISREDIT LABEL .ZCSR = .LINEL"
        "ISREDIT F 'INITIAL=' .LINEL .LINEO FIRST"
        If rc /= 0 Then Do
            "ISREDIT CURSOR =" lin#o+1 0
            Iterate
        End
        "ISREDIT (line) = LINE .ZCSR"
        Parse VAR line . "INITIAL='" iv "'" .
        If iv /= '(' Then Do
            "ISREDIT CURSOR =" lin#o+1 0
            Iterate
        End
        "ISREDIT (line) = LINE" lin#l
        line = Overlay(cn'-LP',LINE,40,28)
        "ISREDIT LINE" lin#l "= (line)"
        "ISREDIT CURSOR =" lin#o+1 0
        "ISREDIT F ' DFHMDF ' NEXT"         /* Get next MDF entry     */
        If rc /= 0 Then Leave
        "ISREDIT LABEL .ZCSR  = .LINER"
        "ISREDIT (lin#r) = LINENUM .ZCSR"
        "ISREDIT F ' DFHMDF ' NEXT"         /* Get end of next MDF    */
        If rc /= 0 Then "ISREDIT LABEL .ZLAST = .LINEN"
        Else            "ISREDIT LABEL .ZCSR  = .LINEN"
        "ISREDIT F 'INITIAL=' .LINER .LINEN FIRST"
        If rc /= 0 Then Do
            "ISREDIT CURSOR =" lin#o+1 0
            Iterate
        End
        "ISREDIT (line) = LINE .ZCSR"
        Parse VAR line . "INITIAL='" iv "'" .
        If iv /= ')' Then Do
            "ISREDIT CURSOR =" lin#o+1 0
            Iterate
        End
        "ISREDIT (line) = LINE" lin#r
        line = Overlay(cn'-RP',LINE,40,28)
        "ISREDIT LINE" lin#r "= (line)"
        "ISREDIT CURSOR =" lin#r+1 0
    End
    "ISREDIT USER_STATE = (status)"

    Return 0
