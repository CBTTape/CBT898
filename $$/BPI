/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* BPI: Build Packaging Index.                                        */
/*                                                                    */
/* In edited member block CC the packaging lines that will then be    */
/* used with the index to select members for packaging using the      */
/* "POPINDEX" command.                                                */
/*                                                                    */
/* Once the Index is built, hit with 'XMB' to build XMITed dataset,   */
/* invoking Multi-Procs "/XM" function.                               */
/*                                                                    */
/*--------------------------------------------------------------------*/
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS"
    If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    Parse SOURCE . xenv xcmd xdd xdsn .
    "VGET (popimac) SHARED"
    Interpret "Signal lab_process"popimac
lab_process:
    Parse VAR parm# memm .
    If memm='' Then memm='$$$INDEX'
    Address ISREDIT;"(status)=USER_STATE"
    "(dsn)=DATASET";memi=dsn'('memm')'
    #m1=Msg('off')
    If Sysdsn("'"memi"'")/='OK' Then Do
        zedlmsg='Missing index name "'memm'" or it does not exist',
                'Correct member name and try again.'
        Address ISPEXEC "SETMSG MSG(ISRZ001)"
        Exit
    End
    ztempf=$ztempf()
    xx=$copymem(memi,ztempf)
    If xx/=0 Then Do
        zedlmsg="Copy of "'memi'" failed RC="xx"."
        Address ISPEXEC "SETMSG MSG(ISRZ001)"
        Exit
    End
    "PROCESS RANGE C";"(cmd)=RANGE_CMD"
    If cmd='C' Then Do
        "(zfrange)=LINENUM .ZFRANGE";"(zlrange)=LINENUM .ZLRANGE"
    End
    Else Do
        zedlmsg="Missing range block command."
        Address ISPEXEC "SETMSG MSG(ISRZ001)"
        Exit
    End
    string=;nat='ABCDEFGHIJKLMNOPQRSTUVWXYZ@#$';ext='0123456789*'
    Do i=zfrange To zlrange
        "(line)=LINE" i
        col#=Verify(line,nat,'M')
        If col#=0 Then Iterate
        member=Strip(Substr(line,col#));Parse VAR member member .
        If Wordpos(Substr(member,Length(member)),':')>0 Then Iterate
        member=Translate(member)
        col#=Verify(member,nat||ext,'N')
        If col#>0 & col<9 Then member=Substr(member,1,col#-1)
        If member='' Then Iterate
        string=string member
    End
    Parse SOURCE . xenv xcmd xdd xdsn .
    Address ISPEXEC
    popimac=1;"VPUT (popimac,string) SHARED"
    Address ISPEXEC "EDIT DATASET('&ZTEMPF') MACRO("xcmd")"
    Address TSO "DELETE '"ztempf"'"
    x=Msg(#m1)

    Exit

lab_process1:
    popimac=;"VPUT (popimac) SHARED"
    "VGET (string) SHARED"
    Address ISREDIT;"(status)=USER_STATE";"X ALL"
    "(linel)=LINE .ZLAST";pref=Strip(Substr(linel,1,1))
    Do While string/=''
        Parse VAR string memb string
        "F '"pref || Substr(memb,1,8)"' 1 15 FIRST"
        If rc/=0 Then Do
            If first/='N' Then Do
                first='N'
                "LINE_AFTER .ZLAST=' '"
                xx='The following members not found in this Index:'
                "LINE_AFTER .ZLAST=(xx)"
                xx=Copies('-',Length(xx))
                "LINE_AFTER .ZLAST=(xx)"
            End
            xx=pref || Substr(memb,1,8)
            xx=Overlay(xx,linel,1,Length(xx))
            xx=Overlay('',xx,Length(xx)+2,80)
            "LINE_AFTER .ZLAST=(xx)"
#tr=Trace('o')
        End
    End
    "DEL ALL X"
    "USER_STATE=(status)"
    Exit

nl: Parse Arg nl1
    "LINE_BEFORE .ZCSR= NOTELINE (nl1)";Return 0
