/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*                                                                    */
/* BR:  BRowse output.                                                */
/*                                                                    */
/* Routine to intercept PUTLINE output from TSO commands and place it */
/* into a dataset and present it under the control of the ISPF        */
/* browser.                                                           */
/*                                                                    */
/* For help, invoke with '?' parameter.                               */
/*                                                                    */
/*--------------------------------------------------------------------*/
BR:br_ver='2.07'
    Arg cmd;Parse Source . . xc .
    zedsmsg=;hlq=Sysvar(Syspref)
    If hlq='' Then hlq=Userid();Else If hlq/=Userid() Then hlq=hlq'.'Userid()
    ispf=Sysvar(Sysispf)
    If cmd='' Then Do
        zedlmsg="Missing command parameter..."
        If ispf='ACTIVE' Then Address ISPEXEC "SETMSG MSG(ISRZ001)"
        Else Say zedlmsg
        Return 8
    End
    Do i=1
        i=Right(i,4,'0');ztempf=hlq'.$BRD'i'.TEMP'
        x=Listdsi("'"ztempf"' NORECALL")
        If x=25 Then Iterate;If x>0 Then Leave
    End
    If cmd='?'  Then Do;Address TSO "%"xc" %"xc" ??";Return 0;End
    If Wordpos('/ASIS',Translate(parm#))>0 Then Do
        asis=1;parm#=Delword(parm#,Wordpos('/ASIS',Translate(parm#)),1)
    End;Else asis=0
    If cmd='??' Then Signal help
    Select
        When Wordpos('/E',cmd)>0 Then Do
            action='EDIT';cmd=Delword(cmd,Wordpos('/E',cmd),1)
        End
        When Wordpos('/V',cmd)>0 Then Do
            action='VIEW';cmd=Delword(cmd,Wordpos('/V',cmd),1)
        End
        When Wordpos('/B',cmd)>0 Then Do
            action='BROWSE';cmd=Delword(cmd,Wordpos('/B',cmd),1)
        End
        Otherwise action='BROWSE'
    End
    If ispf='ACTIVE' Then Address ISPEXEC "CONTROL ERRORS RETURN"

/*--------------------------------------------------------------------*/
/*                                                                    */
/* For ACF2 commands (can be removed for Non ACF2 sites)              */
/*                                                                    */
/*--------------------------------------------------------------------*/
    Parse VAR cmd v1 v2
    If v1='ACF' Then Do
       Do Forever;Parse VAR v2 v3 '\' v2;Queue v3;If v2='' Then Leave;End
       Queue "End"
       cmd=v1
    End
/*--------------------------------------------------------------------*/
    If \asis Then Do
        If Index(cmd,"'")>0 Then cmd='"'cmd'"';Else cmd="'"cmd"'"
    End
    x=Outtrap('OUT.','*','NOCONCAT')
    Signal On Error Name error_on_Interpret
    Interpret cmd
    Signal error_off_Interpret
error_on_Interpret:
    Say 'Failed "'cmd'"'
error_off_Interpret:Signal Off Error
    x=Outtrap('Off')
    dd="$BRD"i
    Signal on Syntax Name error_off_$ALKUVOL
    unitvol='UNIT(SYSALLDA)';unitvol=$alkuvol('$ZTEMPF')
error_off_$ALKUVOL:Signal off Syntax
    Address TSO
    "ALLOC F("dd") DSN('"ztempf"') NEW CATALOG SPACE(10 10)",
        unitvol,
        "TRACKS DSORG(PS) RECFM(V B)",
        "LRECL(133) BLKSIZE(0)"
    'EXECIO' out.0 'DISKW 'dd' (STEM out. FINIS)'
    'FREE FILE('dd')'
    If ispf='ACTIVE' Then Do
        Address ISPEXEC "CONTROL DISPLAY REFRESH"
        Address ISPEXEC "&ACTION DATASET('&ZTEMPF')"
        #m=Msg('Off');"DELETE '"ztempf"'";x=Msg(#m)
    End
    Else Do
        Say 'Enter ISPF and BROWSE/VIEW/EDIT dataset:'
        Say "  '"ztempf"'"
        Say ' '
        Say 'Dataset will have to be manually deleted.'
    End
    Return 0

/*--------------------------------------------------------------------*/
/* Help instructions.                                                 */
/* The variable 'depth' is used to control help. Update it (use the   */
/* 'XX' exclude block command to establish it's value) whenever the   */
/* instructions are updated.                                          */
/*--------------------------------------------------------------------*/
help:
    chgchar='&'
n_st=mark(2)                                /* Mark next line         */
/*

   Good &TOD. &CNAME..

   FUNCTION:
         Prefix any TSO command that issues a PUTLINE to present the
         output under ISPF's browser/viewer/editor in dataset
         '&ZTEMPF.'.


   ENVIRONMENT:
         Can be executed under ISPF or TSO.

         Under ISPF, you will be presenter with the dataset under
         your desired viewer. The default is BROWSE.

         Under TSO the output will be placed into a dataset and an
         appropriate message will be displayed telling you the name
         of the recipient dataset.

   SYNTAX:
         %&XC. any_TSO_command_with_parameter_list {/E | /V | /B}

   OPERANDS:

         /B | /E | /V
            - To either edit, view or browse the dataset.  The
              default is Browse.

         ?  - Help, but you already know this don't you!!

   FEATURES:
         1. ACF2
            You may stack ACF2 commands using a back slash '\' as a
            command seperator. Eg.

               %&XC. ACF LIST LIKE(TP-)\LIST LIKE(SF-)

   GOTCHAs:
      1. Operand processing are destructive. If one is found, it is
         removed from the parameter list. Consequently if /E, /V or
         /B are legitimate operands to the TSO command to be
         executed it is suggested that you explicitly request your
         type of presentation before the TSO command, eg.
            %&XC. /B %ANYLOOK 'SYS2.PARMLIB(IKJTSO00)' /V

      2. This command uses the "Interpret" REXX command. To ensure it
         is interpreted correctly the command is enclosed in the
         converse of either single or double quotes depending on the
         use of single or double quotes in the command.   At times this
         may not generate the disired result and if the command fails
         suggest pass a parameter of "/ASIS" which will prevent the
         addition of either of the quotes.

                              *****

*/
n_en=mark(-2)                               /* Mark prev 2 line       */
    Signal On Syntax Name bypass_$WHOAMI;cname='there';cname=$whoami('C')
bypass_$WHOAMI:Signal Off Syntax
    Parse Value Time() With hh ':' 1 time +5
    Select
        When hh<12 Then tod='morning'
        When hh<18 Then tod='afternoon'
        Otherwise tod='evening'
    End
    x=nl_h('*'Centre('ooo000OOOO000ooo',77,'-')'*')
    Do i=n_st To n_en
        line=Sourceline(i)
        Do While Index(line,chgchar)>0
            Parse VAR line p1 '&' var '.' p2;Interpret 'line=p1||'var'||p2'
        End
        x=nl_h(Substr('*   'line,1,78)'*')
    End
    x=nl_h('*'Copies('-',77)'*')
    Exit

nl_h:Parse Arg n#;Say n#;Return 0
Mark: Arg diff;If Datatype(diff)/='NUM' Then diff=1;Return sigl+diff
