/******************************** REXX ******************************/
/*+----------------------------------------------------------------+*/
/*|                                                                |*/
/*| Program Name    : CATTLE                                       |*/
/*| Objective       : Loop through the CAXWA chain, display cats.  |*/
/*| Associated Code : None                                         |*/
/*| Author          : Maurice L.Clark                              |*/
/*| Company         : Thunderbird Computing Australia Pty Ltd      |*/
/*| Date            : 28/04/93                                     |*/
/*| Parameters      :                                              |*/
/*|   blank         : Will set 1st parm to '??' and invoke BR to   |*/
/*|                   present under browser.                       |*/
/*|   ??            : Issue "Say" statements                       |*/
/*|   MCAT          : Return the name of the Master Catalog and    |*/
/*|                   its volume seperated by a '/'                |*/
/*|   UCAT          : Return the name of the User Catalog (or      |*/
/*|                   Master catalog) name followed by it's volume |*/
/*|                   seperated by a '/' if a match is found (2nd. |*/
/*|                   parameter) or the literal 'NOT FOUND' if no  |*/
/*|                   catalog name matches the 2nd. parameter.     |*/
/*|                                                                |*/
/*+----------------------------------------------------------------+*/
/********************************************************************/
CATTLE:cattle_ver='1.05'
Parse source opsys . cmd_name . dsn_name .

Parse Upper ARG rcat,qcat

If Wordpos(rcat,'MCAT UCAT ??')=0 Then Do
    Address TSO "%$BR CATTLE ??";Return 0
End

If Wordpos(rcat,'MCAT UCAT')=0 Then Do
    rcat=
    Address TSO "CALL 'T32AZK.$LOAD(CLEAR)'"
/*  Call Clear                                                        */
End

If rcat='' Then Do
   dl=30
   wl=62
   Say
   line = 'o------------------ooo00O00ooo-------------------o'
   Say  Centre(line,wl)
   line = 'o          Catalog Information Utility.          o'
   Say  Centre(line,wl)
   line = 'o------------------------------------------------o'
   Say  Centre(line,wl)
   Say

   H1 ='Cat Type__ Catalog Name__________________'
   H2 ='Dev_ Volser'

   line = H1 H2
   Say  Centre(line,wl)
   Say
End

cvt    = c2x(storage(10,4))
amcbs  = c2x(storage(d2x(x2d(cvt)   + x2d(100)),4))
caxwa  = c2x(storage(d2x(x2d(amcbs) + x2d(14)),4))

Do while caxwa Â¬= "0"
    caxcnam   = storage(d2x(x2d(caxwa) + x2d(34)),44)
    caxcnam   = strip(caxcnam)
    caxucbp   = c2x(storage(d2x(x2d(caxwa) + x2d(1c)),4))
    caxaddr   = c2x(storage(d2x(x2d(caxucbp) + x2d(4)),2))
    caxvols   =    (storage(d2x(x2d(caxucbp) + x2d(1C)),6))
    caxmct    = c2x(storage(d2x(x2d(caxwa) + x2d(8)),1))
    caxmct    = substr(x2b(caxmct),6,1)

    If rcat='' Then Do
        If caxmct='1' Then Catalog_Type='MasterCat'
        Else               Catalog_Type='UserCat'

        line=Left(Catalog_Type,10,'_') Left(caxcnam,dl,'.'),
             Substr(caxaddr,1,4) Substr(caxvols,1,6)
        Say  Centre(line,wl)
    End
    Else Do
        If caxmct='1' & rcat='MCAT' Then Return caxcnam'/'caxvols
        Else If caxcnam=qcat Then Return caxcnam'/'caxvols
    End
    caxwa  = c2x(storage(d2x(x2d(caxwa) + x2d(4)),4))
End

If rcat='' Then Say
Else Return 'NOT FOUND'

Exit

/*********************************************************************/
/*                                                                   */
/*                    SYSTEM CAXWA TABLE LOOKUP                      */
/*                                                                   */
/*===================================================================*/
/*                      CONTROL BLOCK CHAINING                       */
/*  AMCBS      : CVT+X'100' ACCESS METHOD CONTROL BLOCK  (CVTCBSP)   */
/*  CAXWA     -> AMCBS+X'14' CATALOG WORK AREA                       */
/*  CAXNEXT   -> CAXWA+X'04' NEXT CAXWA ENTRY                        */
/*  CATFLAG1  -> CAXWA+X'08' CATALOG FLAG BYTE 1                     */
/*   X....... -> BUILD REQUESTED                                     */
/*   .X...... -> OPEN IN CONTROL                                     */
/*   ..X..... -> CLOSE IN CONTROL                                    */
/*   ...X.... -> END OF VOLUME IN CONTROL                            */
/*   ....X... -> I/O SUPPORT COMPLETED                               */
/*   .....X.. -> 1:MASTER CATALOG, 0:USER CATALOG                    */
/*   ......X. -> CATALOG MANAGEMENT IN CONTROL                       */
/*   .......X -> SCHEDULER IN CONTROL                                */
/*  CATFLAG2  -> CAXWA+X'09' CATALOG FLAG BYTE 2                     */
/*   X....... -> CATALOG DELETED                                     */
/*   .X...... -> NO DDNAME FOUND                                     */
/*   ..X..... -> UNABLE TO GET CORE                                  */
/*   ...X.... -> I/O ERROR                                           */
/*   ....X... -> RPL CLEAN UP REQUESTED                              */
/*   .....X.. -> FREE CAXWA IF ERROR                                 */
/*   ......X. -> RECOVERABLE CATALOG                                 */
/*   .......X -> VOLUME TIME STAMP UPDATED                           */
/*  CATFLAG3  -> CAXWA+X'0A' CATALOG FLAG BYTE 3                     */
/*   X....... -> CRA ALT TIOT EXIST                                  */
/*   .X...... -> CRA NOT EXIST                                       */
/*   ..X..... -> RESERVED                                            */
/*   ...X.... -> CATALOG IS RAC DEFINED                              */
/*   ....X... -> NO CATALOG PSWD DEFINED                             */
/*   .....XX. -> 01 NO UPDATE 10 UPDATE                              */
/*   .......X -> RESERVED                                            */
/*   CATACT   -> CAXWA+X'0B' CATALOG ACTIVITY                        */
/*   X....... -> IN STORAGE                                          */
/*   .X...... -> ALLOCATED                                           */
/*  CATUCBP   -> CAXWA+X'1C' ADDRESS OF CATALOG UCB                  */
/*  CATNAME   -> CAXWA+X'34' CATALOG NAME FIELD 44 BYTES             */
/*                                                                   */
/*********************************************************************/
