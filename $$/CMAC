/******************************** REXX ********************************/
/**********************************************************************/
/* TRACE r                                                            */
    n_st=mark(1)                            /* Mark Start             */
/*--------------------------ooo000OOOO000ooo--------------------------*/
/*                                                                    */
/* CMACB:                                                             */
/* Macro to build a CICS CMAC dataset build job from a list of PTFs   */
/* used to update the CMAC dataset from the list defined in a '*DOC'  */
/* APAR/PTF.                                                          */
/*                                                                    */
/* Works on a list of PTF members.  Will remove leading and trailing  */
/* '()'s.                                                             */
/* Use block commands or default to ALL.                              */
/*                                                                    */
/* Infrastructure:                                                    */
/*  REXX:                                                             */
/*    $ALKCICS - Default CICS dataset definer.                        */
/*    CMAC     - This driver.                                         */
/*    JC       - Job card macro and its associated infra.             */
/*                                                                    */
/*--------------------------------------------------------------------*/
    n_en=mark(-2)                           /* Mark End               */

    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN"
    If parm#/='' Then Do
        zedsmsg=
        zedlmsg='This command should be executed as an edit macro.'
        "SETMSG MSG(ISRZ001)";Exit
    End
    "ISREDIT MACRO (parm#) NOPROCESS"
    If rc/=0 Then Do
        zedsmsg=
        zedlmsg='This command should be executed as an edit macro.'
        "SETMSG MSG(ISRZ001)";Exit
    End

byp1:
    "CONTROL ERRORS CANCEL"

ini1:
    Address ISREDIT
    If Wordpos(Word(parm#,1),'? /? /H HELP')>0 Then Signal TSO_help

nohelp:
    "(status)=USER_STATE";"(caps)=CAPS";"CAPS=OFF"
    "(lev)=MACRO_LEVEL"
    If lev>1 Then Do
        Parse VAR parm# labf ',' labt ',' lcmd parm#
        zfrange=labf;zlrange=labt
    End
    Else Do
        "(last)=LINENUM .ZLAST"
        "PROCESS RANGE C"
        "(cmd)=RANGE_CMD"
        If cmd='C' Then Do
            "(zfrange)=LINENUM .ZFRANGE";"(zlrange)=LINENUM .ZLRANGE"
        End
        Else Do;zfrange=1;zlrange=last;End
    End

    nat='ABCDEFGHIJKLMNOPQRSTUVWXYZ@#$';num='0123456789'

    input=
    Do i=zfrange To zlrange
        "(line)=LINE" i
        input=input Strip(line)
    End
    line=Space(input)
    col#=Verify(line,nat,'N');If col#=1 Then line=Substr(line,2)
    j=0
    Do While line/=''
        Parse VAR line memb line
        col#=Verify(memb,nat||num,'N')
        If col#>0 Then memb=Substr(memb,1,col#-1)
        j=j+1;m.j='DFH'Substr(memb,3)
    End
    Address TSO "NEWSTACK"
    Signal on Error Name error_off_00;#msg=Msg('Off')
    Address ISPEXEC "CONTROL ERRORS RETURN"
    "JC MU /Q";Signal error_off_00e
error_off_00:
    Queue '//'Userid()'CM JOB (ACCOUNT),'CMAC Update','
    Queue '//    CLASS=A,MSGCLASS=X,'
    Queue '//    NOTIFY=&SYSUID'
    Queue '//*'
error_off_00e:
    Signal off Error
    Signal on Syntax Name error_off_01
    dsn=$alkcics('sdfhload')
    dsnhlq=Strip(Substr(dsn,1,Lastpos('.',dsn)-1),,"'")
    Signal error_off_01e
error_off_01:
    Say 'COMMAND $ALKCICS NOT FOUND'
    dsnhlq='DNMCICS.CICSTS.V230.CICS'
error_off_01e:
    Signal off Syntax
    Queue '//*'
    Queue '//VARS SET CICSHLQ='dsnhlq'  <- Confirm'
    Queue '//*'
    Queue '//*  ********************************'
    Queue '//*  *                              *'
    Queue '//*  * CMAC Transaction Service JCL *'
    Queue '//*  *                              *'
    Queue '//*  ********************************'
    Queue '//CMACUPD  EXEC PGM=IDCAMS'
    Queue '//SYSPRINT DD SYSOUT=*'
    Queue '//AMSDUMP  DD SYSOUT=*'
    ll=Length(j)
    fixed01=' DD DISP=SHR,DSN=&CICSHLQ..SDFHMSGS'
    fixed02='  REPLACE  OUTFILE(DFHCMACD)'

    cmacl=
    Do k=1 To j
        If Index(cmacl,m.k)>0 Then,
            Queue '//*'Substr('DSN'Right(k,ll,'0'),1,8)fixed01'('m.k')'
        Else Do
            Queue '//'Substr('DSN'Right(k,ll,'0'),1,8)fixed01'('m.k')'
            cmacl=cmacl m.k
        End
    End

    Queue '//*'
    Queue '//DFHCMACD DD DISP=SHR,DSN=&CICSHLQ..DFHCMACD'
    Queue '//SYSIN    DD *'
    cmacl=
    Do k=1 To j
        If Index(cmacl,m.k)>0 Then,
            Queue ' /* REPRO INFILE('m.k')'fixed02' */'
        Else Do
            Queue '    REPRO INFILE('m.k')'fixed02
            cmacl=cmacl m.k
        End
    End
    Queue '/*'
    Queue '//'
    Queue ''

    dd='CM'Time('s');Address ISPEXEC "VGET ztempf"
    Address TSO "ALLOC F("dd") DSN('"ztempf"') SHR REU"
    Address TSO "EXECIO * DISKW "dd" (FINIS"
    Address ISPEXEC "LMINIT DATAID(DATAID) DDNAME("dd")"
    Address ISPEXEC "EDIT DATAID(&DATAID)"
    Address ISPEXEC "LMFREE DATAID(&DATAID)"
    Address TSO "FREE F("dd")"

ext01:
    "CAPS=(caps)";"USER_STATE=(status)"
    Return 0

err01:
    Address ISPEXEC "SETMSG MSG(ISRZ001)"
    Exit

/*--------------------------------------------------------------------*/
/* Tutorial....                                                       */
/*--------------------------------------------------------------------*/
TSO_help:
    Do i=n_st To n_en
        x=nl_h(Sourceline(i))
    End
    Exit

nl_h:Parse Arg n#;"LINE_BEFORE .ZFIRST=NOTELINE (n#)";Return 0
Mark:If Datatype(Arg(1))/='NUM' Then Arg(1)=1;Return sigl+Arg(1)
