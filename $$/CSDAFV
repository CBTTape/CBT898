/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* CSDAFV: Process output from a CSDA F(ddname) command and list the  */
/*         datasets and associated Volumes and generate a volume(s)   */
/*         used summary at the end.                                   */
/* Parameter (postitonal):                                            */
/* nnnn     - Number of datasets to process (default is all).         */
/*--------------------------------------------------------------------*/
CSDAFV: csdafv_ver='1.02'
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS";If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    If Datatype(parm#,'w') Then tally=parm#;Else tally=9999999999
    Address ISREDIT;"(status)=USER_STATE"
    "SCAN=OFF";"(caps)=CAPS";"CAPS=OFF";"RESET"
    "CURSOR=1 0";vol_list=
    x=nl('Volume Dataset name                                 Oopsies')
    x=nl('------ -------------------------------------------- ----------------')
    i=0
    Do Forever
        "SEEK ' DSNAME('";If rc/=0 Then Leave
        "(line)=LINE .ZCSR"
        Parse VAR line 'DSNAME(' dsn ')'
        xx=Listdsi("'"dsn"' NORECALL")
        If xx>4 Then Do
            sysvolume='??????'
            Parse VAR sysmsglvl2 p1 p2 '.' 1 ch3 +3
            If ch3/='IKJ' Then p2=p1 p2;Parse VAR p2 'DATA SET ' p2
            dsn=Substr(dsn,1,44) p2
        End
        Else If Wordpos(sysvolume,vol_list)=0 Then vol_list=vol_list sysvolume
        x=nl(sysvolume dsn,1)
        i=i+1;If i=tally Then Do
            x=nl('* Reached cut of number' tally'.',1);Leave
        End
    End
    x=nl(' ',1);x=nl('Volumes required:',1);x=nl('-----------------',1)
    xx=$sort(vol_list)
    Do While xx/=''
        x=nl($Wordat(xx,72,'l'),1)
        xx=$Wordat(xx,72,'t')
    End
    x=nl(' ',1);x=nl(Center,'*****',72);x=nl(' ',1)

Signal get_out

Get_out:"CAPS=(caps)";"USER_STATE=(status)";Exit
nl:Parse Arg n#,where;If \Datatype(where,'W') Then where='.ZCSR'
"LINE_BEFORE" where"=NOTELINE (n#)";Return 0
