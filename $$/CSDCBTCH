/******************************** REXX ********************************/
/*--------------------------ooo000OOOO000ooo--------------------------*/
/* CSDCBTCH: CSD Changed in BaTCH.                                    */
/*                                                                    */
/* REXX to process an object CSD list and report on all entries that  */
/* have been changed since an input date.                             */
/*                                                                    */
/* Sample JCL at bottom of program.                                   */
/*                                                                    */
/* Requires routine "$DATEMAN" unless you input the explicit julian   */
/* date in format "yy.ddd".                                           */
/*                                                                    */
/* Input parameters (positional):                                     */
/* 1. CSD listing dataset name (fully qualified and quoted).          */
/* 2. Input date is parameter 1 and can have the following format:    */
/*      dd[/mm[/yy]] - European date format.                          */
/*      yy[yy].ddd   - JUlian date format,                            */
/*      -days        - Number of days before today.                   */
/*    Default is -7.                                                  */
/* 3. Start record. Speeds up the processing a tad. Browse dataset 1  */
/*    above and issue a "F 'OBJECTS IN GROUPS'" command.  Note the    */
/*    line number and use it as this parameter. Default is line 1.    */
/*    (Use a "." placeholder if you want to supply the next parm.)    */
/* 4. Start column. Depending on  processing a tad. Browse dataset 1  */
/*--------------------------------------------------------------------*/
    Arg parm#
    Parse Upper VAR parm# infile date start_record scol
    infile="'"Strip(infile,,"'")"'"
    xx=Listdsi(infile " DIRECTORY NORECALL SMSINFO")
    If xx<5 | sysreason=9 | sysreason=25 Then Nop
    Else Do
        Say infile
        Say sysmsglvl1
        Say sysmsglvl2
        Exit
    End
    If \Datatype(scol,'W') Then scol=2
    If \Datatype(start_record,'W') Then start_record=1
    jd=date
    Signal on Syntax Name ini1
    xx=$DATEMAN('VER')
    If date='' Then date='-7'
    If Substr(date,1,1)='-' Then xx=$DATEMAN('E-N',,,date)
    Else xx=$DATEMAN('VER',date,2)
    If Word(xx,1)='Error' Then Do
        zedlmsg=xx;Exit
    End
    If Index(xx,'.')=0 Then jd=$DATEMAN('E2J',xx,2);Else jd=xx
    today=$DATEMAN('E2J',,2)

ini1:
    Signal Off Syntax
    dd='CSDCBTCH'
    Address TSO
    "ALLOC F("dd") DA("infile") SHR REU"
    rc1=rc

    msz=4*(1024*1024)                       /* Memory for read        */
    x=Listdsi(infile)                       /* To return file's LRECL */
    #pr=msz%syslrecl                        /* No. records per read   */
    sr=start_record                         /* Start read at          */
    j=sr                                    /* Output line counter    */
    tr=sr-1                                 /* Total records accum    */
    objects=0;hits=0;tr=0
    Say ' '
    Say '*'Copies('-',98)'*'
    Say 'Processing CSD extract file' infile 'starting at record',
        start_record'.'
    Say 'The following entries have changed since' jd':'
    Say ' '
    Do While rc1/=2                         /* End of file test       */
        "EXECIO "#pr" DISKR "dd sr" (STEM linei. OPEN)"
        rc1=rc;tr=tr+linei.0;sr=
        Do i=1 To linei.0
            j=j+1
            If \objects Then Do
                If Substr(linei.i,2,17)='OBJECTS IN GROUPS' Then objects=1
                Iterate
            End
            Parse Var linei.i w1 +40 110 date time
            If Substr(date,3,1)/='.' Then Iterate
            If date<jd Then Iterate
/*          If date>today Then Iterate                                */
            Say ' 'Substr(w1,scol) date time;hits=hits+1
        End
    End
    Say 'Total records='hits 'of' tr'.'

    "EXECIO 0 DISKR "dd" (STEM linei FINIS)"
    If Substr(infile,1,3)/='DD(' Then ,
        "FREE F("dd")"

ext1:
    Exit

/*   JCL to execute.....

//TSSAKBD  JOB (3000-000000-00-Z-00000000000),
//   'CSDCBTCH',
//* RESTART=ISPFBTCH,                                                        */
//   CLASS=3,COND=(4,LT),
//   MSGCLASS=P,TIME=1440,
//   NOTIFY=&SYSUID
//*                                                                          */
//VARS  SET CSDLIST=TSSAK.CICSSRD4.LIST.TEMP      <- Output list
//      SET  DFHCSD=DNTCICS.CICSSRD4.DFHCSD       <- CSD
//      SET  CODATE='02.001'                      <- Cut off date
//*                                                                          */
//*     "F 'OBJECTS IN GROUPS'"  to get next value or default to 1           */
//*                                                                          */
//      SET   START=1             2500            <- Start at
//      SET STRTCOL=2                             <- 2 for RECFM=VBA
//*                                                                          */
//      SET SYSPROC=TSSAK.$$
//*-------------------------------------------------------------------*      */
//RERUN    EXEC PGM=IEFBR14
//SYSPRINT DD DISP=(MOD,DELETE),DSN=&CSDLIST,
//            UNIT=SYSALLDA,VOL=SER=,SPACE=(TRK,(0,0),RLSE)
//*                                                                          */
//*-------------------------------------------------------------------*      */
//UPDATE   EXEC PGM=DFHCSDUP,PARM='CSD(READONLY)'
//STEPLIB  DD DISP=SHR,DSN=DNMCICS.CICSZOS.V220.CICS.SDFHLOAD
//DFHCSD   DD DISP=SHR,DSN=&DFHCSD,
//            AMP=('BUFNI=30','BUFND=90')
//SYSPRINT DD DISP=(NEW,CATLG),DSN=&CSDLIST,
//            UNIT=SYSALLDA,VOL=SER=,SPACE=(TRK,(300,100),RLSE)
//SYSIN    DD *
LIST ALL OBJECTS
//*                                                                          */
//*-------------------------------------------------------------------*      */
//ISPFBTCH EXEC PGM=IKJEFT01,DYNAMNBR=65,REGION=4096K,
//  PARM=('%CSDCBTCH ''&CSDLIST'' &CODATE &START &STRTCOL')
//SYSPROC  DD DISP=SHR,DSN=&SYSPROC
//SYSTSPRT DD SYSOUT=H
//SYSOUT   DD SYSOUT=*
//SYSTERM  DD SYSOUT=*
//SYSTSIN  DD *
//*                                                                          */
//*-------------------------------------------------------------------*      */
//CLEANUP  EXEC PGM=IEFBR14
//*SYSPRINT DD DISP=(MOD,DELETE),DSN=&CSDLIST,                               */
//*            UNIT=SYSALLDA,VOL=SER=,SPACE=(TRK,(0,0),RLSE)                 */
//*                                                                          */
//*-------------------------------------------------------------------*      */
//


*/
