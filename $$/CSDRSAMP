/******************************** REXX ********************************/
    dsnvsam='AKOS.$.DNDCICS.TEST.DFHCSD'
/*             Seq Itemtype        Record type                        */
/*             --- --------------  ----                               */
    validtypes=,
               ' 1 CONnections     01F4',
               ' 2 CORbaservers    0226',
               ' 3 DB2Conns        1B58',
               ' 4 DB2Entries      1D4C',
               ' 5 DB2Trans        1EDC',
               ' 6 DJars           0258',
               ' 7 DOctemplates    238C',
               ' 8 Enqmodels       157C',
               ' 9 Files           0320',
               ' 0 Groups          0006',
               '11 Journalmodels   0384',
               '12 Lsrpools        028A',
               '13 MapSets         03E8',
               '14 PARTItionsets   07D0',
               '15 PARTNers        09C4',
               '16 PROCesstypes    2134',
               '17 PROFiles        0BB8',
               '18 PROGrams        0FA0',
               '19 Requestmodels   2328',
               '20 Sessions        10ED',
               '21 TCpipservices   23F0',
               '22 TDqueues        1770',
               '23 TErminals       123A',
               '24 TRANClasses     1318',
               '25 TRANSactions    1388',
               '26 TSmodels        1F40',
               '27 TYpeterms       11CB',
               ''
    TD_type="Type_48:1("'01'x"=EXTRA,"'02'x"=INTRA,"'03'x"=INDIRECT)/8"

    itemflds=, /* Format itemtype1 idtext1_hexid1/maxlen1             */
               "TRANSACTIONS   Remote_0A/4_Prog_06/8    ",
               "FILES          Remote_0A/4_Dsname_06/44 ",
               "CONNECTIONS    Netnam_05/8              ",
               "SESSIONS       Conect_05/5              ",
               "JOURNALMODELS  Stream_08/26             ",
               "TDQUEUES       DDname_09/8_Facili_0C/4_Trans_0D/4_" ||,
                               TD_type,
               ""
    desc_id="Desc'n"
    descflds=, /* Format item1 idtext1_hexid1/maxlen1                 */
               "CONNECTIONS    Desc'n_0C/58             ",
               "CORBASERVERS   Desc'n_06/58             ",
               "DB2CONNS       Desc'n_06/58             ",
               "DB2ENTRIES     Desc'n_06/58             ",
               "DB2TRANS       Desc'n_06/58             ",
               "DJARS          Desc'n_06/58             ",
               "DOCTEMPLATES   Desc'n_06/58             ",
               "ENQMODELS      Desc'n_06/58             ",
               "FILES          Desc'n_12/58             ",
               "GROUPS         Desc'n_06/58             ",
               "JOURNALMODELS  Desc'n_06/58             ",
               "LSRPOOLS       Desc'n_15/58             ",
               "MAPSETS        Desc'n_06/58             ",
               "PARTITIONSETS  Desc'n_06/58             ",
               "PARTNERS       Desc'n_06/58             ",
               "PROCESSTYPES   Desc'n_06/58             ",
               "PROFILES       Desc'n_06/58             ",
               "PROGRAMS       Desc'n_06/58             ",
               "REQUESTMODELS  Desc'n_06/58             ",
               "SESSIONS       Desc'n_1C/58             ",
               "TCPIPSERVICES  Desc'n_06/58             ",
               "TDQUEUES       Desc'n_06/58             ",
               "TERMINALS      Desc'n_06/58             ",
               "TRANCLASSES    Desc'n_06/58             ",
               "TRANSACTIONS   Desc'n_15/58             ",
               "TSMODELS       Desc'n_06/58             ",
               "TYPETERMS      Desc'n_06/58             ",
               ""
inval='????'
vsamopen=0;try2open=1;listdesc=1

eioparm='NNOTIFY NLOG NCONT NTRACE'
group='CEE     ';find='CIGZ    ';findtype='TDQUEUES'     ;xx=Get_CSD_Data()
group='ALKNEW  ';find='AUPD    ';findtype='TRANSACTIONS' ;xx=Get_CSD_Data()
group='NBCICS  ';find='DUVT    ';findtype='TRANSACTIONS' ;xx=Get_CSD_Data()
group='XXCTE   ';find='DUVT    ';findtype='TRANSACTIONS' ;xx=Get_CSD_Data()
group='ALKNEW  ';find='DFHLOG  ';findtype='JOURNALMODELS';xx=Get_CSD_Data()
group='ALKNEW  ';find='DFHLGLOG';findtype='JOURNALMODELS';xx=Get_CSD_Data()
group='ALKNEW  ';find='FILEA   ';findtype='FILES'        ;xx=Get_CSD_Data()
group='ALKCONNT';find='ALKSES0T';findtype='SESSIONS'     ;xx=Get_CSD_Data()
group='ALKCONNT';find='CIT0    ';findtype='CONNECTIONS'  ;xx=Get_CSD_Data()
group='CEE     ';find='CIGZ    ';findtype='TDQUEUES'     ;xx=Get_CSD_Data()
group='DFH$FILA';find='L86O    ';findtype='TDQUEUES'     ;xx=Get_CSD_Data()
eioparm=' NOTIFY  LOG NCONT NTRACE'
group='DFH$FILB';find='L86O    ';findtype='TDQUEUES'     ;xx=Get_CSD_Data()
/*          If Wordpos(findtype,itemflds)>0 Then xx=Get_CSD_Data()    */
exi1:
    If vsamopen Then vsamopen=CSDR('f',dd)
    Exit

/*--------------------------------------------------------------------*/
/* If CSD available, get extra information for items as per contents  */
/* of variable "itemflds".                                            */
/*--------------------------------------------------------------------*/
Get_CSD_Data:
    hd1=Substr('  Group('group')',1,18)

    wx=Word(itemflds,Wordpos(findtype,itemflds)+1);wx=TRanslate(wx,' ','_')
    If listdesc Then Do
        wd=Word(descflds,Wordpos(findtype,descflds)+1);wd=TRanslate(wd,' ','_')
        wx=wx wd
    End
    If try2open Then Do
        try2open=0
        dd='VS'Time('s')
        If \vsamopen Then vsamopen=CSDR('o',dd,dsnvsam)
        Address TSO "DELSTACK"
    End
    If vsamopen Then Do
        xx=Wordpos(findtype,Translate(validtypes))
        hexoff=Word(validtypes,xx+1)
        key=Substr(group,1,8)||x2c(right(hexoff,12,'0'))||,
            Substr(find,1,8)
/*      vrc=CSDR('R',dd,key)                                          */
        "EXECIOVS 1 KSDSR"  dd "("eioparm") KEY("key")"
    End;Else vrc=8
    If vrc=0 Then Do
        Parse PULL line;ll=Length(line)
        i_tm=Strip(Substr(key,15,8));ps=Index(line,i_tm,Length(key)+1)-2
    End
    Address TSO "DELSTACK"
    oval=;odesc=;#wx=wx
    Do While #wx/=''
        Parse VAR #wx w1 w2 '/' ml #wx
        pp=ps;val=
        If vrc=0 & pp>0 Then Do
            If Index(w2,':')>0 Then Do
                Parse VAR w2 col ':' il '(' matrix ')'
                val=Substr(line,col,il)
                Interpret "Parse VAR matrix '"val"=' val ','"
            End
            Else Do
                Do Forever
                    id=Substr(line,pp,1);il=c2d(Substr(line,pp+1,1))
                    If id=x2c(w2) Then Do
                        val=Substr(line,pp+2,il)
                        Leave
                    End
                    pp=pp+2+il
                    If pp>ll Then Leave
                End
            End
        End;Else val=inval
        pad=Substr(' ',1,ml-Length(val))
        If w1/=desc_id Then oval=oval||Substr(w1,1,6,'.')'('val'); 'pad
        Else odesc=val
    End
    oval=Substr(Strip(oval),1,Lastpos(')',oval)-1)
/*  x=Output(,hd1'»' oval);hd1=Substr(' ',1,18)                       */
/*  If odesc/='' & odesc/=inval Then x=nl(hd1'*' odesc)               */
Say findtype'('Strip(find)')'
Say hd1'»' oval')';hd1=Left(' ',Length(hd1))
Say hd1'*' odesc
/*  x=nl(' ')                                                         */
    Return 0

