/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*                                                                    */
/* CSMDSNCO:                                                          */
/*                                                                    */
/* Multi- Procs ClOne dataset.                                        */
/* Pass parameter of '/BU' to clone to dataset name suffixed by       */
/* '.BUyymmdd', where yymmdd is todays date.                          */
/*                                                                    */
/*--------------------------------------------------------------------*/
csmdsnco_ver='2.04'
    Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    Arg parm#;If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#)"
    If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    zedsmsg=
    If Wordpos('/T',Translate(parm#))>0 Then Do
        parm#=Delword(parm#,Wordpos('/T',Translate(parm#)),1)
        Trace r
    End
    If Wordpos('/BU',Translate(parm#))>0 Then Do
        create_bu=1;parm#=Delword(parm#,Wordpos('/BU',Translate(parm#)),1)
    End;Else create_bu=0
    Parse VAR parm# parm1 parm2 parm3
    If Wordpos(parm1,'HELP ?')>0 Then Do Then Do
        "SELECT PGM(ISPTUTOR) PARM(HNOHELP)"
        Exit
    End
    If parm1/='' Then dsname=parm1
    Else Do;"VGET (JDSNAME VOLUME) SHARED";dsname="'"jdsname"'";End

    acct=$alkacct();userid=Userid();sysuid=userid
    Parse VALUE 'N Y' With ejcl catit msg
    stc=$ALKUVOL('STC')
    mgc=$ALKUVOL('MGC')
    unit=$ALKUVOL('UNIT')
    csr1='VOLUME';msg=;ejcl='N';catit='Y'
    dsno=dsn
    Parse Value $alkjc('def_ex def_hd') With xcl mcl

    If Substr(dsname,1,1)="'" Then dsn=Strip(dsname,,"'")
    Else dsn=sysuid'.'dsname
    If create_bu Then ndsn="'"dsn".BU"$DATEMAN('REV',,2,'D')"'"
    Else ndsn="'"dsn"'"
    x=Listdsi("'"dsn"' NORECALL")
    If x>4 & sysreason/=3 Then Do
        Parse VAR sysmsglvl2 . zedlmsg
        Signal exi1
    End
    ovol=sysvolume
    smsm='N'                                /* SMS managed flag       */
    dynline=3
    cl=$alkjc('def_ex')                     /* Job class              */

scr1:
    Call build_screen
scr1a:
    zcmd=
    If dynline=3 Then dyncol=23;Else dyncol=22
    "DISPLAY PANEL(DYNAMIC$)"
    If rc/=0 Then Exit
    msg=
    Call parse_screen
    If odsn=dsn Then Do
        zedlmsg="Cloned dataset cannot be the same name as original."
        dynline=3
        "SETMSG MSG(ISRZ001)";Signal scr1a
    End
    If smsm='N' & ovol='' Then Do
        zedlmsg="Must specify volume for Non-SMS managed datasets."
        dynline=5
        "SETMSG MSG(ISRZ001)";Signal scr1a
    End
    If smsm='N' Then Do
        volume="VOLUME("ovol")"
        x=Listdsi("'SYS1.LINKLIB'" volume)
        If sysreason=22 Then Do
            zedlmsg="Volume '"ovol"' not mounted or defined to the",
                  "system."
            dynline=5
            "SETMSG MSG(ISRZ001)";Signal scr1a
        End
    End
    Else volume=
    If smsm='N' | smsm='Y' Then Nop
    Else Do
        zedlmsg="Invalid value."
        dynline=4
        "SETMSG MSG(ISRZ001)";Signal scr1a
    End
    x=Listdsi(ndsn volume)
    If x=0 & repl/='Y' Then Do
        If ovol/='' Then Do
            zedlmsg="Dataset already on requested volume and replace not",
                  "specified."
            dynline=3
            "SETMSG MSG(ISRZ001)";Signal scr1a
        End
        Else Do
            zedlmsg="Dataset already define."
            dynline=3
            "SETMSG MSG(ISRZ001)";Signal scr1a
        End
    End

    prog='CLONE';Signal on Syntax Name error_off_alkjcpg
    x=$Alkjcpg(prog);If x/='' Then prog=x
error_off_alkjcpg:Signal off Syntax

"CONTROL ERRORS CANCEL"
    "FTOPEN TEMP";"FTINCL CSMDSSCO";"FTCLOSE"
    "VGET ZTEMPF";ddz='$Z'Time('s')
/*  ztempf=$ztempf()                                                  */
    Address TSO "ALLOC DD("ddz") DS('"ztempf"') OLD REU"
    If zcmd='EDIT' | Substr(zcmd,1,2)='/E' | ejcl='Y' Then Do
        "LMINIT DATAID(DATAID) DDNAME("ddz")"
        umodel="NOTEZED";"VPUT UMODEL SHARED"
        "EDIT DATAID(&DATAID) MACRO(UMODEL)"
        "LMFREE DATAID(&DATAID)"
    End
    Address TSO "EXECIO * DISKR" ddz "(FINIS"
    Address TSO "FREE DD("ddz")"
/*  #m1=Msg('Off');Address TSO "DELETE '"ztempf"'";x=Msg(#m1)         */
    Queue "ZZ"
    x = MSG('On')
    x = Outtrap('line.',5,'NOCONCAT')
    Address TSO "SUBMIT * END(ZZ)"
    x = Outtrap('Off')
    zedlmsg = line.2
exi1:
    "SETMSG MSG(ISRZ001)"
    Exit

build_screen:
    lineh ='¢'Centre(' Clone and copy a dataset ',78,'-')
    linec =
    line1 =
    line2 ="{ Enter parameters below for dataset¢'"dsn"'{:"
    line3 ='{    New DSNAME}===>~'Substr(ndsn,1,46)'}<='
    line4 ="{    Replace   }===>~"Substr(smsm,1,1)'}<=        ¢Y{or¢N'
    line5 ="{    SMS mang'd}===>~"Substr(smsm,1,1)'}<=        ¢Y{or¢N'
    line6 ='{    Volume    }===>~'Substr(ovol,1,6)'}<=  ',
           "{Required if SMS=¢N"
    line8 ="{ Edit JCL      ===>~"Substr(ejcl,1,1)'}<=        ¢Y{or¢N'
    line15='{ Use¢END{PF key to abandon processing.'
    dyncol=23
    Return 0

parse_screen:
    zcmd =Strip(Substr(linec,12,10))
    y    =Strip(Substr(line3,22,46))
    repl =Strip(Substr(line4,22,1))
    smsm =Strip(Substr(line5,22,1))
    ovol =Strip(Substr(line6,22,6))
    ejcl =Strip(Substr(line8,22,1))
    If Substr(y,1,1)="'" Then;odsn=STrip(y,,"'");Else odsn=sysuid'.'y
    ndsn="'"odsn"'"
    Return 0
