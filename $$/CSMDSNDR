/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* CSMDSNDR:                                                          */
/*                                                                    */
/* HR processing IF DFDSS recovery is used in-lieu of HSM.            */
/*                                                                    */
/* Customised for:                                                    */
/*   Sysid     Description                                            */
/*   -------   ----------------------------------                     */
/*   SRPMVS  - SRC (Standard Register)                                */
/*                                                                    */
/*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-*/
csmdsnDR_ver='1.01'
    Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    Arg parm#;If parm#/= '' Then Signal byp1
    "ISREDIT MACRO (parm#)";If rc=0 Then ima_macro=1

byp1:"CONTROL ERRORS CANCEL"

ini1:
    sysid=Strip(storage(d2x(x2d(c2x(storage(10,4)))+x2d(154)),8))
    Parse SOURCE . . xcmd .
    "VGET (vars) SHARED";"VGET ("vars") SHARED"
    If \Datatype(dfdssgdg,'W') Then dfdssgdg=0
    frdsn=Strip(frdsn,,"'");stodsn=Strip(stodsn,,"'")
    Address TSO
    x=Build_Jobcard()
    Signal on Syntax Name l_def_sysid
    Interpret "x=l_"sysid"()"
    Signal Off Syntax
    Address
    x=View_JCL()
    Return 0

Build_Jobcard:
    sep=Substr('//*',1,70,'-')'*'
    prog='DFDSS-Recover'
    Signal on Syntax Name error_off_alkjcpg
    x=$Alkjcpg(xcmd);If x/='' Then prog=x
error_off_alkjcpg:Signal off Syntax
    userid=Userid();acct=$alkacct()
    v1='def_ex def_hd def_ac def_bb def_vm'
    Interpret "Parse Value $alkjc(v1) With" v1
    mcl=def_bb;xcl=def_ex;hcl=def_hd

    "NEWSTACK"
    Queue "//"userid"R JOB "acct",'"prog"',"
    Queue "//        MSGLEVEL=1,LINES=9999,"
    Queue '//        MSGCLASS='mcl',CLASS='xcl', TIME=1440,'
    Queue '//        NOTIFY=&SYSUID TYPRUN=SCAN'
    Queue '//*'
    Queue sep
    Queue '//* Job has to be manually submitted'
    Queue sep
    Queue '//*'
    Return 0

l_SRPMVS:
    intape='BACKUP.DASD.ONSITE.VL'frvol'('dfdssgdg')'
    Signal Build_JCL
l_def_sysid:
    intape='????????'
Build_JCL:
    Signal off Syntax
    If volser='' Then volser=frvol
    Queue '//RESTORE  EXEC PGM=ADRDSSU,REGION=3M'
    Queue '//SYSPRINT  DD  SYSOUT=*'
    Queue '//INTAPE1   DD  DISP=SHR,DSN='intape
    Queue '//DD1       DD  VOL=SER='volser',UNIT=SYSALLDA,DISP=SHR'
    Queue '//SYSIN     DD  *'
    Queue ' RESTORE DATASET(INCLUDE(' frdsn '-'
    Queue '                         ))-'
    Queue '      CATALOG   -'
    If frdsn/=stodsn Then Do
        Queue '      RENAMEU((' frdsn', -'
        Queue '     ' stodsn ')) -'
    End
    Queue '      CANCELERROR   OUTDDNAME(DD1)   -'
    Queue '      INDDNAME(INTAPE1) REPLACE'
    Queue '/*';Queue '//';Queue ''
    Return 0

View_JCL:
    ztempf=$ztempf()
    Address TSO "ALLOC DD(ZTEMPF) DS('"ztempf"') SHR REU"
    Address TSO "EXECIO * DISKW ZTEMPF (FINIS"
    Address TSO 'DELSTACK'
    "LMINIT DATAID(DATAID) DDNAME(ZTEMPF)"
    zedlmsg='Job has to be manually submitted to recover/restore',
            'dataset.'
    "SETMSG MSG(ISRZ002)"
    "EDIT DATAID(&DATAID)";"LMFREE DATAID(&DATAID)"
    Address TSO "FREE DD(ZTEMPF)"
    #m1=Msg('Off');Address TSO "DELETE '"ztempf"'";x=Msg(#m1)
    Return 0
