/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* CSMDSNFX: 'FX' option under Multi-Procs that will let you recover  */
/*           members of a PDS one at a time.                          */
/*                                                                    */
/* Packaging:                                                         */
/* REXX:                                                              */
/*   CSMDSNFX - Front end driver                                      */
/*   FX       o Routine to perform fix housekeeping. Use TSO %FX ?    */
/*              for further details.                                  */
/*   QUIT     o Routine to break fast track looping.                  */
/*                                                                    */
/* Panels:                                                            */
/*   FPDSP1   o Self extracted.                                       */
/*   FPDSP0   - DSN entry and skip panel                              */
/*                                                                    */
/* Messages:                                                          */
/*   FPDS00   - Messages used by SPFPDS                               */
/*                                                                    */
/* Loadmod:                                                           */
/*   PANELINC - Panel exit to keep track of fast skips                */
/*   SPFPDS   - DSN entry and skip panel                              */
/*   SPFPDS#  o Version of SPFPDS permitting bypass of browsing       */
/*              recovered members during fast tracking.               */
/*                                                                    */
/*--------------------------------------------------------------------*/
CSMDSNFX:csmdsnfx_ver='2.01'
    Arg parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#)"
    If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    zedsmsg=;prog='SPFPDS#'
    Parse Upper VAR parm# CSDSN00 .
    If csdsn00='' Then "VGET (csdsn00) SHARED"
    "VPUT (csdsn00) PROFILE"
    dataset=csdsn00

    "VGET (ZAPPLID) ASIS"
    If zapplid/='CSM' Then Do
        Parse SOURCE . . xcmd .
        "SELECT CMD(%"xcmd parm#") NEWAPPL(CSM)"
        Return 0
    End

    hlq=Userid();pref=$alkhlq();pref=Sysvar(Syspref)
    fpds0001="'"pref".FPDS0001.CNTL'"

display:
    zcmd=
    "DISPLAY PANEL(FPDSP1)"
    If rc>8 Then Do
        zedlmsg='Severe error displaying panel "FPDSP1".'
        "ISPEXEC SETMSG MSG(ISRZ001)"
        Exit
    End
    rc1=rc
    x=Msg('Off')
    If rc1=8 Then Signal chkpf3

    xx=Listdsi(dataset)
    If sysusedpages/='' Then Do
        zedlmsg='Cannot FIX a PDSE.'
        "ISPEXEC SETMSG MSG(ISRZ001)"
        Signal display
    End

chkpf3:
    If rc1=8 Then Do
         Address TSO
         "FREE FI(FPDS0000 FPDS0001)"
         "DELETE" fpds0001
         Exit
    End

alloc:
    Address TSO
    "FREE FI(FPDS0000 FPDS0001)"
    "DELETE" fpds0001
    "ALLOC FI(FPDS0000) DA("dataset") SHR REU"
    alloccc=rc
    If alloccc/=0 Then Do
         Address ISPEXEC "DISPLAY PANEL(FPDSP1) MSG(FPDS003I)"
         rc1=rc
         Signal chkpf3
    End
    "ALLOC FI(FPDS0001) DA("fpds0001") SP(2 2) CYL",
        "RECFM(F B) LRECL(80) BLKSIZE(6160) DSORG(PS)"
    alloccc=rc
    If alloccc/=0 Then Do
         "ISPEXEC DISPLAY PANEL(FPDSP1) MSG(FPDS004I)"
         rc1=rc
         Signal chkpf3
    End
    zcmd=
    Address ISPEXEC
    "VPUT ZCMD ASIS"
    "SELECT PGM("prog")"
    rc1=rc
    If rc1=4 Then Signal display
    If rc1=8 Then Do
        "DISPLAY PANEL(FPDSP1) MSG(FPDS001I)"
        rc1=rc
        Signal chkpf3
    End
    dataset=
    "DISPLAY PANEL(FPDSP1) MSG(FPDS000I)"
    rc1=rc
    Signal chkpf3
