/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*                                                                    */
/* CSMDSNML:                                                          */
/*                                                                    */
/* Process Multi-Procs Member List (ML) processing option.            */
/*                                                                    */
/*--------------------------------------------------------------------*/
CSMDSNML:csmdsnml_ver='1.02'
    Arg parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#)"
    If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    Parse Upper VAR parm# parm1 parm2 parm3
    class=
    "VGET (MEMBER ALKVOL) SHARED"
    jdsname=Strip(parm1,,"'")
    If Index(jdsname,'(')=0 Then Do
        Parse VAR jdsname jdsname '(' member ')'
        member=Substr(Strip(member)'*',1,8)
    End
    "VGET (CSMDSNLS) PROFILE"
    If csmdsnls='' Then csmdsnls='B'        /* Browse default 'S'     */
    If alkvol='' Then volser=;Else volser='VOLUME('alkvol')'

strt1:
    Do Forever
        "ISPEXEC LMINIT DATAID(DATA1) DATASET('&JDSNAME')",
             "ENQ(EXCLU) &VOLSER"
        If rc/=0 Then Do
            zedsmsg=;"SETMSG MSG(&ZERRMSG)"
            Leave
        End
        "LMOPEN DATAID(&DATA1) OPTION(OUTPUT)"
        rc1=4
        Do Forever
            If rc1 = 0 Then Do
/*--------------------------------------------------------------------*/
/* Member list Display                                                */
/*--------------------------------------------------------------------*/
                "ISPEXEC CONTROL NONDISPL ENTER"
                Select
                  When zlcmd# = 'A'  Then zludata = '*ASMBLED'
                  When zlcmd# = 'L'  Then zludata = '*LINKED'
                  When zlcmd# = 'AL' Then zludata = '*ASM/LKD'
                  When zlcmd# = 'B'  Then zludata = '*BROWSED'
                  When zlcmd# = 'C'  Then zludata = '*COPIED'
                  When zlcmd# = 'CR' Then Do
                      If SYSDSN("'"jdsname"("zlmember")'")/='OK' Then,
                          rc1 = 4
                      Else zludata = '*CREATED'
                  End
                  When zlcmd# = 'D' Then zludata = '*DELETED'
                  When zlcmd# = 'E' Then zludata = '*EDITED'
                  When zlcmd# = 'J' Then zludata = '*J('jobnumb')'
                  When zlcmd# = 'P' Then zludata = pid
                  When zlcmd# = 'R' Then Do
                      newname = zludata
                      zludata = '*RENAMED'
                      "ISPEXEC LMMDISP DATAID(&DATA1) OPTION(ADD)",
                           "MEMBER(&NEWNAME) ZLUDATA(*&ZLMEMBER)"
                  End
                  When zlcmd# = 'X' Then zludata = xid
                  Otherwise
                End
                If rc1 = 0 Then,
                    "ISPEXEC LMMDISP DATAID(&DATA1) OPTION(&OPT)",
                         "MEMBER(&ZLMEMBER) ZLUDATA(&ZLUDATA)"
            End

/*--------------------------------------------------------------------*/
/* Member list Display                                                */
/*--------------------------------------------------------------------*/
            zcmd = ''
            "ISPEXEC LMMDISP DATAID(&DATA1) OPTION(DISPLAY)",
              "MEMBER(&MEMBER) TOP(&MEMBTOP) PANEL(CSMDSPML)",
              "CURSOR(&CURSOR) COMMANDS(ANY) FIELD(&MLFIELD) STATS(YES)"
            If rc/=0 Then Leave
            If zcmd = 'REFRESH' Then Leave
            If zcmd = '' & zllcmd = '' Then Do
                rc1 = 4
                Iterate
            End

            PARSE VAR zcmd zcmd cmdr
            If zcmd = 'TRACE' Then Do
                If cmdr = 'OFF' Then Do
                    trace = 'N'
                    Trace Off
                End
                Else Do
                    trace = 'Y'
                    Trace R
                End
                Iterate
            End

            If SUBSTR(zcmd,1,3) = ".S=" Then Do
                csmdsnls = SUBSTR(zcmd,4,LENGTH(zcmd)-3)
                If csmdsnls = '?' Then ,
                     "ISPEXEC VGET CSMDSNLS PROFILE"
                Else "ISPEXEC VPUT CSMDSNLS PROFILE"
                zedlmsg = "'S' option default set to '"csmdsnls"'",
                        "under list members."
                rc1 = 4
                Iterate
            End
            opt = "PUT"
            If zcmd/="" Then Do
                PARSE VAR zcmd cmdsel zlmember zludata
                zllcmd = cmdsel
            End
            Else zlmember = STRIP(zlmember)
            membtop = zlmember
            valsel = INDEX('ABCDEJMPRSX=',zllcmd)
            If valsel = 0 Then Do
                zedlmsg = "Invalid member list selection."
                Iterate
            End

            If zllcmd = "=" Then Do
                If savelc/="" Then Do
                    zllcmd  = savelc
                    zlcmd#  = zllcmd
                    zludata = saveld
                End
                Else Do
                    zedlmsg = "No previous command to repeat."
                    Iterate
                End
            End
            Else Do
                savelc  = zllcmd
                saveld  = zludata
            End

            If zllcmd = 'S' Then zllcmd = csmdsnls
            zlcmd# = zllcmd
            If zlcmd# = 'E' & ,
                  SYSDSN("'"jdsname"("zlmember")'")/='OK' Then Do
                zlcmd# = 'CR'
                opt    = 'ADD'
            End

            sdsname = jdsname'('zlmember')'
            Select
              When zlcmd# = 'A' | zlcmd# = 'AL' | zlcmd# = 'L' Then Do
                  pdsname = sdsname
                  parm = zlcmd#
                  "ISPEXEC VPUT (PDSNAME MEMBER PARM EDIT) SHARED"
                  Address TSO "%ASMCL"
                  Iterate
              End

              When zlcmd# = 'B' Then Do
                  action  = 'BROWSE'
                  Call edbrow
                  Iterate
              End

              When zlcmd# = 'E' Then Do
                  action  = 'EDIT'
                  Call edbrow
                  Iterate
              End

              When zlcmd# = 'CR' Then Do
                  action  = 'EDIT'
                  Call edbrow
                  Iterate
              End

              When zlcmd# = 'C' Then Do
                  If zludata = '' Then Do
                      rc1 = 4
                      zedlmsg = "Destination member name required for",
                              "copy command. Please re-enter."
                      "ISPEXEC SETMSG MSG(ISRZ001)"
                  End
                  Else Do
                      Address TSO "%COPYMEMB" sdsname,
                                              jdsname"("zludata")"
                      rc1 = rc
                  End
                  Iterate
              End

              When zlcmd# = 'D' Then Do
                  If SYSDSN("'"sdsname"'")/='OK' Then Do
                      zedlmsg = "Memeber '"zlmember"' no longer found."
                      "ISPEXEC SETMSG MSG(ISRZ001)"
                  End
                  Else Do
                      member=zlmember;qdsname="'"jdsname"'"
                      l#=$dat@csr('/L#');zwinttl='CSMDSNML Process'
                      #popr=l#-2;#popc=17;zcmd=
                      zedsmsg=;zedlmsg=;"SETMSG MSG(ISRZ001)"
                      "ADDPOP ROW(&#POPR) COLUMN(&#POPC)"
                      "DISPLAY PANEL($MEMBDEL)";rc1=rc;"REMPOP"
                      If rc1/=0 Then ,
                          zedlmsg="Deletion of member '"zlmember" abandoned."
                      Else Do
                          "ISPEXEC LMMDEL DATAID(&DATA1)",
                              "MEMBER(&ZLMEMBER)"
                          rc1=rc
                          If rc/=0 Then Do
                              zedlmsg = "Delete failed."
                              membtop  = zlmember
                          End
                          Else zedlmsg = ''
                      End
                  End
                  "ISPEXEC SETMSG MSG(ISRZ001)"
                  Iterate
              End

              When zlcmd# = 'J' Then Do
                  If SYSDSN("'"sdsname"'")/='OK' Then Do
                      zedlmsg = "Memeber '"zlmember"' no longer found."
                      rc1 = 4
                  End
                  Else Do
                      x = MSG('On')
                      x = OUTTRAP('line',5,'NOCONCAT')
                      Address TSO "SUBMIT '"sdsname"'"
                      PARSE VAR line1 v1 v2 v3 .
                      If SUBSTR(v1,1,3) = 'IKJ' Then v2 = v3
                      lb = INDEX(v2,'(')
                      jobnumb = SUBSTR(v2,lb+4,LENGTH(v2)-(lb+4))
                      zedlmsg = line1
                      rc1 = 0
                  End
                  "ISPEXEC SETMSG MSG(ISRZ001)"
                  Iterate
              End

              When zlcmd#='R' Then Do
                  If zludata='' Then Do
                      xx=member;member=zlmember;newmemb=zludata
                      "ISPEXEC DISPLAY PANEL($MEMBREN)"
                      rc1=rc
                      member=xx;zludata=newmemb
                      If rc1/=0 Then Do
                          zedlmsg="Rename abandoned."
                          "ISPEXEC SETMSG MSG(ISRZ001)"
                          Iterate
                      End
                  End
                  Select
                      When zludata='' Then,
                          zedlmsg="Missing new member name."
                      When SUBSTR(zludata,1,1)='*' Then,
                          zedlmsg="Invalid new member name."
                      Otherwise Do
                          "ISPEXEC LMMREN DATAID(&DATA1)",
                              "MEMBER(&ZLMEMBER) NEWNAME(&ZLUDATA)"
                          rc1=rc
                          If rc1=0 Then Do
                              zedlmsg=
                              membtop=zludata
                          End
                          Else zedlmsg="Rename failed."
                      End
                  End
                  "ISPEXEC SETMSG MSG(ISRZ001)"
                  Iterate
              End

              When zlcmd# = 'P' Then Do
                  If SYSDSN("'"sdsname"'")/='OK' Then ,
                      zedlmsg = "Memeber '"zlmember"' no longer found."
                  Else Do
                      If zludata = '' Then dest = 'LOCAL'
                      Else                 dest = STRIP(zludata)
                      x = MSG('On')
                      x = OUTTRAP('line',5,'NOCONCAT')
                      Address TSO "PRINTOFF '"sdsname"' DEST("dest")",
                          class
                      rc1 = rc
                      x = MSG('Off')
                      PARSE VAR line1 v1 v2 v3 v4
                      If v1 = 'PROCESSING' & v2 = 'STARTED' &,
                          v3 = 'FOR' Then pid = '*PRINTED'
                      Else                pid = '*FAILED'
                      zedlmsg = line1
                  End
                  "ISPEXEC SETMSG MSG(ISRZ001)"
                  Iterate
              End

              When zlcmd# = 'X' Then Do
                  Address TSO "EXEC '"sdsname"'"
                  rc1 = rc
                  If rc1 = 0 Then xid = '*EXECUTD'
                  Else            xid = '*FAILED'
                  rc1 = 0
                  Iterate
              End

              Otherwise Nop

            End
        End
        Call eoj1
        If zcmd = "REFRESH" Then Iterate
        Else Leave
    End

/*--------------------------------------------------------------------*/
/* Close/housekeeping                                                 */
/*--------------------------------------------------------------------*/
eoj1:
    "ISPEXEC LMMDISP DATAID(&DATA1) OPTION(FREE)"
    "ISPEXEC LMCLOSE DATAID(&DATA1)"
    "ISPEXEC LMFREE DATAID(&DATA1)"
    Return 0

/*--------------------------------------------------------------------*/
/* Edit/Browse routine                                                */
/*--------------------------------------------------------------------*/
edbrow:
    Call ON ERROR NAME errbr1

    rc1 = 0
    edtvol = alkvol
    "ISPEXEC &ACTION DATASET('&SDSNAME') &VOLSER"
    Call OFF ERROR
    Return 0

errbr1:
    Call OFF ERROR
    If rc1 > 4 Then "ISPEXEC SETMSG MSG(&ZERRMSG)"
    Else rc1 = 0
    Return 0

