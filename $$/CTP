/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*  CTP:                                                              */
/*  CICS Trace Print.                                                 */
/*--------------------------------------------------------------------*/
CTP_ver='1.02'
    Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    Arg parm#;If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS";If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    Parse SOURCE . . xcmd .
    Parse VAR parm# auxdsn edit
    If auxdsn='' Then Do
        zedlmsg='Missing dataset name to recall.';zedsmsg=
        "SETMSG MSG(ISRZ001)";Return 8
    End

    edit='Yes'
    auxdsn=Strip(auxdsn,,"'")
Report_JCL:
/*--------------------------------------------------------------------*/
    #m1=Msg('On');x=Outtrap('line.',500,'NOCONCAT')
    Address TSO "STATUS"
    x=Outtrap('Off');x=Msg(#m1);list=
    Do i=1 To line.0
        Parse VAR line.i . id act .
        If Wordpos(act,'EXECUTING WAITING')>0 Then Do
            Interpret "Parse VAR id '"Userid()"' suf '('"
            list=list suf
        End
    End
    nat='ABCDEFGHIJKLMNOPQRSTUVWXYZ01234567890@#$';id='A'
    Do i=1 to Length(nat)
        suf=Substr(nat,i,1)
        If Wordpos(suf,list)>0 Then Iterate
        id=suf;Leave
    End
/*--------------------------------------------------------------------*/
    Address TSO 'NEWSTACK'
    Signal on Syntax Name error_on_JC
    pgmn=xcmd;pgmn=$alkjcpg(xcmd)
    x=JC(id '/Q /9');Signal error_off_JC
error_on_JC:
    Queue "//"userid()||id "JOB (2567,ALK),'"pgmn"',"
    Queue '//             CLASS=A,COND=(4,LT),'
    Queue '//             MSGCLASS=X,TIME=1440,REGION=0M,'
    Queue '//             NOTIFY=&SYSUID,LINES=9999'
/*  Queue '//             NOTIFY=,LINES=9999'                         */
error_off_JC:Signal off Syntax
    Queue '//*'
    Queue "//*  Gen'd by: '"$alkhlq()".$$("xcmd")'"
    Queue '//*'
    Queue '//*'
    x=Build_jcl()
    If edit='' Then Do
        Queue "ZZ"
        #m1=Msg('On');x=Outtrap('line.',10,'NOCONCAT')
        Address TSO "SUBMIT * END(ZZ)"
        x=Msg(#m1)
        Do i=1 To line.0
            If Index(line.i,'SUBMITTED')>0 Then Do
                If Wordpos('JOB',line.i)=0 Then Iterate
                zedlmsg=line.i
                "SETMSG MSG(ISRZ001)"
            End
        End
        Return 0
    End
    Queue ''
    dd='HR'Time('s')
    Signal on Syntax Name error_on_$ZTEMPF1
    ztempf=$ztempf();Signal error_off_$ZTEMPF1
error_on_$ZTEMPF1:
    Address ISPEXEC "VGET ZTEMPF"
error_off_$ZTEMPF1:Signal off Syntax
    zedlmsg='You will have to manually submit this JCL or forever',
            'loose it to the bit bucket.'
    Address ISPEXEC "SETMSG MSG(ISRZ001)"
    Address TSO "ALLOC F("dd") DSN('"ztempf"') SHR REU"
    Address TSO "EXECIO * DISKW "dd" (FINIS"
    Address ISPEXEC "LMINIT DATAID(DATAID) DDNAME("dd")"
    Address ISPEXEC "EDIT DATAID(&DATAID)"
    Address ISPEXEC "LMFREE DATAID(&DATAID)"
    Signal on Syntax Name error_on_$ZTEMPF2
    Address TSO "FREE F("dd")";xx=$ztempf(ztempf,'/D')
error_on_$ZTEMPF2:Signal off Syntax
    Return 0

Build_jcl:
v1='sdfhlink sdfhload'
Interpret "Parse Value $alkcics(v1) With" v1
Parse VAR auxdsn hlq '.' .
If Wordpos(hlq,'DNMCICS DNDCICS DNTCICS DNQCICS DNPCICS')>0 Then Do
    ch=Substr(hlq,3,1)
    If Index(sdfhload,'%')>0 Then sdfhload=$Cstring(sdfhload,'%',ch,'*')
    If Index(sdfhlink,'%')>0 Then sdfhlink=$Cstring(sdfhlink,'%',ch,'*')
End
Queue '//*********************************************************************'
Queue '//AUXTRCE  EXEC PGM=DFHTU650'
Queue '//STEPLIB  DD  DISP=SHR,DSN='sdfhload
Queue '//         DD  DISP=SHR,DSN='sdfhlink
Queue '//*'
Queue '//* Same DD for trace "A" and "B".'
Queue '//*'
Queue '//DFHAUXT  DD  DISP=SHR,DSN='auxdsn
Queue '//*'
Queue '//DFHAXPRT DD  SYSOUT=*'
Queue '//DFHAXPRM DD  *'
Queue '  ABBREV'
Queue '/*'
Queue '//'
Queue ' '
Queue '*------------------------------------------------------------------*'
Queue '* Customise from these:                                             '
Queue '*------------------------------------------------------------------*'
Queue '  PAGESIZE=(NNN)'
Queue '  ABBREV | FULL'
Queue '  ALL'
Queue '  KE_NUM=(XXXX,XXXX,...)'
Queue '  TASKID=(XXXX,XXXX,...) | (XXXX-XXXX)'
Queue '  TERMID=(XXXX,XXXX,...)'
Queue '  TRANID=(XXXX,XXXX,...)'
Queue '  TIMERG=(HHMMSS-HHMMSS,HHMMSS-HHMMSS,...)'
Queue '  TYPETR=(DDXXXX,DDXXXX,...) | (DDXXXX-XXXX,DDXXXX-XXXX,...)'
Queue '*------------------------------------------------------------------*'
Return 0
