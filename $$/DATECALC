/*--------------------------ooo00 REXX 00ooo--------------------------*/
n_st=mark(1)                                /* Mark Start

    DATECALC:

    This macro is used to generate a template holiday base in member
    "DATEHOL" that is used by the "DATE" command.
    (Refer to "DATEHOL" for further datils.)

    It uses the templates at the bottom (Aust & US) to generate
    floating dates (eg. father's day).
    The template used is in the format,
      * Queen's Birthday (2nd. Mon Jun -)
      * D                (#    d   m   h)
    where:-
      * - Comment identifier.
      D - Description of the holiday
      ( - Seperator. Note the LAST "(" is used.
      # - Occurence. 2 represents 2nd. occurence of that day in the
          month.
          If this is not numeric (it would be the last day of week in
          the month) thereby the 5th. occurance will be tested for
          and if is the next month the 4th. occurance will be used.
      d - Day of week.
      m - Month.
      h - Hi-lite byte used for colourisation.
      ) - Seperator. Note the LAST ")" is used.

    Parameters:
    1. Year to generate holidays for (default is current year).
       Format is yyyy.

    The following members are required to package this function.
    Those marked with an 'o' seperator are optional and enhance the
    available fetures:
    REXX:
      $DATEMAN - Date manipulation routine to process dates.
      DATECALC - This driver.
      DATEHOL  - Holidays member to modify.
      DATE     o Used by the date command (parameter "HOL").

                              *****
 Mark End */;n_en=mark(-1)

datecalc:Ver='1.02'
    Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    Arg parm#;If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS";If rc=0 Then ima_macro=1

byp1:
    w1=Word(parm#,1);If Wordpos(w1,"? ??")=0 Then Signal No_help_#1
    If w1="??" Then Signal Help_me;"ISREDIT (m)=MEMBER";rcm=rc
    If rcm=0 Then Signal Help_me;Signal On Syntax Name Help_me
    Parse Source . . xcmd .;x=$BR(xcmd "??");Return
    Mark:If Datatype(Arg(1))/="NUM" Then Arg(1)=1;Return sigl+Arg(1)
    Help_me:Signal Off Syntax;#tr=Trace("o")
    Do i=n_st To n_en;nl=Sourceline(i)
    If rcm=0 Then "ISREDIT LINE_BEFORE 1=NOTELINE (nl)"
    Else Say Substr(nl,1,79);End;Return 0

No_help_#1:
    Address ISREDIT;"(status)=USER_STATE";"RESET"
    "F ' (Aust)' LAST"
    If rc/=0 Then Do
        zedlmsg="Eyecatcher ' (Aust) ' not found."
        Address ISPEXEC "SETMSG MSG(ISRZ001)"
        Exit
    End
    "(zfrange)=LINENUM .ZCSR";"ISREDIT (zlrange)=LINENUM .ZLAST"
    mths='JAN FEB MAR APR MAY JUN JUL AUG SEP OCT NOV DEC'
    If Datatype(parm#,'W') Then parm#='01/01/'parm#;Else parm#=
    Parse Value $Dateman('VER',parm#) With . '/' . '/' yy
    x=out_title('*-------- 'yy' -----------------*')
    "LABEL" zfrange"=.MARKF";"LABEL" zlrange"=.MARKT"
    "F 'Day_in_lieu' 1 .MARKF .MARKT FIRST"
    If rc=0 Then Do
        "(line)=LINE .ZCSR"
        Parse VAR line "'" day_in_lieu "'"
    End
    Else day_in_lieu=
    hol_wend=
    Do i=zfrange To zlrange
        "(line)=LINE" i
        Parse VAR line w1 wr 1 ch2 +2 .
        If Datatype(ch2,'w') | ch2='xx' Then Do
            If ch2='xx' Then Do
                x=out_title(line);Iterate
            End
            hd=w1'/'yy
            If Wordpos($Dateman('E2N',hd),'6 7')=0 Then Do
                x=out_title(line);Iterate
            End
            If Wordpos(w1,day_in_lieu)=0 Then Do
                x=out_title(Strip(line)'_NoSub');Iterate
            End
            hol_wend=hol_wend hd
            Do While Wordpos(hd,hol_wend)>0
                hd=$Dateman('E-N',hd,,+1)
                If Wordpos($Dateman('E2N',hd),'6 7')>0 Then,
                    hol_wend=hol_wend hd
            End
            hol_wend=hol_wend hd;Parse VAR hd dd '/' mm '/' .
            x=out_title(line)
            hd=dd'/'mm Strip(wr)_"Sub"
            x=out_title(hd)
            Iterate
        End
        If ch2/='# ' Then Iterate
        xx=Lastpos('(',line)
        Interpret "Parse VAR line . title +"xx-1" qual"
        Parse Upper Var qual 2 wn +1 . dow mth . ')'
        Parse Var qual 2 . +1 . . . hi ')'
        If hi='' Then hi=' '
        Parse VAR dow dow +2
        mn=Wordpos(Substr(mth,1,3),mths)
        If mn=0 Then Do;x=out_title('??/??' hi Strip(line));Iterate;End
        If \datatype(wn,'W') Then Do
            wn=5
            dater=$Dateman('NED','01/'mn'/'yy,,dow,wn,'M')
            Parse VAR dater dd '/' mm '/'
            If mn/=mm Then wn=4
        End
        dater=$Dateman('NED','01/'mn'/'yy,,dow,wn,'M')
        Parse VAR dater dd '/' mm '/'
        x=out_title(dd'/'mm hi Strip(title))
    End
    "USER_STATE=(status)"

    Exit

Out_Title: Parse Arg nl
    "LINE_BEFORE .ZFIRST=NOTELINE (nl)"
    Return 0
Mark:Arg diff;If \Datatype(diff,'W') Then diff=1;Return sigl+diff
