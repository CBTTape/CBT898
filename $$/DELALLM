/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*  DELALLM: DELete ALL Members.                                      */
/*                                                                    */
/*  Parameters (positional):                                          */
/*    1. Quoted, qualified dataset (else SYSPREF is prefixed).        */
/*       Pass value of '/T' so routine can be executed to test for    */
/*       availability without requiring/processing a dataset.         */
/*       If you get a syntax code then it is not available.           */
/*    2. Volume for uncatalogued dataset.                             */
/*    3. Don't audit (say statements) indicator (any non-blank).      */
/*    4. Compress indicator (any non-blank)                           */
/*  For space delimiter parameter format use '.' place holders for    */
/*  missing parameters if subsequent parameters are being used.       */
/*  Examples of use:                                                  */
/*  1. If Delallm('/T')='OK' Then Signal process_delete_members       */
/*     Else Say 'DELALLM unavailable.'                                */
/*  2. Signal on Syntax Name error_on_DELALLM                         */
/*     x=Delallm(olddsn,,'SUPRESS_MSG','COMPRESS_AFTER')              */
/*     ......                                                         */
/*     error_on_DELALLM:Signal off Syntax                             */
/*     Say 'DELALLM unavailable.'                                     */
/*--------------------------------------------------------------------*/
delallm_ver='1.04'
    Parse Upper Arg parm#;If parm#='/T' Then Return 'OK'
    Parse Upper Arg dsname,volume,audit,comp
    Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS"
    If rc=0 Then ima_macro=1
byp1:
/*  "CONTROL ERRORS CANCEL"                                           */
ini1:
    zedsmsg=
    If volume audit comp='' Then,
        Parse Upper VAR parm# dsname ',' volume ',' audit ',' comp
    If volume audit comp='' Then,
        Parse Upper VAR parm# dsname volume audit comp
    If Substr(dsname,1,1)/="'" Then dsname="'"Sysvar(Syspref)"."dsname"'"
    If volume='.' Then volume=;If audit='.' Then audit=
    If volume/='' Then Do
        volume='VOLUME('volume')'
        x=Listdsi("'SYS1.LINKLIB'" volume)
        If sysreason=22 Then Do
           zedlmsg=volume "not mounted or defined to the system."
           "SETMSG MSG(ISRZ001)";Exit
        End
    End
    Address ISPEXEC
    "LMINIT DATAID(DATA1) DATASET("dsname")" volume "ENQ(EXCLU)"
    "LMOPEN DATAID(&DATA1) OPTION(OUTPUT)"
    If rc=0 Then Do
        member=
        Do Forever
            "LMMLIST DATAID(&DATA1) OPTION(LIST) MEMBER(MEMBER) STATS(NO)"
            If rc/=0 Then Leave
            "LMMDEL DATAID(&DATA1) MEMBER(&MEMBER) NOENQ"
            If audit='' Then Do
                If rc/=0 Then Say Left(member,8) '** NOT deleted.'
                Else          Say Left(member,8) 'deleted.'
            End
        End
    End
    "LMFREE DATAID(&DATA1)"
    If comp='' Then Exit 0

    Address TSO;#m=Msg('On')
    "ALLOC F(SYSPRINT) DUMMY OLD REU"
    "ALLOC F("dd1") DA("dsname") SHR REU"
    If rc/=0 Then Do
        zedlmsg="Allocation of dataset '"dsname"' for compression failed."
        Address ISPEXEC "SETMSG MSG(ISRZ001)"
        Exit rc
    End
    Address ISPEXEC "VGET ZTEMPF"
    "ALLOC F(SYSIN) DA('"ztempf"') SHR REU"
    'NEWSTACK';Queue '  COPY INDD='dd1',OUTDD='dd1;Queue ''
    "EXECIO * DISKW SYSIN (FINIS"
    "CALL *(IEBCOPY)"
    "FREE F("dd1" SYSIN)";x=Msg(#m)
    Exit 0
