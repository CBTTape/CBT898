/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* DPCH#1: DuPont JCL change #1.                                      */
/*                                                                    */
/* Function:                                                          */
/*   1. Change comment at the top of the JCL. Change supplied between */
/*      the mark "start" and "end" comments.                          */
/*      A '//*' will be placed at the start of each line and on the   */*/
/*      first line a USA date and your initials will be inserted.     */
/*   2. Permits up to 3 string changes. These are an ALL change so    */
/*      ensure you do not corrupt data.                               */
/*                                                                    */
/* Notelines will be inserted above where the change will be UNLESS   */
/* a parameter of '/A' Is supplied.                                   */
/*                                                                    */
/* If you are making mass changes and are confident with the macro,   */
/* pass a parameter of "/X" to exit (and save) edit session after     */
/* processing.  You must also use the "/A" parameter to make exit     */
/* on completion meaningful. Without the "/A" parameter it will be    */
/* ignored.                                                           */
/*                                                                    */
/* This routine requires access to routines $WHOAMI and $DATEMAN.     */
/* $DATEMAN is used in preferance to DATE('U') in case you need a four*/
/* digit year, eg. date=$DATEMAN('VUR',,4)                            */
/*                                                                    */
/*--------------------------------------------------------------------*/
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS"
    If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */
    If Wordpos('/A',Translate(parm#))>0 Then apply=1;Else apply=0
    xoend=0;If apply Then If Wordpos('/X',Translate(parm#))>0 Then xoend=1

ini1:
    zedsmsg=
/*--------------------------------------------------------------------*/
/* Enter up to 3 changes                                              */
/*--------------------------------------------------------------------*/
    fchg1='DSN=SYPCICS.C130TEST.SDFHLOAD'
    tchg1='DSN=SYPCICS.C130PROD.SDFHLOAD'
    fchg2='DSN=SYPCICSA.C130TEST.SDFHAUTH'
    tchg2='DSN=SYPCICSA.C130PROD.SDFHAUTH'
    fchg3=
    tchg3=

    n_st=mark(5)                            /* Mark Start             */
/*--------------------------------------------------------------------*/
/* Enter text to be included as comments between /* .. */.            */
/*--------------------------------------------------Col 71------------*/
/*                                                  |
CHANGED SYPCICS.C130TEST.SDFHLOAD IN THE DFHRPL
CONCAT TO SYPCICS.C130PROD.SDFHLOAD AND
SYPCICSA.C130TEST.SDFHAUTH IN THE STEPLIB CONCAT
TO SYPCICSA.C130PROD.SDFHAUTH BECAUSE OF NEW
MAINTENANCE (FIX STORAGE VIOLATION)
*/
    n_en=mark(-2)                           /* Mark End               */

    Address ISREDIT;"(status)=USER_STATE";"(dsn)=DATASET";"RESET"
    "F '//*' FIRST";"(lin#)=LINENUM .ZCSR"
    "F '/' FIRST 4 9";If rc=0 Then "(lin#)=LINENUM .ZCSR"

    Signal on Syntax Name error_off_00
    name=$Whoami()
    Init=Substr(Word(name,1),1,1) || Substr(Word(name,2),1,1)' '
    date=$DATEMAN('VUR',,2)
    pref='  'date init
    line=Substr('//*'pref Sourceline(n_st),1,71)

    If \apply Then "LINE_BEFORE .ZCSR=NOTELINE (line)"
    Else "LINE_BEFORE" lin#"=(line)";lin#=lin#+1
    pref=Substr(' ',1,Length(pref))
    Do i=n_st+1 To n_en
        line=Substr('//*'pref Sourceline(i),1,71)
        If \apply Then "LINE_BEFORE .ZCSR=NOTELINE (line)"
        Else "LINE_BEFORE" lin#"=(line)";lin#=lin#+1
    End

    "LABEL" lin#"=.MARKF"
    x=Change_it(fchg1,tchg1)
    If fchg2/='' Then x=Change_it(fchg2,tchg2)
    If fchg3/='' Then x=Change_it(fchg3,tchg3)
    If xoend Then Do;"END";Exit;End
    "USER_STATE=(status)"
    If \apply Then Do
        zedlmsg='Changes to be applied have been inserted as NOTELINES.',
              'Pass parameter of "/A" to actually apply changes.'
        Address ISPEXEC "SETMSG MSG(ISRZ001)"
    End

    Exit

Mark:If Datatype(Arg(1))/='NUM' Then Arg(1)=1;Return sigl+Arg(1)
Change_it:Arg fchg0,tchg0
    If apply Then Do
        "C '"fchg0"' '"tchg0"' .MARKF .ZLAST ALL"
        Return 0
    End
    "CURSOR=1 0"
    Do Forever
        "F '"fchg0"'";If rc/=0 Then Return 0
        "(line)=LINE .ZCSR"
        line=$cstring(line,fchg0,tchg0,,'*')
        "LINE_BEFORE .ZCSR=NOTELINE (line)"
    End

error_off_00:
    Signal off Syntax
    zedsmsg=
    zedlmsg='Routine $WHOAMI or $DATEMAN cannot be found.',
            'Cannot identify user.'
    Address ISPEXEC "SETMSG MSG(ISRZ001)"
    Return 8
