/*            REXX                                                    */
/**********************************************************************/
/*    TRACE r                                                         */
/*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-*/
/*                                                                    */
/* Component of the DS suite, to do a full pack backup.               */
/*                                                                    */
/*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-*/
    PARSE UPPER ARG parm1 parm2 parm3
    if parm1 ¬= "" THEN SIGNAL byp1
    ADDRESS ISPEXEC
    "ISPEXEC CONTROL ERRORS RETURN"
    "ISPEXEC ISREDIT MACRO (parm1,parm2,parm3)"
    IF rc > 0 THEN SIGNAL byp1
    PARSE UPPER VAR parm1 parm1
    PARSE UPPER VAR parm2 parm2
    PARSE UPPER VAR parm3 parm3

byp1:
    cactn = "RETURN"
    cactn = "CANCEL"
    "ISPEXEC CONTROL ERRORS" cactn

ini1:
    "ISPEXEC VGET (VOL DSBACKUP) SHARED"
    If dsbackup = 'Y' Then Signal macro

    macro = 'DSBACKUP'
    ADDRESS TSO
    "NEWSTACK"
    Queue,
       "//"userid()"BU  JOB "acct","vol","||,
       "CLASS=A,MSGCLASS=X,"
Queue,
   "// TYPRUN=SCAN,          <===== Remove after test"
    Queue,
       "//          NOTIFY="USERID()",REGION=2048K"
    Queue,
       "/*JOBPARM  LINES=999,TIME=1440"
    Queue,
       SUBSTR("//*",1,71,'*')
    Queue,
       SUBSTR("//*",1,70,' ')"*"
    Queue,
       SUBSTR("//*    DFDSS backup of "vol".",1,70)"*"
    Queue,
       SUBSTR("//*",1,70,' ')"*"
    Queue,
       SUBSTR("//*",1,71,'*')
    Queue,
       "//COPY      EXEC PGM=ADRDSSU,REGION=7000K"
    Queue,
       "//SYSPRINT  DD   SYSOUT=*"
    Queue,
       "//IN1       DD   DISP=SHR,UNIT=SYSALLDA,VOL=SER="vol
    Queue,
       "//OUT1      DD   DSN=ZTS.#BACKUP."vol".D"||,
             SUBSTR(Date('e'),7,2)||,
             SUBSTR(Date('e'),4,2)||,
             SUBSTR(Date('e'),1,2)","
    Queue,
       "//          DISP=(NEW,CATLG,DELETE),"
    Queue,
       "//          UNIT=CART,LABEL=(1,SL,EXPDT=99000),VOL=(,,,10),"
    Queue,
       "//          DCB=(P.MODEL)"
    Queue,
       "//SYSIN     DD   *"
    Queue,
       "  DUMP  INDDNAME(IN1)   -"
    Queue,
       "        OUTDDNAME(OUT1) -"
    Queue,
       "        FULL            -"
    Queue,
       "        TOLERATE(ENQFAILURE) -"
    Queue,
       "        OPTIMIZE(3)"
    Queue,
       "/*"
    Queue ""                            /* Dummy last             */
    x = MSG(Off)
    "ISPEXEC FTOPEN TEMP"
    "ISPEXEC FTCLOSE"
    "ISPEXEC VGET ZTEMPF"
    Address TSO "ALLOC DD(ZTEMPF) DS('"ztempf"') MOD REU"

    rc1 = rc
    If rc1 ¬= 0 Then Do
        gc009   = "Allocation of 'OUTFILE' failed, rc="rc1"."
        Address TSO "FREE F(INFILE)"
        Signal ext1
    End

    Address TSO "EXECIO * DISKW ZTEMPF (FINIS"
    Address TSO "FREE DD(ZTEMPF)"
    Address TSO "DELSTACK"

    Address TSO "ALLOC DD(ZTEMPF) DS('"ztempf"') old reu"
    "ISPEXEC LMINIT DATAID(DATAID) DDNAME(ZTEMPF)"

    dsbackup = 'Y'
    "ISPEXEC VPUT (VOL DSBACKUP) SHARED"
    "ISPEXEC EDIT DATAID(&DATAID) MACRO(&MACRO)"
    "ISPEXEC LMFREE DATAID(&DATAID)"
    Address TSO "FREE DD(ZTEMPF)"
    tempsub = ztempf
    "ISPEXEC VPUT TEMPSUB SHARED"
    Address TSO "%TEMPSUB"
    'ISPEXEC VGET MESSAGE SHARED'
    gc009 = message
    'ISPEXEC SETMSG MSG(gc009)'
    Exit

macro:
    Address ISPEXEC
    dsbackup = ''
    "ISPEXEC VPUT DSBACKUP SHARED"
    "ISPEXEC VGET VOL SHARED"
    "ISREDIT CAPS OFF"
    nline = SUBSTR('*',1,70,'-')"*"
    'ISREDIT LINE_BEFORE 1 = NOTELINE (nline)'
    nline = SUBSTR('*',1,70,' ')"*"
    'ISREDIT LINE_BEFORE 1 = NOTELINE (NLINE)'
    nline = "* JCL to backup pack" vol". Change the output dataset",
            "name if you    *"
    'ISREDIT LINE_BEFORE 1 = NOTELINE (nline)'
    nline = SUBSTR('* are not happy with it.',1,70,' ')"*"
    'ISREDIT LINE_BEFORE 1 = NOTELINE (NLINE)'
    nline = SUBSTR('*',1,70,' ')"*"
    'ISREDIT LINE_BEFORE 1 = NOTELINE (NLINE)'
    nline = SUBSTR('* Use END PF Key to submit job',1,70,' ')"*"
    'ISREDIT LINE_BEFORE 1 = NOTELINE (NLINE)'
    nline = SUBSTR('*         - OR -',1,70,' ')"*"
    'ISREDIT LINE_BEFORE 1 = NOTELINE (nline)'
    nline = "* delete all the lines or enter DONTSUB to abandon",
            "submission.        *"
    'ISREDIT LINE_BEFORE 1 = NOTELINE (nline)'
    nline = SUBSTR('*',1,70,' ')"*"
    'ISREDIT LINE_BEFORE 1 = NOTELINE (nline)'
    nline = SUBSTR('*',1,70,'-')"*"
    'ISREDIT LINE_BEFORE 1 = NOTELINE (nline)'
    "ISREDIT CURSOR = 13 22"
    "ISREDIT CAPS ON"
    Address TSO "DELSTACK"
    Return 0
