/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*                                                                    */
/* DSECTBP: DSECT Build Pointers.                                     */
/*                                                                    */
/* Routine to build DSECT pointer to full version of IBM macros in    */
/* either MACLIB or MODGEN that can be used by "SHOWSTOR" for         */
/* 'mapping' options.                                                 */
/*                                                                    */
/* Designed to be called by LINEMAC.                                  */
/*                                                                    */
/*--------------------------------------------------------------------*/
dsectbp_ver='1.01'
    Address ISPEXEC "CONTROL ERRORS RETURN"
    Address ISREDIT
    "MACRO (parm#) NOPROCESS"
    maclib='SYS1.MACLIB'
    modgen='SYS1.MODGEN'

    "(line)=LINE .ZCSR";"(lin#)=CURSOR"
    Parse VAR line 2 memb +8 1 '(' macro ')'
    Parse value memb macro With memb macro .
    If Length(memb)>7 Then Do
        zedlmsg='Cannot create a pointer to member with 8 character names.',
                'Rename shorter and try again or don''t create a pointer.'
        Signal erx1
    End
    If macro='' Then Do
        zedlmsg='No IBM macro defined in brackets.';Signal erx1
    End
    dsect_source=Translate(Sh$parms('dsect_source'),' ','/')

    dsn_list=
    Do While dsect_source/=''
        Parse VAR dsect_source dd dsn dsect_source
        If Sysdsn("'"dsn"("macro")'")='OK' Then do
            mod=dsn;Leave
        End
        dsn_list=dsn_list dsn
    End
    If mod/='' Then Signal make_memb
    zedlmsg='Macro "'macro'" not found in "'Space(dsn_list)'".'
    Signal erx1

make_memb:
    linex.1='*  'Substr(dd,1,8)'  'macro
    linex.0=1
    nmemb='@'Strip(memb);"(dataset)=DATASET"
    Address TSO "ALLOC F(DSECTBP) DA('"dataset"("nmemb")') SHR REU"
    Address TSO "EXECIO * DISKW DSECTBP (STEM linex. FINIS)"
    Address TSO "FREE F(DSECTBP)"
    zedlmsg='Member "'nmemb'" already in $$$INDEX.'
    "F '¦"nmemb"' 1 FIRST";If rc/=0 Then Do
        "F '¦"memb"' 1 FIRST"
        linex=Overlay(nmemb,line,2,8)
        linex=Overlay('Pointer to full macro ('macro')',linex,12,60)
        "LINE_BEFORE .ZCSR=(linex)"
        zedlmsg='Member "'nmemb'" added to $$$INDEX.'
    End
erx1:
    zedsmsg=
    Address ISPEXEC "SETMSG MSG(ISRZ001)"
