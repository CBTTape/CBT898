/******************************** REXX ********************************/
/**********************************************************************/
/* TRACE r                                                            */
/*--------------------------ooo000OOOO000ooo--------------------------*/
/* DSIZE: Datasets SIZE                                               */
/*                                                                    */
/* Goes through JCL looking for "DEFINE CLUSTER" and "(NEW,CATLG)"    */
/* to establish VSAM and NONVSAM datasets allocated by the job.       */
/* It will then extract the llq and prefix it with whatever the       */
/* variable "hlq" is set to to get sizing info for previously         */
/* allocated datasets.                                                */
/*                                                                    */
/* Will insert details as NOTELINES so runit and see.                 */
/*                                                                    */
/*--------------------------------------------------------------------*/
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS"
    If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    Parse Upper VAR parm# parm#;Parse VAR parm# parm1 parm2 .

    sysid=Strip(storage(d2x(x2d(c2x(storage(10,4)))+x2d(154)),8))

    If Wordpos(parm1,'PROD QA TEST IVP')=0 Then parm1='TEST'
    hlqc=Dsize$(parm1)
    If hlqc='Error' Then Do
        zedsmsg=
        gc009='Hlq translation not set up for "'sysid'". Check out',
              'member DSIZE$, modify and rerun.'
        Signal mexit1
    End

    Address ISREDIT;"(status)=USER_STATE";"RESET";"(caps)=CAPS";"CAPS=OFF"
    "CURSOR=1 0"
    find='APPLID='
    "F '"find"' FIRST"
    If rc/=0 Then Do;find='STCNAME=';"F '"find"' FIRST";End
    If rc/=0 Then Do;gc009="'APPLID=/STCNAME=' not found.";Signal mexit1;End
    "(line)=LINE .ZCSR";Interpret "Parse VAR line '"find"' applid ."
    Parse VAR applid applid ',' .
    hlq=hlqc'.'applid                       /* Change as required     */

Say '*--VSAM------------------------------------------*'
    "CURSOR=1 0"
    Do Forever
        "F 'DEFINE CLUSTER'"
        If rc/=0 Then Leave
        "F 'NAME('"
        "(line)=LINE .ZCSR"
        Parse VAR line 'NAME(' dsn ')'
        llq=Strip(Substr(dsn,Lastpos('.',dsn)+1),,"'")
        nlq=Strip(Substr(dsn,1,Lastpos('.',dsn)-1),,"'")
        pri=;sec=;typ=
        x=get_VSAM_info(hlq'.'llq)
        If x/=0 Then x=get_VSAM_info(nlq)
        If x/=0 Then Do;pri='???';sec='???';typ='???';End
        linex='Type(' typ ')   Primary(' pri ')   Secondary( 'sec ')'
        "LINE_BEFORE .ZCSR=NOTELINE (linex)"
    End
Say '*--NonVSAM---------------------------------------*'
    "CURSOR=1 0"
    Do Forever
        "F 'NEW,CATLG'";If rc/=0 Then Leave
        "(lin#)=LINENUM .ZCSR"
        "F 'DSN=' PREV"
        "(line)=LINE .ZCSR"
        Parse VAR line 'DSN=' dsn .
        Parse VAR dsn dsn ','
        llq=Strip(Substr(dsn,Lastpos('.',dsn)+1),,"'")
        x=get_NONVSAM_info(hlq'.'llq)
        If x/=0 Then Do;pri='???';sec='???';typ='???';End
        linex='Type(' typ ')   Primary(' pri ')   Secondary( 'sec ')'
        "LINE_BEFORE .ZCSR=NOTELINE (linex)"
        "CURSOR="lin#+1 0
    End
    "CAPS=(caps)";"USER_STATE=(status)";Exit
mexit1:
    Address ISPEXEC "SETMSG MSG(GC009)"
    Exit

get_VSAM_info:
    x=Outtrap('linei.',500,'NOCONCAT');#msg=MSG('On')
    Address TSO "LISTC ENT('"Arg(1)"') ALL"
Say Right(rc,2) Arg(1)
    x=Outtrap('Off');x=MSG(#msg)
    If rc/=0 Then Return rc
    data=0;index=0
    Do i=1 To linei.0
        If pri/='' & sec/='' Then Leave
        w1=Word(linei.i,1)
        If w1='DATA' Then data=1
        If w1='INDEX' Then index=1
        If \data Then Iterate
        If Index(linei.i,'SPACE-TYPE')>0 Then Do
            Parse VAR linei.i 'SPACE-TYPE' typ .
            typ=Strip(TRanslate(typ,' ','-'))
            Iterate
        End
        If Index(linei.i,'SPACE-PRI')>0 Then Do
            Parse VAR linei.i 'SPACE-PRI' pri .
            pri=Strip(TRanslate(pri,' ','-'))
            Iterate
        End
        If Index(linei.i,'SPACE-SEC')>0 Then Do
            Parse VAR linei.i 'SPACE-SEC' sec .
            sec=Strip(TRanslate(sec,' ','-'))
            Iterate
        End
    End
    Return 0

get_NONVSAM_info:
    xx=Listdsi("'"Arg(1)"' NORECALL")
Say Right(xx,2) Arg(1)
    If xx<5 | sysreason=9 | sysreason=25 Then Nop;Else Return 4
    pri=Strip(SYSPRIMARY)
    sec=Strip(SYSSECONDS)
    typ=Strip(SYSUNITS)
    Return 0
