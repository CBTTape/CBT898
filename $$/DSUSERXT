/*            REXX                                                    */
ARG parm1 parm2 parm3
/*    TRACE r                                                         */
/*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-*/
/*                                                                    */
/* System default DS user exit to advise either invalid selection or  */
/* invalid concatenation.                                             */
/* This user exit must be called 'DSUSERXT' and included in your      */
/* SYSPROC or SYSEXEC concatenation. It will be invoked if any of the */
/* non standard selections are made with the selection placed in the  */
/* 1 byte ISPF variable 'SEL' and placed in the SHARED subpool prior  */
/* to invoking this EXEC. If you return a non-zero return code,       */
/* subsequent processing of additional selections will be terminated. */
/*                                                                    */
/* Use this as a template to build user exit.                         */
/*                                                                    */
/*  The following table details are available to the user exit after  */
/*  issuing the following command:                                    */
/*            "ISPEXEC TBSKIP &DSTABLE NUMBER(0)"                     */
/*                                                                    */
/*      Table name:                                                   */
/*          &DSTABLE                                                  */
/*      Keys:                                                         */
/*          Name Length Contents                                      */
/*          ~~~~ ~~~~~~ ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~  */
/*          UCB#   2    UCB in HEX (used for sorting)                 */
/*          VOL    6    Volume                                        */
/*          IX     2    Indexec VTOC indicator (value IX/blank)       */
/*          UCB    3    UCB in characters                             */
/*          FS     9    Free space                                    */
/*          EX     4    Number of extents                             */
/*          LE     9    Largest extent                                */
/*          DT     8    Device type                                   */
/*          MA     9    Mount attribute                               */
/*          AS     6    Allocation status                             */
/*          DS     6    Device status                                 */
/*          UCBHEX 128  Control blocks in hex                         */
/*                                                                    */
/* The following is required for the following selections:            */
/*   'B' - Backup volume.                                             */
/*      REXX:                                                         */
/*        DSBACKUP - Generate JCL to backup into dataset              */
/*                   'ZTS.#BACKUP.volume.Dyymmdd'.                    */
/*                                                                    */
/*   'F' - DeFrag volume.                                             */
/*      Skeleton:                                                     */
/*        DSDEFRAG - DEFRAG skeleton.                                 */
/*                                                                    */
/*   'H' - HEX display.                                               */
/*      REXX:                                                         */
/*        DSHEXDIS - Refer to this EXEC's constituents for further    */
/*                   details.                                         */
/*      Panels:                                                       */
/*        DYNAMIC$ - Dynamic panel. (Permits building from within the */
/*                   dialog.                                          */
/*                                                                    */
/*   'IO'/'IX' - Index VTOC off and on.                               */
/*      Skeleton:                                                     */
/*        DSIXVTOC - JCL to turn VTOC index on/off.                   */
/*                                                                    */
/*   'LD' - Caching status of volume.                                 */
/*                                                                    */
/*   'LC' - Caching status and counts of specified volume.            */
/*                                                                    */
/*   'O' - Vary volume off-line:                                      */
/*      REXX:                                                         */
/*        DSOPCMD  - Routine to execute operator commands under TSO   */
/*                   details.                                         */
/*                                                                    */
/*   'R' - Rename volume.                                             */
/*      REXX:                                                         */
/*        RNVOLSER - Refer to this EXEC's constituents for further    */
/*                   details.                                         */
/*                                                                    */
/* The following is required for the following commands:              */
/*   '/LD' - Caching status of all volumes.                           */
/*                                                                    */
/*   '/ONLINE' - Vary unit on-line:                                   */
/*      REXX:                                                         */
/*        DSOPCMD  - Routine to execute operator commands under TSO   */
/*                   details.                                         */
/*                                                                    */
/*                                                                    */
/*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-*/
    PARSE UPPER ARG parm1 parm2 parm3
    if parm1 ¬= "" THEN SIGNAL byp1
    ADDRESS ISPEXEC
    "ISPEXEC CONTROL ERRORS RETURN"
    "ISPEXEC ISREDIT MACRO (parm1,parm2,parm3)"
    IF rc > 0 THEN SIGNAL byp1
    PARSE UPPER VAR parm1 parm1
    PARSE UPPER VAR parm2 parm2
    PARSE UPPER VAR parm3 parm3

byp1:
    cactn = "RETURN"
/*  cactn = "CANCEL"                                                  */
    "ISPEXEC CONTROL ERRORS" cactn

ini0:
    #acct  = '01845'                        /* ZTS default account    */
    "ISPEXEC VGET ZSYSID ASIS"
    Select
        When zsysid = 'MVS1' Then jclass = 1
        When zsysid = 'MVS2' Then jclass = 2
        Otherwise                 jclass = 'A'
    End
    userid = USERID()
    unit   = 'SYSALLDA'
    rcx    = 0
    "ISPEXEC VGET ACCT PROFILE"
    If rc ¬= 0 Then acct = #acct

ini1:
    "ISPEXEC VGET (ZCMD SEL DSTABLE) SHARED"
    "ISPEXEC TBSKIP &DSTABLE NUMBER(0)"

    If WORD(zcmd,1) = "/ONLINE" Then Signal cmd_online
    If WORD(zcmd,1) = "/LD" Then Signal cmd_listdata

    If sel = "B"  Then Signal opt_bkup
    If sel = "R"  Then Signal opt_r
    If sel = "F"  Then Signal opt_defrag
    If sel = "H"  Then Signal opt_hexdisp
    If sel = "IX" Then Signal opt_vtocix
    If sel = "IO" Then Signal opt_vtocix
    If sel = "LD" Then Signal opt_listdata
    If sel = "LC" Then Signal opt_listdataC
    If sel = "O"  Then Signal opt_offline
    gc009 = "Invalid selection '"sel"'. Default system 'DSUSERXT'",
            "invoked."
    rcx = 8
    Signal msg_exit

opt_bkup:
    "ISPEXEC VPUT VOL SHARED"
    Address TSO "%DSBACKUP"
    Return

opt_r:
    "ISPEXEC SELECT CMD(%RNVOLSER &VOL &VOL)"
    Return 0

opt_vtocix:
    f4 = OBTAIN(vol)
    If SUBSTR(f4,1,1) ¬= 'F4'x Then Do
        gc009 = 'Error reading the Format4 DSCB, Please investigate.'
        rcx = 8
        Signal msg_exit
    End
    vi = SUBSTR(f4,15,1)
    If BITAND('01'x,vi) = '01'x & sel = 'IX' Then Do
        gc009 = 'VTOC is already operating in IX Format.'
        rcx = 4
        Signal msg_exit
    End
    If BITAND('01'x,vi) ¬= '01'x & sel = 'IO' Then Do
        gc009 = 'VTOC is already operating in OS Format.'
        rcx = 4
        Signal msg_exit
    End
    edit = "Y"
    "ISPEXEC FTOPEN TEMP"
    "ISPEXEC FTINCL DSIXVTOC"
    "ISPEXEC FTCLOSE"
    "ISPEXEC VGET ZTEMPF"
    Address TSO "ALLOC DD(ZTEMPF) DS('"ztempf"') OLD REU"
    "ISPEXEC LMINIT DATAID(DATAID) DDNAME(ZTEMPF)"
    IF edit = "Y" THEN DO
        umodel = "NOTEZED"
        "ISPEXEC VPUT UMODEL SHARED"
        "ISPEXEC EDIT DATAID(&DATAID) MACRO(UMODEL)"
    END
    ELSE "ISPEXEC EDIT DATAID(&DATAID)"
    "ISPEXEC LMFREE DATAID(&DATAID)"
    Address TSO "FREE DD(ZTEMPF)"
    tempsub = ztempf
    "ISPEXEC VPUT TEMPSUB SHARED"
    DROP tempsub
    Address TSO "%TEMPSUB"
    rcx = rc
    If rcx ¬= 0 Then Do
        Address ISPEXEC
        "ISPEXEC VGET MESSAGE SHARED"
        gc009 = message
    End
    Else Address ISPEXEC "SELECT CMD(%MVSCON)"
    Signal msg_exit

opt_defrag:
    edit = "Y"
    "ISPEXEC FTOPEN TEMP"
    "ISPEXEC FTINCL DSDEFRAG"
    "ISPEXEC FTCLOSE"
    "ISPEXEC VGET ZTEMPF"
    Address TSO "ALLOC DD(ZTEMPF) DS('"ztempf"') OLD REU"
    "ISPEXEC LMINIT DATAID(DATAID) DDNAME(ZTEMPF)"
    IF edit = "Y" THEN DO
        umodel = "NOTEZED"
        "ISPEXEC VPUT UMODEL SHARED"
        "ISPEXEC EDIT DATAID(&DATAID) MACRO(UMODEL)"
    END
    ELSE "ISPEXEC EDIT DATAID(&DATAID)"
    "ISPEXEC LMFREE DATAID(&DATAID)"
    Address TSO "FREE DD(ZTEMPF)"
    tempsub = ztempf
    "ISPEXEC VPUT TEMPSUB SHARED"
    DROP tempsub
    Address TSO "%TEMPSUB"
    rcx = rc
    Address ISPEXEC
    "ISPEXEC VGET MESSAGE SHARED"
    gc009 = message
    Signal msg_exit

opt_hexdisp:
    "ISPEXEC VPUT (VOL UCBHEX) SHARED"
    Address TSO "%DSHEXDIS"
    Return 0

opt_offline:
    gc009 = "Vary '"vol"("ucb")' offline. Press <ENTER> to confirm",
            "or END PF key to cancel operation."
    'ISPEXEC SETMSG MSG(GC009)'
    'ISPEXEC DISPLAY'
    If rc ¬= 0 Then Do
        gc009 = "Vary '"vol"("ucb")' offline operation cancelled"
        rcx = 0
        Signal msg_exit
    End

    Address TSO "%DSOPCMD V "ucb",OFFLINE"
    rcx = rc
    gc009 = "Press <ENTER> to refresh display."
    Signal msg_exit

opt_listdata:
    sl = INDEX(dt,'/')
    If sl > 0 Then dt = SUBSTR(dt,1,sl-1)
    Address TSO "%BR LISTDATA DSTATUS UNIT("dt") VOL("vol") DEVICE"
    Return 0

opt_listdataC:
    sl = INDEX(dt,'/')
    If sl > 0 Then dt = SUBSTR(dt,1,sl-1)
    Address TSO "%BR LISTDATA COUNTS UNIT("dt") VOL("vol") DEVICE"
    Return 0

cmd_listdata:
    x = LISTDSI("'SYS1.LINKLIB'")
    dt = sysunit
    vol = sysvolume
    Address TSO "%BR LISTDATA DSTATUS UNIT("dt") VOL("vol") SUBSYS"
    Return 0

cmd_online:
    ucb = WORD(zcmd,2)
    gc009 = "Vary '"ucb"' online. Press <ENTER> to confirm",
            "or END PF key to cancel operation."
    'ISPEXEC SETMSG MSG(GC009)'
    'ISPEXEC DISPLAY'
    If rc ¬= 0 Then Do
        gc009 = "Vary '"ucb"' online operation cancelled"
        rcx = 0
        Signal msg_exit
    End

    Address TSO "%DSOPCMD V "ucb",ONLINE"
    rcx = rc
    gc009 = "Press <ENTER> to refresh display."
    Signal msg_exit

msg_exit:
    'ISPEXEC SETMSG MSG(GC009)'
    Return rcx
