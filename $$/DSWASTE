/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* DSWASTE: DataSpace WASTagE.                                        */
/*                                                                    */
/* Routine to summarise space wastage on DASD based on the output from*/
/* the CBT VTOC listing command.                                      */
/*                                                                    */
/* This routine is based on listing generated with the following VTOC */
/* formatted headings:                                                */
/*                                                                    */
/*   List fields ===> DSN VOLUME ALLOC USED PCT                       */
/*                                                                    */
/* Execute against the edit/view session of the VTOC output.          */
/* Parameter is the percent of wastage (greater or equal to supplied  */
/* parameter).  Parameter must be a digit greater than 0 and less or  */
/* equal to 100. (Default is 50.)                                     */
/*                                                                    */
/*--------------------------------------------------------------------*/
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS"
    If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    Parse VAR parm# p1 .
    If Datatype(p1,'w') Then Do
        If p1>0 & p1< 101 Then p1=100-p1;Else p1=50
    End;Else p1=50
    volA.=0;volU.=0;vol_list=
    Parse SOURCE . xenv xcmd xdd xdsn .
    Address ISREDIT;"(status)=USER_STATE";"RESET";"(caps)=CAPS";"CAPS OFF"
    "(last)=LINENUM .ZLAST";spref=Sysvar(syspref)

/*  last=1000                                                         */

    Do i=1 To last
        "(line)=LINE" i;Parse var line 2 dsn vol alloc used pct .
        If Wordpos(dsn,'VTOC DSNAME TOTALS')>0 Then Iterate
        If alloc+used=0 Then Iterate
        If Wordpos(vol,vol_list)=0 Then vol_list=vol_list vol
        volA.vol=volA.vol + alloc
        volU.vol=volU.vol + used
        If pct<=p1 Then,
           x=nl('Waste('Right(vola.vol-volU.vol,7)' -' Right(100-pct,3)'%)',
                vol dsn)
    End
    vol_list=$Sort(vol_list)
    x=nl(' ')
    x=nl('Volume Allocated    Used             U% Wastage          W%')
    x=nl('------ ------------ ------------ ------ ------------ ------')
    Do While vol_list/=''
        Parse VAR vol_list vol vol_list
        x=nl(vol Right(volA.vol,12) Right(volU.vol,12),
             Format((volU.vol/volA.vol)*100,3,2),
             Right(volA.vol-volU.vol,12),
             Format(100-(volU.vol/volA.vol)*100,3,2))
    End

exi1:
    "USER_STATE=(status)";"CAPS=(caps)"
    Exit
nl:Parse Arg n#
    "LINE_BEFORE .ZFIRST=NOTELINE (n#)";Return 0
