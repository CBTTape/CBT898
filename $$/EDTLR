/******************************** REXX ********************************/
/*--------------------------ooo000OOOO000ooo--------------------------*/
/*                                                                    */
/* Program - EDTLR                                                    */
/*                                                                    */
/* Purpose - To force user into a valid device type through a series  */
/*           of panels.                                               */
/*                                                                    */
/* Parameters:                                                        */
/*    DASD - Bring up the DASD list.                                  */
/*    TAPE - Bring up the TAPE list.                                  */
/*    LIST - Return a list of DAS and TAPEs to the caller.            */
/*         - (blank) prompt for DASD/TAPE selection.                  */
/*                                                                    */
/* Members required to package this feature:                          */
/*   REXX:                                                            */
/*         EDTLR        - This driver                                 */
/*                                                                    */
/*   Panel:                                                           */
/*         EDTLP1       - POP up panel for device TYPE selection.     */
/*         EDTLP2       - POP up panel for device selection ( 1-16).  */
/*         EDTLP3       - POP up panel for device selection (17-32).  */
/*         EDTLP4       - POP up panel for device selection (33-48).  */
/*                                                                    */
/*   Messages:                                                        */
/*         GC00         - Generic message member                      */
/*                                                                    */
/*   Programs:                                                        */
/*         EDTLIST      - Assembler program to provide a list of      */
/*                        generic device names for a device type in a */
/*                        REXX variable "EDTLIST".                    */
/*                                                                    */
/*--------------------------------------------------------------------*/
    Arg edt,row,col,debug

    Parse source opsys env xcmd . xdsn .
    If debug/='' Then Trace r
    zedsmsg=;x=Msg('Off')
    Address ISPEXEC
    If row='' Then row=5;If col='' Then col=5
    If edt/='' Then Signal Call_edtlist

pop_up_2:
    zwinttl='Device Types';"ADDPOP ROW("row") COLUMN("col")"
    "DISPLAY PANEL(EDTLP1)";rc1=rc;"REMPOP"
    If rc1/=0 Then Do
        zedlmsg='Device type selection abandoned.';"SETMSG MSG(ISRZ001)"
        If env='FUNCTION' Then Return '?';Else Exit
    End
Call_edtlist:
    If edt='LIST' Then Do
        list='DASD:' expand_edt_list('DASD')
        Return Space(list 'TAPE:' expand_edt_list('TAPE'))
    End

    Call edtlist
    edtocr=c2d(Substr(edtlist,5,4));edtlist=Substr(edtlist,9)
    If Word(edtlist,1)='Error' Then Do
        zedlmsg='Error found while looking for device type "'EDT'".',
                'Please refer to dialogue developer.'
        "SETMSG MSG(ISRZ001)"
        Return 8
    End
    Do i=1 To edtocr
        Interpret 'edtt'i' = Substr(edtlist,(i-1)*8+1,8);T'i'='i
    End
pop_up_3:
    zwinttl='Device Selection';"ADDPOP ROW("row") COLUMN("col")"
    Select
        When edtocr < 17 Then "DISPLAY PANEL(EDTLP2)"
        When edtocr < 33 Then "DISPLAY PANEL(EDTLP3)"
        Otherwise             "DISPLAY PANEL(EDTLP4)"
    End
    rc1=rc;"REMPOP"
    If rc1/=0 Then Do
        zedlmsg='Device type selection abandoned.';"SETMSG MSG(ISRZ001)"
        Signal pop_up_2
    End
    Interpret 'Return Strip(edtt'tt')'

expand_edt_list:
    edt=Arg(1);Call edtlist
    edtocr=c2d(Substr(edtlist,5,4));edtlist=Substr(edtlist,9)
    xx=
    Do i=1 To edtocr
        xx=xx Strip(Substr(edtlist,(i-1)*8+1,8))
    End
    Return xx
