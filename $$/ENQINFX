/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*                                                                    */
/* Exit for ENQINFO.                                                  */
/*                                                                    */
/*--------------------------------------------------------------------*/
    Address ISPEXEC;"CONTROL ERRORS RETURN"
    Parse ARG tablegm,#dsn

/*  "CONTROL ERRORS CANCEL"                                           */

    zerrhm='*';zerralrm='NO';zerrsm=
    x_r='CSMDSN'

    If tablegm='' Then Do
        zerrlm="Missing parameter...."
        Signal erx1
    End

    "TBGET &TABLEGM"
    Parse VAR rname dsn .
    Select
        When (Substr(#dsn,1,1)='?') Then Do
            zerrlm=rname
            Signal erx1
        End
        When (Substr(#dsn,1,1)='/') Then Do
            zerrlm="Invalid option specified."
            Signal erx1
        End
        Otherwise Do
            Call expand_operand
            If Index(#dsn,dsn)=0 Then Do
                Parse VAR #dsn #dsn #rest
                #dsn=#dsn "'"dsn"'" #rest
            End
            If Strip(#dsn)="'"dsn"'" Then Do
                zerrlm="Unidentified parameter."
                Signal erx1
            End
            xc=Call_csmdsn()
        End
    End

ext1:Return xc

erx1:
    "SETMSG MSG(ISRZ002)"
    Return 8

/*--------------------------------------------------------------------*/
/* Call multi-procs with:                                             */
/*    1st. operand - operation                                        */
/*    2nd. operand - datset name                                      */
/*    3rd. operand - additional multi-procs parameter                 */
/*--------------------------------------------------------------------*/
Call_csmdsn:
    Parse VAR #dsn f00 parm4 parms
    p0    = '?'
    parm1 = '%MDS'
    parm2 = p0
    parm3 = f00
    parm5 = Strip(parms)
    parm6 = ''
    parm7 = szcmd
    parm7 = ''
    parm8 = ''
    x_r   = 'CSMDSN'
    Signal On Syntax Name syntax_handle
    rc1=CSMDSN(parm1 parm2 parm3 parm4 parm5 parm6 parm7 parm8)
    Signal Off Syntax
    Return rc1

/*--------------------------------------------------------------------*/
/* Parses the original value with the new value in reverse order and  */
/* stops processing on the first miss match and uses that value as    */
/* input to the next parsing step where:                              */
/* Expand operand to substitute quoted DSNAME for each '/' found.     */
/* Any data abutted to the '/' will also be included in the quotes.   */
/*--------------------------------------------------------------------*/
expand_operand:
    Trace Off
    #dsn = Strip(#dsn)
    dsn  = Strip(dsn,'t')
    dsns = Strip(dsn)
    If #dsn = '' Then Do
        #dsn = "'"dsns"'"
        Return 0
    End
    natchar = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789@#$'
    dt    = 1

/*--------------------------------------------------------------------*/
/* Remove matching tail                                               */
/*--------------------------------------------------------------------*/
    lo = Length(dsn)
    ln = Length(#dsn)
    If lo > ln Then lm = lo
    Else            lm = ln
    Do j=0 To lm
        If Substr(dsn,lo-j,1) /= Substr(#dsn,ln-j,1) Then Leave
        If lo-j = 1 | ln-j = 1 Then Leave
    End
    #dsn = Substr(#dsn,1,ln-j)

/*--------------------------------------------------------------------*/
/* Substitute                                                         */
/*--------------------------------------------------------------------*/
    Do Forever
        dt = Index(#dsn,'/',dt)
        If dt = 0 Then Leave
        #rest = Substr(#dsn,dt+1)
        #dsn  = Substr(#dsn,1,dt-1)
        If Substr(#rest,1,1) /= '' Then,
            Parse VAR #rest #dsnX #rest
        Else #dsnX = ''
        #dsn = #dsn || "'"dsns || #dsnx"'" #rest
        dt = dt +1
    End
    #dsn = Strip(Translate(#dsn))

    Return 0

/*--------------------------------------------------------------------*/
/* Error detection handler                                            */
/*--------------------------------------------------------------------*/
syntax_handle:
    Signal Off syntax
    zerrlm = "Routine '"x_r"' is not available to you. Please contact",
            "your dialogue administrator."
    "SETMSG MSG(ISRZ002)"

    Return 8

