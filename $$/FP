/*--------------------------ooo00 REXX 00ooo--------------------------*/
n_st=mark(1)                                /* Mark Start

   Member : FP

   Find panel:
      Command processor that determines the panelid using ISPF variable
      "ZPANELID" and then either views the first occurance of the panel
      or presents a list of the ISPPLIB concatenations and indicates all
      the datasets the panel is found in.

      Pass parameter of '*' to find the displayed panel and view it.
      This option will parse any LIBDEFs and then the ISPPLIB
      concatenation and present the first hit.
      (Can use parameter: "/", "*", "FIRST" or "THIS".)

      To pick up panels that are LIBDEFD, use parameter of 'ALL'. This
      option will use either ISPFALOC (if available) or ISRDDN to show
      all hits.
      Without any parameters, only the ISPPLIB concatenation will be
      scanned.

   Parameters:
    Keyword:-
     /ALL      - Look in all datasets allocated.
     /ISPfaloc - Use ISPFALOC (if site default customised to ISRDDN).
     /ISRddn   - Use ISRDDN even if ISPFALOC (default) is available.
     * | / | FIRST | THIS
               - View first (active) panel.
    Positional:-
     cccccccc  - Any other non-blank data will be treated as a supplied
                 "panelid" and the appropriate find will be performed.

   The following members are required to package this routine:
     REXX:
       $BR      o Routine to display this help under browser.
       $DDALLOC - Find DSN allocated to a DD (used by $FIBDMEM).
       $FINDMEM - Routine to find first occurance of a members in a DD
                  concatenation.
       $VERNAME - Verify member name (used by $DDALLOC).
       FP       - This driver.
       ISPFALOC o ISPF allocation (and it's associated packaging).
                  If unavailable ISRDDN will be used.

*/;n_en=mark(-1)                            /* Mark End               */
/*--------------------------------------------------------------------*/
FP:fp_ver='1.04'
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#)"
    If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    default_routine='ISPFALOC'              /* ISPFALOC -or- ISRDDN   */
    Parse Upper VAR parm# parm1 .
    Parse SOURCE . . xcmd .
    If parm1='?' Then Do;x=$BR(xcmd '??');Exit;End
    If parm1='??' Then Do
        Do i=n_st To n_en;Say Substr(Sourceline(i),1,79);End;Exit
    End
    If Wordpos('/ALL',Translate(parm#))>0 Then Do
        look_all=1;parm#=Delword(parm#,Wordpos('/ALL',Translate(parm#)),1)
    End;Else look_all=0
    If Index(parm#,'/I')>0 Then Do
        wp=Index(parm#,'/I')
        routine=$Wordat(parm#,wp,'W');routine=Substr(routine,2,3)
    End;Else routine=default_routine
    zedsmsg=;first_hit=0
    Do i=1 To Words(parm#)
        wp=Wordpos(Word(parm#,i),'/ * FIRST THIS');If wp=0 Then Iterate
        first_hit=1;parm#=Delword(parm#,i,1)
        Leave
    End
    Parse VAR parm# zpanelid .
    If zpanelid='' Then "VGET (zpanelid)"
passed_panel:
/*--------------------------------------------------------------------*/
/* ISPFALOC users toggle the next comment block                       */
/*--------------------------------------------------------------------*/
    If first_hit Then Do
        Parse Value '' With typev idv dsn
        "QLIBDEF ISPPLIB TYPE(typev) ID(idv)"
        Do While idv/=''
            Parse VAR idv dsn idv;dsn=Strip(dsn,,"'")
            If Sysdsn("'"dsn"("zpanelid")'")='OK' Then Leave
            dsn=
        End
        If dsn='' Then Parse Value $Findmem('ISPPLIB',zpanelid) With dsn .
        "VIEW DATASET('&DSN(&ZPANELID)')"
        Exit
    End
    Signal On Syntax Name Use_ISPFALOC
    Interpret 'Signal Use_'routine
Use_ISP:
Use_ISPFALOC:Signal On Syntax Name Use_ISRDDN
    x=Ispfaloc('/T')                        /* Test availability      */
    Signal Off Syntax
    If look_all Then #dd='ALL';Else #dd='DDNAME(ISPPLIB)'
    "SELECT CMD(%ISPFALOC "#dd" FIND("zpanelid")"
    Signal ext1
Use_ISR:
Use_ISRDDN:Signal Off Syntax
    If look_all Then #dd=;Else #dd='ISPPLIB'
    zopt='Only' #dd';Member' zpanelid
    "SELECT CMD(ISRDDN" zopt")"
/*--------------------------------------------------------------------*/
    Signal ext1

err1:"ISPEXEC SETMSG MSG(ISRZ001)"
ext1:Exit
Mark:Arg diff;If \Datatype(diff,'W') Then diff=1;Return sigl+diff
