/*--------------------------ooo00 REXX 00ooo--------------------------*/
    n_st=mark(1)                            /* Mark Start

    FTPIBM:        [V1.03]
    -------

    This function is intended to be executed over the eMail sent by
    IBM about the PTFs' details.

    Cut/paste at least the first 20 lines of the email into a view or
    edit session.  For safety ensure the last line at least includes
    the "binary" line entry from the eMail.

    Also ensure you have CAPS OFF in destination when cutting/pasting
    as the IBM server is case sensitive for source file names.
    (If you screw this up turn caps off and then either re cut/paste
    or enter "LC99" on line 1.)

    The text will be parsed and a parameter entry panel will be
    presented to fine tune your parameters and dataset (.TERSED) sizing
    (which will be untersed into the .PTF dataset automatically using
    the TTL routine).
    Note: The hlq, space units, primary and secondary allocations are
          sticky fields and will be remembered for the same ISPF
          applid.
    You just need to hit [ENTER] and the JCL will be presented which
    will need to be manually submitted (after final fine tuning if
    required).

    Packaging: Members marked with a 'o' seperator are optional and
               without them the full features will not be available.
               (So if your jobcard is lacking and you have to manually
               unterse, look for those missing members.)
     REXX:
      $ALKACCT o Routine that sets site default accounts.
                 Default is "(NOT-REQUIRED)".
      $ALKJC   o Routine that sets site default JOBCARD parameters.
                 Default values are CLASS=A,MSGCLASS=X.
      $ALKJCPG o Routine that sets site default JOBCARD program
                 value.
                 Default value is FTPIBM.
      $ALKUJBS o Routine that returns an unique job id called by
                 $ALKJC.  Default "F".
      $DDALLOC o Routine that finds the location of routine TTL.
                 Without this you will need to manually unterse.
      FTPIBM   - This routine.
      JC       o Build jobcard.
      TTL      o Self extracting TRSMAIN loadlib and REXX driver.
                 Without this you will need to manually unterse.
     Panel:
      FTPIBM1  - Parameter verification/entry panel. This panel is
                 self-extracted out of "FTPIBM".

                               *****
*/; n_en=mark(-1)                           /* Mark End               */

FTPIBM_ver='1.02'
    Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    Arg parm#;If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS";If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    If Wordpos(Word(parm#,1),'? ?? HELP /H /HELP')>0 Then Do
        If ima_macro Then Do;"ISREDIT (caps)=CAPS";"ISREDIT CAPS=OFF";End
        Do i=n_st To n_en;x=nl_h(Substr(Sourceline(i),1,79));End
        If ima_macro Then "ISREDIT CAPS=(caps)"
        Exit
    End
    Signal on Syntax Name error_off_$ALKUVOL
    unitvolp="UNIT=SYSALLDA";unitvolp=$ALKUVOL('SMS',,'J')
error_off_$ALKUVOL:Signal off Syntax
    If ima_macro Then Do
        "ISREDIT (status)=USER_STATE"
        "ISREDIT (caps)=CAPS";"ISREDIT CAPS=OFF"
        "ISREDIT F 'FTP TO:' FIRST"
        If rc/=0 Then Signal question_ftp_instructions
        "ISREDIT (line)=LINE .ZCSR";Parse VAR line ':' ftpsite .
        "ISREDIT F 'anonymous'"
        If rc/=0 Then Signal question_ftp_instructions
        "ISREDIT (line)=LINE .ZCSR";Parse VAR line 'password "' password '"' .
        "ISREDIT F 'cd /'"
        If rc/=0 Then Signal question_ftp_instructions
        "ISREDIT (cddir)=LINE .ZCSR"
        "ISREDIT F 'get ftp'"
        If rc/=0 Then Signal question_ftp_instructions
        "ISREDIT (line)=LINE .ZCSR";Parse VAR line 'get ftp' ftporder '.' .
        "ISREDIT F 'hsgdsh dghgdhg shdewqt evedevev'"
        action='Confirm FTP details:'
        Signal Process_panel
    End

question_ftp_instructions:
    zedlmsg='Not FTP instructions available. Enter manually.'
    "SETMSG MSG(ISRZ001) MSGLOC(.)"
    action='Enter FTP details:'
Process_panel:
    Call build_panel
    vars='ftpihlq ftpistyp ftpiprim ftpisec'
    "VGET ("vars") PROFILE"
    If rc/=0 Then,
        Interpret "Parse Value '"hlq0".$FTPIBM TRK 100 25' With" vars
    #popr=10;#popc=10
    Signal on Syntax Name scr1
    acct='(NOT-REQUIRED)';acct=$alkacct()
scr1:Signal Off Syntax
    "CONTROL ERRORS CANCEL"
    "CONTROL DISPLAY SAVE"
    "ADDPOP ROW(&#POPR) COLUMN(&#POPC)"
    "DISPLAY PANEL(FTPIBM1)";rc1=rc;"REMPOP"
    "CONTROL DISPLAY REFRESH"
    If rc1>0 Then Do
        zedlmsg="Paremeter" action "panel abandoned."
        Signal del_exit
    End
    "VPUT ("vars") PROFILE"
Report_JCL:
/*--------------------------------------------------------------------*/
Parse SOURCE . . xcmd .
    Address TSO 'NEWSTACK'
    Signal on Syntax Name error_on_JC
    pgmn=xcmd;id='F'
    id=$alkjc('def_id')
    x=JC(id '/Q /9 /P('pgmn')');Signal error_off_JC
error_on_JC:
    Queue "//"userid()||id "JOB" acct",'"pgmn"', *** Verify JOBcard ***"
    Queue '//             CLASS=A,COND=(4,LT),'
    Queue '//             MSGCLASS=X,TIME=1440,REGION=0M,'
/*  Queue '//             NOTIFY=&SYSUID,LINES=9999'                  */
    Queue '//             NOTIFY=,LINES=9999'
error_off_JC:Signal off Syntax
Queue '//*********************************************************************'
Queue '//RENUN EXEC PGM=IDCAMS'
Queue '//SYSPRINT DD SYSOUT=*'
Queue '//SYSIN DD *'
Queue ' DELETE 'hlq0'.FTP'ftporder'.TEXT'
Queue ' DELETE 'ftpihlq'.MLST'ftporder'.HOLDDATA'
Queue ' DELETE 'ftpihlq'.MPTF'ftporder'.TERSED'
Queue ' IF MAXCC<9 THEN SET MAXCC=0'
Queue '//ALLOC EXEC PGM=IEFBR14'
Queue '//MLST     DD DSN='ftpihlq'.MLST'ftporder'.HOLDDATA,'
Queue '//            DISP=(,CATLG),SPACE=(TRK,(1,1)),'
Queue '//            DCB=(RECFM=FB,LRECL=80,DSORG=PS),'
Queue '//            'unitvolp
Queue '//BIN      DD DSN='ftpihlq'.MPTF'ftporder'.TERSED,'
XX=ftpistyp',('ftpiprim','ftpisec
Queue '//            DISP=(,CATLG),SPACE=('xx'),RLSE),'
Queue '//            DCB=(RECFM=FB,LRECL=1024,DSORG=PS),'
Queue '//            'unitvolp
Queue '//FTP1     EXEC PGM=FTP'
Queue '//SYSPRINT DD SYSOUT=*'
Queue '//OUTPUT   DD SYSOUT=*'
Queue '//SYSIN    DD *'
Queue ftpsite
Queue 'anonymous' password
Queue cddir
Queue 'ascii'
Queue 'get ftp'ftporder'.txt'
Queue "get mlst"ftporder".txt '"ftpihlq".MLST"ftporder".HOLDDATA' (REPLACE"
Queue 'binary'
Queue "get mptf"ftporder".bin '"ftpihlq".MPTF"ftporder".TERSED' (REPLACE"
Queue 'quit'
Queue '/*'
Queue '//*********************************************************************'
Queue '//*                                                                   *'
Queue '//* It the UNTERSE step does not follow, then "TTL" was not found in  *'
Queue '//* your SYSPROC or SYSEXEC concatenations and you will need to       *'
Queue '//* unterse the "**.TERSED" dataset after this process.               *'
Queue '//*                                                                   *'
Queue '//*********************************************************************'
Signal on Syntax Name error_on_$DDALLOC
xx=$ddalloc('+SYSPROC',,xcmd)
If xx/='' Then If Sysdsn("'"xx"("xcmd")'")/='OK' Then xx=
If xx='' Then xx=$ddalloc('SYSPROC',,xcmd)
If xx/='' Then If Sysdsn("'"xx"("xcmd")'")/='OK' Then xx=
If xx='' Then xx=$ddalloc('+SYSEXEC',,xcmd)
If xx/='' Then If Sysdsn("'"xx"("xcmd")'")/='OK' Then xx=
If xx='' Then xx=$ddalloc('SYSEXEC',,xcmd)
If xx/='' Then If Sysdsn("'"xx"("xcmd")'")/='OK' Then xx=
If xx='' Then Signal error_on_$DDALLOC
Queue '//*                                                                   *'
Queue '//* The tersed dataset will be untersed by this step.                 *'
Queue '//*                                                                   *'
Queue '//* "TTL" will self-extract TRSMAIN, use it to unterse the dataset    *'
Queue '//* and clean up after itself.  No do-dos left behind here.           *'
Queue '//*                                                                   *'
Queue '//*********************************************************************'
Queue '//UNTERSE EXEC PGM=IKJEFT01,DYNAMNBR=65,REGION=4096K'
Queue '//SYSPROC  DD DISP=SHR,DSN='xx
Queue '//SYSTSPRT DD SYSOUT=X'
Queue '//SYSOUT   DD SYSOUT=*'
Queue '//SYSTERM  DD SYSOUT=*'
Queue '//SYSTSIN  DD *'
Queue '  %TTL /L +'
Queue '    'ftpihlq'.MPTF'ftporder'.TERSED +'
Queue '    'ftpihlq'.MPTF'ftporder'.PTF +'
Queue '    UNPACK +'
Queue '    . +'
Queue '    'hlq0'.$TRSMAIN.SYSPRINT +'
Queue '    . +'
If ftpistyp='CYL' Then Do;ftpiprim=ftpiprim*15;ftpisec=ftpisec*15;End
Queue '    'ftpiprim%5 '+'
Queue '    'ftpisec%5 '+'
Queue '    . . .'
Queue '/*'
error_on_$DDALLOC:Signal off Syntax
    Queue ''
    dd='HR'Time('s')
    Signal on Syntax Name error_on_$ZTEMPF1
    ztempf=$ztempf();Signal error_off_$ZTEMPF1
error_on_$ZTEMPF1:
    Address ISPEXEC "VGET ZTEMPF"
error_off_$ZTEMPF1:Signal off Syntax
    zedlmsg='You will have to manually submit this JCL or forever',
            'loose it to the bit bucket.'
    Address ISPEXEC "SETMSG MSG(ISRZ001)"
    Address TSO "ALLOC F("dd") DSN('"ztempf"') SHR REU"
    Address TSO "EXECIO * DISKW "dd" (FINIS"
    Address ISPEXEC "LMINIT DATAID(DATAID) DDNAME("dd")"
    Address ISPEXEC "EDIT DATAID(&DATAID)"
    Address ISPEXEC "LMFREE DATAID(&DATAID)"
    Signal on Syntax Name error_on_$ZTEMPF2
    Address TSO "FREE F("dd")";xx=$ztempf(ztempf,'/D')
error_on_$ZTEMPF2:Signal off Syntax
del_exit:
    "ISREDIT CAPS=(CAPS)"
    "ISREDIT USER_STATE=(status)"
    "LIBDEF ISPPLIB"
    m#=Msg('Off')
    Address TSO
    "DELETE '"dsnfx"'"
    Return 0

Mark:Arg diff;If \Datatype(diff,'W') Then diff=1;Return sigl+diff
nl_h:Parse Arg n#
    If ima_macro Then "ISREDIT LINE_BEFORE .ZFIRST=NOTELINE (n#)";Else Say n#
    Return 0
/*--------------------------------------------------------------------*/
/* Allocate panel                                                     */
/*--------------------------------------------------------------------*/
build_panel:
#tr=Trace('o')
    u=Userid();p=Sysvar(syspref);If u/=p&p/='' Then u=p'.'u;hlq0=u
    dsnfx= hlq0'.$$$TEMP.FTPIBM'
    #msg=Msg('Off')
    Address TSO
    Signal on Syntax Name error_off_$ALKUVOL
    unitvolt="UNIT(SYSALLDA)";unitvolt=$ALKUVOL('$ZTEMPF')
error_off_$ALKUVOL:Signal off Syntax
    "DELETE '"dsnfx"'"
    "ALLOC DA('"dsnfx"') NEW CATALOG TRACK SPACE(1,1)",
       "DSORG(PO) LRECL(80) BLKSIZE(0)" unitvolt "DIR(1)",
       "RECFM(F B) REU"
    "FREE DA('"dsnfx"')"
    members='FTPIBM1'
    dd='TT'Time('s')
    Do While members/=''
        Parse VAR members memb members
        #tr=Trace('o')
        Interpret "Call make_mem_"memb
        "NEWSTACK"
        Do i=n_st To n_en;Queue Sourceline(i);End;Queue ''
        x=Trace(#tr)
        "ALLOC F("dd") DA('"dsnfx"("memb")') SHR REU"
        "EXECIO * DISKW "dd" (FINIS"
        If rc/=0 Then Do
            Say "Error outputing to datadset '"dsnfx"("memb")', rc="rc"."
            Exit
        End
    End
    Address
    Address ISPEXEC "LIBDEF ISPPLIB DATASET ID('&dsnfx') STACK"
    x=Msg(#msg)
    x=Trace(#tr)                            /* Restore trace setting  */
    Return 0

/*--------------------------------------------------------------------*/
/* Create help panel(s) from sourcelines between the '/* */'          */
/*--------------------------------------------------------------------*/
make_mem_FTPIBM1:n_st = mark(2)
/*
)ATTR
  @ TYPE(TEXT) INTENS(HIGH) COLOR(YELLOW)
  + TYPE(TEXT) INTENS(LOW)  COLOR(GREEN)   SKIP(ON)
  ~ TYPE(TEXT) INTENS(HIGH) COLOR(TURQ)
  _ TYPE(INPUT) INTENS(HIGH) HILITE(USCORE) CAPS(OFF)
  ` TYPE(INPUT) INTENS(HIGH) HILITE(USCORE)
)BODY EXPAND(\\) WINDOW(56,15)
@\-\ FTP parameters \-\
%&SID ===>`ZCMD\ \+
+
~&ACTION:
+Order #. ._Z    +
+Password ._PASSWORD  +
+IBM site ._FTPSITE\ \
+IBM dir. ._CDDIR\ \
+HLQ. . . .`FTPIHLQ\ \
+Space. . ._Z  ~(TRK/CYL)+Prim_Z   +Sec_Z   +
+Account. ._ACCT                          +
+Unit/vol ._UNITVOLP                        +
+
@END+key will terminate processing.
+
)INIT
 &SID=&ZSYSID
 .ZVARS = '(FTPORDER,FTPISTYP,FTPIPRIM,FTPISEC)'
)PROC
 VER (&FTPORDER,NB,NUM)
 VER (&PASSWORD,NB)
 VER (&FTPSITE,NB)
 VER (&CDDIR,NB)
 VER (&FTPIHLQ,NB,DSNAME)
 VER (&FTPISTYP,NB,LIST,TRK,CYL)
 VER (&FTPIPRIM,NB,NUM)
 VER (&FTPISEC,NB,NUM)
 VER (&ACCT,NB)
 IF (&UNITVOLP = &Z) &UNITVOLP='UNIT=SYSALLDA'
)END
*/
    n_en = mark(-2);Return 0

