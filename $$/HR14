/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*  HR14:                                                             */
/*  This function submits a batch job to recall migrated datasets.    */
/*  It axecutes IEFBR14 against a SHR allocation of the migrated      */
/*  dataset to force storage manager (kike FDR) to do an immediate    */
/*  recall rather than queue it for a subsequent process.             */
/*                                                                    */
/*  Parameters:                                                       */
/*  1. Dataset name. Fully qualified (quoted) or system prefix is     */
/*     added free of charge.                                          */
/*  2. Any non-blank charecter will present the JCL under the editor  */
/*     and will need to be manually submitted.                        */
/*--------------------------------------------------------------------*/
hr14_ver='1.04'
    Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    Arg parm#;If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS";If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    Parse VAR parm# dsname edit
    If dsname='' Then Do
        zedlmsg='Missing dataset name to recall.';zedsmsg=
        "SETMSG MSG(ISRZ001)";Return 8
    End

    dsname=Strip(dsname,,"'")
Report_JCL:
/*--------------------------------------------------------------------*/
    #m1=Msg('On');x=Outtrap('line.',500,'NOCONCAT')
    Address TSO "STATUS"
    x=Outtrap('Off');x=Msg(#m1);list=
    Do i=1 To line.0
        Parse VAR line.i . id act .
        If id='JOB' Then Parse VAR line.i . . id act .
        If Wordpos(act,'EXECUTING WAITING')>0 Then Do
            Interpret "Parse VAR id '"Userid()"' suf '('"
            list=list suf
        End
    End
    nat='ABCDEFGHIJKLMNOPQRSTUVWXYZ01234567890@#$';id='A'
    Do i=1 to Length(nat)
        suf=Substr(nat,i,1)
        If Wordpos(suf,list)>0 Then Iterate
        id=suf;Leave
    End
/*--------------------------------------------------------------------*/
    Address TSO 'NEWSTACK'
    Signal on Syntax Name error_on_JC
    pgmn=xcmd;pgmn=$alkjcpg(xcmd)
    x=JC(id '/Q /9 /BB');Signal error_off_JC
error_on_JC:
    Queue "//"userid()||id "JOB (2567,ALK),'"pgmn"',"
    Queue '//             CLASS=A,COND=(4,LT),'
    Queue '//             MSGCLASS=X,TIME=1440,REGION=0M,'
/*  Queue '//             NOTIFY=&SYSUID,LINES=9999'                  */
    Queue '//             NOTIFY=,LINES=9999'
error_off_JC:Signal off Syntax
    Queue '//*'
    Queue "//*  Gen'd by: 'AKOS.$$("xcmd")'"
    Queue '//*'
    Queue '//*'
    Queue '//RECALL   EXEC PGM=IEFBR14'
    Queue '//SYSPRINT DD DISP=SHR,DSN='dsname
    Queue '//*'
    If edit='' Then Do
        Queue "ZZ"
        #m1=Msg('On');x=Outtrap('line.',10,'NOCONCAT')
        Address TSO "SUBMIT * END(ZZ)"
        x=Msg(#m1)
        Do i=1 To line.0
            If Index(line.i,'SUBMITTED')>0 Then Do
                If Wordpos('JOB',line.i)=0 Then Iterate
                zedlmsg=line.i
                "SETMSG MSG(ISRZ001)"
            End
        End
        Return 0
    End
    Queue ''
    dd='HR'Time('s')
    Signal on Syntax Name error_on_$ZTEMPF1
    ztempf=$ztempf();Signal error_off_$ZTEMPF1
error_on_$ZTEMPF1:
    Address ISPEXEC "VGET ZTEMPF"
error_off_$ZTEMPF1:Signal off Syntax
    zedlmsg='You will have to manually submit this JCL or forever',
            'loose it to the bit bucket.'
    Address ISPEXEC "SETMSG MSG(ISRZ001)"
    Address TSO "ALLOC F("dd") DSN('"ztempf"') SHR REU"
    Address TSO "EXECIO * DISKW "dd" (FINIS"
    Address ISPEXEC "LMINIT DATAID(DATAID) DDNAME("dd")"
    Address ISPEXEC "EDIT DATAID(&DATAID)"
    Address ISPEXEC "LMFREE DATAID(&DATAID)"
    Signal on Syntax Name error_on_$ZTEMPF2
    Address TSO "FREE F("dd")";xx=$ztempf(ztempf,'/D')
error_on_$ZTEMPF2:Signal off Syntax
    Return 0

