/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* IC: Install C(AFC).                                                */
/*                                                                    */
/* Make sufficient changes to S/F JCL for site.                       */
/*                                                                    */
/* - Change HLQ's.                                                    */
/* - Change VOLUME.                                                   */
/* - Add SYMSUB disclaimer id Variables found in SYSIN                */
/*                                                                    */
/*--------------------------------------------------------------------*/
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS";If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
/*--------------------------------------------------------------------*/
/* Changes to be made                                                 */
/*--------------------------------------------------------------------*/
    fr1='GSESF.CICSCAFC.CUST'     ; to1='DNMCICS.CICSCAFC.V450.CUST'
    fr2='HLQ.CICSCAFC'            ; to2='&&HLQCAFC.'
    fr3='HLQ.STCNAME.DFHCSD'      ; to3='&&DFHCSD'
    fr4='HLQ.STCNAME'             ; to4='&&HLQCAFCR.'
    fr5='??????'                  ; to5='&&VOLUME'
    fr6='CICSPREFIX.'             ; to6='&&HLQCICS..'
    fr7='LIST(STCNAME)'           ; to7='LIST(&&CSDLIST)'
/*--------------------------------------------------------------------*/
/* Lines to be inserted after the 1st. "//**************" line.       */*/
/*--------------------------------------------------------------------*/
    iv1="//VARS1 SET HLQCAFC='DNDCICS.CICSCAFC.V440'      <- Change DNxCICS"
    iv2="//      SET VOLUME=SYS010                        <- Change SYSxxx"
    iv3="//      SET HLQCICS=CICS13                       <- Change CICS hlq"
    iv4="//      SET HLQCAFCR='DNDCICS.CICSCAFC.CICSTSTX' <- Change CICSTSTX"
    iv5="//      SET DFHCSD='DNDCICS.CICS.V230.DFHCSD'    <- Change DNxCICS"
    iv6="//      SET CSDLIST=regnlist                     <- Change regnlist"

    Address ISREDIT;"(status)=USER_STATE";"(caps)=CAPS";"CAPS=OFF"
    "CURSOR=1 0";"SCAN=OFF";"(dataset)=DATASET"
    Do i=1 To 9999                          /* Make the changes fr-to */
        Interpret "xxx=fr"i";yyy=to"i
        If xxx='FR'i Then Leave
        "C '"xxx"' '"yyy"' ALL"
    End
    no_changes=i-1
    "F '//****' 1 FIRST"
    If rc/=0 Then Do
        zedlmsg='No "//****" found to insert data after. Manually',
                'insert and run again.'
        Address ISPEXEC "SETMSG MSG(ISRZ001)"
        Exit
    End
    "(lin#)=LINENUM .ZCSR"
    x=lb("//"Copies('*',69));"CURSOR=1 0";ss_c=0
    Do i=1 To no_changes
        Interpret "x=test_4_SYMSUB_disclaimer(to"i")";If ss_c Then Leave
    End
    "F '//****' 1 FIRST";"F '//****' 1 NEXT"
    Do i=1 To 9999                          /* Insert lines           */
        Interpret "xxx=iv"i
        If xxx='IV'i Then Leave
        x=lb(xxx)
    End
    x=c_MAXCC()

Signal get_out

lb:Parse Arg nl;"LINE_BEFORE .ZCSR=(nl)";Return 0
la:Parse Arg nl;"LINE_AFTER  .ZCSR=(nl)";Return 0
c_MAXCC:
    "CURSOR=1 0"
    Do Forever
        "F 'MAXCC'";If rc/=0 Then Leave
        "(line)=LINE .ZCSR";Parse VAR line w1 w2 .;Parse VAR w2 w2 +5 .
        If w1='SET' & w2='MAXCC' Then Nop;Else Iterate
        xx=Index(line,'S')
        line=Substr(' ',1,xx-1)'IF MAXCC<9 THEN SET MAXCC=0'
        "LINE .ZCSR=(line)"
        "(lin#)=LINENUM .ZCSR";"CURSOR="lin#+1 0
    End
    Return 0

test_4_SYMSUB_disclaimer:Parse Arg si_var
    If Index(si_var,'&')=0 Then Return 0
    Do Forever
        "F '"si_var"'";If rc/=0 Then Leave
        "(line)=LINE .ZCSR";Parse VAR line ch2 +2 .;If ch2='//' Then Iterate
        "F '//****' 1 FIRST";"(x)=LINENUM .ZCSR";"CURSOR=" x+1 0
        ss_c=1;"SYMSUB /DD";Leave
    End
    Return 0
Get_out:"CAPS=(caps)";"USER_STATE=(status)";Exit
