/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*                                                                    */
/* For further details refer to ISPFALOC.                             */
/*                                                                    */
/*--------------------------------------------------------------------*/
ISPFALO1:ISPFALO1_ver='2.01'
    Arg parm#
    Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#)";If rc=0 Then ima_macro=1

byp1:
    "CONTROL ERRORS CANCEL"

ini1:
    Parse Upper VAR parm# parm1 parm2 parm3 parm4
    ddname  =parm1
    table2  =parm2
    mp      =parm3
    find    =parm4
    panelid ='ISPFALC2'
    panelp  ='ISPFALC3'
    table   =table2
    keep    ='Y'
    all     ='ALL'
    msub    =0
    #sel    =
    dro2    ='ALL'
    "VGET ZSYSID ASIS"
    sid     =zsysid

    "VPUT (TABLE KEEP PANELID DDNAME ALL) SHARED"
    "SELECT PGM(DSNALLOC)"

    If find/=''  Then Do
        member=find
        Signal find
    End

skip:
dis1:
    zcmd=;memb=
    "TBDISPL "table2" PANEL("panelid")"
    If rc>4  Then Signal exit

    Parse VAR zcmd zcmd zcmdr .

    If zcmd='?' Then Do
        ddd='HELPDRVR SYSEXEC SYSPROC';tutememb='ISPFALDH'
        Do While ddd/=''
            Parse Var ddd dd ddd
            xxx=$ddalloc(dd)
            Do While xxx/=''
                Parse Var xxx xx xxx
                If Sysdsn("'"xx"("tutememb")'")='OK' Then Do
                    ddd=;Leave
                End
            End
        End
        x=helpdrvr(dd'='tutememb zcmdr);Signal dis1
    End

    Parse VAR zcmd cmd p1 p2
    If cmd='TRACE' Then Do
        Select
            When p1='OFF' Then Trace('N')
            When p1='ON'  Then Trace('R')
            Otherwise Do                    /* Toggle                 */
                #tr=Trace()
                If Verify(#tr,'NO')=0 Then Trace('R');Else Trace('N')
            End
        End
        #tr=Trace();Signal skip
    End
    If zcmd='KEEP' Then Do
        concfree='N'
        "VPUT (concfree) SHARED"
        Signal skip
    End
    If Wordpos(zcmd,'SORT /S')>0 Then Do
        If zcmdr='ALLOC' Then "TBSORT &TABLE2 FIELDS(SNUM,CA)"
        If zcmdr='DSN' Then "TBSORT &TABLE2 FIELDS(DSN,CA)"
        If zcmdr='VOL' Then "TBSORT &TABLE2 FIELDS(VOLUME,CA)"
        Signal skip
    End
    If zcmd='HMP' Then Do
        If \mp Then Do
            gc009="Help not available for MULTI-PROCS. "||,
                  "Re-execute with parameter 'MP'."
            "SETMSG MSG(GC009)"
            Signal dis1
        End
        "SELECT PGM(ISPTUTOR) PARM(CSMDSHX1)"
        "CONTROL ERRORS &CNTLACTN"
        Signal skip
    End
    If zcmd='TN' Then Do
        gc009="Table name in use is '"table2"'."
        "SETMSG MSG(GC009)"
        Signal skip
    End
    If zcmd='MP' Then Do
        If zcmdr='OFF' Then mp=0;Else mp=1
        Signal skip
    End
    If Wordpos(zcmd,'F FM EM EF BM BF VM VF')>0 Then Signal find
    If zcmd='FS' Then Do
        If zcmdr='ALL' Then Do
            dro2='ALL';dddisp='DD'
        End
        Else Do
            dddisp='DDNAME'
            " TBTOP &TABLE2"
            Do Forever
                "TBSKIP &TABLE2"
                If rc/=0 Then Leave
                If Index(dsn,zcmdr) > 0 Then ind='*';Else ind=
                "TBMOD &TABLE2"
            End
            "TBTOP &TABLE2";"TBVCLEAR &TABLE2"
            dro2='SCAN';ind=
            "TBSARG &TABLE2 NAMECOND(IND,NE)"
        End
        Signal skip
    End
    If zcmd/='' Then Do
        gc009 = "Invalid command."
        "SETMSG MSG(GC009)"
        Signal skip
    End

noz1:
    msub=ztdsels
    Signal con1

mus1:
    "TBDISPL &TABLE2"

con1:
    "CONTROL DISPLAY SAVE"
    vol="VOLUME("volume")"
    zdlvol=volume

    If sel='FC' Then Signal free
    If sel='=' Then sel=#sel;Else #sel=sel
    If sel='B' Then Signal BROW
    If (sel='BM' | sel='BF') & lfind/='' Then Signal browmemb
    If sel='S' Then Signal brow
    If sel='E' Then Do;act='EDIT';Signal edit;End
    If sel='V' Then Do;act='VIEW';Signal edit;End
    If sel='F' Then Signal free_DD
    If (sel='EM' | sel='EF') & lfind/='' Then Do
        act='EDIT'
        Signal editmemb
    End
    If (sel='VM' | sel='VF') & lfind/='' Then Do
        act='VIEW'
        Signal editmemb
    End
    If sel="X" Then Do
        "TBDELETE &TABLE2"
        Signal gnx1
    End
    If sel='SLM' Then Signal slm
    If sel='REV' Then Signal review
    If sel='ZAP' Then Signal zap_it


    "VPUT SEL SHARED"
    "SELECT CMD(%ISPFALUX &TABLE2)"
    rc1=rc

    If rc1=8 Then Do                        /* Error found in exit    */
        "CONTROL DISPLAY RESTORE"
        Signal skip
    End
    If rc1=0 Then Signal gnx1               /* Hit in exit proc OK    */

    If \mp Then Do
        Signal gnx1
    End

    If sel/='' Then Do
        gc009=
        "SETMSG MSG(GC009)"
        If sel='S' Then sel='B'
callqds:
        f00  =sel
        zcmd =f00
        If LENGTH(zcmd)>1 Then ,
          If Wordpos(zcmd,'CE CB CL')>0 | Substr(zcmd,1,2)='CC' Then Do
              szcmd=zcmd
              "VPUT (SZCMD) SHARED"
          End
        csdsn00="'"dsn"'"
        If volume/='' Then parms='V='volume
        "VPUT (MDS P0 F00 DD CSDSN00 PARMS) SHARED"
        parm1 ='%MDS'
        parm2 =p0
        parm3 =f00
        parm4 =csdsn00
        parm5 =Strip(parms)
        parm6 =parms1
        parm7 =szcmd
        parm8 =dd
        "SELECT CMD(%CSMDSN" parm1 parm2 parm3,
            parm4 parm5 parm6 parm7 parm8")"
        Signal gnx1
    End
gnx1:
    "CONTROL DISPLAY RESTORE"
    msub=msub-1;If msub<1 Then Signal skip;Else Signal mus1

exit:
    "TBCLOSE &TABLE2"
    EXIT

edit:
    If memb='' Then "&ACT DATASET('&DSN') &VOL"
    Else            "&ACT DATASET('&DSN(&MEMB)') &VOL"
    If quit='YES' Then msub=0
    If rc>8 Then "SETMSG MSG(&ZERRMSG)"
    Signal gnx1
editmemb:
    "&ACT DATASET('&DSN(&LFIND)') &VOL"
    If quit='YES' Then msub=0
    If rc>8 Then "SETMSG MSG(&ZERRMSG)"
    Signal gnx1

brow:
    "CONTROL ERRORS RETURN"
    If memb='' Then "BROWSE DATASET('&DSN') &VOL"
    Else            "BROWSE DATASET('&DSN(&MEMB)') &VOL"
    If quit='YES' Then msub=0
    If rc>4 Then "SETMSG MSG(&ZERRMSG)"
    Signal gnx1

browmemb:
    "CONTROL ERRORS RETURN"
    "BROWSE DATASET('&DSN(&LFIND)') &VOL"
    If quit='YES' Then msub=0
    If rc>4 Then "SETMSG MSG(&ZERRMSG)"
    Signal gnx1

slm:
    slmm="'"dsn"("lfind")'"
    If Sysdsn(slmm)/='OK' Then Do
        gc009=slmm 'not found.'
        "SETMSG MSG(GC009)"
        msub=0
        Signal gnx1
    End
    Address TSO '%SLM' slmm
    Signal gnx1

Review:
    Address TSO "REV '"dsn"'"
    "CONTROL DISPLAY REFRESH"
    Signal gnx1


zap_it:
    Address TSO zap "'"dsn"'" volume
    "CONTROL DISPLAY REFRESH"
    Signal gnx1

find:
    lfind=member
    "TBTOP &TABLE2";"TBQUERY &TABLE2 ROWNUM(#row)"
    "VPUT (ZWINTTL)";"ADDPOP ROW(5) COLUMN(6)"
    nat='ABCDEFGHIJKLMNOPQRSTUVWXYZ01234567890@#$.,'
    yyy=Copies('y',Length(nat))
    #row=#row+0;addpop=1;#dsn=
    #tr=Trace('o')
    Do While rc=0
        "TBSKIP &TABLE2 ROWID(row#)"
        If rc/=0 Then Leave
        "LMINIT DATAID(MS2) DATASET('&DSN') ENQ(SHR) ORG(DSO)"
        If #tr/='N' Then Say rc dsn
        If rc=0 Then Do
            If dso/='PO' Then Do
                "LMFREE DATAID(&MS2)";Iterate
            End
            check=;ind=
            "LMOPEN DATAID(&MS2) OPTION(INPUT)"
            If rc=0 Then
                If pos('*',lfind)>0 | pos('%',lfind)>0 Then,
                    "LMMLIST DATAID(&MS2) MEMBER(CHECK)",
                             "PATTERN(&LFIND) OPTION(LIST)"
                Else "LMMFIND DATAID(&MS2) MEMBER(&LFIND)"
            If rc=0 Then Do
                ind='*'
                If Wordpos(dsn,#dsn)=0 Then #dsn=#dsn || dsn'  '
            End
            If addpop Then Do
                row#=row#+0
                da1=#dsn
                sh1=Translate(#dsn,yyy,nat)
                "CONTROL DISPLAY LOCK"
                "DISPLAY PANEL(&PANELP)"
                If rc>8 Then addpop=0
            End
            "LMFREE DATAID(&MS2)"
        End
        "TBMOD &TABLE2"
/*--------------------------------------------------------------------*/
/*--------------------------------------------------------------------*/
    End
    x=Trace(#tr)
    If addpop Then "REMPOP"
    Signal skip

free_DD:
    f.0 = 0

    msub = ztdsels
    table3 = 'TB'Time('S')
    "TBCREATE &TABLE3 KEYS(DSN) NOWRITE"
    Signal con2

mus2:
    "TBDISPL &TABLE2"

con2:
    "CONTROL DISPLAY SAVE"
    "TBADD &TABLE3"

gnx2:
    "CONTROL DISPLAY RESTORE"
    msub = msub - 1
    If msub < 1 Then  Signal build
    Else              Signal mus2

build:
    Address TSO
    tempdsn = "'"USERID()".$REALLOC.$$'"
    ddt     = "DD"Time('s')
    "ALLOC DD("ddt") DSN("tempdsn") SPACE (1,1) TRACKS DSORG(PS)",
        "RECFM (F B) RECFM(F B) LRECL(80) BLKSIZE(6160) NEW CATALOG"
    If rc /= 0 Then Do
        gc009 = "Dataset "tempdsn" already exixts. Delete and",
                "re-execute command."
        Address ISPEXEC
        "SETMSG MSG(GC009)"
        Signal skip
    End

    "NEWSTACK"
    Address ISPEXEC "TBTOP &TABLE2"
    Queue Substr("    ALLOC F("ddname") DA(",1,71)"+"
    Do Forever
        Address ISPEXEC "TBSKIP &TABLE2"
        If rc /= 0 Then Leave
        Address ISPEXEC "TBSCAN &TABLE3"
        If rc = 0 Then Iterate
        Queue Substr("                        '"dsn"'",1,71)"+"
    End
    Address ISPEXEC "TBEND &TABLE3"
    Queue "                       ) SHR REU"
    Queue "    DELETE "tempdsn
    Queue "    %ISPF"
    Queue ''

/*  k = QUEUED()                                                      */
/*  Trace Off                                                         */
/*  Do i=1 to k                                                       */
/*      PULL line                                                     */
/*      Say line                                                      */
/*  End                                                               */

    "EXECIO * DISKW "ddt" (FINIS"
    "FREE F("ddt")"

    "DELSTACK"
    Address ISPEXEC
    "CONTROL DISPLAY SAVE"
    "EDIT DATASET(&TEMPDSN)"
    "CONTROL DISPLAY RESTORE"
    gc009 = "Under native TSO, enter 'EXEC $REALLOC'."
    "SETMSG MSG(GC009)"

    Signal skip

exi1:
    EXIT
