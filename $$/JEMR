/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* JEMR: JEM Reverse engineer.                                        */
/*                                                                    */
/* Take the output of the JEM commands and create legible JCL.        */
/* Copies member being edited, hits it with JEM, then issues message  */
/* to MD lines and reissue the "JEMR" command.                        */
/*                                                                    */
/*--------------------------------------------------------------------*/
JEMR:jemr_ver='1.06'
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS"
    If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    Parse SOURCE . . xcmd .
    If Wordpos('C3',Translate(parm#))>0 Then Do
        C3=1;parm#=Delword(parm#,Wordpos('C3',Translate(parm#)),1)
    End;Else c3=0
    Address ISREDIT
    "(member)=MEMBER";"(dataset)=DATASET"
    "(status)=USER_STATE";"(caps)=CAPS";"CAPS=OFF"
    Address ISPEXEC "VGET (jemr) SHARED";Interpret "Signal Macro_"jemr
Macro_:
    jemr=;Address ISPEXEC "VPUT (jemr) SHARED"
/*  ztempf=$Ztempf()                                                  */
/*  x=$Copymem("'"dataset"("member")'","'"ztempf"'")                  */
    Address TSO "NEWSTACK"
    "(last)=LINENUM .ZLAST"
    #tr=Trace('o')
    Do i=1 To last
        "(line)=LINE" i;Queue line
    End
    x=Trace(#tr)                            /* Restore trace setting  */
    Queue ''
    dd='EI'Time('s');ztempf=$ztempf()
    Address TSO "ALLOC F("dd") DSN('"ztempf"') SHR REU"
    Address TSO "EXECIO * DISKW "dd" (FINIS"
    Address TSO "FREE F("dd")"
    zedlmsg='Issue line command "MD9999" on line 1 and then enter',
            '"'xcmd'" on the command line.'
    Address ISPEXEC "SETMSG MSG(ISRZ001)"
    jemr=1;Address ISPEXEC "VPUT (jemr) SHARED"
    Address ISPEXEC "EDIT DATASET('"ztempf"') MACRO(JEM)"
    ztempf=$Ztempf(ztempf,'/D')
    jemr=;Address ISPEXEC "VPUT (jemr) SHARED"
    Exit

Macro_1:
    jemr=;Address ISPEXEC "VPUT (jemr) SHARED"
    If member/='' Then Do
        zedlmsg='In a member.  Processing abandoned.'
        Address ISPEXEC "SETMSG MSG(ISRZ001)"
        Exit
    End
    "FIND '//' 1 FIRST"
    "(lin#)=LINENUM .ZCSR"
    If lin#>1 Then Do
        "LABEL" lin#-1"=.MARKT"
        "X ALL .ZFIRST .MARKT"
    End
    "X '*ADVISORY' 1 ALL"
    "X '----+----1----+----2----+----3----+----4----+----5----+----6' ALL"
    "CURSOR=1 0"
    Do Forever
        "F '  I1--' 1 NEXT";If rc/=0 Then Leave
        "(lin#)=LINENUM .ZCSR";"(line)=LINE" lin#-1
        line=Overlay('*',line,7,1)
        "LINE" lin#-1"=(line)"
        "XSTATUS" lin#-1"=X"
    End
    "CURSOR=1 0"
    Do Forever
        "F '  I2--' 1 NEXT";If rc/=0 Then Leave
        "(lin#)=LINENUM .ZCSR";"(line)=LINE" lin#-1
        line=Overlay('*',line,7,1)
        "LINE" lin#-1"=(line)"
        "XSTATUS" lin#-1"=X"
    End
    "CURSOR=1 0"
    Do Forever
        "F '  I3--' 1 NEXT";If rc/=0 Then Leave
        "(lin#)=LINENUM .ZCSR";"(line)=LINE" lin#-1
        line=Overlay('*',line,7,1)
        "LINE" lin#-1"=(line)"
        "XSTATUS" lin#-1"=X"
    End
    "CURSOR=1 0"
    Do Forever
        "F '  P1--' 1 NEXT";If rc/=0 Then Leave
        "(lin#)=LINENUM .ZCSR";"(line)=LINE" lin#-1
        line=Overlay('*',line,7,1)
        "LINE" lin#-1"=(line)"
        "XSTATUS" lin#-1"=X"
    End
    "CURSOR=1 0"
    Do Forever
        "F '  P2--' 1 NEXT";If rc/=0 Then Leave
        "(lin#)=LINENUM .ZCSR";"(line)=LINE" lin#-1
        line=Overlay('*',line,7,1)
        "LINE" lin#-1"=(line)"
        "XSTATUS" lin#-1"=X"
    End
    "SNAP /T1"
    "C '  I1--' '  I1//' ALL"
    "C '  I2--' '  I1//' ALL"
    "C '  I3--' '  I1//' ALL"
    "C '  I1$$' '  I1//' ALL"
    "C '  I2$$' '  I1//' ALL"
    "C '  P1--' '  P1//' ALL"
    "C '  P2--' '  P1//' ALL"
    "C '  P1$/' '  P1//' ALL"
    "C '  P2$/' '  P1//' ALL"
    Do i=1 To 5
        "C '  I"i"//' '//' ALL"
        "C '  P"i"//' '//' ALL"
    End
    "SNAP /R1"
    "X '  I1' 1 4 ALL"
    "X '  I2' 1 4 ALL"
    "X '  I3' 1 4 ALL"
    "X '  P1' 1 4 ALL"
    "X '  P2' 1 4 ALL"
    "X '  P3' 1 4 ALL"
    "DEL ALL X"

exi1:
    "USER_STATE=(status)";"CAPS=(caps)"
    If c3 Then "ISREDIT C3 AE"
    Exit

nl:Parse Arg n#
    "LINE_BEFORE .ZFIRST=NOTELINE (n#)";Return 0
