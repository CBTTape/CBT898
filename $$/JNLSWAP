/******************************** REXX ********************************/
/**********************************************************************/
/* TRACE r                                                            */
/*--------------------------ooo000OOOO000ooo--------------------------*/
/*                                                                    */
/* Macro to process CICS message log and calculate the interval       */
/* between system journal swaps and report on them.                   */
/*                                                                    */
/* Use parameter of '/T' to just display the swap traces.             */
/* Use parameter of '/2' to acces journal 02. (Default is the sytem   */
/* journal '/S'.)                                                     */
/*                                                                    */
/* Either get the output using the TSO OUTPUT command or use the      */
/* following SDSF PRINT commands to a predefined dataset:             */
/*       PRINT ODSN $SDSF.OUTPUT * SHR                                */
/*       PRINT 1 999999                                               */
/*       PRINT CLOSE                                                  */
/*       TSO EOSDSF                                                   */
/*                                                                    */
/*--------------------------------------------------------------------*/
    Arg parm#
    If parm# /= '' Then Signal byp1
    Address ISPEXEC
    "CONTROL ERRORS RETURN"
    "ISREDIT MACRO (parm#)"
    If rc > 0 Then SIGNAL byp1

byp1:
    cactn = "RETURN"
/*  cactn = "CANCEL"                                                  */
    "ISPEXEC CONTROL ERRORS" cactn

ini1:
    Parse Upper VAR parm# parm1 parm2 parm3
    If parm1 = "HELP" | parm1 = "?" Then Do
        "SELECT PGM(ISPTUTOR) PARM(HNOHELP)"
        Exit
    End
    "ISREDIT (status) = USER_STATE"

pro1:
    find = 'CICS SYSTEM LOG.'
    If Datatype(Substr(parm1,2)) = 'NUM' Then
        find = 'CICS JOURNAL' Right(Substr(parm1,2),2,'0')
    If Datatype(Substr(parm2,2)) = 'NUM' Then
        find = 'CICS JOURNAL' Right(Substr(parm2,2),2,'0')
    If parm1 = '/S' | parm2 = '/S' | parm1 = '/1' | parm2 = '/1' |,
       parm1 = '/01' | parm2 = '/01' Then ,
        find = 'CICS SYSTEM LOG.'

    If parm1 = '/T' | parm2 = '/T' Then Do
        "ISREDIT X ALL"
        "ISREDIT FIND '"find"' ALL"
/*      "ISREDIT DEL ALL X"                                           */
        "ISREDIT USER_STATE = (status)"
        Exit
    End

pro2:
    "ISREDIT FIND '"find"' FIRST"
    If rc ^= 0 Then Do
        gc009 = 'No SYSTEM log swaps detected.'
        'SETMSG MSG(GC009)'
        Exit
    End
    "ISREDIT (line) = LINE .ZCSR"
    Parse Var line hh '.' mm '.' ss .
    t1   = ss + (mm*60) + (hh*60*60)
    li   = 86400
    lt   = 'N/A'
    mxi  = 1
    inc. = 0
    gra. = ''
    tots = 0
    Do Forever
        "ISREDIT FIND '"find"' NEXT"
        If rc ^= 0 Then Leave
        "ISREDIT (line) = LINE .ZCSR"
        Parse Var line hh '.' mm '.' ss .
        t2 = ss + (mm*60) + (hh*60*60)
        d  = t2 - t1
        t1 = t2
        If d < 0 Then d = d + 86400
        dx = d % 60
        If d//60 > 30 Then dx = dx + 1
        If dx = 0 Then dx = 1
        If dx < li Then Do
            li = dx
            lt = hh'.'mm'.'ss
        End
        If dx > mxi Then mxi = dx
        inc.dx = inc.dx + 1
        gra.dx = gra.dx'*'
        tots = tots + 1
    End

    Address TSO "CLEAR"
    Say Centre('oooOOO000OOOO000OOOooo',79,'-')
    Say 'Number of journal swaps in specific intervals.'
    Say 'Total of' tots 'swaps with worst at' lt'.'
    Say '             ----+----1----+----2----+----3----+----4----+----5'
    dl  = 'N'
    st  = 0
    pcp = 0
    Do i=1 To mxi
        st = st + inc.i
        If dl = 'N' & i//15=1 & st > 0 & i^=1 & st ^= st# Then Do
            pc = 100*st%tots
            pcd = pc - pcp
            pcp = pc
            Say '---- Sub total' Right(st,3) '('Right(pcd,3)'% /'||,
                  Right(pc,3)'%) ----'
            st# = st
        End
        If dl = 'N' & i > 59 Then Do
            dl = 'Y'
            Say '--------- Rest are OK --------------'
        End
        If inc.i > 0 Then
            Say Right(i,3)' Mins' Right(inc.i,3) gra.i
    End
    If tots > 0 Then pc = 100*st%tots
    Else             pc = 100
    Say '---- Sub total' Right(st,3) '('Right(pcd,3)'% /'||,
          Right(pc,3)'%) ----'

end1:
    "ISREDIT USER_STATE = (status)"

ext1:
    Exit

