/******************************** REXX ********************************/
n_st=mark(2)    /* Mark Start */                                  /*QH*/
/*                                                                /*QH*/
/*--------------------------ooo000OOOO000ooo--------------------------*/
/*                                                                    */
/* Member : KM (Kill Member).                                         */
/*                                                                    */
/* This routine is designed to be issued from the command line of a   */
/* PDS(member) being browsed/edited.  It will delete the member being */
/* edited/browsed.                                                    */
/*                                                                    */
/* Take care as deletions are permanent especially with the "/NP"     */
/* parameter whereby the cowboy operator's failsafe deletion          */
/* confirmation screen is bypassed.                                   */
/*                                                                    */
/* GOTCHAs:                                                           */
/* 1. If you have "KM" set up in a command table and in an edit/view  */
/*    session, use the ">KM" edit command otherwise you will remain   */
/*    in the session with the member in memory even though it has     */
/*    been deleted.  As a result the member is subject to any ISPF    */
/*    default "save" processing and the deletion may inadvertantly be */
/*    reversed.                                                       */
/*                                                                    */
/* Parameters:                                                        */
/*   /P  - Prompt for deletion (default).                             */
/*   /NP - Do not prompt for deletion.                                */
/*   /DELALIAS(member)                                                */
/*       - Called by the "ALIAS" routine to delete alias if it        */
/*         exists.                                                    */
/*                                                                    */
/* The following members are required to package this routine:        */
/*   REXX:                                                            */
/*     KM       - Driver                                              */
/*                                                                    */
/*   Panels:                                                          */
/*     $MEMBDEL - Member delete confirmation panel.                   */
/*                                                                    */
/*--------------------------------------------------------------------*/
*/                                                                /*QH*/
n_en=mark(-2)   /* Mark End   */                                  /*QH*/
Address ISPEXEC "CONTROL ERRORS RETURN";ima_macro=0               /*QH*/
    Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    ARG parm#;If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#)"
    IF rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */
    Parse Upper VAR parm# parm#

/*--------------------------------------------------------------------*/
/*                                                                    */
/* Test APPLID and if not the default edit environment, reinvoke this */
/* routine using the appropriate edit environment thus ensuring       */
/* normal PF Key settings.                                            */
/*                                                                    */
/*--------------------------------------------------------------------*/
ini0:
If Word(parm#,1)="?" Then Signal Help_me;Else Signal No_help_#1   /*QH*/
Mark:If Datatype(Arg(1))/="NUM" Then Arg(1)=1;Return sigl+Arg(1)  /*QH*/
Help_me:"ISREDIT (m)=MEMBER";rcm=rc;#tr=Trace("o")                /*QH*/
Do i=n_st To n_en;nl=Sourceline(i)                                /*QH*/
If rcm=0 Then "ISREDIT LINE_BEFORE 1=NOTELINE (nl)"               /*QH*/
Else Say Substr(nl,1,79);End;Return 0                             /*QH*/
                                                                  /*QH*/
No_help_#1:                                                       /*QH*/
    zedsmsg=
    If Wordpos(Word(parm#,1),'/? ? /H HELP')>0 Then Do
        "SELECT PGM(ISPTUTOR) PARM(KMH000)"
        Exit
    End

    If ima_macro Then Do
        "ISREDIT (jdsname)=DATASET";"ISREDIT (member)=MEMBER"
    End
    Else Do
        "VGET (zscreeni zscreenc)"
/*      zscreeni=$screenc('ASIS')                                     */
        headline=Substr(zscreeni,161,80)
        Parse VAR headline . jdsname '(' member ')' .
        jdsname=Strip(jdsname);member=Strip(member)
    End
    If Wordpos(Word(parm#,1),'/F /NP')>0 Then kill_prompt=0;Else kill_prompt=1
    If kill_prompt Then Do
        qdsname="'"jdsname"'";zwinttl='KillMemb Process';#popr=6;#popc=12
scr1:
        zcmd=
        zedsmsg=;zedlmsg=;"SETMSG MSG(ISRZ001)"
        "ADDPOP ROW(&#POPR) COLUMN(&#POPC)"
        "DISPLAY PANEL($MEMBDEL)";rc1=rc;"REMPOP"
        If rc1/=0 Then Do
            zedlmsg="Delete of member "member" abandoned."
            Signal ext1
        End
        If zcmd='BROWSE' Then Do
            "BROWSE DATASET('&JDSNAME(&MEMBER)')"
            Signal scr1
        End
    End
    Else Do
        Parse Var parm# . '/DELALIAS(' xx ')';If xx/='' Then member=Strip(xx)
    End

    "LMINIT DATAID(KILLID) DATASET('&JDSNAME') ENQ(SHRW)"
    "LMOPEN DATAID(&KILLID) OPTION(OUTPUT)"
    "LMMDEL DATAID(&KILLID) MEMBER(&MEMBER) NOENQ"
    "LMFREE DATAID(&KILLID)"
    zedsmsg='Member deleted'
    zedlmsg="'"dsn"("member")' deleted."
    "SETMSG MSG(ISRZ001)"
    "CONTROL DISPLAY REFRESH"
    If ima_macro Then "ISREDIT CANCEL"
    Else "CONTROL NONDISPL END"

ext1:
    Exit
