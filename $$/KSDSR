/*--------------------------ooo000 REXX 0ooo--------------------------*/
/*  KSDSR:                                                            */
/*  This routine will accept paramaters process a CSD dataset.        */
/*  Use this as a generic interface for indexed reads on KSDS datasets*/
/*  using a key provided.                                             */
/*                                                                    */
/*  Parameters:                                                       */
/*  1. Operation. R-Read, O-Open, T-Test or C-Close (any other        */
/*  char-Close).                                                      */
/*  2. DD to process. Not required for 'T' operation.                 */
/*  3. For operation:                                                 */
/*     R -Key of record to be read    : Returns data in stack         */
/*     O -DSNAME of CSD dataset       : Returns 1 if opened, else 0.  */
/*     T -Not required and is ignored : Returns 1. Syntax error in    */
/*                                      caller if unavailable.        */
/*     C -not required and is ignored : Returns with RC of DD free    */
/*                                      operation.                    */
/*  4. Reserved for future expansion.                                 */
/*--------------------------------------------------------------------*/
KSDSR:KSDSR_ver='1.02'
    Parse ARG oper,dd,p3,p4
    Address TSO
    oper=Translate(oper)
    Select
        When (oper='R') Then Signal KSDSR_Read_file
        When (oper='O') Then Return KSDSR_Alloc_Open_file()
        When (oper='T') Then Return 1
        Otherwise Signal KSDSR_Close_file
    End

KSDSR_Read_file:
    Address TSO "EXECIOVS 1 KSDSR"  dd "(NNOTIFY NLOG NCONT NTRACE) KEY("p3")"
    Return rc

KSDSR_Alloc_Open_file:p3=Strip(p3,,"'");
    If p3='' Then Return 0
    If Sysdsn("'"p3"'")/='OK' Then Return 0
    "ALLOC F("dd") DSN('"p3"') SHR REU"
    If rc/=0 Then Do
        Say "Allocate failed for '"p3"' with RC="rc".";Return 0
    End
KSDSR_Open_file:"EXECIOVS 0 KSDSRF" dd "(OPEN LOG NCONT NTRACE)"
    If rc/=0 Then Do;Call KSDSR_Report;Return 0;End;Return 1
KSDSR_Close_file:"EXECIOVS 0 KSDSRN" dd "(FINIS NOTIFY NLOG NCONT NTRACE) "
    If rc/=0 Then Call KSDSR_Report
KSDSR_Free_file:"FREE F("dd")";Return rc
KSDSR_Report:s_k=Queued();Do s_k;Parse PULL line;Say line;End;Return 0
