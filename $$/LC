/******************************** REXX ********************************/
/**********************************************************************/
/* TRACE r                                                            */
/*--------------------------ooo000OOOO000ooo--------------------------*/
/*                                                                    */
/* LC: Long Change.                                                   */
/*                                                                    */
/* Issue a change "from.string" to "to.string" that will not fit on   */
/* the command line.                                                  */
/*                                                                    */
/*--------------------------------------------------------------------*/
    Arg parm#
    Address ISPEXEC "CONTROL ERRORS RETURN"
    Address ISREDIT
    "MACRO (parm#) NOPROCESS"
    "(status)=USER_STATE"

    fstring=;tstring=;parms=;csr1=
    ch1=Substr(parm#,1,1)
    If ch1="'" | ch1='"' Then fstring=$gqd(parm#);Else Parse Var parm# fstring .
    parm#=Strip(Substr(parm#,Length(fstring)+1))
    ch1=Substr(parm#,1,1)
    If ch1="'" | ch1='"' Then tstring=$gqd(parm#);Else Parse Var parm# tstring .
    parms=Strip(Substr(parm#,Length(tstring)+1))

    If parms='' Then parms='all'
    If fstring/='' & tstring/='' Then csr1='PARMS'
scr1:
    Address ISPEXEC "DISPLAY PANEL(LCP) CURSOR(&CSR1)"
    If rc/=0 Then Do
        zedsmsg=;zedlmsg='Long change entry screen abandoned.'
        Signal ext1
    End
    fstring=set_delim('fstring')
    tstring=set_delim('tstring')
    "C" fstring tstring parms
ext1:
    "USER_STATE=(status)"
    Address ISPEXEC "SETMSG MSG(ISRZ001)"
    Exit

set_delim: Interpret "string="Arg(1)
    ch1=Substr(string,1,1)
    Select
        When ch1="'" Then string=Strip(string,,"'")
        When ch1='"' Then string=Strip(string,,'"')
        Otherwise Nop
    End
    sep="'"
    If Index(string,"'")>0 Then Do
        If Index(string,'"')>0 Then Do
            zedsmsg=;zedlmsg='Cannot have both '' and " in the string.'
            Address ISPEXEC "SETMSG MSG(ISRZ001)"
            csr1=Arg(1);Signal scr1
        End
        sep='"'
    End
    If Index(tstring,'"')>0 Then Do
        If Index(tstring,"'")>0 Then Do
            zedsmsg=;zedlmsg='Cannot have both '' and " in the string.'
            Address ISPEXEC "SETMSG MSG(ISRZ001)"
            csr1=Arg(1);Signal scr1
        End
    End
    Return sep || string || sep
