/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*  LINKL:                                                            */
/*                                                                    */
/* Allocate all the datasets in the link list to a DD and present it  */
/* under ISPFALOC for subsequent processing.                          */
/*                                                                    */
/* Parameter:                                                         */
/* Keyword:                                                           */
/*   /CC     - Check catalog.  If not catalogued it will be ignored   */
/*             and set up in a message.                               */
/* Positional:                                                        */
/*   member  - Optional parameter is member to be found.              */
/*                                                                    */
/*--------------------------------------------------------------------*/
LINKL:linkl_ver='1.03'
Arg parm#
    Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#)"
    If rc=0 Then ima_macro=1
byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    If Wordpos('/CC',Translate(parm#))>0 Then Do
        catc=1;parm#=Delword(parm#,Wordpos('/CC',Translate(parm#)),1)
    End;Else catc=0
    Parse Upper VAR parm# parm1 parm2 parm3 trace
    If parm1/='' Then Do
        If $vername(parm1)/=0 Then Do
            gc009="Invalid name format of the supplied parameter."
            "SETMSG MSG(GC009)"
            Exit
        End
        find='FIND('parm1')'
    End
    Else find=

/*                                                                    */
/*      Define variables                                              */
/*                                                                    */
    cvt=STORAGE(10,4)                                /* POINT TO CVT */
    cvtllta=1244                 /* X'04DC' - OFFSET INTO CVT FOR LLT */
    llt=Storage(D2X(C2D(CVT)+cvtllta),4)              /* POINT TO LLT */
/*                                                                    */
/*      Get link list entries                                         */
/*                                                                    */
                                               /* GET LLT ENTRY COUNT */
    Parse Value '' With dsname dsname_nc
    lltcount = C2D(STORAGE(D2X(C2D(llt)+4),4))
    lltptr = C2D(llt)+8                      /* INIT PTR TO LLT ENTRY */
    #tr=Trace('o')
    Do lltcount
        dsn="'"Strip(Storage(D2X(lltptr+1),44))"'" /* PRINT LLT ENTRY */
        If catc Then Do
            If Verify(#tr,'R','M')>0 Then Say Sysdsn(dsn) dsn
            If Sysdsn(dsn)='OK' Then dsname=dsname dsn
            Else Do
                If Verify(#tr,'R','M')>0 Then Say 'Excluding ->' dsn
                dsname_nc=dsname_nc dsn
            End
        End
        Else dsname=dsname dsn
        lltptr=lltptr+45                       /* POINT TO NEXT ENTRY */
    End
    x=Trace(#tr)

    ddname="$C"Time('s')
    #3m=Msg('On');x=Outtrap('lineo.',500,'NOCONCAT')
    Address TSO "ALLOC F("ddname") DA("dsname") SHR REUSE"
    rc1=rc;x=Outtrap('Off')
    If rc1/=0 Then Do
        Do i=1 To lineo.0;zedlmsg=zedlmsg lineo.i;End
        zedlmsg=Space(zedlmsg)'. -> Try again with "/CC" parameter.'
        "SETMSG MSG(ISRZ001)"
        Exit
    End
    Else zedlmsg="Datasets concatenated to DDname '"ddname"'."
    If dsname_nc/='' Then zedlmsg=zedlmsg 'The following uncatalogued',
        'datasets have been excluded:' dsname_nc'.'
    "SETMSG MSG(ISRZ001)";"VPUT DDNAME SHARED"
    Address TSO "%ISPFALOC DDNAME("ddname")" find
    Address TSO "FREE F("ddname")"
    Exit
