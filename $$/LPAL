/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* LPAL:                                                              */
/*                                                                    */
/* Allocate all the datasets in the LPA list to a DD and present it   */
/* under ISPFALOC for subsequent processing.                          */
/*                                                                    */
/*--------------------------------------------------------------------*/
LPAL:lpal_ver='1.05'
    Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    Arg parm#;If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#)"
    If rc=0 Then ima_macro=1

byp1:
/*  "ISPEXEC CONTROL CANCEL"                                          */
    Parse Upper VAR parm# parm#
    Parse Upper VAR parm# parm1 parm2 parm3 trace

ini1:
    zedsmsg=
    If parm1/='' Then Do
        If $vername(parm1)/='OK' Then Do
            zedlmsg='Invalid name format of the supplied parameter',
                  '"'parm1'".'
            "SETMSG MSG(ISRZ001)"
            Exit
        End
        find='FIND('parm1')'
    End
    Else find=

/*--------------------------------------------------------------------*/
/*                                                                    */
/*      Define variables                                              */
/*                                                                    */
/*--------------------------------------------------------------------*/
    cvt = Storage(10,4)                               /* POINT TO CVT */
    cvtsmext = 1196              /* X'04AC' - OFFSET INTO CVT FOR SME */
    smext = Storage(D2X(C2D(CVT)+cvtsmext),4)         /* POINT TO SME */
    cvteplps = 56                /* X'0038' - OFFSET INTO VSX FOR LPA */
    lpa = Storage(D2X(C2D(smext)+cvteplps),4)         /* POINT TO LPA */
/*--------------------------------------------------------------------*/
/*                                                                    */
/*      Get link list entries                                         */
/*                                                                    */
/*--------------------------------------------------------------------*/
                                               /* GET LPA ENTRY COUNT */
    dsname=
    lpacount=C2D(Storage(D2X(C2D(lpa)+4),4))
    lpaptr=C2D(lpa)+8                        /* INIT PTR TO LPA ENTRY */
    Do lpacount
        dsn=Storage(D2X(lpaptr+1),44)              /* PRINT LPA ENTRY */
        dsname=dsname "'"Strip(dsn)"'"
        lpaptr=lpaptr+45                       /* POINT TO NEXT ENTRY */
    End
    ddname="$C"Time('s');retried=0
alloacte:
    x=Msg('On');x=Outtrap('line.',500,'NOCONCAT')
    Address TSO "ALLOC F("ddname") DA("dsname") SHR REUSE"
    rc1=rc;x=Outtrap('Off')
    If rc1/=0 Then Do
        zedlmsg=line.1;"SETMSG MSG(ISRZ001)"
        If retried Then Exit
        retried=1
        xxx=dsname;dsname=
        Do While xxx/=''
            Parse VAR xxx xx xxx
            If Sysdsn(xx)='OK' Then dsname=dsname xx
            Else Say "Dataset" xx "not found. Excluded from concatenation."
        End
        dsname=Space(dsname)
        Signal alloacte
    End
    Else zedlmsg="Datasets concatenated to DDname '"ddname"'."
    "SETMSG MSG(ISRZ001)"
    "VPUT DDNAME SHARED"
    Address TSO "%ISPFALOC DDNAME("ddname")" find
    Address TSO "FREE F("ddname")"
    Exit
