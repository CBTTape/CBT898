/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* LURX1:                                                             */
/*                                                                    */
/* REXX to process the offloaded RACF database and extract a          */
/* particular record type.                                            */
/*                                                                    */
/* Parameters:                                                        */
/*   1. RACF offload dataset name.                                    */
/*   2. Record type.                                                  */
/*   3. Extract dataset name prefix. Will append '.XRTrtype'.         */
/*   4. Extract file LRECL. If not numeric the proper record length   */
/*      will be used from the matrix at the bottom of this routine.   */
/*      (Note: Full record length is unnecessary if not all record is */
/*             used, hence this feature.)                             */
/*   5. Trace flag (Y). Useful to keep some sort of progress when     */
/*      processing large datasets.                                    */
/*                                                                    */
/* For a listing of RACF offloaded database record layout fields etc  */
/* refer to dataset 'siteid.JCL.CNTL(RACFDBFF)'.                      */
/*                                                                    */
/*--------------------------------------------------------------------*/
LURX1:lurx_ver='1.03'
    ARG parm#
    Address TSO

    Parse VAR parm# ifile rtype opref olrecl trace .

    #pr=5000                                /* No records per read    */
    #pw=1000                                /* No records per write   */
    #tl=1000                                /* Trace limit reporter   */

    rtype=Right(rtype,4,'0')

    Call set_vars

    xx=Index(string,rtype' ')
    If xx=0 Then Do
        Say 'Invalid record type' rtype 'entered. Correct and re-run..'
        Exit
    End

    If \Datatype(olrecl,'w') Then Do
        xx=Substr(string,xx,15)
        Parse VAR xx '(' olrecl ')' .
        olrecl=olrecl+0
    End

    xx=Time('s');ddi='IP'xx;ddo='OP'xx
    ifile="'"ifile"'";ofile="'"opref".XRT"rtype"'"
    x=Listdsi(ifile)
    If x/=0 Then Do
        Say ifile Sysdsn(ifile);Exit
    End

    "ALLOC F("ddi") DA("ifile") SHR REU"
    rc1=rc
    #m=Msg('Off');"DELETE "ofile;x=Msg(#m)
    Signal on Syntax Name error_off_$ALKUVOL
    unitvolp="UNIT(SYSALLDA)";unitvolp=$ALKUVOL('SMS')
error_off_$ALKUVOL:Signal off Syntax
    "ALLOC F("ddo") DA("ofile") MOD CATALOG SPACE(30,30)",
           unitvolp "TRACKS RECFM(F B) LRECL("olrecl")",
           "BLKSIZE(0)"
    j=0;k=0;l=0
    #tr=Trace('o')
    Do While rc1/=2
        "EXECIO "#pr" DISKR "ddi" (STEM linei. OPEN)"
        rc1=rc
        If #tr='R' Then Do
            Say 'Looping' linei.0
            k=k+linei.0
        End
        Do i=1 To linei.0
            If #tr='R' Then Do
                If i//#tl=1 Then Do
                Say 'R('Right(k+i,8)'), W('Right(l,8)')',
                    Substr(linei.i,1,70)
                End
            End
            If Substr(linei.i,1,4)/=rtype Then Iterate
            j=j+1;lineo.j=linei.i
            If j=#pw Then Do
                If #tr='R' Then Do
                    l=l+j
                    Say 'Writing' l Substr(lineo.j,1,70)
                End
                "EXECIO "j" DISKW "ddo" (STEM lineo. FINIS"
                j=0
            End
        End
    End
    j=j+1;lineo.j='End of file'
    If #tr='R' Then Do
        l=l+j
        Say 'Writing' l Substr(lineo.j,1,70)
    End
    "ALLOC F("ddo") DA("ofile") MOD REU RELEASE"
    "EXECIO "j" DISKW "ddo" (STEM lineo. FINIS)"
    "EXECIO 0 DISKR "ddi" (STEM linei. FINIS)"
    "FREE F("ddi ddo")"

    Exit

set_vars:
    string =,
       '0100  (  357)  Group Basic Data                     ',
       '0101  (   22)  Group Subgroups                      ',
       '0102  (   31)  Group Members                        ',
       '0103  (  287)  Group Installation Data              ',
       '0110  (   49)  Group DFP Data                       ',
       '0120  (   24)  Group OMVS Data                      ',
       '0130  (   24)  Group OVM Data                       ',
       '0141  (  260)  Group TME Data                       ',
       '0200  (  540)  User Basic Data                      ',
       '0201  (   19)  User Categories                      ',
       '0202  (   22)  User Classes                         ',
       '0203  (   22)  User Group Connections               ',
       '0204  (  287)  User Installation Data               ',
       '0205  (  134)  User Connect Data                    ',
       '0206  (  133)  User RRSF Data                       ',
       '0207  (  293)  User Certificate Name                ',
       '0210  (   49)  User DFP Data                        ',
       '0220  (  221)  User TSO Data                        ',
       '0230  (   34)  User CICS Data                       ',
       '0231  (   17)  User CICS Operator Classes           ',
       '0240  (   21)  User Language Data                   ',
       '0250  (  834)  User OPERPARM Data                   ',
       '0251  (   22)  User OPERPARM Scope                  ',
       '0260  (  757)  User WORKATTR Data                   ',
       '0270  ( 2072)  User OMVS Data                       ',
       '0280  (  306)  User NETVIEW Segment                 ',
       '0281  (   19)  User OPCLASS                         ',
       '0282  (   19)  User DOMAINS                         ',
       '0290  ( 2140)  User DCE Data                        ',
       '02A0  ( 3096)  User OVM Data                        ',
       '0400  (  520)  Data Set Basic Data                  ',
       '0401  (   62)  Data Set Categories                  ',
       '0402  (  107)  Data Set Conditional Access          ',
       '0403  (   63)  Data Set Volumes                     ',
       '0404  (   80)  Data Set Access                      ',
       '0405  (  330)  Data Set Installation Data           ',
       '0410  (   65)  Data Set DFP Data                    ',
       '0421  (  568)  Data Set TME Data                    ',
       '0500  ( 1020)  General Resource Basic Data          ',
       '0501  (  628)  General Resource Tape Volume Data    ',
       '0502  (  266)  General Resource Categories          ',
       '0503  (  559)  General Resource Members             ',
       '0504  (  267)  General Resource Volumes             ',
       '0505  (  284)  General Resource Access              ',
       '0506  (  534)  General Resource Installation Data   ',
       '0507  (  311)  General Resource Conditional Access  ',
       '0510  (  312)  General Resource Session Data        ',
       '0511  (  302)  General Resource Session Entities    ',
       '0520  (  265)  General Resource DLF Data            ',
       '0521  (  269)  General Resource DLF Job Names       ',
       '0540  (  293)  General Resource Started Task Data   ',
       '0550  (  278)  General Resource SystemView Data     ',
       '0570  (  507)  General Resource TME Data Record     ',
       '0571  (  507)  General Resource TME Child Record    ',
       '0572  ( 1028)  General Resource TME Resource Record ',
       '0573  (  269)  General Resource TME Group Record    ',
       '0574  (  772)  General Resource TME Role Record     ',
       ''
    Return 0
