/******************************** REXX ********************************/
    dd='DA'Time('s')
    dsn='TSSAK.$'
    Address TSO

    list1=Read_file(dsn'(F1)',1)            /* DADSVOL dataset/memb   */
    list2=Read_file(dsn'(F2)',2)            /* CONFIGL dataset/memb   */
    listo=
    Do While list1/=''
        Parse VAR list1 unit1 list1
        If Wordpos(unit1,list2)=0 Then p2='????'
        Else Do
            list2=Delword(list2,Wordpos(unit1,list2),1)
            Parse VAR line.2.unit1 ',(' x1 '),' status .
            p2=Substr(x1,1,24) || status
        End
        linex.unit1=Substr(Strip(line.1.unit1,'l'),1,60) p2
        listo=listo unit1
    End
    Do While list2/=''
        Parse VAR list2 unit2 list2
        linex.unit2=Substr(unit2'    ?????',1,84)' OFFLINE'
        listo=listo unit2
    End
/*--------------------------------------------------------------------*/
/* You may want to sort variable "listo" or sort the output.          */
/*--------------------------------------------------------------------*/
    Say listo
    listo=$sort(listo)
    Say listo
    i=0
    Do While listo/=''
        Parse VAR listo unit listo
        i=i+1;lineo.i=linex.unit
    End
    lineo.0=i
    Do i=1 To lineo.0
        Say lineo.i
    End

    Exit

Read_file:Arg infile,f#
    list=
    "ALLOC F("dd") DA('"infile"') SHR REU"
    "EXECIO * DISKR "dd" (STEM linei. FINIS)"
    Do i=1 To linei.0
        line=linei.i
        If Substr(word(line,2),1,1)='(' Then Parse VAR line '(' unit ')'
        Else unit=Word(line,1)
        list=list unit;line.f#.unit=line
    End
    "FREE F("dd")"
    Return list
