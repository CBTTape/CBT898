/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* OCJ: Operator Command JCL for batch execvution.                    */
/*                                                                    */
/* Creates JCL and submits job to issue operator command in batch.    */
/*                                                                    */
/* Use Arg(2)='/S' to bypass command verification and automatically   */
/* submit job.                                                        */
/* Used by LINEMAC/LMEXIT.                                            */
/* Can be used stand alone edit macro by passing a line number as     */
/* the first parameter of no parameter and cursor placement.          */
/*                                                                    */
/*--------------------------------------------------------------------*/
    ARG parm#,sub_it;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS";If rc=0 Then ima_macro=1
byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    opercmd_prog='OPERCMD'                  /* OPERCMD program name   */
    opercmd_prog='$OPERCMD'                 /* OPERCMD program name   */
    opercmd_prog='@OPERCMD'                 /* OPERCMD program at ETS */
    If Datatype(parm#,'W') Then "ISREDIT (parm#)=LINE" parm#
    Else If parm#='' Then Do
        "ISREDIT PROCESS RANGE C";"ISREDIT (cmd)=RANGE_CMD"
        If cmd='C' Then "ISREDIT (parm#)=LINE .ZFRANGE"
        Else "ISREDIT (parm#)=LINE .ZCSR"
    End
    Address TSO
    If sub_it='/S' Then Signal auto_sub
    Say 'Verify executing command (enter "Q" to abandon):'
    Say '  'parm#
    Pull rep;If Substr(rep,1,1)='Q' Then Do;"DELSTACK";Return 8;End
auto_sub:
    x=queue_jcl()
    Queue parm#;Queue '/*';Queue 'ZZ'
    #m1=Msg('On');x=Outtrap('line.',500,'NOCONCAT')
    "SUBMIT * END(ZZ)"
    x=Outtrap('Off');x=Msg(#m1)
    zedsmsg=;zedlmsg=
    Do i=1 To line.0;zedlmsg=zedlmsg||line.i' ';End
    Address ISPEXEC "SETMSG MSG(ISRZ001)"
    Return 4

queue_jcl:
    "NEWSTACK"
    v1='def_ex def_hd def_ac def_bb';Interpret "Parse Value $alkjc(v1) With" v1
    userid=userid();def_hd=def_bb
    Signal on Syntax Name error_off_alkjcpg
    x=$Alkjcpg(jcl);If x/='' Then jcl=x
error_off_alkjcpg:Signal off Syntax

    Queue "//"userid"J JOB "def_ac",'"jcl"',"
    Queue "//   MSGCLASS="def_hd",CLASS="def_ex","
    Queue "//   NOTIFY=&SYSUID,MSGLEVEL=(1,1)"
    Queue "//*"
    Queue '//STEP1    EXEC PGM='opercmd_prog
    Queue '//STEPLIB  DD  DISP=SHR,DSN='Sh$parms('authlib')
    Queue '//SYSIN    DD  *'
    Return 0
