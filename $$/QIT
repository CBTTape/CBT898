/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* QIT                                                                */
/* Change lines into 'Queue' statements.............................. */
/*                                                                    */
/* Parameters:                                                        */
/*   OFF   - Reverse. Remove "Queue ".....                            */
/*   nn    - Set delimiters (quotes) ending column....                */
/*   C1    - Starts in column 1                                       */
/*                                                                    */
/*--------------------------------------------------------------------*/
QIT:qit_ver='1.03'
    Arg parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS";If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    Address ISREDIT
    "(status)=USER_STATE";"(caps)=CAPS";"CAPS=OFF"

    "(lev) = MACRO_LEVEL"
    If lev>1 Then Do
        Parse VAR parm# labf ',' labt ',' lcmd parm#;parm#='C1' parm#
    End
    Else Do
        "PROCESS RANGE C";"(cmd)=RANGE_CMD";"(lrecl)=LRECL"
        If cmd='C' Then Do
            "(labf)=LINENUM .ZFRANGE";"(labt)=LINENUM .ZLRANGE"
        End
        Else Do
            "(labf)=LINENUM .F"
            If rc=0 Then Do
                "(labt) = LINENUM .T"
                If rc/=0 Then "(labt)=LINENUM .ZLAST"
            End
            Else Do
                "(labf)=LINENUM .ZFIRST";"(labt)=LINENUM .ZLAST"
            End
        End
    End
    Parse Upper VAR parm# parm1 parm2 .
    If Datatype(parm1)='NUM' & parm1<=lrecl & parm1>15 Then Do
        col=parm1-1;pad=1
    End
    Else pad=0
    If parm1='C1' | parm2='C1' Then lead='';Else lead='    '

    Do i=labf To labt
#tr=Trace('o')
        "(line)=LINE" i
        If parm1/='OFF' Then Do
            linex=lead'Queue'
            If Index(line,"'")>0 Then dlm='"';Else dlm="'"
            If pad Then linex=Substr(dlm || line,1,col) || dlm
            Else linex=dlm || Strip(line,'t') || dlm
            If linex = dlm || dlm Then linex=lead"Queue ' '"
            Else linex=lead"Queue" linex
        End
        Else Do
            If Translate(Word(line,1))='QUEUE' Then Do
                Parse VAR line . line
                line=Strip(line)
                linex=Substr(line,2,Length(line)-2)
            End
        End
        "LINE" i"=(linex)"
    End

ext1:

    "CAPS=(caps)";"USER_STATE=(status)"
    Exit
