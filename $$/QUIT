/*--------------------------ooo00 REXX 00ooo--------------------------*/
n_st=mark(2)                            /* Mark Start

   QUIT:

   Generic procedure to interrupt looping operation.

   Parameter (positional):
    1. Process id:
       /FX   - Used for "FX" Multi-Procs processing.
       *xxxx - 1st. char '*' sets variable "multquit" to full
               parameter value and puts it into the shared
               profile. For unconditional quit, suggest us parameter of,
                  'TSO QUIT **'
               The original dialog must issue the following and take
               appropriate action set up using 'QUIT',
                  Address ISPEXEC "VGET (multquit)"
                  Parse VAR multquit 1 aster +1 instructions
                  If aster='*' Then Do
                      If instructions='.....
                      ......
                      Signal ....
                  End

                              *****
*----------------------------------------------------------------------*
*/; n_en=mark(-2)                           /* Mark End               */

    Arg parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#)"
    If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    zedsmsg=
    Parse Upper VAR parm# parm1 parm2 parm3
    Parse SOURCE . . xcmd .
    If parm1='??' Then Signal Help_me
    If Wordpos(parm1,'HELP /HELP ? /?')>0 Then Do
        x=BR('%'xcmd '??')
        Exit
    End
    Select
        When (parm1='/FX') Then Do
            "VGET (PANELINC,SKIPVALU,DISPVALU,SKIP)"
            If parm2='?' Then Do
                zedlmsg="At" dispvalu"/"skipvalu"."
                "ISPEXEC SETMSG MSG (ISRZ001)"
                Exit
            End
            zedlmsg="FX operations interrupted with the 'QUIT' command."
            "SETMSG MSG (ISRZ001)"
            skip='QUIT'
            "VPUT (SKIP)";"CONTROL NONDISPL END"
        End
        When (Substr(parm1,1,1)='*') Then Do
            zedlmsg="Looped operation interrupted. Use END PF to",
                    "effect interrupt."
            "SETMSG MSG (ISRZ001)"
            multquit=parm1
            "VPUT (MULTQUIT)"
        End
        Otherwise Do
            zedlmsg="Invalid 'QUIT' parameter."
            "SETMSG MSG (ISRZ001)"
            Exit
        End
    End
ext1:Exit

Help_me:
    Do i=n_st To n_en
        Say Substr(Sourceline(i),1,79)
    End
    Exit

Mark:Arg diff;If \Datatype(diff,'W') Then diff=1;Return sigl+diff
