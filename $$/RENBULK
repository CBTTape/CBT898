/*--------------------------ooo00 REXX 00ooo--------------------------*/
RENBULK:renbulk_ver='1.02'
ts=mark(2)                                  /* Mark Start             */
/*

   RENBULK:

   Routine to generate ALTER and RENAME statements from output of a
   LISTCAT command and wrap them in a batch envelope.
   Use "MD" line commands to convert NOTELINES to executable JCL.

   Parameters:
     Keyword:-
      /D   - Make changes as valid data rather than NOTELINES.
     Positional:-
      suff - Suffix for renames. (Default is ".OLD".)
             Notes:
               1. Leading '.' is optional. If it is not present, one
                  will be inserted.
               2. No name lengths are checked so ensure that
                  appending a suffix does not blow 44 bytes.
                  appending a suffix does not blow 44 bytes.
                  (If the name would be > 44 bytes, use the "VA"
                  command for VSAM files.)
      oldstring/newstring
           - where
                oldstring - existing string in name
                /         - data delimiter
                newstring - value to change 'oldsting' to
             Notes:
               1. If oldstring is not found in the dataset name, the
                  suffix (or ".OLD" by default) will be appended.
               2. If more than one occurance of 'oldstring' is found,
                  only the first occurance will be changed.

*/
te=mark(-2)                                 /* Mark End               */
/*--------------------------------------------------------------------*/
rengen_ver='1.01'                           /* Version #              */
    Arg parm#;Address ISPEXEC;ima_macro=0;"CONTROL ERRORS RETURN"
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS"
    If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    zedsmsg=
    If \ima_macro Then Do
        zedlmsg='Must be in edit/view mode.'
        "SETMSG MSG(ISRZ001)"
        Exit
    End
    Parse Upper VAR parm# parm#
    Address ISREDIT;"(caps)=CAPS";"CAPS=OFF";noteline='NOTELINE'
    If Wordpos(Word(parm#,1),'? /? /HELP /H HELP ??')>0 Then Do
        Do i=ts To te;line=Sourceline(i);x=nl(line);End;"CAPS=(caps)";Exit
    End

    "(status)=USER_STATE"
    Parse Source . . xcmd .
    If Wordpos('/D',parm#)>0 Then Do
        noteline=;i=0;parm#=Delword(parm#,Wordpos('/D',parm#),1)
    End
/*  If noteline='' Then "LINE_BEFORE .ZFIRST=' '"                     */
    Parse Value Space(parm#,0) With oldval '/' newval
    If oldval/='' & newval/='' Then Do
        c_string=1;suff='.OLD';#suff=suff
    End
    Else Do
        c_string=0
        Parse VAR parm# suff .;If suff='' Then suff='OLD'
        If Substr(suff,1,1)/='.' Then suff='.'suff;#suff=suff
    End

    "SCAN=OFF";"RESET";"X ALL"
    "F 'NONVSAM -' 1 13 ALL"
    "F 'DATA -' 1 10 ALL"
    "F 'INDEX -' 1 11 ALL"
    "CURSOR=1 0"

    prog='Rename datasets'
    jid=Userid()'R';acct='(ACCT)';def_ex='A';def_hd='X'
    Signal on Syntax Name error_off_alk
    v1='def_ex def_hd def_ac'
    v2=$alkjc(v1);Interpret "Parse var v2" v1
    acct=$alkacct()
    x=$Alkjcpg(prog);If x/='' Then prog=x
error_off_alk:Signal off Syntax
    x=nl('//'jid  "JOB  "acct",'"prog"',CLASS="def_ex",")
    x=nl('//  MSGCLASS='def_hd',NOTIFY=&SYSUID,REGION=0M')
    x=nl('//*')
    x=nl('//*'Copies('-',67)'*')
    x=nl(Substr('//* VSAM',1,70)'*')
    x=nl('//*'Copies('-',67)'*')
    x=nl("//RENVSAM   EXEC PGM=IDCAMS")
    x=nl("//SYSPRINT DD SYSOUT=*")
    x=nl("//SYSIN     DD *")
    nonvsam=
    Do Forever
        "F '------- ' NEXT NX";If rc/=0 Then Leave
        "(line)=LINE .ZCSR"
        Parse VAR line type . odsname .
        If Wordpos(type,'DATA INDEX')>0 Then Do
            ndsname=odsname
            If c_string Then Do
                ndsname=$Cstring(odsname,oldval,newval)
                If ndsname/=odsname Then suff=;Else suff=#suff
            End
            ollq=Substr(odsname,Lastpos('.',odsname)+1)
            ohlq=Substr(odsname,1,Lastpos('.',odsname)-1)
            nhlq=Substr(ndsname,1,Lastpos('.',ndsname)-1)
            If type='DATA' Then Do
                x=nl('  ALTER ' ohlq '-')   /* CLUSTER                */
                x=nl('         NEWNAME('nhlq || suff')')
            End
            x=nl('  ALTER ' odsname '-')    /* DATA & INDEX           */
            x=nl('         NEWNAME('nhlq || suff'.'ollq')')
        End
        Else nonvsam=nonvsam odsname
    End
    x=nl("/*")
    x=nl('//*'Copies('-',67)'*')
    x=nl('//* Non-VSAM')
    x=nl('//*'Copies('-',67)'*')
    x=nl('//RENNVSAM EXEC PGM=IKJEFT01,DYNAMNBR=65')
    x=nl('//SYSTSPRT DD SYSOUT=X')
    x=nl('//SYSOUT   DD SYSOUT=*')
    x=nl('//SYSTERM  DD SYSOUT=*')
    x=nl('//SYSTSIN  DD *')
    Do while nonvsam/=''
        Parse VAR nonvsam odsname nonvsam
        ndsname=odsname
        If c_string Then Do
            ndsname=$Cstring(odsname,oldval,newval)
            If ndsname/=odsname Then suff=;Else suff=#suff
        End
        x=nl("  RENAME '"odsname"' -")
        x=nl("         '"ndsname || suff"'")
    End
    x=nl("/*")
    x=nl("//")
    x=nl(" ");x=nl(" ");x=nl(" ")
    zedlmsg='Use "MD" to convert NOTELINES into data and submitt job',
           'You may leave the listing at the bottom (JCL terminated',
           'with a "//".'
    Address ISPEXEC "SETMSG MSG(ISRZ001)"
/*  If noteline='' Then "DEL 1 1 NX"                                  */
    "USER_STATE=(status)"
    Exit
nl:Parse Arg n#
    If noteline/='' Then "LINE_BEFORE .ZFIRST=NOTELINE (n#)"
    Else Do;i=i+1;"LINE_BEFORE &I=(n#)";End
    Return 0
Mark:Arg diff;If \Datatype(diff,'W') Then diff=1;Return sigl+diff
