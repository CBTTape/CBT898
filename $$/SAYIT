/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*                                                                    */
/* SAYIT:                                                             */
/*                                                                    */
/* Macro to create Say statements on a line of the format:            */
/*    Say 'var1=>'var1'<' 'var2=>'var2'<' etc....                     */
/* Parameter '/I' will identify say. Places member: in front of say.  */
/*                                                                    */
/*--------------------------------------------------------------------*/
    Address ISPEXEC;"ISREDIT MACRO (parm#) NOPROCESS"
/*  "CONTROL ERRORS CANCEL"                                           */

    Address ISREDIT
    "(lev)=MACRO_LEVEL"
    If lev>1 Then Parse VAR parm# labf ',' labt ',' lcmd parm#
    Else Do
        "PROCESS RANGE C";"(cmd)=RANGE_CMD"
        If cmd='C' Then Do
            "(labf)=LINENUM .ZFRANGE";"(labt)=LINENUM .ZLRANGE"
        End
        Else Do
            "(labf)=LINENUM .ZFIRST";"(labt)=LINENUM .ZLAST"
        End
    End

    If Strip(parm#)='/I' Then Do
        "(mm)=MEMBER";linei="'"Substr(mm,1,8)":' "
    End
    Else linei=
    Do i=labf To labt
        "(line)=LINE" i
        linex=
        Do While line/=''
            Parse VAR line wd line
            If Index(wd,"'")>0 Then sep='"';Else sep="'"
            linex=linex wd"=>"||sep||wd||sep"<"
        End
        linex="Say" linei||sep||Strip(linex)||sep
        If linex/='' Then "LINE" i"=(linex)"
    End

ext1:
    Exit
