/******************************** REXX ********************************/
/**********************************************************************/
/* TRACE r                                                            */
/*--------------------------ooo000OOOO000ooo--------------------------*/
/* SFAD - Software Factory Delete Allocate dataset.                   */
/*                                                                    */
/* Generate JCL to delete reallocate datasets.                        */
/*                                                                    */
/*--------------------------------------------------------------------*/
    ARG parm#
    Address ISPEXEC
    "CONTROL ERRORS RETURN"
    "CONTROL ERRORS CANCEL"
    ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS"
    If rc>0 Then SIGNAL byp1
    ima_macro=1

byp1:
    Parse SOURCE . . xcmd .

    Parse Upper VAR parm# Parm#
    Address ISREDIT
    "(status)=USER_STATE"
    all=parm#;ls=1
    "(last) = LINENUM .ZLAST"
    If trace = 'Y' Then Trace r

    If all = 'ALL' Then Do
        zfrange = 1
        "(zlrange) = LINENUM .ZLAST"
    End
    Else Do
        "PROCESS RANGE C"
        "(cmd) = RANGE_CMD"
        If cmd = 'C' Then Do
            "(zfrange) = LINENUM .ZFRANGE"
            "(zlrange) = LINENUM .ZLRANGE"
        End
        Else Do
            GC009 = 'No line numbers selected.'
            Address ISPEXEC "SETMSG MSG(GC009)"
            Exit
        End
    End
    dsns=;dsna=;dsno=
    Do i=zfrange To zlrange
        "(line) = LINE" i
        Parse VAR line dsn1 dsn2 .
        If dsn2='' Then dsn2=dsn1
        dsno=dsno dsn2
        dsns=dsns dsn1
        Parse VAR dsn1 . '.' mlq
        dsna=dsna 'HLQ.'mlq
    End
    x=n_l('//'Userid()"   JOB (ACCOUNT),'MEMBER-YOU',")
    x=n_l('//             NOTIFY=&SYSUID,')
    x=n_l('//             MSGLEVEL=(1,1),MSGCLASS=H')
    x=n_l(Substr('//',1,72,'*'))
    x=n_l('//* LIB : GSESF.CICSZOS.V220.CUST(ALCCICS)')
    x=n_l('//* REF : N/A')
    x=n_l('//* DOC : ALLOCATE RUNTIME TARGET DATASETS')
    x=n_l('//* DATE: 09-09-03')
    x=n_l('//* TIME: 17:16')
    x=n_l(Substr('//',1,72,'*'))
    x=n_l('//DELTARG EXEC PGM=IDCAMS,REGION=4M,TIME=5')
    x=n_l('//SYSPRINT DD  SYSOUT=*')
    x=n_l('//SYSIN    DD  *')
    xxx=dsna
    Do While xxx/=''
        Parse VAR xxx dsn xxx
        x=n_l('  DELETE' Substr(dsn,1,40) 'NONVSAM')
    End
    x=n_l('  IF MAXCC < 9 THEN SET MAXCC = 0')
    x=n_l('/*')
    x=n_l('//*')
    x=n_l('//ALCTARG EXEC PGM=IEFBR14,REGION=64K,TIME=5')
    xxx=dsns;yyy=dsna;zzz=dsno
    Do While xxx/=''
        Parse VAR zzz dsnz zzz
        Parse VAR xxx dsn xxx
        xx=Listdsi("'"dsn"' DIRECTORY SMSINFO")
        If xx=16 Then xx=Listdsi("'"dsnz"' DIRECTORY SMSINFO")
        Parse VAR yyy dsn yyy
        llq=Strip(Substr(dsn,Lastpos('.',dsn)+1),,"'")
        x=n_l('//'Substr(llq,1,8) 'DD  DSN='dsn',')
        x=n_l('//             DISP=(NEW,CATLG,DELETE),')
        If sysadirblk/='' & Datatype(sysadirblk)/='NUM' Then Do
            x=n_l('//             DSNTYPE=LIBRARY,')
            sysadirblk=250
        End
        x=n_l('//             UNIT=3390,')
        If Index(llq,'AUTH')>0 Then vol='?AUTH?'
        Else                        vol='??????'
        x=n_l('//             VOL=SER='vol',')
        x=n_l('//             RECFM='sysrecfm',')
        x=n_l('//             LRECL='syslrecl',')
        If Substr(sysunits,1,1)='C' Then xx='CYL'
        Else If Substr(sysunits,1,1)='T' Then xx='TRK'
             Else xx=sysblksize
        If Datatype(xx,'W') & xx>26999 Then Do
            xx='TRK'
            If sysalloc//2>0 Then rnd=1
            Else                  rnd=0
            sysalloc=sysalloc%2 + rnd
        End
        space=sysalloc','sysseconds
        If sysdsorg='PO' Then space=space','sysadirblk
        If sysrecfm='FB' & sysblksize>27920 Then sysblksize=27920
        If sysrecfm='U' Then x=Listdsi("'"dsnz"' DIRECTORY SMSINFO")
        x=n_l('//             BLKSIZE='sysblksize',')
        x=n_l('//             SPACE=('xx',('space'))')
    End
    x=n_l('//*')
    x=n_l('// ')
    x=n_l(' ')
    "(caps)=CAPS";"CAPS=OFF"
    x=n_l(' Used macro "'xcmd'" to create this member from a dataset')
    x=n_l(' list. Ask the S/F if not available to you.')
    x=n_l(' ')
    "CAPS=(caps)"
Exi1:
    "USER_STATE = (status)"
    Return

n_l:#o=Arg(1);"LINE_BEFORE" ls "= NOTELINE (#o)";Return 0
