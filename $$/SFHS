/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* SFHS - Software Factory Holddata Summary.                          */
/*                                                                    */
/* Macro to look at S/F packing list and summarise all the HOLDDATA   */
/* PTFs for inclusion into $SFLOG member.                             */
/* Must point to line that contains packing list dataset (or copy) and*/
/* will put NOTELINEs after it to be 'MDD'd.                          */
/*                                                                    */
/*--------------------------------------------------------------------*/
    Address ISPEXEC;"CONTROL ERRORS RETURN"
    "ISREDIT MACRO (parm#) NOPROCESS"

ini1:
    zedsmsg=
    Parse VAR parm# "TABLE(" table "),"holdreas
    If table='' Then Do
        zedlmsg="Table name missing."
        "SETMSG MSG(ISRZ001)"
        Exit
    End
    holdreas=' ERR'holdreas
    @holdreas=$cstring(holdreas,' ',' @',,'*')
    Interpret $cstring(Space(@holdreas),' ','=;',,'*')'='
    "TBTOP &TABLE"
    Do While rc=0
        "TBSKIP &TABLE";If rc/=0 Then Leave
        xxx=reason
        Do while xxx/=''
            Parse var xxx xx ',' xxx;xx=Substr(xx,Verify(xx,'*-#'))
            Interpret '@'xx'=@'xx member
        End
    End
    "TBTOP &TABLE"

    xXx=holdreas;i=1
    Do While xXx/=''
        Parse VAR xXx xx xXx
        Interpret 'val=@'xx
        If val='' Then Iterate
        pref='         | 'substr(xx,1,11) ':'
        Do While val/=''
            xx=Space($Wordat(val,46,'L'),,',')
            val=$Wordat(val,46,'T');If val/='' Then xx=xx','
            x=o_l(pref xx)
            pref='         |             :'
        End
    End
    lineo.i=

    dd='RP'Time('s')
    ztempf=$Ztempf()
    Address TSO "ALLOC F("dd") DSN('"ztempf"') SHR REU"
    Address TSO "EXECIO * DISKW "dd" (STEM lineo. FINIS)"
    "LMINIT DATAID(DATAID) DDNAME("dd")"
    zedlmsg="Cut and paste this data as required."
    "SETMSG MSG(ISRZ001)"
    "EDIT DATAID(&DATAID)"
    "LMFREE DATAID(&DATAID)"
    Address TSO "FREE F("dd")";x=$ztempf(ztempf,'/D')
Exi1:
    "USER_STATE=(status)"
    Return
o_l:lineo.i=Arg(1);i=i+1;Return 0
