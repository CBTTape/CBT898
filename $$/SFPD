/******************************** REXX ********************************/
/**********************************************************************/
/* TRACE r                                                            */
/*--------------------------ooo000OOOO000ooo--------------------------*/
/* SFPD - Software Factory Populate datasets.                        */
/*                                                                    */
/* Populate datasets created by SFAD.                                 */
/*                                                                    */
/*--------------------------------------------------------------------*/
    ARG parm#
    Address ISPEXEC
    "CONTROL ERRORS RETURN"
    "CONTROL ERRORS CANCEL"
    ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS"
    If rc>0 Then SIGNAL byp1
    ima_macro=1

byp1:
    zedsmsg=
    Parse SOURCE . . xcmd .

    Parse Upper VAR parm# parm#
    Address ISREDIT
    "RESET"
    "(status)=USER_STATE"
    all=parm#;ls=1
    "(last) = LINENUM .ZLAST"
    If trace = 'Y' Then Trace r

    If all = 'ALL' Then Do
        zfrange = 1
        "(zlrange) = LINENUM .ZLAST"
    End
    Else Do
        "PROCESS RANGE C"
        "(cmd) = RANGE_CMD"
        If cmd = 'C' Then Do
            "(zfrange) = LINENUM .ZFRANGE"
            "(zlrange) = LINENUM .ZLRANGE"
        End
        Else Do
            zedlmsg = 'No line numbers selected.'
            Address ISPEXEC "SETMSG MSG(ISRZ001)"
            Exit
        End
    End
    dsno=;dsna=;dsni=
    Do i=zfrange To zlrange
        "(line) = LINE" i
        Parse VAR line dsn dsn2
        If dsn2='' Then Do
            zedlmsg='Missing "from" dataset for '''dsn''' on line' i'.'
            Address ISPEXEC "SETMSG MSG(ISRZ001)"
            Exit
        End
        dsni=dsni dsn2
        dsno=dsno dsn
        Parse VAR dsn . '.' mlq
        dsna=dsna 'HLQ.'mlq
    End
    x=n_l('//'Userid()"   JOB (ACCOUNT),'MEMBER-YOU',")
    x=n_l('//             NOTIFY=&SYSUID,')
    x=n_l('//             MSGLEVEL=(1,1),MSGCLASS=H')
    x=n_l(Substr('//',1,72,'*'))
    x=n_l('//* LIB : GSESF.CICSZOS.V220.CUST(ALCCICS)')
    x=n_l('//* REF : N/A')
    x=n_l('//* DOC : POPULATE RUNTIME TARGET DATASETS')
    x=n_l('//* DATE: 09-09-03')
    x=n_l('//* TIME: 17:16')
    x=n_l(Substr('//',1,72,'*'))
    x=n_l('//IEBCOPY EXEC PGM=IEBCOPY,REGION=4M,TIME=60')
    seq=0
    xxx=dsna;yyy=dsni
    Do While xxx/=''
        seq=seq+1
        Parse VAR xxx adsn xxx
        Parse VAR yyy idsn yyy
/*      Parse VAR adsn . '.' tlq '.' llq                              */
/*      idsn='DNMCICS.'tlq'.vvvv.'llq                                 */
        xx=Right(seq,2,'0')
        x=n_l('//INP'xx'    DD  DISP=SHR,DSN='idsn)
        x=n_l('//OUT'xx'    DD  DISP=SHR,DSN='adsn)
    End
    x=n_l('//SYSUT3   DD  UNIT=SYSDA,DISP=(NEW,DELETE,DELETE),')
    x=n_l('//             SPACE=(CYL,(5,5))')
    x=n_l('//SYSUT4   DD  UNIT=SYSDA,DISP=(NEW,DELETE,DELETE),')
    x=n_l('//             SPACE=(CYL,(5,5))')
    x=n_l('//SYSPRINT DD  SYSOUT=*')
    x=n_l('//SYSIN    DD  *')
    xxx=dsno;yyy=dsni
    Do i=1 to seq
        Parse VAR xxx odsn xxx
        Parse VAR yyy idsn yyy
        xx=Listdsi("'"odsn"'")
        If sysrecfm='U' Then Do
            oblk=sysblksize
            xx=Listdsi("'"idsn"'")
            If oblk/=sysblksize Then Do
                mb=',MAXBLK='oblk;ct='COPYMOD'
            End
            Else Do;mb=;ct='COPY';End
        End
        Else Do;mb=;ct='COPY';End
        xx=Right(i,2,'0')
        x=n_l('  'ct 'OUTDD=OUT'xx',INDD=((INP'xx',R))'mb)
    End
    x=n_l('/*')
    x=n_l('//*')
    x=n_l('// ')
    x=n_l(' ')
    "(caps)=CAPS";"CAPS=OFF"
    x=n_l(' Used macro "'xcmd'" to create this member from a dataset')
    x=n_l(' list. Ask the S/F if not available to you.')
    x=n_l(' ')
    "CAPS=(caps)"
Exi1:
    "USER_STATE = (status)"
    Return

n_l:zz=Arg(1);"LINE_BEFORE" ls "= NOTELINE (zz)";Return 0
