/******************************** REXX ********************************/
/**********************************************************************/
/* TRACE r                                                            */
/*--------------------------ooo000OOOO000ooo--------------------------*/
/*                                                                    */
/* Macro to look at S/F packing list and summarise all the HOLDDATA   */
/* PTFs for inclusion into $SFLOG member.                             */
/* Must point to line that contains packing list dataset (or copy) and*/
/* will put NOTELINEs after it to be 'MDD'd.                          */
/*                                                                    */
/*--------------------------------------------------------------------*/
    Address ISPEXEC
    "CONTROL ERRORS RETURN"
    "ISREDIT MACRO (parm#) NOPROCESS"

ini1:
    Address ISREDIT
    "(status)=USER_STATE"
    "(lev)=MACRO_LEVEL"
    "(lin#,col#)=CURSOR"
    If lev<2 Then Do
        Parse VAR parm# labf ',' labt ',' lcmd parm#
        dsn= '                       AKOS.$(HOLD1)                 '
        "(line)=LINE .ZCSR"
        If col#=0 Then Do
            gc009='Gimme a break!!! Cursor not in body of text.'
            Address ISPEXEC "SETMSG MSG(GC009)"
            Exit
        End
        dsn=$Wordat(line,col#)
        xx=$Vername(dsn)
        If xx/='OK' Then Do
            gc009=xx
            Address ISPEXEC "SETMSG MSG(GC009)"
            Exit
        End
        If Sysdsn(dsn)/='OK' Then Do
            If Sysdsn("'"dsn"'")='OK' Then dsn="'"dsn"'"
            Else If Sysdsn("'"Userid"."dsn"'")='OK' Then,
                      dsn="'"Userid"."dsn"'"
        End
    End
    Else dsn=parm#
    If Sysdsn(dsn)/='OK' Then Do
        gc009="No valid dataset could be extracted from cursor"
              "placement."
        Address ISPEXEC "SETMSG MSG(GC009)"
        Exit
    End
    dd='RP'Time('s')
    Address TSO "ALLOC F("dd") DSN("dsn") SHR REU"
    Address TSO "EXECIO * DISKR "dd" (STEM linei. FINIS)"
    Address TSO "FREE F("dd")"
    nfnd=1
    Do i=1 To linei.0
        If Index(linei.i,'PTF ')=1 Then Do
            nfnd=0;Leave
        End
    End
    If nfnd Then Do
        gc009='"PTF " not found in column 1 of dataset '''dsn'''',
              'processing aborted.'
        Address ISPEXEC "SETMSG MSG(GC009)"
        Exit
    End
    line3=linei.i
    x=i-1
    line2=linei.x
    x=x-1
    line1=linei.x
    line1=Strip(substr(line1,26))
    line2=Strip(substr(line2,26))
    line3=Strip(substr(line3,26))
    @sys='@PTF @FMID @ISU'
    sys='PTF FMID ISU'
    Do While line1/=''
        Parse VAR line1 c1 +1 +1 line1
        Parse VAR line2 c2 +1 +1 line2
        Parse VAR line3 c3 +1 +1 line3
        sys=sys c1||c2||c3
        @sys=@sys '@'c1||c2||c3
    End
    Interpret $cstring(Space(@sys),' ','=;',,'*')'='
    sysmods=i+2
    Do i=i+2 To linei.0
        line=linei.i
        If Verify(line,'ABCDEFGHIJKLMNOPQRSTUVWXYZ@#$','M')/=1 Then,
            Leave
        Interpret 'Parse var line' sys '.'
        xXx=Subword(sys,4)
        If Wordpos(fmid,@fmid)=0 Then @fmid=@fmid fmid
        If Â¬Datatype(#.fmid,'w') Then #.fmid=1;Else #.fmid=#.fmid+1
        Do While xXx/=''
            Parse VAR xXx xx xXx
            Interpret 'val='xx
            If val='.' Then Iterate
            Interpret '@'xx'=@'xx ptf
        End
    End
    sysmods=i-sysmods
    xXx=Subword(sys,4)
    "CURSOR=" lin#+1 0
    Do While xXx/=''
        Parse VAR xXx xx xXx
        Interpret 'val=@'xx
        If val='' Then Iterate
        pref='         | 'Substr(xx,1,3) 'PTFS    :'
        Do While val/=''
            xx=Translate($Wordat(val,46,'L'),',',' ')
            val=$Wordat(val,46,'T')
            If val/='' Then xx=xx','
            x=n_l(pref xx)
            pref='         |     PTFS    :'
        End
    End
    x=n_l('         | NOTE :' sysmods 'SYSMODS Presented for PROCESSing')
    Do While @fmid/=''
        Parse VAR @fmid fmid @fmid
        x=n_l('         |        FMID('fmid') -' #.fmid 'PTFS')
/*      Say fmid #.fmid                                               */
    End
Exi1:
    "USER_STATE=(status)"
    Return

n_l:xx=Arg(1);"LINE_BEFORE .ZCSR = NOTELINE (xx)";Return 0
