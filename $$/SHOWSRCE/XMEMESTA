XMEMESTA CSECT
*        PRINT NOGEN
         LR    R2,R15                   R1 SET AS BASE
         USING XMEMESTA,R2              BASE REGISTER
         USING COMMONA,R10              ADDRESABILITY TO COMMON AREA
START    DS    0H
         LM    R10,R13,12(R15)          RESTORE REGISTERS FROM "BASES1"
         B     MOVEERR                  BYPASS R10 - R13 SAVE AREA
BASES#1  DS    6F
*---------------------------------------------------------------------*
MOVEERR  DS    0F
         STM   R0,R13,BASES#2           STORE REGISTERS INTO "BASES2"
*---------------------------------------------------------------------*
*                                                                     *
*        CONTROL GETS HERE IF THE AN ABEND OCCURED DURING A MOVE      *
*        FROM SECONDARY TO PRIMARY. USUALLY CAUSED BY AN ADDRESS      *
*        EXCEPTION ERROR.                                             *
*                                                                     *
*---------------------------------------------------------------------*
         CL    R0,=F'12'                  Q. SDWA CREATED BY RTM2
         BE    MESTAER2                      N. DO NOT RETRY
*---------------------------------------------------------------------*
* SDWA OK.
*---------------------------------------------------------------------*
         LR    R9,R1
         USING SDWA,R9                    SDWA MAPPING
         TM    SDWAERRD,SDWACLUP          Q. IS RETRY POSSIBLE
         BNZ   NORETRY2                      N. DO NOT RETRY
*        BNZ   NORETRY1                      N. DO NOT RETRY
*---------------------------------------------------------------------*
*                                                                     *
*        DECIDE WHETHER OR NOT WE CAN RETRY. RETRY IS ONLY ALLOWED    *
*        FOR 0CX SYSTEM ABENDS.                                       *
*                                                                     *
*---------------------------------------------------------------------*
         L     R3,SDWAABCC                LOAD USER/SYSTEM ABEND CODE
         SRL   R3,12                      MOVE OUT USER CODE.
         STCM  R3,3,SYSRCODE              EXTRACT SYSTEM RETURN CODE.
         NC    SYSRCODE(2),=X'0FFF'       ISOLATE RETURN CODE.
         CLI   SYSRCODE,X'00'             CHECK FOR SYSTEM
         BNE   NORETRY1                      ABEND 0CX.
         TM    SYSRCODE+1,X'C0'                 AND IF NO =
         BNO   NORETRY1                            NO RETRY
         B     CONT#10                    BYPASS
NORETRY1 DS    0H
         B     NORETRY2                  <=======
CONT#10  DS    0H
*---------------------------------------------------------------------*
*   PERFORM A DUMMY RETRY. USED TO CLEAR WORK AREAS BEFORE PHYSICAL   *
*   RETRY.                                                            *
*---------------------------------------------------------------------*
         LA    R0,MESTAER
         LA    R15,4
         SETRP FRESDWA=YES,RC=4,RETADDR=MESTAER,DUMP=NO
RETURN1  DS    0H
         BR    R14                       RETURN
NORETRY2 DS    0H
         SETRP WKAREA=(R9),RC=0,DUMP=NO
RETURN2  DS    0H
         LA    R15,MESTAER               ADDRESABILITY BY R15
         B     MESTAER
MESTAER  DS    0F
         LM    R0,R13,8(R15)             RESTORE REGISTERS
         B     MESTAER2
BASES#2  DS    14F
MESTAER2 DS    0H
*---------------------------------------------------------------------*
*        MODESET KEY=ZERO              SUPERVISOR STATE
*        L     R4,OURASN               OUT OF XMEM MODE
*        SSAR  R4
*        XR    R4,R4                   REG 2 - 0
*        AXSET AX=(R4)                 AUTH INDEX - 0
*        MODESET KEY=NZERO             PROBLEM STATE
*---------------------------------------------------------------------*
RETMAIN1 DS    0H
         L     R1,RETNADR1              LOAD RETURN ADDRESS
         BR    R1                       BRANCH TO RETURN ADDRESS
*=====================================================================*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
SYSRCODE DS    F
COMMONA  DSECT
         COPY XMEMCOMA
*
         PRINT NOGEN
*
         IHASDWA DSECT=YES          ENTRY : SDWA
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
