/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*                                                                    */
/* SITE: Macro to bring up default site help member.                  */
/*                                                                    */
/* Parematers (positional):                                           */
/*  ccc - Member id in format of '@HINTccc'.                          */
/*        - Default is current member. Use '*' for a list of all the  */
/*          site help members.  This may be omitted if the str starts */
/*          with ' or " (see below).                                  */
/*  str - Find string. Enclose in quotes (single or double) if string */
/*        contains spaces (or if the 'ccc' parameter is missing, even */
/*        if no imbedded spaces).                                     */
/*                                                                    */
/* Keyword:                                                           */
/*  /A  - All members, ie. members @*HINT*.                           */
/*  /E  - Edit member instead of view. Only required if "EM" command  */
/*        is not available.                                           */
/*  /I  - Bring up site member and only display "index" lines. These  */
/*        are lines that have '/' in columns 3-5 (indicating date).   */
/*                                                                    */
/*--------------------------------------------------------------------*/
n_en=mark(-1)                               /* Mark End               */
SITE:site_ver='1.06'
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN"
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#)"

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    If Wordpos(Word(parm#,1),'? ??')>0 Then Signal Help_me
    Parse Upper VAR parm# parm#

    defparm=Site$()                         /* System ID              */

    "VGET (sitemacr) SHARED";If sitemacr='Y' Then Signal macro_exec_1
    Call parse_parms

    dsname=$alkhlq()'.'$alkllqr()
    member="'"dsname"("Strip(Substr('@HINT'site,1,8))")'"
    If Index(site,'*')>0 Then Nop
    Else Do
        If Sysdsn(member)/='OK' Then Do
            gc009="New member "member" created."
            "ISPEXEC SETMSG MSG(GC009)"
        End
    End
    If parm#/='' | indx Then Do
        sitemacr='Y';"VPUT (sitemacr parm# indx) SHARED"
        macro='MACRO(SITE)'
    End
    Else macro=
    "&ACTN DATASET(&MEMBER)" macro
    sitemacr=;"VPUT (sitemacr) SHARED"
exi1:Exit

macro_exec_1:
    Address ISPEXEC "CONTROL ERRORS CANCEL"
    sitemacr=;"VPUT (sitemacr) SHARED";"VGET (indx parm#) SHARED"
    Address ISREDIT;"(status)=USER_STATE";"RESET"
    If indx | parm#/='' Then Do
        If indx Then Do
            "X ALL";"F '/' 3 5 ALL";lin#=1
            "X '//' 3 7 ALL"
        End
        Else Do
            If substr(parm#,1,1)/="'" Then parm#="'"parm#"'";"F" parm# "FIRST"
            "(lin#)=LINENUM .ZCSR";If lin#=0 Then lin#=1
        End
        "CURSOR="lin# 0
    End
    Else "USER_STATE=(status)"
    Exit

Help_me:
    Signal on Syntax Name error_off_1
    If Word(parm#,1)='?' Then Do;x=$BR('SITE ??');Exit;End
error_off_1: Signal Off Syntax
    Do i=1 To n_en;Say Substr(Sourceline(i),1,79);End
    Exit

nl_h:Parse Arg n#
    If ima_macro Then "LINE_BEFORE .ZFIRST=NOTELINE (n#)";Else Say n#
    Return 0
Mark:If Datatype(Arg(1))/='NUM' Then Arg(1)=1;Return sigl+Arg(1)
/*--------------------------------------------------------------------*/
/* Parse parameters                                                   */
/*--------------------------------------------------------------------*/
parse_parms:
    defparm=Strip(Substr(defparm,1,3))
    If Wordpos('/E',parm#)>0 Then Do
        actn='EDIT';parm#=Delword(parm#,Wordpos('/E',parm#),1)
    End;Else actn='VIEW'
    If Wordpos('/I',parm#)>0 Then Do
        indx=1;parm#=Delword(parm#,Wordpos('/I',parm#),1)
    End;Else indx=0
    If Wordpos('/A',parm#)>0 Then Do
        parm#=Delword(parm#,Wordpos('/A',parm#),1);parm#=parm# '*'
        If parm#='*' Then Do;parm#=;defparm='*';End
    End
    Select
        When parm#='' | Verify(parm#,'''".','m')>0 Then site=defparm
        Otherwise Do
            Parse VAR parm# site .
            If site='' Then site=defparm
        End
    End

    Return 0

err01:
    Address ISPEXEC "SETMSG MSG(ISRZ001)";Exit 8

/*--------------------------------------------------------------------*/
/* Parse string 'parm#' with 'mask'. Result set to 'N' if mask not    */
/* found or set to 'Y' if found. Can pass positive/negative result    */
/* substitution as the 2 and 3 parameter. Defaults are Y and N.       */
/* Can set null return value for either by passing 'NULL'. Can set    */
/* either to the original mask by entering '*' (suggest 'NULL' for the*/
/* other value).                                                      */
/* Can pass a mask length as fourth parameter. Comma delimit to pad   */
/* out if prior parameters are not supplied.                          */
/* Can pass 'WORD' as fourth parameter to return the word that        */
/* contain that mask (word does not need to start with mask).         */
/*--------------------------------------------------------------------*/
parse_cons: Procedure Expose parm#;Arg mask,posr,negr,maskl
    If posr=''     Then posr='Y' ;If negr=''     Then negr='N'
    If posr='NULL' Then posr=''  ;If negr='NULL' Then negr=''
    If posr='*'    Then posr=mask;If negr='*'    Then negr=mask
    Select
        When (maskl='WORD') Then Do
            y#=0;xxx=parm#
            Do i=1 To Words(xxx)
                Parse VAR xxx x_x xxx
                If Index(x_x,mask)>0 Then Do;y#=i;posr=x_x;Leave;End
            End
        End
        When (Datatype(maskl,'W')) Then Do
            y#=0;xxx=parm#
            Do i=1 To Words(xxx)
                Parse VAR xxx x_x xxx
                If Substr(x_x,1,maskl)=Substr(mask,1,maskl) Then Do
                    y#=i;Leave
                End
            End
        End
        Otherwise y#=Wordpos(mask,parm#)
    End
    If y#>0 Then Do;parm#=Delword(parm#,y#,1);Return posr;End
    Return negr

/*--------------------------------------------------------------------*/
/* Parse string 'parm#' with 'mask'. Result set to 'N' if mask not    */
/* found or set to next space delimeted value immediately following   */
/* the mask (NULLs is a valid value).                                 */
/* Can pass positive/negative result substitution as the 2 parameter. */
/* Can pass 'NUM' as third parameter to check for numerics and return */
/* negative result if not numeric.                                    */
/*--------------------------------------------------------------------*/
parse_varb: Procedure Expose parm#;Arg mask,negr,dt
    If negr='' Then negr='N'
    parm##=' 'parm#;m#=' 'mask;y#=Index(parm##,m#)
    If y# > 0 Then Do
        If Substr(parm##,y#+Length(m#),1)='' Then Do
            px='';data="Parse VAR parm## p1 '"m#"' p2"
        End
        Else data="Parse VAR parm## p1 '"m#"' px p2"
        Interpret data
        parm#=Strip(p1) Strip(p2)
        If dt='NUM' & Datatype(px)/='NUM' Then Return negr
        Else                                   Return px
    End
    Return negr
