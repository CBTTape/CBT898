/*--------------------------ooo00 REXX 00ooo--------------------------*/
SITO_ver='1.08'
n_st=mark(1)                                /* Mark Start             */
/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*                                                                    */
/* &TOD. &CNAME..                                                     */
/*                                                                    */
/* SITO: SIT Override compare.                                        */
/*                                                                    */
/* Overlay SIT settings from dataset pointed to by variable "MODEL"   */
/* with notelines into edited/viewed member.                          */
/* Notelines are only presented for discrepancies.                    */
/*                                                                    */
/* May be used for SIT DSECT table format or SIT override parameters. */
/* Parameters must be coded 1 per line to be affective.               */
/*                                                                    */
/* On invocation, you will be presented with a screen for entering    */
/* member for comparison.  Place cursor on line for selection.        */
/* By default, some model members may be presented.  However, it is   */
/* your responsibility to ensure the dataset name is correct.         */
/*                                                                    */
/* Parameters (positional):                                           */
/*   None                                                             */
/*                                                                    */
/* Parameters (non-positional keyword):                               */
/*   ALL    - to present notelines for all lines (equal or not).      */
/*   BO     - Build Overrides.  Accumulates all the differences from  */
/*            source as notelines at top.  These can be used to       */
/*            build SIT overrides.  This is a superset of all the     */
/*            missing entries and the differences at the top to be    */
/*            "MD" (made data).                                       */
/*   NX     - No eXclude of same entries.  This is mutually exclusive */
/*            with the "ALL" parameter.                               */
/*                                                                    */
/* Requires:                                                          */
/*   $BR    o Optional REXX routine to present tutorial in a browse   */
/*            session.                                                */
/*   SITOP  - Panel to permit compare member detail's entry (self     */
/*            extracting).                                            */
/*                                                                    */
/*--------------------------------------------------------------------*/
n_en=mark(-1)                               /* Mark End               */
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS"
    If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    If Wordpos(Word(parm#,1),'? HELP /? /H ??')>0 Then Do
        If Word(parm#,1)='??' | ima_macro Then Signal Help_me
        #msg=Msg('Off');Signal on Syntax Name Help_me
        Parse SOURCE . . xcmd .
        x=$BR()
        zedsmsg=;zedlmsg=;Address ISPEXEC "SETMSG MSG(ISRZ001)"
        x=$BR(xcmd '??');Signal off Syntax;Exit
    End

    Parse Upper VAR parm# parm#;Parse Source . . xcmd .
    If Wordpos('NX',parm#)>0 Then dont_x=1;Else dont_x=0
    If Wordpos('BO',parm#)>0 Then bld_oride=1;Else bld_oride=0
    If Wordpos('ALL',parm#)>0 Then Do;dont_x=1;diff_only=0;End;Else diff_only=1

    vars='model1,model2,model3,model4,model5,',
         'model6,model7,model8,model9,model0,'
    "VGET ("vars") PROFILE"
    x=Build_panel()
    If model1='' Then model1="'CICSCSC.V310.CICS.SDFHSAMP(DFHSIT$$)'"
If model2='' Then model2="'CICSCSC.V310.CICS.SDFHSAMP(DFHSIT6$)'"

Signal on Error Name No_panel;csr='MODEL1'
scr1:zcmd=;Address ISPEXEC "DISPLAY PANEL("xcmd"P)";If rc/=0 Then Signal ex1
No_panel:Signal Off Error
    Interpret "model="csr
    If Sysdsn(model)/='OK' Then Do
        zedsmsg=;zedlmsg='Dataset (member) not found.'
        Address ISPEXEC "SETMSG MSG(ISRZ001)";Signal scr1
    End
    If Wordpos(zcmd,'EDIT VIEW BROWSE')>0 Then Do
        Address ISPEXEC zcmd "DATASET ("model")";SIgnal scr1
    End
    x=Listdsi(model)
    If Index(model,'(')=0 &sysdsorg='PO' Then Do
        zedsmsg=;zedlmsg='Invalid member name (missing).'
        Address ISPEXEC "SETMSG MSG(ISRZ001)";Signal scr1
    End

    dd='EI'Time('s');j=0;Address TSO
    "ALLOC F("dd") DSN("model") SHR REU"
    "EXECIO * DISKR "dd" (STEM linei. FINIS)"
    "FREE F("dd")"

    Address ISREDIT;"(status)=USER_STATE";"RESET";"(caps)=CAPS";"CAPS=OFF"

    l_b='LINE_BEFORE';pad=Substr(' ',1,15)
    x=nl('Comparing this SIT with' model'.')
    x=bo('Comparing this SIT with' model'.')
    If bld_oride Then x=bo('Differences to source SIT:')
    Else x=nl('Source SIT parameters missing from member:')

    Do i=1 To linei.0
        If Wordpos('TYPE=CSECT,',linei.i)>0 Then Leave
    End
    If i>linei.0 Then Do;i=0;pad=;End;Else pad=Substr(' ',1,15)
    Do i=i+1 To linei.0
        line=Strip(linei.i)
/*      If Substr(line,1,1)='*' Then Iterate                          */
        Parse VAR line fld '=' .
        "CURSOR=1 0"
        Do Forever
            "SEEK '"fld"=' NEXT";If rc/=0 Then Leave
            "(linem)=LINE .ZCSR";Parse VAR linem fldm '=' .
            If Word(fldm,1)=fld Then leave
        End
        If rc/=0 Then Do
            If bld_oride Then x=bo(pad||line);Else Call new_field
            Iterate
        End
        If diff_only Then If Word(linem,1)=Word(line,1) Then Do
                              If \dont_x Then "XSTATUS .ZCSR=X"
                              Iterate
                          End
        "(,col@)=CURSOR"
        lineo=Substr(' ',1,col@-1)line
        x=nl(lineo,'.ZCSR');If bld_oride Then x=bo(linem)
    End

    If bld_oride Then Do
        "RESET";Do i=1 To j;l_b 1"=NOTELINE '"lineo.i"'";End
    End
    x=nl('*'Copies('-',70)'*');"USER_STATE=(status)";"CAPS=(caps)"

ex1:Address ISPEXEC;"VPUT ("vars") PROFILE"
    "LIBDEF ISPPLIB"
    m#=Msg('Off')
    Address TSO "DELETE '"dsnfx"'"
    Exit

new_field:x=nl(pad||line,,'XNO');Return 0
nl:Parse Arg nl,wh,xno;If wh='' Then wh='1';l_b wh"=NOTELINE (nl)";Return 0
bo:Parse Arg nl;j=j+1;lineo.j=nl;Return 0
Help_me:
    Parse Value Time() With hh ':' 1 time +5
    Select
        When hh<3  Then tod='Hello night-owl'
        When hh<12 Then tod='Good morning'
        When hh<18 Then tod='Good afternoon'
        Otherwise tod='Good evening'
    End
    chgchar='&';cname='there'
    Signal on Syntax Name error_off_$WHOAMI
    Parse Value $Whoami() With cname sname .
error_off_$WHOAMI: Signal Off Syntax
    If ima_macro Then Do
        Address ISREDIT;"(caps)=CAPS";"CAPS=OFF";l_b='LINE_BEFORE'
        "(wh,dl2)=DISPLAY_LINES";If wh>1 Then wh=wh+1
        Do i=n_st To n_en
            line=Sourceline(i);If Index(line,chgchar)>0 Then line=evar(line)
            x=nl(line,wh)
        End;"CAPS=(caps)";Exit
    End
    Else Do i=n_st To n_en
        line=Sourceline(i);If Index(line,chgchar)>0 Then line=evar(line)
        Say Substr(line,1,79)
    End;Exit
evar:
    Do While Index(line,chgchar)>0
        Interpret "Parse VAR line p1 '"chgchar"' var '.' p2"
        If Index(var,'(')>0 Then Do
            Parse VAR var var '(' vl ')'
            If Â¬Datatype(vl,'W') Then vl=Length(var)
            Interpret 'line=p1||Substr('var',1,vl)||p2'
        End
        Else Interpret 'line=p1||'var'||p2'
    End;Return line
Mark:If Datatype(Arg(1))/='NUM' Then Arg(1)=1;Return sigl+Arg(1)

/*--------------------------------------------------------------------*/
/* Create panel(s) from sourcelines between the '/* */'               */
/*--------------------------------------------------------------------*/
build_panel:
Trace r
    u=Userid();p=Sysvar(syspref);If u/=p&p/='' Then u=p'.'u;hlq0=u
    dsnfx= hlq0'.$$$TEMP.SITO#DSN'
    #msg=Msg('Off')
    Address TSO
    "DELETE '"dsnfx"'"
    "ALLOC DA('"dsnfx"') NEW CATALOG TRACK SPACE(1,1)",
       "DSORG(PO) LRECL(80) BLKSIZE(0) UNIT(SYSDA) DIR(1)",
       "RECFM(F B) REU"
    "FREE DA('"dsnfx"')"
    members=xcmd"P"
    dd='SO'Time('s');lab=0
    Do While members/=''
        Parse VAR members memb members
        #tr=Trace('o')
        lab=lab+1
        Interpret "Call make_mem_"lab
        "NEWSTACK"
        Do i=n_st To n_en;Queue Sourceline(i);End;Queue ''
        x=Trace(#tr)
        "ALLOC F("dd") DA('"dsnfx"("memb")') SHR REU"
        "EXECIO * DISKW "dd" (FINIS"
        If rc/=0 Then Do
            Say "Error outputing to datadset '"dsnfx"("memb")', rc="rc"."
            Exit
        End
    End
    Address
    Address ISPEXEC "LIBDEF ISPPLIB DATASET ID('&dsnfx') STACK"
    x=Msg(#msg)
    Return 0

/*--------------------------------------------------------------------*/
/* Create help panel(s) from sourcelines between the '/* */'          */
/*--------------------------------------------------------------------*/
make_mem_1:n_st = mark(2)
/*
)ATTR  DEFAULT(%+_)
    ~ TYPE(TEXT) INTENS(HIGH) SKIP(ON)
    _ TYPE(INPUT) INTENS(HIGH) COLOR(RED) HILITE(USCORE)
    @ TYPE(TEXT) INTENS(HIGH) COLOR(YELLOW)
)BODY EXPAND(\\)
@\-\ SIT base member \-\
%&SID%===>_ZCMD                                                               +
%
+Enter the SIT base member to be used as comparison with the current member
+being EDITed/VIEWed. Format DSN(member), place cursor on one to use:
+ Dsname #1%===>_MODEL1                                                 ~<=
+ Dsname #2%===>_MODEL2                                                 ~<=
+ Dsname #3%===>_MODEL3                                                 ~<=
+ Dsname #4%===>_MODEL4                                                 ~<=
+ Dsname #5%===>_MODEL5                                                 ~<=
+ Dsname #6%===>_MODEL6                                                 ~<=
+ Dsname #7%===>_MODEL7                                                 ~<=
+ Dsname #8%===>_MODEL8                                                 ~<=
+ Dsname #9%===>_MODEL9                                                 ~<=
+ Dsname #0%===>_MODEL0                                                 ~<=
+
+Enter@BROWSE/EDIT/VIEW+on command line and cursor on dataset to do it.
+
+Use@END+PF key to abandon processing.
)INIT
 &SID   = &ZSYSID
 .CURSOR='MODEL1'
)REINIT
)PROC
 &CSR=.CURSOR
 VER(&MODEL1,NB)
)END
*/
n_en = mark(-2);Return 0

