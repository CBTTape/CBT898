/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* SRCUJCL: Update DEFINE CSD update JCL with member being edited     */
/*                                                                    */
/*--------------------------------------------------------------------*/
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS"
    If rc=0 Then ima_macro=1

byp1:
    "CONTROL ERRORS CANCEL"

ini1:
    jcl="'"Sysvar(Syspref)".$.SRC.DEFINES($CSDCHG)'"

    "VGET (srcujclm) SHARED"
    If srcujclm=1 Then Signal macro_1

    Parse SOURCE . . xcmd .
    "ISREDIT (member)=MEMBER"
    "ISREDIT (ch)=DATA_CHANGED"
    If ch="YES" Then Do
        "ISREDIT (stype)=SESSION"
        If stype='EDIT' Then "ISREDIT SAVE"
    End
    srcujclm=1;"VPUT (srcujclm,member) SHARED"
    Address ISPEXEC "EDIT DATASET("jcl") MACRO("xcmd")"

exi1:
    Exit

Macro_1:
    srcujclm=;"VPUT (srcujclm) SHARED"
    "VGET (member) SHARED"
    "ISREDIT (status1)=USER_STATE"
    "ISREDIT F 'MEMBER=' FIRST";If rc/=0 Then Exit
    "ISREDIT (line)=LINE .ZCSR"
    Parse VAR line start 'MEMBER=' omemb rest
    linex=start'MEMBER='member rest
    "ISREDIT LINE .ZCSR=(linex)"
    zedlmsg='Enter "SYMSUB /A" on the command line and if happy (or',
            '"SYMSUB /UX" if you are a thrill seeker), submit it.'
    Address ISPEXEC "SETMSG MSG(ISRZ001)"
    "ISREDIT USER_STATE=(status1)"

    Exit
