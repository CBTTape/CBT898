/*-------------------------Rexx---------------------------------------*/
/* Rexx routine to read a list of datasets from ddname INDSNS and     */
/* open each dataset so that the Last Referenced Date is updated.     */
/*                                                                    */
/* The dataset should be the first item in the input list.            */
/*                                                                    */
/* JCL to run the routine:                                            */
/*                                                                    */
/* //BATCHTSO EXEC PGM=IKJEFT01,DYNAMNBR=128                          */
/* //SYSTSPRT DD SYSOUT=*                                             */
/* //INDSNS   DD DSN=UAUTSCL.JCL(Z1),DISP=SHR   <- List of datasets   */
/* //SYSEXEC  DD DISP=SHR,DSN=UAUTSCL.REXX      <- where this routine */
/* //SYSTSIN  DD *                                 is located         */
/*  PROFILE PREFIX(UAUTSCL)                                           */
/*  %TOUCHDSN                                                         */
/*                                                                    */
/*--------------------------------------------------------------------*/
Address TSO
"EXECIO * DISKR INDSNS (STEM indsn. FINIS)"
Do i = 1 to indsn.0
  dsn = word(indsn.i,1)
  x = Listdsi("'"dsn"'" NORECALL)
  Select
     When (sysreason=0)  Then msg=sysvolume
     When (sysreason=5)  Then msg='<--- Dataset not catalogued'
     When (sysreason=8)  Then msg="<--- Doesn't reside on DASD"
     When (sysreason=9)  Then msg='<--- DFHSM migrated dataset'
     When (sysreason=11) Then msg='<--- Insuf. access authority'
     When (sysreason=12) Then msg='<--- VSAM dataset'
     When (sysreason=16) Then msg='<--- Invalid dataset name'
     When (sysreason=22) Then msg='<--- Volume not mounted'
     When (sysreason=24) Then msg='<--- Dataset not found'
     When (sysreason=25) Then msg='<--- Migrated to non-DASD'
     Otherwise
       Do
         Say '   ----> Dataset not processed:' dsn', sysreason=' ,
             sysreason msg
         Iterate
       End
  End

  If sysreason = 0 Then
    Do
      Select
        When (sysdsorg = 'PO') Then
         Do
           x = outtrap(mem.)
           "LISTDS '"dsn"' MEMBERS"
           x = outtrap('off')
           say 'Dataset processed:' dsn
         End
        When (sysdsorg = 'PS') Then
         Do
           "Alloc F(INDD) Da('"dsn"') SHR"
           "EXECIO 1 DISKR INDD (STEM dummy."
           "EXECIO 0 DISKR INDD (Finis"
           "FREE F(INDD)"
           say 'Dataset processed:' dsn
         End
        Otherwise
           say '  ----> Unsupported dataset type:' dsn sysdsorg
      End
    End
  Else
    Say '   ----> Dataset not processed:' dsn', sysreason=' ,
        sysreason msg
End  /* main Do loop */

Say 'Total no. of datasets=' indsn.0
