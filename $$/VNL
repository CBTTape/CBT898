/******************************** REXX ********************************/
/**********************************************************************/
/* TRACE r                                                            */
/*--------------------------ooo000OOOO000ooo--------------------------*/
/*                                                                    */
/* VNL - Verify Name Length                                           */
/*                                                                    */
/* Verifies that COBOL names are not > 30 bytes (block commandable).  */
/*                                                                    */
/*--------------------------------------------------------------------*/
    ARG parm#
    Address ISPEXEC
    macro = 'N'
    If parm# /= "" Then Signal byp1
    "ISPEXEC CONTROL ERRORS RETURN"
    "ISPEXEC ISREDIT MACRO (parm#) NOPROCESS"
    If rc > 0 Then SIGNAL byp1
    macro = 'Y'
    "ISREDIT (status) = USER_STATE"

byp1:
    cactn = "RETURN"
/*  cactn = "CANCEL"                                                  */
    "ISPEXEC CONTROL ERRORS" cactn

ini1:
    Parse Upper VAR parm# parm1 .
    If Datatype(parm1) = 'NUM' & Index(parm1,'.') = 0 Then Do
        zfrange = parm1
        zlrange = parm1
        Signal process
    End

    zfrange = ''
    "ISREDIT PROCESS RANGE C"
    "ISREDIT (cmd) = RANGE_CMD"
    If cmd = 'C' Then Do
        "ISREDIT (zfrange) = LINENUM .ZFRANGE"
        "ISREDIT (zlrange) = LINENUM .ZLRANGE"
    End
    Else Do
        If zfrange = '' Then Do
            "ISREDIT (zfrange) = CURSOR"
            zlrange = zfrange
        End
    End
process:
    Do i=zfrange To zlrange
        "ISREDIT (line) = LINE" i
        Parse VAR line 8 lev vname .
        If Length(Vname) > 30 Then Do
            xx = Index(line,vname) +30 -8
            xx = Right('> 30 bytes --.',xx)
            "ISREDIT LINE_BEFORE" i "= NOTELINE (xx)"
        End
    End
