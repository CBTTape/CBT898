/******************************** REXX ********************************/
/**********************************************************************/
/* TRACE r                                                            */
/*--------------------------ooo000OOOO000ooo--------------------------*/
/*                                                                    */
/* Macro to accumulate response times from Vic. Police's Mainview     */
/* TASKS screen dumps.                                                */
/*                                                                    */
/*--------------------------------------------------------------------*/
    Address ISPEXEC
    ARG parm#
    Parse SOURCE . . xcmd .
    If parm# /= "" Then Signal byp1
    "ISPEXEC CONTROL ERRORS RETURN"
    "ISPEXEC ISREDIT MACRO (parm#) NOPROCESS"
    If rc > 0 Then SIGNAL byp1

byp1:
    cactn = "RETURN"
/*  cactn = "CANCEL"                                                  */
    "ISPEXEC CONTROL ERRORS" cactn

ini1:
    "ISREDIT (status) = USER_STATE"

    "ISREDIT (last) = LINENUM .ZLAST"
    "ISREDIT (caps) = CAPS"
    "ISREDIT CAPS = OFF"
    "ISREDIT RESET"

    "ISREDIT (line) = LINE 1"
    Parse VAR line st . 32 uid .
    #mm = 0
    Do i=1 To last
        "ISREDIT (line) = LINE" i
        Parse VAR line . +39 rt .
        Parse VAR rt mm '.' ss .
        If Index(mm,'us') > 0 Then mm = 0
        If Datatype(l.mm) /= 'NUM' Then l.mm = 0
        l.mm = l.mm +1
        If mm > #mm Then #mm = mm
    End
    "ISREDIT (line) = LINE" last
    Parse VAR line et .
    x = nl(last+0 'transactions processed for',
           Strip(uid) 'between' st 'to' et'.')
    x = nl(' ')
    Do i=0 To #mm
        If Datatype(l.i) /= 'NUM' Then Iterate
        If i = 0 Then x = nl(' <1 -' Right(l.i,5))
        Else          x = nl(Right(i,3) '-' Right(l.i,5))
    End


get_out:
    "ISREDIT CAPS = (caps)"
    "ISREDIT USER_STATE = (status)"
    Exit

nl: Parse Arg ntl
    "ISREDIT LINE_BEFORE .ZFIRST = NOTELINE (ntl)"
    Return 0

