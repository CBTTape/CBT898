/*--------------------------ooo00 REXX 00ooo--------------------------*/
n_st=mark(1)    /* Mark Start */
/*--------------------------ooo000OOOO000ooo--------------------------*/
/*                                                                    */
/* VPSDELO: DELete VPS Obsolete printers.                             */
/*                                                                    */
/* While viewing/editing a listing of printer terminal definitions    */
/* (created by a regular KMART housekeeping batch job) in dataset     */
/* 'CICSESA.CSD.PRINT' hit it with this macro.  It will read dataset  */
/* 'T32AZK.OBSOLETE.PRINTERS' (defined by variable obs_printers) and  */
/* it will generate the appropriate "DELETER TERMINAL() GROUP()"      */
/* statement(s) for each of the appropriate CSD's the printers are    */
/* identified in.                                                     */
/*                                                                    */
/* The JCL will need to be manually submitted.                        */
/*                                                                    */
/* As a "bonus" any discrepancies between LU nad VPS names will be    */
/* displayed as NOTELINES at the top of the dataset.                  */
/*                                                                    */
/* Parameters: (keyword)                                              */
/*   /N  - report netnames being deleted.  (Use for debugging         */
/*         purposes.  DO NOT use this parameter for the final run.)   */
/*   /O  - Apply fixex to the original CSD. By default the JCL        */
/*         generated will be to the CSD with suffix of '.STyymmdd'    */
/*         using the date of processing VPSDELO.                      */
/*                                                                    */
/*--------------------------------------------------------------------*/
n_en=mark(-2)   /* Mark End   */

VPSDELO:vpsdelo_ver='1.03'
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS"
    If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    w1=Word(parm#,1);If Wordpos(w1,"? ??")=0 Then Signal No_help_#1
    If w1="??" Then Signal Help_me;"ISREDIT (m)=MEMBER";rcm=rc
    If rcm=0 Then Signal Help_me;Signal On Syntax Name Help_me
    Parse Source . . xcmd .;x=$BR(xcmd "??");Return
    Mark:If Datatype(Arg(1))/="NUM" Then Arg(1)=1;Return sigl+Arg(1)
    Help_me:Signal Off Syntax;#tr=Trace("o")
    Do i=n_st To n_en;nl=Sourceline(i)
    If rcm=0 Then "ISREDIT LINE_BEFORE 1=NOTELINE (nl)"
    Else Say Substr(nl,1,79);End;Return 0

No_help_#1:
    If Wordpos('/N',Translate(parm#))>0 Then Do
        r_nn=1;parm#=Delword(parm#,Wordpos('/N',Translate(parm#)),1)
    End;Else r_nn=0
    If Wordpos('/O',Translate(parm#))>0 Then Do
        csd_s=;parm#=Delword(parm#,Wordpos('/O',Translate(parm#)),1)
    End;Else csd_s='.ST'$Dateman('REV',,2,'D')
    obs_printers="'T32AZK.OBSOLETE.PRINTERS'"
    dd='EI'Time('s')
    Address TSO "ALLOC F("dd") DSN("obs_printers") SHR REU"
    Address TSO "EXECIO * DISKR "dd" (STEM linei. FINIS)"
    Address TSO "FREE F("dd")"
    start=9

    Address ISREDIT;"(status)=USER_STATE";"(caps)=CAPS";"CAPS=OFF"
    "RESET";"X ALL";j.=0;csd_list=
Say 'Parsing for obsolete LU names:'
    Do i=start To linei.0
        If i//100=1 Then say i'/'linei.0
        If linei.i='' Then Iterate
        Parse VAR linei.i drs lu vps .
        If vps/='' Then Do
            If lu/=vps Then x=nl(linei.i)
            Iterate
        End
        "F '("lu"' FIRST";If rc/=0 Then Iterate
        Do While rc=0
            "(line)=LINE .ZCSR"
            Parse VAR line term csd group . 'NETNAME(' nn ')' .
            If Wordpos(csd,csd_list)=0 Then csd_list=csd_list csd
            j.csd=j.csd+1;v=j.csd
            If \r_nn Then lineo.csd.v='DELETE' group term 'REMOVE'
            Else lineo.csd.v='DELETE' group term 'NETNAME('nn')' 'lu='lu
            "F '("lu"' NEXT X"
        End
    End
    xxx=csd_list
    Do While xxx/= ''
        Parse VAR xxx csd xxx
        j.csd=j.csd+1;v=j.csd;lineo.csd.v='*'
        j.csd=j.csd+1;v=j.csd;lineo.csd.v='* Second pass deletes'
        j.csd=j.csd+1;v=j.csd;lineo.csd.v='*'
    End
Say 'Parsing for obsolete DRS names:'
    ii=0
    Address TSO "NEWSTACK"
    "JC DT /Q"
    Do i=start To linei.0
        If i//100=1 Then say i'/'linei.0
        If linei.i='' Then Iterate
        Parse VAR linei.i drs lu vps .
        If vps/='' Then Do
/*          If lu/=vps Then x=nl(linei.i)                             */
            Iterate
        End
        "F '("drs"' FIRST X"
        If rc/=0 Then Iterate
        Do While rc=0
            "(line)=LINE .ZCSR"
            Parse VAR line term csd group . 'NETNAME(' nn ')' .
            If Wordpos(csd,csd_list)=0 Then csd_list=csd_list csd
            j.csd=j.csd+1;v=j.csd
            If \r_nn Then lineo.csd.v='DELETE' group term 'REMOVE'
            Else lineo.csd.v='DELETE' group term 'NETNAME('nn')' 'drs='drs
            "F '("drs"' NEXT X"
        End
    End
Say csd_list
    Do While csd_list/= ''
        Parse VAR csd_list csd csd_list
        x=csd_jcl()
        Do i=1 to j.csd
            Queue lineo.csd.i
        End
        Queue '/*'
    End
    Queue '//'
    Queue ''
    ztempf=$ztempf();dd="TA"Time('s')
    Address TSO "ALLOC DD("dd") DS('"ztempf"') SHR REU"
    Address TSO "EXECIO * DISKW" dd "(FINIS"
    Address ISPEXEC "LMINIT DATAID(DATAID) DDNAME("dd")"
    Address ISPEXEC "EDIT DATAID(&DATAID)"
    Address ISPEXEC "LMFREE DATAID(&DATAID)"
    Address TSO "FREE DD("dd")";x=$ztempf(ztempf,'/D')
/*Do Queued();Parse PULL line +72;Say line;End                        */
exi1:"USER_STATE=(status)";"CAPS=(caps)"
    Exit

nl:Parse Arg n#;"LINE_BEFORE .ZFIRST=NOTELINE (n#)";Return 0
csd_jcl:
ii=ii+1
dfhcsd=csd'.DFHCSD' || csd_s
Queue '//*-------------------------------------------------------------------*'
Queue "//U"Left(ii,6) "EXEC PGM=DFHCSDUP,PARM='CSD(READWRITE)',REGION=0M"
Queue '//STEPLIB  DD DISP=SHR,DSN=SYS1.CICS310.SDFHLOAD'
Queue '//*        DD DISP=SHR,DSN=Migrated.table.if.not.&CSDLOAD'
Queue '//DFHCSD   DD DISP=SHR,DSN='dfhcsd','
Queue "//            AMP=('BUFNI=30','BUFND=90'),"
Queue '//            VOL=SER=,UNIT=SYSALLDA'
Queue '//SYSPRINT DD SYSOUT=X,OUTLIM=3000000'
Queue '//CBDOUT   DD SYSOUT=X,OUTLIM=3000000'
Queue '//SYSIN    DD *'
Return 0
