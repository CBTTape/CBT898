/*--------------------------ooo00 REXX 00ooo--------------------------*/
n_st=mark(2)                                /* Mark Start             */
/*
   VS:  View Save.

   Macro to quick 'SAVE' viewed member/dataset without the need to
   "c9999_replace_member".

   Parameters:
     C   - Cancel without save. Bypasses the ISPF data changed
           notification window.
     R   - Restore from last saved/original version.  (Usefull option
           in an edit session as well if you cream the data. Saves
           "CAN" and re-select.)
     X   - Save and eXit.
     /C  - Same as "C".
     /R  - Same as "R".
     /X  - Same as "X".
     ?, /?, HELP, /HELP or /H
         - Display these instructions.
     blank/any other character (default)
         - Save.

   Notes:
   1. Option "R" or default:
      A change to the session will be registered to ISPF even if the
      session has not been previously changed, so you will need to "C"
      or "X" out of it or face the perilous "changed warning" thhat we
      are trying to avoid.
   2. Command "VS /C" can be used in an edit session but only loonies
      would prefer that to the "CAN" command.
   3. Command "VS /X" can also be used in an edit session but only
      bigger loonies would prefer that to the "END" PF key.

   Packaging:
    REXX:
     VC  - Stand alone View Cancel routine.
     VR  - Stand alone View Restore routine.

*/
    n_en=mark(-2)                           /* Mark End               */
/*--------------------------------------------------------------------*/
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS";If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    Address ISREDIT
    zedsmsg=
    Parse Upper VAR parm# parm1 1 p11 +1
    If Wordpos(parm1,'? /? HELP /H /HELP')>0 Then Signal Help_me
    "(member)=MEMBER";"(dataset)=DATASET"
    Select
        When p11='C' | parm1='/C' Then Do;"VC";Exit;End
        When p11='R' | parm1='/R' Then Do;"VR";Exit;End
        Otherwise Do
            "(last)=LINENUM .ZLAST"
            If last=0 Then Do
                zedlmsg="Cannot 'VS' save an empty member. Use some",
                        "other method."
            End
            Else Do
                If member/='' Then Do
                    "REPLACE" member ".ZFIRST .ZLAST"
                    zedlmsg="Memeber '"member"' has been saved."
                End
                Else Do
/*                  "EXP '&DATASET'"                                  */
                    "REPLACE '"dataset"' .ZFIRST .ZLAST"
                    zedlmsg="Dataset '"dataset"' has been saved."
                End
            End
        End
    End
    Address ISPEXEC "SETMSG MSG(ISRZ001)"
    If p11='X' | parm1='/X' Then "CANCEL"
    Exit

Help_me:
    "(caps)=CAPS";"CAPS=OFF"
    x=nl_h('*'Centre('ooo000OOOO000ooo',70,'-')'*')
    Do i=n_st To n_en
        line=Sourceline(i)
        Do While Index(line,chgchar)>0
            Interpret "Parse VAR line p1 '"chgchar"' var '.' p2"
            If Index(var,'(')>0 Then Do
                Parse VAR var var '(' vl ')'
                If Â¬Datatype(vl,'W') Then vl=Length(var)
                Interpret 'line=p1||Substr('var',1,vl)||p2'
            End
            Else Interpret 'line=p1||'var'||p2'
        End
        x=nl_h('*'Substr(line,1,70)'*')
    End
    x=nl_h('*'Copies('-',70)'*')
    If ima_macro Then Do;"CAPS=(caps)";Address;End
    Exit

nl_h:Parse Arg n#;"LINE_BEFORE .ZFIRST=NOTELINE (n#)";Return 0
Mark:Arg diff;If \Datatype(diff,'W') Then diff=1;Return sigl+diff
