/*--------------------------ooo00 REXX 00ooo--------------------------*/
/*                                                                    */
/* Routine to identify all the TSO users loged on to the system from  */
/* the CONSOLE command and placed into this member after label names: */
/*                                                                    */
/*--------------------------------------------------------------------*/
    #cname1st=1                          /* 1=ChrisSurn, 0=SurnChris  */

    Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    Parse Upper ARG parm#;If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#)";If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

ini1:
    racfcmd='XRACF LU'                      /* Telstra                */
    racfcmd='ADMLU'                         /* DuPont NIB             */
    racfcmd='LU'                            /* Most sites             */
    zerrhm='*';zerralrm='NO';zerrsm=

    panelp='POPMSG1';addpop=1
    #popr=10;#popc=25                      /* Where popup            */
    nat='ABCDEFGHIJKLMNOPQRSTUVWXYZ01234567890@#$., '||,
        'abcdefghijklmnopqrstuvwxyz'
    yyy=Copies('y',Length(nat));bbb=Copies('b',Length(nat))
    zwinttl='Hear Ye! Hear Ye!'
    "VPUT (ZWINTTL)"

    cn=Userid();sol=sysvar('SOLDISP');unsol=sysvar('UNSDISP')
Refresh:
    Address TSO
    "CONSPROF SOLDISP(NO) UNSOLDISP(NO)"
    x=Outtrap('xx.')
    "CONSOLE ACTIVATE NAME("cn"W)"  /* ACTIVATE CONSOLE               */
    rc1=rc
    If rc1/=0 Then Do               /* if console was not active      */
       'CONSOLE DEACTIVATE'         /* then deactivate it             */
       rc1=rc
    End
    x=Outtrap('Off')
    If rc1/=0 Then Do
        zerrlm=xx.2
        Signal erx1
    End
    i=xx.0                          /* Check last msg for active      */
    Parse VAR xx.i . ' NAME IS ' acn '.'
    If cn/=acn & acn/='' Then Do
        zerrlm='Console already active with name='acn
        Signal erx1
    End
    "CONSOLE SYSCMD(D TS,L) CART('ALEX1')"
    msg=Getmsg('consmsg.',,'ALEX1',,5)     /* wait 5 secs for results */
    If msg/=0 Then Do
        zerrlm='No output from console command to process'
        Signal erx1
    End
    'CONSPROF SOLDISP('sol') UNSOLDISP('unsol')' /* restore profile   */
    'CONSOLE DEACTIVATE'            /* Deactivate it                  */

    #m=(consmsg.0-3)*4;#mc=0

    table='TB'Time('s')
    Address ISPEXEC;"TBCREATE &TABLE KEYS(USER)",
                    "NAMES(GROUP SNAME CNAME NAME STATUS1 STATUS2)",
                    "REPLACE NOWRITE"
    Do i=4 To consmsg.0       /* for each line of active users */
        Parse VAR consmsg.i v.1 18 v.2 35 v.3 52 v.4
        Do j=1 To 4
            If v.j='' Then Leave
            Parse VAR v.j user status1 status2
            If user='*LOGON*' Then Iterate
            Call get_name
            "TBMOD &TABLE"
        End
    End
    If addpop Then "REMPOP"
    "TBSORT &TABLE FIELDS(SNAME)"
    "TBTOP &TABLE"
    Do Forever
        act=;acmd=;zcmd=;sort=;func=;refr=0
        "TBDISPL &TABLE PANEL(WHOONP)"
        If rc>4 Then Leave
        If zcmd='REFRESH' Then Do;refr=1;Leave;End
        Parse VAR zcmd cmd parm1 parm2
        If cmd='TRACE' Then Do
            If parm1='OFF' Then trace='O';Else trace='R'
            x=Trace(trace)
            Iterate
        End
        Select
          When sort=1 Then Do
            "TBSORT &TABLE FIELDS(USER)";ztdsels=0
          End
          When sort=2 Then Do
            "TBSORT &TABLE FIELDS(GROUP)";ztdsels=0
          End
          When sort=3 Then Do
            "TBSORT &TABLE FIELDS(SNAME)";ztdsels=0
          End
          Otherwise NOP
        End
        Do While ztdsels>0
          Select
            When (func=1) Then Do
                zerrlm='Function not developed....'
                "SETMSG MSG(ISRZ002)"
                Iterate
/*              call cancel_userid                                    */
/*              address ispexec "TBDELETE" whotab                     */
            End
            When (func=2 | act='W') Then Do
                "CONTROL DISPLAY SAVE"
                zerrlm="Enq's held by" cname sname"."
                "SETMSG MSG(ISRZ002)"
                Address TSO 'ENQINFO' user'* /J'
                "CONTROL DISPLAY RESTORE"
            End
            Otherwise NOP
          End

          If ztdsels>1 Then "TBDISPL &TABLE";Else ztdsels=0
        End
    End
    "TBEND &TABLE";If refr Then Signal refresh
    Exit

erx1:
    If xx.0=0 Then                  /* If console was active          */
       Address CONSOLE 'DEACTIVATE' /*     then deactivate it         */
    'SETMSG MSG(ISRZ002)'
    If addpop Then "REMPOP"
    Exit

get_name:
/*Say 'Processing' user '.'                                           */
    usl=Length(user);uid=Substr(user,1,usl)
    x=MSG('On');x=Outtrap('line.',5,'NOCONCAT')
    Address TSO racfcmd uid
    x=line.1
    Parse VAR line.1 v1 . 'NAME=' name 'OWNER=' .
    If Substr(v1,1,4) = "ICH3" | v1='INVALID' Then do
         name='?????';Return 8
    End
    name=Translate(name,'  ',',.')
    If #cname1st Then Parse VAR name cname sname .
    Else              Parse VAR name sname cname .
    name=sname',' cname
name=$u2lower(name,2,'*')
    x=$addpop()
    Parse VAR line.2 . "-GROUP=" group "PASS" .;group=Strip(group)
    Return 0

$addpop:
    If \addpop then Return 4
    #mc=#mc+1;p_m=Right(#mc,3)'/'Left(#m,3)
    da1='Processing:' p_m ' ';Parse VAR name n1 ', ' n2 .
    sh1='ggggggggggg wwwbwwwggy';da1=da1 || n2 n1
    sh1=sh1 || Translate(Substr(n2,2),bbb,nat)' y'
    sh1=sh1 || Translate(Substr(n1,2),bbb,nat)
    "ADDPOP ROW(&#POPR) COLUMN(&#POPC)";"CONTROL DISPLAY LOCK"
    "DISPLAY PANEL(&PANELP)"
    If rc>8 Then addpop=0 ;Else "REMPOP"
    Return 0
