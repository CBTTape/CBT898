/******************************** REXX ********************************/
/**********************************************************************/
/* TRACE r                                                            */
/*--------------------------ooo000OOOO000ooo--------------------------*/
/*                                                                    */
/* XC:                                                                */
/*                                                                    */
/* Xmit Compress or unCompress.                                       */
/*                                                                    */
/* If a parameter is passed that is not a second and llq of $ALLIN1,  */
/* the dataset will be xmitted into a member of it's llq into         */
/* dataset 'userid.$ALLIN1' (all members).  If it is DSORG(PS) then   */
/* it will just be copied.                                            */
/*                                                                    */
/* If the parameter passed is dataset 'userid.$ALLIN1' or spaces,     */
/* each member will be processed and the oportinity provided to       */
/* receive the members. You may enter your own PDS name or let it     */
/* default.                                                           */
/*                                                                    */
/* Enter "NO" to bupass receipt or "Q" to terminate process.          */
/*                                                                    */
/*                                                                    */
/*--------------------------------------------------------------------*/

    Address ISPEXEC
    Arg dsnin

    Parse SOURCE . . xcmd .

    If dsnin='?' Then Signal help_me

    userid  = Sysvar(Syspref)
    $allin1u= Userid".$ALLIN1"
    $allin1 = "'"$allin1u"'"
    x=Listdsi($allin1 "NORECALL")
    If x /= 0 Then Signal add_member
    If dsnin='' | dsnin=$allin1 Then Signal extract_members

add_member:
    #msg=Msg('Off')

    "CONTROL ERRORS RETURN"
    "CONTROL NONDISPL END"
    "DISPLAY PANEL(CSMDSPXM)"
    If rc>8 Then isp_env=0
    Else         isp_env=1

    new=Wordpos('/NEW',Translate(dsnin))
    If new>0 Then Do
        dsnin = Delword(dsnin,new,1)
        Address TSO "DELETE" $allin1
        x=8
    End
    If x /= 0 Then Do
        Address TSO "ALLOC DSN("$allin1") CATALOG NEW UNIT(SYSALLDA)",
           "CYL SPACE(100,100) DIR(50) DSORG(PO) RECFM(F B)",
           "LRECL(80) BLKSIZE(3200)"
        If rc /=0 Then Do
            gc009="Daqtaset" $allin1 "could not be created."
            "SETMSG MSG(GC009)"
            Exit
        End
    End
    xxx=dsnin
    Parse Value Date('e') with dd '/' mm '/' yy
    tempf="'"Userid".$A"||yy||mm||dd".$A"Time('s')"'"
    Do While xxx /= ''
        Parse VAR xxx dsnin xxx
        If Substr(dsnin,1,1) /= "'" Then dsnin="'"dsnin"'"
        Parse VAR dsnin "'" dsninu "'"
        member = Strip(Substr(dsnin,Lastpos('.',dsnin)+1),,"'")
/*      Address TSO "CLEAR"                                           */
        resp=
        Do While resp=''
          Say ' '
          Say "For "dsnin" enter:"
          xx=Listdsi(dsnin "DIRECTORY")
          Say '  "/"  - to create member('member') or enter a member name',
              'to change to.'
          Say '  "/N" - to bypass member creation.'
          Say '  "/Q" - to Quit processing altogether.'
          Say '  "?"  - for help documentation.'
          If isp_env & sysdsorg='PO' Then Do
              Say 'This is a PDS with' sysmembers 'members. Enter:'
              Say '  "/S" - to enter a member selection XMIT. You will',
                  'facility for partial'
              Say '         be redirected to enter member name.'
              Say '         (May not be available at all sites/users.)'
              Say 'Just hit <ENTER> to repeat message'
              Parse Upper PULL resp
              If resp='/S'Then Do
                  #member=member
                  member='*';jdsname=dsninu
                  xcinvoke='A A' tempf
                  "VPUT (JDSNAME MEMBER XCINVOKE) SHARED"
                  "ISPEXEC SELECT CMD(%CSMDSNXM)"
                  Say ' '
                  If rc=0 Then Do
                      dsnin=tempf
                      Say 'Input dataset has been changed to flat file.'
                  End
                  Else Say 'You have abandoned member selection.'
                  Say ' '
                  resp=
                  member=#member
              End
          End
          Else Parse Upper PULL resp
          resp=V_resp_format(resp,'/ /Q /N /NO ?')
          If resp='?' Then Do
              Address TSO xcmd '?'
              resp=
          End
        End
        If resp='/Q' Then Do;xxx=;Iterate;End
        If resp='/N' | resp='/NO' Then Iterate
        If resp /='/' Then member=resp
        If Sysdsn("'"$allin1u"("member")'")='OK' Then resp=
        Do While Sysdsn("'"$allin1u"("member")'")='OK' & resp /= '/'
            Say ' '
            Say "Member("member") already exists."
            Say 'Enter "/" to confirm member name or any other value',
                'at prompt for name change.'
            Parse Upper PULL resp
            resp=V_resp_format(resp,'/ /Q /N /NO ?')
            If resp='' Then NOP
            Else If Substr(resp,1,1) /='/' Then Do;member=resp;resp=;End
                 Else Leave
        End
        If resp='/Q' Then Do;xxx=;Iterate;End
        If resp='/N' | resp='/NO' Then Iterate
        Address TSO "FREE DSN("$allin1")"
        If Sysdsorg = 'PO' Then Do
            Address TSO "ALLOC DSN("tempf") NEW REU",
                "CATALOG UNIT(SYSALLDA)",
                "CYL SPACE(100,100) DSORG(PS) RECFM(F B)",
                "LRECL(80) BLKSIZE(3200)"
            Parse VAR $allin1 "'" #$allin1 "'"
            Address TSO "XMIT A.A DA("dsnin")",
                "OUTDSN("tempf") NONOTIFY NOLOG"
            dsnin=tempf
        End
        "LMINIT DATAID(dataid1) DATASET(&$ALLIN1)"
        "LMINIT DATAID(dataid2) DATASET(&DSNIN)"
        "LMCOPY FROMID(&DATAID2)",
               "TODATAID(&DATAID1) TOMEM("member") REPLACE"
        c_rc=rc
        "LMFREE DATAID(&DATAID1)"
        "LMFREE DATAID(&DATAID2)"
        Address TSO "FREE DSN("tempf")"
        Address TSO "DELETE" tempf
        If c_rc=0 Then xx=update_index()
    End
    xx=update_index('C')
    "CONTROL DISPLAY REFRESH"
    x=Listdsi($allin1 "DIRECTORY")
    If sysmembers>0 Then "BROWSE DATASET(&$ALLIN1)"
    xx=Msg('On')

    Exit

extract_members:
    "CONTROL ERRORS RETURN"
    "CONTROL NONDISPL END"
    "DISPLAY PANEL(RECVP)"
    If rc>8 Then isp_env=0
    Else         isp_env=1

    Parse VAR $allin1 "'" $allin1 "'"
    member=
    "LMINIT DATAID(dataid1) DATASET('&$ALLIN1')"
    "LMOPEN DATAID(&DATAID1) OPTION(INPUT)"
    imem='$$$INDEX'
    Do While rc = 0
        "LMMLIST DATAID(&DATAID1) OPTION(LIST) MEMBER(MEMBER) "||,
                "STATS(NO)"
        If rc /= 0 Then Leave
        If Index(imem,member)>0 Then Iterate
        x=receive_prompt()
    End
    "LMMLIST DATAID(&DATAID1) OPTION(FREE)"
    "LMCLOSE DATAID(&DATAID1)"
    "LMFREE DATAID(&DATAID1)"
    Return 0

receive_prompt:
    If isp_env Then Do
        Address TSO "%RECV '"$allin1"("Strip(member)")'"
        Return 0
    End
    Address TSO "DELSTACK"
    Address TSO "NEWSTACK"
    Queue 'End'
    xx = PROMPT('Off')
    xx=Outtrap(lista.)
    Address TSO "RECEIVE INDA('"$allin1"("Strip(member)")') PARM(END)"
    xx = outtrap(off)
    xx = PROMPT('On')
    If lista.0 = 0 Then Return 4
    msg=
    memb=
    imsg='INMR906A INMR042I INMR043I IKJ56286I IKJ56284I'
    Do k=1 to lista.0
       linha = lista.k
       If Index(imsg,Word(linha,1))=0 Then Say SubWord(linha,2)
       Select
         When word(linha,1) = 'INMR901I' Then,
            odsn="'"Word(linha,3)"'"
         When Word(linha,1) = 'INMR000I' Then Do
            gc009 = Subword(linha,2)
            "SETMSG MSG(GC009)"
            Return 4
         End
         When Word(linha,1) = 'INMR003I' then do
            gc009 = Subword(linha,2)
            "SETMSG MSG(GC009)"
            Return 4
         End
         Otherwise nop
      End
    End
    Address TSO "DELSTACK"
    Say ' '
    Say 'For dataset' odsn' from member('member'):'
prompt_4_dsn:
    Say 'Enter "/" to create asis or enter a dataset name to change to.'
    Say 'Enter "/N" to bypass receiving it or "/Q" to Quit processing:-'
    Parse Upper PULL resp
    resp=V_resp_format(resp,'/ /Q /N /NO')
    If resp='' Then Signal prompt_4_dsn
    If resp='/Q' Then Do
        rc=8;Return 'Q'
    End
    If resp='/NO' | resp='/N' Then Return 0
    If resp/='/' Then resp=odsn
    If Listdsi(resp) = 0 Then xtra = 'SHR'
    Else                      xtra = 'NEW'
    Address TSO "DELSTACK"
    Address TSO "NEWSTACK"
    xx = PROMPT('On')
    Queue "DSN("resp")" xtra
    xx=Outtrap(lista.)
    Address TSO "RECEIVE INDA('"$allin1"("Strip(member)")')"
    xx = outtrap(off)
    If rc=0 Then Do
        Say 'Dataset restored to' resp'.';Say ' '
    End
    Address TSO "DELSTACK"
    Return 0

V_resp_format: Arg resp,v_resp
    If resp='' Then Return resp
    If Index(' 'v_resp' ',' 'Strip(resp)' ')>0 Then Return resp
    If Vername(resp) = 'OK' Then Return resp
    Say 'Member('resp') is not a valid member name.'
    Say ' '
    Return ''

Vername: Arg xx,act
    If xx='' Then Return "Missing dataset name."
    If Substr(xx,1,1)="'" Then Parse VAR xx "'" xx "'"
    Parse VAR xx yyy '(' memb ')' .
    Do While yyy /= ''
        Parse VAR yyy yy '.' yyy
        If Length(yy) > 8 Then Return 'Error: Qualifier "'yy'" > 8 bytes.'
        x=Verify(Substr(yy,1,1),'ABCDEFGHIJKLMNOPQRSTUVWXYZ@#$')
        If x>0 Then Return 'Error: Qualifier "'yy'" must start with a',
                           'national character.'
        x=Verify(Substr(yy,2),'ABCDEFGHIJKLMNOPQRSTUVWXYZ@#$0123456789')
        If x>0 Then Return 'Error: Qualifier "'yy'" must only contain',
                           'national and numeric characters.'
    End
    If memb /= '' Then Do
        yy=memb
        x=Verify(Substr(yy,1,1),'ABCDEFGHIJKLMNOPQRSTUVWXYZ@#$')
        If x>0 Then Return 'Error: Member name "'yy'" must start with a',
                           'national character.'
        x=Verify(Substr(yy,2),'ABCDEFGHIJKLMNOPQRSTUVWXYZ@#$0123456789')
        If x>0 Then Return 'Error: Member name "'yy'" must only contain',
                           'national and numeric characters.'
    End
    Return 'OK'

update_index:
    If line.0 = 'LINE.0' Then Do
        If Arg(1)='C' Then Return 4
        dd='$I'Time('S')
        Address TSO "ALLOC DD("dd") DS('"$allin1u"($$$INDEX)') SHR REU"
        If Sysdsn("'"$allin1u"($$$INDEX)')")='OK' Then Do
            Address TSO "EXECIO * DISKR" dd "(STEM line. FINIS"
            st=1
        End
        Else Do
            line.0=0;st=0
        End
    End
    If Arg(1)='C' Then Do
        Address TSO "EXECIO * DISKW" dd "(STEM line. FINIS"
        Address TSO "FREE DD("dd")"
        Return 0
    End
    Do i=st To line.0
        If Word(line.i,1)=dsninu Then Leave
    End
    If Word(line.i,1)/=dsninu Then line.0=i
    ut = Substr(sysunits,1,1)
    If Datatype(sysadirblk)/='CHAR' Then db=','sysadirblk
    Else                                 db=
    Select
        When uc='C' Then ut='CYL'
        When uc='T' Then ut='TRK'
        Otherwise        ut='BLK'
    End
    line.i = Substr(member,1,8) dsninu,
            "SPACE("ut",("sysprimary","sysseconds || db"))",
            "MEMBERS("sysmembers")"
    Return 0

help_me:
    Say ' '
    Say ' '
    Say 'Help not yet available....'
    Say ' '
    Say ' '

    Exit
