/*--------------------------ooo00 REXX 00ooo--------------------------*/
/* XRPL: eXtract dfhRPL datasets from SDSF held output for infeed to  */
/*       CRMEMB for duplicate analysis.                               */
/*                                                                    */
/* Subtract DFHRPL information out of JESJCL part of held output for  */
/* CRMEB processing.  This option uses SJF (Sdsf Jcl Format) and JF   */
/* (Jcl Format) to format output and remove the unnecessary crap.     */
/* This process also retains the STEPLIB DD's just in case it may be  */
/* required.                                                          */
/*--------------------------------------------------------------------*/
XRPL:xrpl_ver='1.02'
    ARG parm#;Address ISPEXEC;"CONTROL ERRORS RETURN";ima_macro=0
    If parm#/='' Then Signal byp1
    "ISREDIT MACRO (parm#) NOPROCESS"
    If rc=0 Then ima_macro=1

byp1:
/*  "CONTROL ERRORS CANCEL"                                           */

/*ini1:                                                               */
    Parse SOURCE . xenv xcmd xdd xdsn .
    Address ISREDIT;"(status)=USER_STATE"
    "(line)=LINE 1"
    Parse VAR line '//' memb .
    "SJF ALL"
    "(last)=LINENUM .ZLAST"
    'JF  1,'last',JF ALL'
    "CURSOR= 1 0"
    fit='PGM=DFHSIP';"F '"fit"' FIRST";If rc/=0 Then Signal err1
    fit='//STEPLIB';"F '"fit"' NEXT";If rc/=0 Then Signal err1
    "(lin#)=LINENUM .ZCSR";"CURSOR="lin#-1 0
    "DELETE ALL NX .ZFIRST .ZCSR"
    linex='//*';"LINE_BEFORE .ZFIRST=(linex)"
    linex='//*' memb;"LINE_BEFORE .ZFIRST=(linex)"
    linex='//*';"LINE_BEFORE .ZFIRST=(linex)"
    fit='//DFHSIP.DFHRPL';"F '"fit"' FIRST"
    If rc=0 Then "DELETE ALL NX .ZCSR .ZCSR"
    fit='//DFHRPL';"F '"fit"' FIRST";If rc/=0 Then Signal err1
    Do Forever
        fit="P'¬¬¬'";"F "fit" 1 NEXT";If rc/=0 Then Signal err1
        "(line)=LINE .ZCSR";If Substr(line,1,3)/='//*' Then Leave
    End
    "(lin#)=LINENUM .ZCSR";"LABEL" lin#"=.ENDRPL"
    fit='//DFHRPL';"F '"fit"' FIRST"
    fit="' DDNAME='"
    Do Forever
        "F "fit" 4 40 NEXT .ZFIRST .ENDRPL";If rc/=0 Then Leave
        "(line)=LINE .ZCSR";If Substr(line,1,3)='//*' Then Iterate
        Signal err2
    End
    "CURSOR="lin# 0;"DELETE ALL NX .ZCSR .ZLAST"

exi1:
    "USER_STATE=(status)"
    Exit

err1:
    zedlmsg='Could not find "'fit'".'
    Address ISPEXEC "SETMSG MSG(ISRZ001)"
    Signal exi1
err2:
    zedlmsg='Found a "'fit'" reference. Suggest you doctor the bottom',
            'of this member manually.'
    Address ISPEXEC "SETMSG MSG(ISRZ001)"
    Exit
