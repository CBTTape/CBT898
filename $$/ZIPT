/******************************** REXX ********************************/
/**********************************************************************/
/* TRACE r                                                            */
/*--------------------------ooo000OOOO000ooo--------------------------*/
/*                                                                    */
/* Routine invoked by ZIP to process PDSses.......................... */
/*                                                                    */
/*--------------------------------------------------------------------*/
    PARSE UPPER ARG parm1 parm2 parm3
    If parm1 ¬= "" Then Signal byp1
    ADDRESS ISPEXEC
    "ISPEXEC CONTROL ERRORS RETURN"
    "ISPEXEC ISREDIT MACRO (parm1,parm2,parm3)"
    If rc > 0 Then SIGNAL byp1
    PARSE UPPER VAR parm1 parm1
    PARSE UPPER VAR parm2 parm2
    PARSE UPPER VAR parm3 parm3

byp1:
    cactn = "RETURN"
    cactn = "CANCEL"
    "ISPEXEC CONTROL ERRORS" cactn

ini1:
    zerrhm   = '*'
    zerralrm = 'NO'
    zerrsm   = ''

    If parm1 = "HELP" | parm1 = "?" Then Do
        "ISPEXEC SELECT PGM(ISPTUTOR) PARM(HNOHELP)"
        Exit
    End
    table = "$M"SUBSTR(date('e'),1,2)||,
                SUBSTR(date('e'),4,2)||,
                SUBSTR(date('e'),7,2)

    dsn   = parm1
    membl = parm2

    Address "ISPEXEC"
    "ISPEXEC TBCREATE &TABLE KEYS(MEMBER) NAMES(LMDATE LMTIME "||,
         " ZLCDATE ZLMDATE ZLMTIME SELCTD "||,
         " ZLCNORC ZLINORC ZLMNORC ZLUSER )"||,
         " REPLACE NOWRITE"

    ISPEXEC "LMINIT DATAID(DATA1) DATASET('&DSN')"
    ISPEXEC "LMOPEN DATAID(&DATA1) OPTION(INPUT)"
    lmrc = rc
    Trace Off
    member = ""
    selctd = ''
    Do While lmrc = 0
        ISPEXEC "LMMLIST DATAID(&DATA1) OPTION(LIST) MEMBER(MEMBER) "||,
                "STATS(YES)"
        lmrc = rc
        member = STRIP(member)
        If lmrc ¬= 0 Then Leave
        ISPEXEC "TBADD &TABLE"
    End
    ISPEXEC "LMMLIST DATAID(&DATA1) OPTION(FREE)"
    ISPEXEC "LMCLOSE DATAID(&DATA1)"
    ISPEXEC "LMFREE  DATAID(&DATA1)"

    tp1 = '0'
    tp2 = '0'
    ISPEXEC "TBTOP &TABLE"
    If membl ¬= '' Then Do
        cmd = 'SEL'
        parm1 = membl
        Signal mls1
    End

dis1:
    sel = ""
    Address "ISPEXEC"
    ISPEXEC "TBDISPL &TABLE PANEL(MEMBLIST)"
    If rc > 4 Then Signal pskel
    Parse Upper VAR zcmd cmd parm1 parm2 .

    If zcmd = 'TRACE' Then Do
        If parm1 = 'OFF' Then Trace Off
        Else                  Trace r
        Signal dis1
    End
    If zcmd = 'RESET' Then Do
        tp1 = 0
        tp2 = 0
        Signal dis1
    End

    If SUBSTR(cmd,1,3) = "CAN" |,
       SUBSTR(cmd,1,3) = "/CA" Then Signal ext1

mls1:
    If cmd = 'SEL' | cmd = 'USEL' Then Do
        If parm1 = '*' Then parm1 = ''
        Else Do
            parm1 = Substr(parm1,1,Index(parm1,'*')-1)
            pl    = Length(parm1)
        End
        If cmd = 'SEL' Then oper = '<==='
        Else                oper = ''
        ISPEXEC "TBTOP &TABLE"
        tt = 0
        Do Forever
            ISPEXEC "TBSKIP &TABLE"
            If rc ¬= 0 Then Leave
            If parm1 ¬= '' Then Do
                mm = Substr(member,1,pl)
                If mm < parm1 Then Iterate
                If mm > parm1 Then Leave
            End
            selctd = oper
            ISPEXEC "TBMOD &TABLE"
            zerrsm = ''
            zerrlm = tt 'members processed.'
            "ISPEXEC SETMSG MSG(ISRZ002)"
            tt = tt + 1
        End
        ISPEXEC "TBTOP &TABLE"
        Signal dis1
    End

    If cmd = 'L' Then Do
        If parm1 = '' Then Do
            zerrlm = 'Missing Locate parameter.'
            "ISPEXEC SETMSG MSG(ISRZ002)"
            Signal dis1
        End
        ISPEXEC "TBTOP &TABLE"
        If Index(parm1,'*') > 0 Then
             member = Substr(parm1,1,Index(parm1,'*')-1)
        member = parm1'*'
        ISPEXEC "TBSCAN &TABLE ARGLIST(MEMBER) CONDLIST(GE)"
        Signal dis1
    End

    If cmd = 'TAG' Then Do
        "ISREDIT (stat1) = USER_STATE"
        "ISREDIT CURSOR = 1 0"
        find = '!(I) 'dsn
        strt = Length(dsn)
        If parm1 ¬= '' Then parm1 = Substr(parm1,1,4)
        Else                parm1 = '<--<'
        Do Forever
            "ISREDIT F '"find"' 1"
            If rc ¬= 0 Then Leave
            ISPEXEC "TBTOP &TABLE"
            "ISREDIT (line) = LINE .ZCSR"
            lb = Index(line,'(',strt)
            rb = Index(line,')',strt)
            member = Substr(line,lb+1,rb-(lb+1))
            ISPEXEC "TBSCAN &TABLE ARGLIST(MEMBER) CONDLIST(EQ)"
            If rc = 0 Then Do
                selctd  = parm1
                ISPEXEC "TBMOD &TABLE"
            End
        End
        "ISREDIT USER_STATE = (stat1)"
        Signal dis1
    End

    If cmd = 'SORT' | cmd = '/SORT' Then Do
        Select
           When (parm1 = 'CHANGED') Then sp = 'ZLMDATE,C,D,ZLMTIME,C,D'
           When (parm1 = 'ID')      Then sp = 'ZLUSER,C,A'
           Otherwise                     sp = 'MEMBER,C,A'
        End
        ISPEXEC "TBSORT &TABLE FIELDS(&SP)"
        Signal dis1
    End

    If Substr(cmd,1,2) = "U=" Then Do
        who = Substr(cmd,3)
        ISPEXEC "TBTOP &TABLE"
        Do Forever
            ISPEXEC "TBSKIP &TABLE"
            If rc ¬= 0 Then Leave
            If zluser = '' Then Iterate
            If zluser ¬= who Then Iterate
            selctd  = "<==="
            ISPEXEC "TBMOD &TABLE"
        End
        Signal dis1
    End
    If Substr(cmd,1,2) = "D=" Then Do
        date = Substr(cmd,3)
        ISPEXEC "TBTOP &TABLE"
        Do Forever
            ISPEXEC "TBSKIP &TABLE"
            If rc ¬= 0 Then Leave
            If zlmdate = '' Then Iterate
            If zlmdate ¬= date Then Iterate
            selctd  = "<==="
            ISPEXEC "TBMOD &TABLE"
        End
        Signal dis1
    End
    If Substr(cmd,1,2) = "D>" Then Do
        date = Substr(cmd,3)
        ISPEXEC "TBTOP &TABLE"
        Do Forever
            ISPEXEC "TBSKIP &TABLE"
            If rc ¬= 0 Then Leave
            If zlmdate = '' Then Iterate
            If zlmdate <= date Then Iterate
            selctd  = "<==="
            ISPEXEC "TBMOD &TABLE"
        End
        Signal dis1
    End
    If Substr(cmd,1,2) = "D<" Then Do
        date = Substr(cmd,3)
        ISPEXEC "TBTOP &TABLE"
        Do Forever
            ISPEXEC "TBSKIP &TABLE"
            If rc ¬= 0 Then Leave
            If zlmdate = '' Then Iterate
            If zlmdate >= date Then Iterate
            selctd  = "<==="
            ISPEXEC "TBMOD &TABLE"
        End
        Signal dis1
    End

    If sel = '' Then Signal dis1

    msub = ztdsels
    Signal con1

mus1:
    ISPEXEC "TBDISPL &TABLE"

con1:
    Select
/*  Put block commands first                                          */
        When (sel = "SS") Then Do
            If msub = 2 Then Do
                "ISPEXEC TBQUERY &TABLE ROWNUM(rownum) POSITION(tp1)"
                "ISPEXEC TBDISPL &TABLE"
                "ISPEXEC TBQUERY &TABLE ROWNUM(rownum) POSITION(tp2)"
            End
            Else Do
                "ISPEXEC TBQUERY &TABLE ROWNUM(rownum) POSITION(tpx)"
                If tp1 = 0 Then Do
                    tp1 = tpx
                    gc009 = "Incomplete '"sel"' block command",
                            "from member '"Strip(member)"'."
                    gc009# = gc009
                    "ISPEXEC SETMSG MSG(GC009)"
                    Signal dis1
                End
                Else Do
                    tp2 = tpx
                    If tp1 > tp2 Then Do
                        tp2 = tp1
                        tp1 = tpx
                    End
                End
            End
            tpd = (Tp2 - tp1) +1
            tp3 = tp1 -1
            "ISPEXEC TBTOP &TABLE"
            "ISPEXEC TBSKIP &TABLE NUMBER(&TP3) POSITION(TABPTR)"
            Do tpd
                "ISPEXEC TBSKIP &TABLE"
                selctd  = "<==="
                ro      = "<==="
                "ISPEXEC TBMOD &TABLE"
            End
            tp1 = 0
            tp2 = 0
            Signal dis1
        End
        When (tp1 > 0) Then Do
              gc009 = gc009#
              "ISPEXEC SETMSG MSG(GC009)"
              Signal dis1
        End
        When (sel = "S") Then Do
            selctd  = "<==="
            ISPEXEC "TBMOD &TABLE"
        End
        When (sel = "E") Then Do
            "ISPEXEC CONTROL DISPLAY SAVE"
            ISPEXEC "EDIT DATASET('&DSN(&MEMBER)')"
            "ISPEXEC CONTROL DISPLAY RESTORE"
        End
        When (sel = "B") Then Do
            "ISPEXEC CONTROL DISPLAY SAVE"
            ISPEXEC "BROWSE DATASET('&DSN(&MEMBER)')"
            "ISPEXEC CONTROL DISPLAY RESTORE"
        End
        When (sel = "D") Then ,
            ISPEXEC "TBDELETE &TABLE"
        When (sel = "X" | sel = "U") Then Do
            selctd  = ""
            ISPEXEC "TBMOD &TABLE"
        End
    End

gnx1:
    msub = msub - 1
    If msub < 1 Then  Signal dis1
    Else              Signal mus1

pskel:
    ISPEXEC "VPUT TABLE SHARED"
    Return 0

ext1:
    ISPEXEC "TBEND &TABLE"
    Return 4
