***********************************************************************
*                                                                     *
*    Title:    'PUTL MACRO USAGE EXAMPLE'                             *
*    System:   N/A                                                    *
*    Module:   $PUTLEX                                                *
*    Program:  $PUTLEX                                                *
*    Function: The purpose of this program is to provide an example   *
*              of the usage of the PUTL macro.                        *
*              The program checks for addressability to the CPPL      *
*              by default that calls to the program would have the    *
*              high order bit in R1 set to indicate end of parameter  *
*              list.                                                  *
*                                                                     *
*    Parameters:                                                      *
*              N/A (ignored).                                         *
*                                                                     *
*    Notes:    Program is re-entrant                                  *
*              AMODE 31, RMODE any.                                   *
*                                                                     *
*    Authors:  ASYSTS Pty. Ltd.                                       *
*              (Alex Kara)                                            *
*                                                                     *
***********************************************************************
*         PRINT OFF
*         COPY  @MACROS                 COMMON MACROS
*         PRINT ON
***********************************************************************
$PUTLEX  KICKOFF (R12),WORKAREA,WORKAREL,LIST=N
                                                                SPACE 1
         LR    R7,R1                   CPPL ADDR
         MVC   TPUTWK1(TPUTCONL),TPUTCON1  MOVE CONSTANT TO DSECT
         MVC   OUTPUTA(40),=C'TPUT567890123456789012345678901234567890'
         MVI   TRACE,C'Y'              <---- FOR PDUMP DIAGNOSIS
         LA    R1,TPUTRTN              LOAD TPUT ADDRESS AS OUTPUT RTN
         ST    R1,DISPRTN@             STORE DISPLAY ROUTINE ADDRESS
*---------------------------------------------------------------------*
*   Get parameter type                                                *
*---------------------------------------------------------------------*
         CLI   0(R7),X'80'             Q. CALL TYPE PARAMETER LIST
         BE    PROCESS                    Y. PROCESS SUCH
         USING CPPL,R7                 ADDRESABILITY TO CPPL
         LA    R1,PUTLRTN              LOAD TPUT ADDRESS AS OUTPUT RTN
         ST    R1,DISPRTN@             STORE DISPLAY ROUTINE ADDRESS
         MVC   OUTPUTA(4),=C'PUTL'
         PUTL  LIST=YES,MF=E,CPPL=(R7) INITIALISE PUTL ROUTINE
PROCESS  DS    0H
         LA    R8,10                   OUTPUT LENGTH FOR PUTLINE
         LA    R9,20                   OUTPUT LENGTH FOR PUTLINE
         CALL  CLEAR                   CLEAR SCREEN
LOOP1    DS    0H
         STH   R8,OUTPUT               STORE LENGTH
         L     R15,DISPRTN@            LOAD DISPLAY ROUTINE ADDRESS
         BALR  R14,R15                 GO AND DISPLAY IT
         LA    R8,1(R8)                BUMP LENGTH
         BCT   R9,LOOP1                LOOP TILL END
                                                                SPACE 1
EXITPR00 DS    0H
         RACKOFF  RC=0                 ZERO RETURN CODE
                                                                SPACE 1
*---------------------------------------------------------------------*
PUTLRTN  DS    0H
         ST    R14,R14SAVER            SAVE RETURN REGISTER
         PUTL  OUTPUT,LIST=YES,MF=E    PUTLINE
         L     R14,R14SAVER            LOAD RETURN REGISTER
         BR    R14                     RETURN TO CALLER
*---------------------------------------------------------------------*
TPUTRTN  DS    0H
         ST    R14,R14SAVER            SAVE RETURN REGISTER
         TPUT  OUTPUTA,(R8),EDIT,WAIT,,BREAKIN,HIGHP,MF=(E,TPUTWK1)
         L     R14,R14SAVER            LOAD RETURN REGISTER
         BR    R14                     RETURN TO CALLER
*=====================================================================*
         LTORG                         LOAD LITERAL POOL HERE
*---------------------------------------------------------------------*
SPACES   DC    CL256' '
                                                                SPACE 1
         PUTL  MF=L,LTYPE=CON,LIST=Y   CONSTANT FORMAT OF PUTL MACRO
                                                                SPACE 1
TPUTCON1 TPUT  MF=L
TPUTCONL EQU   *-TPUTCON1
                                                                SPACE 1
*---------------------------------------------------------------------*
                                                                SPACE 1
WORKAREA DSECT
REGSAVAR DS    18F                     REGISTER SAVE AREA
R14SAVER DS    F                       REGISTER 14 SAVE AREA
DISPRTN@ DS    F                       DISPLAY ROUTINE ADDRESS
TRACE    DS    CL1                     'PDUMP' diagnosis tool
                                                                SPACE 1
OUTPUT   DS    H                       OUTPUT LENGTH FOR PUTLINE
OUTPUTA  DS    CL80                    TPUT BUILD AREA
                                                                SPACE 1
*---------------------------------------------------------------------*
         PUTL  MF=L,LTYPE=VAR,LIST=Y   VARIABLE FORMAT OF PUTL MACRO
TPUTWK1  TPUT  MF=L
*=====================================================================*
WORKAREL EQU   *-WORKAREA
                                                                SPACE 1
         PUTL  LTYPE=IBM,MF=L,LIST=Y   Required IBM DSECTs for putline
                                                                SPACE 1
*---------------------------------------------------------------------*
         END
