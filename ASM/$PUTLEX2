***********************************************************************
*                                                                     *
*  TITLE   : EXAMPLE OF PUTLINE.                                      *
*  SYSTEM  : .....................                                    *
*  MODULE  : TEST11                                                   *
*  PROGRAM : TEST11                                                   *
*  FUNCTION: SIMPLIFIED EXAMPLE OF PUTLINE.                           *
*            THIS EXAMPLE SETS UP THE I/O PARAMETER LIST FOR YOU.     *
*            WHILE THIS MAY BE NICE, YOU NEED TO USE TWO REGISTERS    *
*            TO POINT TO THE UPT AND ECT (YOU CANNOT USE A FIELD TO   *
*            THESE ADDRESSES AS THE MACRO EXPANSION CAUSES THE        *
*            FOLLOWING CODE TO BE GENERATED:                          *
*                REGISTER FORMAT ECT=(R4)                             *
*                --->   ST  R4,4(0,1)                                 *
*                LABEL FORMAT    ECT=ECT@ (WILL CAUSE EXECUTION ABEND)*
*                --->   LA  R14,ECT@     (SHOULD BE  L  R14,ECT@)     *
*                --->   ST  R14,4(0,1)                                *
*                (CODING UPT=UPT@ DOESN'T SEEM TO WORRY IT)           *
*            IF YOU ARE HAPPY TO CONTROL YOUR OWN PROCESSING USE THIS *
*            AS A BASIS.                                              *
*  COMPANY : ASYSTS PTY. LTD.                                         *
*  AUTHOR  : A. KARA                                                  *
*  DATE    : 28/09/94                                                 *
*                                                                     *
***********************************************************************
PUTLINE  KICKOFF (R12),LIST=NO
         LR    R2,R1                   SAVE ADDRESS OF THE CPPL
         USING CPPL,R2                 ADDRESSABILITY FOR THE CPPL
         MVC   UPT@(4),CPPLUPT         SAVE ADDRESS OF THE UPT
         MVC   ECT@(4),CPPLECT         SAVE ADDRESS OF THE UPT
         DROP  R2                      RELEASE ADDRESSABILITY
         LA    R9,20                   LOOP COUNTER
LOOP     DS    0H
         UNPACK R9,4,MESSAGE+8         STORE LOOP COUNTER IN MESSAGE
         BAL   R14,PUTLRTN             INVOKE PUTLINE ROUTINE
         BCT   R9,LOOP                 LOOP TILL END
EXITPR00 DS    0H
         RACKOFF RC=0
PUTLRTN  DS    0H
         ST    R14,SAVER14             SAVE RETURN ADDRESS
         L     R3,UPT@                 LOAD UPT ADDRESS
         L     R4,ECT@                 LOAD ECT ADDRESS
         LA    R5,MESSAGEO             LOAD MESSAGE ADDRESS
         XC    ECB@,ECB@               INITIALISE ECB
         PUTLINE PARM=PUTBLOCK,UPT=(R3),ECT=(R4),ECB=ECB@,             +
               OUTPUT=((R5),TERM,SINGLE,DATA),                         +
               MF=(E,IOPLADS)
         L     R14,SAVER14             RESTORE RETURN ADDRESS
         BR    R14                     RETURN TO CALLER
         LTORG
SAVER14  DS    F
XXX@     DS    F
ECB@     DS    F
UPT@     DS    F
ECT@     DS    F
PUTBLOCK PUTLINE MF=L
MESSAGEO DC    H'83'                   LENGTH OF OUTPUT LINE
         DC    H'0'                    RESERVED
MESSAGE  DC    CL79'PUTLINE .... COUNT-DOWN'
IOPLADS  DC    4F'0'                   SPACE FOR I/O PARAMETER LIST
SPACES   DC    CL256'                  '
         IKJCPPL
         END  ,
