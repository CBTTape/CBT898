*---------------------------------------------------------------------*
START    DS    0H
         LR    R7,R1                   CPPL ADDR
         ST    R1,R1SAVER              STORE REGISTER 1 LATER USE
         USING CPPL,R7                 ADDRESABILITY TO CPPL
         BAL   R14,GETMAIN1            GO AND GET WORK AREA
         .........
*---------------------------------------------------------------------*
*- SET UP 'IOPL' FOR PUTLINE -----------------------------------------*
*---------------------------------------------------------------------*
         LA    R15,MYIOPL              LOAD ADDRESS TO IOPL
         USING IOPL,R15                ESTABLISH ADDRESABILITY
         MVC   IOPLUPT(4),CPPLUPT      MOVE ADDRESS OF UPT TO IOPL
         MVC   IOPLECT(4),CPPLECT      MOVE ADDRESS OF ECT TO IOPL
         LA    R0,MYECB                LOAD ADDRESS OF MY ECB
         ST    R0,IOPLECB              STORE ADDRESS IN IOPL
         XC    MYECB,MYECB             CLEAR ECB ADDRESS
         LA    R0,MYPTPB               LOAD @ OF PUTLINE PARM BLOCK
         ST    R0,IOPLIOPB             STORE IT IN IOPL
         DROP  R15                     LELINQUISH ADDRESABILITY
         L     R15,16                  CVT ADDRESS
         L     R15,0(R15)              POINT TO CVT
         TM    444(R15),X'80'          Q. IS PUTLINE LOADED? (MVS)
         BNO   PUTLOAD                    N. BRANCH TO LOAD
         L     R15,444(,R15)           USE CVTPUTL
         B     PUTLOADX                BRANCH AROUND LOAD
PUTLOAD  DS    0H
         LA    R0,=CL8'IKJPUTL '       LOAD PROGRAM NAME
         LOAD  EPLOC=(0)               LOAD PROGRAM
         LR    R15,R0                  GET ENTRY ADDRESS
         LA    R15,0(,R15)             CLEAR HI BYTE FOR DELETE ROUTINE
PUTLOADX DS    0H
         ST    R15,MYPUTLEP        SAVE PUTLINE ENTRY ADDRESS
*---------------------------------------------------------------------*
         ...........
*---------------------------------------------------------------------*
*- PUTLINE ROUTINE (CALLED) ------------------------------------------*
*---------------------------------------------------------------------*
TPUTRTN  DS    0H
         ST    R14,R14SAVER            SAVE RETURN REGISTERS
         XC    MYSEG1(4),MYSEG1        INITIALISE LENGTH
         MVC   MYPTPB(12),MODLPTPB     MOVE IN MODEL FROM CONSTANT
         LA    R14,79                  LENGTH OF MESSAGE
         LA    R14,4(,R14)             ADD 4
         STH   R14,MYSEG1              STORE LENGTH
         L     R15,MYPUTLEP
         PUTLINE PARM=MYPTPB,OUTPUT=(MYSEG1,DATA),MF=(E,MYIOPL),       +
               ENTRY=(15)
         LA    R15,4                   SET NON ZERO RETURN CODE
         L     R14,R14SAVER            RESTORE RETURN REGISTERS
         BR    R14                     RETURN TO CALLER
*---------------------------------------------------------------------*
*- DATA AREA (CONSTANT) ----------------------------------------------*
*---------------------------------------------------------------------*
MODLPTPB PUTLINE OUTPUT=(1,TERM,SINGLE,DATA),                          X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
*---------------------------------------------------------------------*
*- DATA AREA (VARIABLE) ----------------------------------------------*
*---------------------------------------------------------------------*
MYPUTLEP DS    F                    USED BY PUTLINE ROUTINE
MYECB    DS    F                    USED BY PUTLINE ROUTINE
MYIOPL   DS    4F                   USED BY PUTLINE ROUTINE
MYPTPB   DS    3F                   USED BY PUTLINE ROUTINE
MYSEG1   DS    2H                   USED BY PUTLINE ROUTINE
MESSAGEG DS    CL79                 GENERAL MESSAGE LINE
*---------------------------------------------------------------------*
*- IBM SUPPLIED DSECTS -----------------------------------------------*
*---------------------------------------------------------------------*
         IKJCPPL COMMAND           PROCESSOR PARAMETER LIST
         IKJIOPL                   I/O SERVICE ROUTINE PARAMETER LIST
*---------------------------------------------------------------------*
