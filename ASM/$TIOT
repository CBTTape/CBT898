***********************************************************************
* TIOT NAVIGATION WITH SWA BOTH BELOW AND ABOVE THE LINE.             *
***********************************************************************
         PRINT OFF
         COPY  @MACROS                 COMMON MACROS
         PRINT ON
TIOTNAV  KICKOFF (R11,R12),WORKAREA,WORKAREL,LIST=NO,AMODE=31
                                                                SPACE 1
*---------------------------------------------------------------------*
* GET TIOT FROM JOBSTEP TCB                                           *
*---------------------------------------------------------------------*
         XR    R7,R7                   INITIALISE COUNTER
         L     R1,X'21C'               CURRENT TCB (SHOULD BE MINE)
         L     R1,X'7C'(R1)            JOBSTEP TCB (SHOULD BE MINE)
         L     R3,X'0C'(R1)            TIOT ADDRESS
*---------------------------------------------------------------------*
* LOOP THROUGH THE TIOT                                               *
*---------------------------------------------------------------------*
OUTLOOP  DS    0H
         USING TIOT,R3                 ADDRESABILITY TO TIOT
         XR    R1,R1                   CLEAR REGISTER
         IC    R1,TIOELNGH             LOAD TIO ENTRY LENGTH
         LTR   R1,R1                   Q. = 0 (END OF CHAIN)
         BZ    ENDTIOT                    Y. EXIT OUTER LOOP
         STH   R1,ELENGTH              STORE ENTRY LENGTH
*---------------------------------------------------------------------*
         XR    R5,R5                   CLEAR REGISTER
         ICM   R5,B'0111',TIOEJFCB     LOAD TIO ENTRY'S JCFB ADDRESS
  PDUMP TIOEJFCB,MSG='R5-JFCB@',QUIT=EXITPROG,REGS=NO    <====
         CLI   TIOEJFCB,X'00'          Q. HIGH ORDER BYTE ZERO
*                                         (INDICATING A TOKEN FOR >16M)
         BNE   GOTJFCB                    N. BYPASS ALTERNATE GET
*---------------------------------------------------------------------*
* NOT AN ADDRESS BUT A TOKEN. NEED TO USE THE 'SWAREQ' MACRO TO GET   *
* JFCB DETAILS.                                                       *
*---------------------------------------------------------------------*
         LA    R7,EPA                  GET ADDRESS OF EPA
         ST    R7,SWEPAPTR             INITIALISE EPA POINTER
         USING ZB505,R7                ESTABLISH ADDRESABILITY TO EPA
         XC    SWAEPA,SWAEPA           INITIALISE THE EPA
         XC    SWAEPAX,SWAEPAX         INITIALISE THE EPAX
         MVC   SWVA,TIOEJFCB           MOVE SVA OF JFCB INTO EPA
         SWAREQ FCODE=RL,EPA=SWEPAPTR,MF=(E,SWAPARMS), LOCATE THE JFCB +
               UNAUTH=YES
         L     R5,SWBLKPTR             POINT TO JFCB
         SH    R5,=H'16'               BACK UP TO START OF JFCB
*---------------------------------------------------------------------*
GOTJFCB  DS    0H
  PDUMP (R5),QUIT=EXITPROG,REGS=NO,MSG='JFCB'    <=====
         USING JFCB,R5                 ADDRESABILITY TO JFCB
         AH    R3,ELENGTH              UP SOURCE BY LENGTH OF ENTRY
         B     OUTLOOP                 GET NEXT RECORD
                                                                SPACE 1
ENDTIOT  DS    0H
EXITPROG DS    0H
         RACKOFF RC=(R15)              RETURN TO CALLER
                                                                SPACE 1
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
SWAPARMS SWAREQ MF=L
*---------------------------------------------------------------------*
                                                                SPACE 1
*---------------------------------------------------------------------*
*  GETMAINED WORK AREA                                                *
*---------------------------------------------------------------------*
WORKAREA DSECT
         DS    18F                     REGISTER SAVE AREA
ELENGTH  DS    H                       TIOT ENTRY LENGTH
*---------------------------------------------------------------------*
SWEPAPTR DS    F
EPA      DS    CL16
*---------------------------------------------------------------------*
                                                                SPACE 1
WORKAREL EQU   *-WORKAREA
                                                                SPACE 1
*---------------------------------------------------------------------*
TIOT     DSECT
         IEFTIOT1                      ENTRY : TIOT
JFCB     DSECT
DUMMY    DS    CL16                    POINT PAST CRAP AT FRONT
*                                          (COMPENSATION) FOR HEADER
         IEFJFCBN LIST=YES             ENTRY : JFCB
         IEFJESCT
         IEFZB505 LOCEPAX=YES
         IHASDWA  DSECT=YES            ENTRY : SDWA (FOR ESTAE)
         CVT   DSECT=YES               CVT
                                                                SPACE 1
*---------------------------------------------------------------------*
         END
