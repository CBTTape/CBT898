*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
**
**       TITLE:    'ABEND WITH S0C7'
**       SYSTEM:   N/A
**       MODULE:   ABEND0C7
**       PROGRAM:  ABEND0C7
**       FUNCTION: CREATES AN ABEND (0C7) ON DEMAND AND TRAPS IT AND
**                 CONTINUES EXECUTING.
**
**       AUTHOR:   A. KARA.
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
A0C7CONT KICKOFF (R13,R12)
         WTO   'START OF PROGRAM',                                     *
               ROUTCDE=(2,12),DESC=(7)
*---------------------------------------------------------------------*
* PREPARE FOR "ESTAE" WHEN END OF ADDRESS SPACE IS REACHED.           *
*---------------------------------------------------------------------*
         STM   R10,R13,BASES#1         STORE MAIN REGISTERS
         LA    R1,ESTAEOF1             RETURN ADDRESS
         ST    R1,ESTAERTN             STORE RETURN ADDRESS
         LA    R5,PROCESTA
         ESTAE (5)                     TRAP "ABEND"
         ZAP    DWORK8(8),FRED(4)
ESTAEOF1 DS    0H
         ESTAE 0                    TURN ESTAE OFF
         WTO   'FINISHED ESTAE PROCESSING',                            *
               ROUTCDE=(2,12),DESC=(7)
EXITPROG DS    0H
         LH    R15,RCODE                SET RETURN CODE
         RACKOFF RC=(R15)
*=====================================================================*
         LTORG                          LOAD LITERAL POOL HERE
*---------------------------------------------------------------------*
DWORK8   DS    D
FRED     DC    C'FRED'
RCODE    DC    H'0'
*---------------------------------------------------------------------*
PROCESTA DS    0F
         LR    R2,R15                   R1 SET AS BASE
         USING PROCESTA,R2              BASE REGISTER
         LM    R10,R13,12(R15)          RESTORE REGISTERS FROM "BASES1"
         B     MOVEERR                  BYPASS R10 - R13 SAVE AREA
BASES#1  DS    4F
XXXXXX   DC    CL30'----->ESTAE EYE CATCHER<------'
SYSRCODE DS    CL2                      SYSTEM RUTURN CODE SAVE AREA
*---------------------------------------------------------------------*
MOVEERR  DS    0H
         STM   R0,R15,BASES#2           STORE REGISTERS INTO "BASES2"
*---------------------------------------------------------------------*
*                                                                     *
*        CONTROL GETS HERE IF THE AN ABEND OCCURED DURING A MOVE      *
*        FROM SECONDARY TO PRIMARY. USUALLY CAUSED BY AN ADDRESS      *
*        EXCEPTION ERROR.                                             *
*                                                                     *
*---------------------------------------------------------------------*
         WTO   'IN ESTAE ROUTINE',                                     *
               ROUTCDE=(2,12),DESC=(7)
         LM    R0,R15,BASES#2             RESTORE BEFORE 'WTO'
*        LA    R15,12
         C     R0,=F'12'                  Q. SDWA CREATED BY RTM2
         BE    MESTAER2                      N. BYPASS PROCESSING
*---------------------------------------------------------------------*
         LM    R0,R15,BASES#2           RESTORE AFTER 'WTO'
         LR    R9,R1
         USING SDWA,R9                    SDWA MAPPING
         L     R3,SDWAABCC                LOAD USER/SYSTEM ABEND CODE
         SRL   R3,12                      MOVE OUT USER CODE.
         STCM  R3,3,SYSRCODE              EXTRACT SYSTEM RETURN CODE.
         NC    SYSRCODE(2),=X'0FFF'       ISOLATE RETURN CODE.
         HEXCHMAC SYSRCODE,WTO1+21,2      DISPLAYABLE ABEND CODE
         B     WTO1                       BYPOASS POSIIBLE FILLERS
WTO1     DS    0F
         WTO   'ABEND CODE X''....'' TRAPPED',                         *
               ROUTCDE=(2,12),DESC=(7)
         LM    R0,R15,BASES#2           RESTORE AFTER 'WTO'
*---------------------------------------------------------------------*
         SETRP FRESDWA=YES,RC=4,RETADDR=MESTAER,DUMP=NO
         BR    R14                       RETURN
MESTAER  DS    0F
         LM    R0,R13,8(R15)             RESTORE REGISTERS
         B     MESTAER2
BASES#2  DS    16F
MESTAER2 DS    0H
*---------------------------------------------------------------------*
         MVC   RCODE(2),=H'8'           SET ERRO RETURN CODE
         L     R1,ESTAERTN              LOAD ESTAE RETURN ADDRESS
         BR    R1                       CONTINUE
ESTAERTN DS    A                        RETURN ADDRESS FROM ESTAE
*---------------------------------------------------------------------*
         IHASDWA DSECT=YES          ENTRY : SDWA
*---------------------------------------------------------------------*
         END
