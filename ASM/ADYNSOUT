*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
**
**       TITLE:    'TEST SYSOUT DYNAMIC ALLOCATION'
**       SYSTEM:   N/A
**       MODULE:   INTRDRSU
**       PROGRAM:  INTRDRSU
**       FUNCTION: SUBMIT A JOB(JCL) USING AN INTERNAL READER.
**
**                 PROGRAM RE-ENTRANT.
**
**       AUTHOR:   A. KARA.
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
ADYNSOUT KICKOFF (R12),WORKAREA,WORKAREL,LIST=NO
         LA    R10,SVC99WRK                SVC 99 PARAMATER LIST ADDRS
         BAL   R14,FREEDDS             ENSURE DATASETS ARE FREE
         BAL   R14,PROCESS             GO AND PROCESS
         BAL   R14,FREEDDS             FREE DATASETS
EXITPROG RACKOFF RC=(R15)
*
*=====================================================================*
PROCESS  DS    0H
         ST    R14,SAVSUBR1            SAVE RETURN REGISTERS
INIT1    DS    0H
         MVC   OPENL(OPENSKLL),OPENSKL MOVE CONSTANT TO DSECT
         MVC   INTRDR(INTRDRL),INTRDRC MOVE CONSTANT TO DSECT
         MVC   SYSPRINT(SYSPRINL),SYSPRINC MOVE CONSTANT TO DSECT
*
ALLOC1   ADYN  ACTION=ALLOC,DD=SYSPRINT,RECFM=FBA,LRECL=133,           X
               BLKSIZE=1330,SYSOUT=X,PARMT=PARM,UCLOSE=YES,DSORG=PS,   X
               MF=E,BASE=(R10),DEST=FRED01
*
         PDUMPR (R10),MSG='SYSPRINT ALLOCATE',QUIT=EXITPROG
*
ALLOC2   ADYN  ACTION=ALLOC,DD=INTRDR,RECFM=F,LRECL=80,BASE=(R10),     X
               BLKSIZE=80,SYSOUT=(A,INTRDR),PARMT=PARM,UCLOSE=YES,     X
               DSORG=PS,BUFNO=1,MF=E
*
OPENFILE DS    0H
         LA    R3,SYSPRINT             DCB FOR SYSPRINT
         LA    R1,OPENL                OPEN LIST
         OPEN  ((R3),(OUTPUT)),MF=(E,(R1)) OPEN INTERNAL READER
         LA    R2,INTRDR               DCB FOR INTERNAL READER
         LA    R1,OPENL                OPEN LIST
         OPEN  ((R2),(OUTPUT)),MF=(E,(R1)) OPEN INTERNAL READER
         USING IHADCB,R2               AND TELL ASSEMBLER ABOUT IT.
         TM    DCBOFLGS,DCBOFOPN       Q. IS IT OPEN
         BNO   ERRIOPE                    N. THEN REPORT ERROR
         DROP  R2                      TELL ASSEMBLER TO FORGET IT.
         BAL   R14,SUBMIT              SENT ARCHIVE JOB.
         LA    R1,OPENL                OPEN LIST
         CLOSE ((R2)),MF=(E,(R1))      CLOSE INTERNAL READER
         B     RETMAIN1                EXIT ROUTINE
ERRIOPE  DS    0H
         PUT   (R3),OPENERR1           PUT OPEN ERROR
RETMAIN1 DS    0H
         LA    R1,OPENL                OPEN LIST
         CLOSE ((R3)),MF=(E,(R1))      CLOSE INTERNAL READER
         L     R14,SAVSUBR1            RESTORE SAVED REGISTERS
         BR    R14                     RETURN TO CALLER
*=====================================================================*
SUBMIT   DS    0H
         ST    R14,SAVSUBR2            SAVE RETURN REGISTERS
INIT2    DS    0H
         LA    R5,JOBCARD              LOAD CARD 1
         LA    R7,JOBDECK              LOAD LENGTH OF JOB DECK
         PUT   (R3),TRACE1             PUT TRACE HEADING
         MVC   TRACE,SPACES            CLEAR OUTPUT
PUTLOOP  DS    0H
         MVC   TRACE+5(80),0(R5)       PRINT JCL SENT TO INTRDR
         PUT   (R2),(R5)               PUT CARD DETAILS
         PUT   (R3),TRACE              PUT CARD DETAILS TO TRACE
         LA    R5,80(R5)               POINT TO NEXT CARD
         BCT   R7,PUTLOOP              LOOP TILL END
RETMAIN2 DS    0H
         L     R14,SAVSUBR2            RESTORE SAVED REGISTERS
         BR    R14                     RETURN TO CALLER
*=====================================================================*
FREEDDS  DS    0H
         ST    R14,SAVSUBR1            SAVE RETURN REGISTERS
FREE1    ADYN  ACTION=UNALOC,DD=INTRDR,BASE=(R10),MF=E
FREE2    ADYN  ACTION=FREE,DD=SYSPRINT,BASE=(R10),MF=E
RETMAIN3 DS    0H
         L     R14,SAVSUBR1            RESTORE SAVED REGISTERS
         BR    R14                     RETURN TO CALLER
*=====================================================================*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
SPACES   DC    CL256' '
OPENERR1 DC    CL133'1ERROR OPENING THE INTERNAL READER - DD ''INTRDR''X
               .'
TRACE1   DC    CL133'1THE FOLLOWING CARDS HAVE BEEN SENT TO THE INTERNAL
               L READER :-'
JOBCARD  DC    CL80'//C838704I JOB TPCICS001,''HALKEY PRINT'','
         DC    CL80'//    MSGCLASS=X,CLASS=X,NOTIFY=&&SYSUID,'
         DC    CL80'//    TYPRUN=SCAN'
         DC    CL80'//STEP1  EXEC PGM=IEFBR14'
         DC    CL80'//DD1    DD   SYSOUT=*'
JOBDECK  EQU   (*-JOBCARD)/80
*---------------------------------------------------------------------*
INTRDRC  DCB   DDNAME=INTRDR,              INTERNAL READER DCB         X
               BUFNO=1,                                                X
               DSORG=PS,                                               X
               MACRF=(PM)
INTRDRL  EQU   *-INTRDRC
*---------------------------------------------------------------------*
SYSPRINC DCB   DDNAME=SYSPRINT,                                        X
               DSORG=PS,                SEQUENTIAL                     X
               MACRF=(PM),                                             X
               LRECL=133,                                              X
               BLKSIZE=133,                                            X
               RECFM=FA
SYSPRINL EQU   *-SYSPRINC
*---------------------------------------------------------------------*
OPENSKL  OPEN  (),MF=L
OPENSKLL EQU   *-OPENSKL
*
*---------------------------------------------------------------------*
WORKAREA DSECT
REGSAVE  DS    18F
SAVSUBR1 DS    18F
SAVSUBR2 DS    18F
TRACE    DS    CL133
SVC99WRK DS    CL300
*---------------------------------------------------------------------*
INTRDR   DCB   DDNAME=INTRDR,              INTERNAL READER DCB         X
               BUFNO=1,                                                X
               DSORG=PS,                                               X
               MACRF=(PM)
*---------------------------------------------------------------------*
SYSPRINT DCB   DDNAME=SYSPRINT,                                        X
               DSORG=PS,                SEQUENTIAL                     X
               MACRF=(PM),                                             X
               LRECL=133,                                              X
               BLKSIZE=133,                                            X
               RECFM=FA
*---------------------------------------------------------------------*
OPENL    OPEN  (),MF=L
*---------------------------------------------------------------------*
         ADYN  MF=L
*---------------------------------------------------------------------*
WORKAREL EQU   *-WORKAREA
*---------------------------------------------------------------------*
         DCBD
*---------------------------------------------------------------------*
IBMDSECT ADYN  DATA=IBM
*---------------------------------------------------------------------*
         END
