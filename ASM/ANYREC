*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*
**       TITLE:    TEST FOR EMPTY DATASET
**       SYSTEM:   N/A
**       MODULE:   ANYREC
**       PROGRAM:  ANYREC
**       FUNCTION: Reads the first record of a dataset.
**                 If 1st. read sucessfull, then there are records in
**                 dataset otherwise EODAD processing occurs.
**       PARAMETERS:
**                 INPUT  - You may pass a one digit parameter either
**                          to be used as a return code rather than 8
**                          for empty datasets.
**                 OUTPUT - RC - 0 for records
**                             - 8 for empty dataset (or passed
**                               parameter).
**
**       AUTHOR:   ASYSTS P/L (A. Kara.) for Vic Roads.
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*        PRINT OFF
*        COPY  @MACROS                 COMMON MACROS
*        PRINT ON
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
ANYREC   KICKOFF (R13),LIST=NO,HELP=NO
         LR    R7,R1                CPPL ADDR
         CLI   0(R7),X'80'          Q. CONVENTIONAL PARAMETER TYPE
         BNE   OPENFILE                N. PROCESS W/O PARAMETER
         L     R7,0(R7)             LOAD ADDRESS OR PARAMETER
         LH    R1,0(R7)             LOAD LENGTH OF PARAMETER
         LTR   R1,R1                Q. IS PARAMETER LENGTH ZERO
         BZ    OPENFILE                N. ERROR EXIT
         LA    R2,2(R7)             POINT PAST PARAMETER LENGTH
         CLI   0(R2),C'0'           Q. NON NUMERIC RETURN CODE
         BL    OPENFILE                Y. ERROR EXIT
         PACK  DWORK8(8),0(1,R2)    PACK RETURN CODE
         CVB   R9,DWORK8            CONVERT TO BINARY
         STH   R9,RCODE             STORE RETURN CODE
OPENFILE DS    0H
         OPEN  (INFILE,(INPUT))     OPEN INPUT FILE
         GET   INFILE,WORKAREA      GET FIRST RECORD
         XR    R2,R2                EODAD NOT REACHED SET RC=0
         B     EXITPROG             EXIT PROGRAM
INFEND   DS    0H
         LH    R2,RCODE             END OF FILE ON 1ST. READ ERROR
EXITPROG DS    0H
         CLOSE INFILE               CLOSE OUTPUT DISK FILE
         LR    R15,R2               SET RETURN REGISTER
         RACKOFF RC=(R15)           EXIT PROGRAM
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*---------------------------------------------------------------------*
         LTORG                         LOAD LITERAL POOL HERE
*---------------------------------------------------------------------*
DWORK8   DS    D
RCODE    DC    H'8'
*
INFILE   DCB   DDNAME=INFILE,                                          +
               DSORG=PS,                                               +
               MACRF=(GM),                                             +
               SYNAD=INFEND,                                           +
               EODAD=INFEND
*
WORKAREA DS    CL32760                 WORK AREA
*
         END   ANYREC
