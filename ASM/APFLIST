***********************************************************************
*                                                                     *
*    Title:    'RETURN APF LIST FOR MVS 4.3 OR ABOVE'                 *
*    System:   N/A                                                    *
*    Module:   APFLIST                                                *
*    Program:  APFLIST                                                *
*    Function: This program returns the APF list for MVS version 4.3  *
*              or above.                                              *
*                                                                     *
*              If invoked as a command processor (ie. CPL found),     *
*              the data is displayed using PUTLINE which will permit  *
*              the capture of the data using OUTTRAP for subsequent   *
*              processing.                                            *
*              Otherwise, TPUT is used after a clear screen.          *
*                                                                     *
*    Notes:    Program not is re-entrant                              *
*              AMODE 24, RMODE Any.                                   *
*    Authors:  ASYSTS Pty. Ltd. (A. Kara)                             *
*    Date:     15th February, 1995.                                   *
*                                                                     *
***********************************************************************
*         PRINT OFF
*         COPY  @MACROS                 COMMON MACROS
*         PRINT ON
***********************************************************************
APFLIST  KICKOFF (R12),LIST=N
*---------------------------------------------------------------------*
         LR    R7,R1                   CPPL ADDR
         MVI   TRACE,C'Y'              <---- FOR PDUMP DIAGNOSIS
         LA    R1,TPUTRTN              LOAD TPUT ADDRESS AS OUTPUT RTN
         ST    R1,DISPRTN@             STORE DISPLAY ROUTINE ADDRESS
*---------------------------------------------------------------------*
*   Get parameter type:                                               *
*    If command processor, use putline as display vehicle (permitting *
*    capture using OUTTRAP command), otherwise, use TPUT for displays.*
*---------------------------------------------------------------------*
         CLI   0(R7),X'80'             Q. CALL TYPE PARAMETER LIST
         BNE   CMDPROC                    N. COMMAND PROCEDURE
         CALL  CLEAR                   CLEAR SCREEN
         B     PROCESS                 PROCESS SUCH
CMDPROC  DS    0H
         USING CPPL,R7                 ADDRESABILITY TO CPPL
         LA    R1,PUTLRTN              LOAD TPUT ADDRESS AS OUTPUT RTN
         ST    R1,DISPRTN@             STORE DISPLAY ROUTINE ADDRESS
         PUTL  LIST=YES,CPPL=(R7)      INITIALISE PUTL ROUTINE
PROCESS  DS    0H
         LA    R2,DATA                 ADDRESS OF RETURNED DATA
         CSVAPF REQUEST=LIST,ANSAREA=(R2),ANSLEN=DATAL
         USING APFHDR,R2               ADDRESABILITY TO APF HEADER
         L     R9,APFH#REC             LOAD NUMBER OF RECORDS
         A     R2,APFHOFF              OFFSET TO 1ST. ENTRY
         DROP  R2
         USING APFE,R2
LOOP01   DS    0H
         MVC   VOLUME,APFEVOLUME       MOVE VOLUME
         MVC   DSNAME,APFEDSNAME       MOVE DATASET NAME
         MVC   SMSF,=CL3'NO'
         TM    APFEFLAGS,APFESMS       Q. SMS MANAGED
         BNO   NOTSMS                     N. BYPASS
         MVC   SMSF,=C'YES'            SET SMS MANAGED FLAG
NOTSMS   DS    0H
         L     R15,DISPRTN@            LOAD DISPLAY ROUTINE ADDRESS
         BALR  R14,R15                 GO AND DISPLAY IT
         AH    R2,APFELEN              UP BY ENTRY LENGTH
         BCT   R9,LOOP01               LOOP TILL END
EXITPROG DS    0H
EXITPR00 DS    0H
         RACKOFF RC=0                  EXIT PROGRAM
                                                                SPACE 1
*---------------------------------------------------------------------*
PUTLRTN  DS    0H
         ST    R14,R14SAVER            SAVE RETURN REGISTER
         MVC   OUTPUT(2),=H'79'        SET UP LENGTH OF OUTPUT
         PUTL  OUTPUT,LIST=YES         PUTLINE
         L     R14,R14SAVER            LOAD RETURN REGISTER
         BR    R14                     RETURN TO CALLER
*---------------------------------------------------------------------*
TPUTRTN  DS    0H
         ST    R14,R14SAVER            SAVE RETURN REGISTER
         TPUT  OUTPUTA,L'OUTPUTA,EDIT,WAIT,,BREAKIN,HIGHP
         L     R14,R14SAVER            LOAD RETURN REGISTER
         BR    R14                     RETURN TO CALLER
*=====================================================================*
*#####################################################################*
         LTORG
                                                                SPACE 1
R14SAVER DS    F                       RETURN REGISTER SAVE AREA
DISPRTN@ DS    F                       DISPLAY ROUTINE ADDRESS
DATAL    DC    A(L'DATA)               LENGTH OF DATA
OUTPUT   DS    H                       OUTPUT LENGTH FOR PUTLINE
OUTPUTA  DC    CL79' '                 TPUT BUILD AREA
         ORG   OUTPUTA                 REDEFINE OUTPUTA
DSNAME   DS    CL44                    DATASET NAME
         DS    CL1                     SEPERATOR
VOLUME   DS    CL6                     VOLSER
         DS    CL1                     SEPERATOR
SMSF     DS    CL3                     SMS FLAG
         ORG
TRACE    DS    CL1                     RESERVED FOR 'PDUMP' TRACING
DATA     DS    CL32760                 DATA AREA
*---------------------------------------------------------------------*
         PUTL  LTYPE=IBM,MF=L,LIST=Y   Required IBM DSECTs for putline
         CSVAPFAA
         END
