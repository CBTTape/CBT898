READACCT CSECT
READACCT AMODE ANY
READACCT RMODE 24
         USING  READACCT,R12
         SAVE   (14,12)
         LR    R12,R15
         ST    R13,SAVE+4
         LA    R15,SAVE
         ST    R15,8(R13)
         LR    R13,R15
         LM    R2,R3,0(R1)
         LA    R2,0(R2)
         LA    R3,0(R3)
         SR    R1,R1
         L     R4,MSET1
         BSM   R1,R4
MSET1    DC    A(MSET2+X'80000000')
MSET2    ST    R1,MODESAVE
         CLC   0(2,R2),=2X'FF'      REQUEST TO CLOSE THE FILE.
         BNE   OPEN                 GO CHECK FOR OPENING FILE.
         CLI   OPEN+1,X'F0'         IF FILE WAS NOT OPENED, THEN
         BNE   QUIT                 DON'T CLOSE IT.
         CLOSE (ACCTNO1),MODE=31
         LTR   R15,R15              VSAM CLOSE OKAY?
         BZ    DLVRP                YES.
         WTO   'READACCT - VSAM CLOSE FAILED FOR ACCTNO1',             +
               DESC=(7),ROUTCDE=(11)
         ABEND 4004,DUMP
DLVRP    DLVRP TYPE=LSR,MODE=31,SHRPOOL=2
         MVI   OPEN+1,X'00'         NEXT READ WILL OPEN FILE.
         B     QUIT
         EJECT
OPEN     NOP READ                   BRANCH IF FILE ALREADY OPENED.
         MVI   OPEN+1,X'F0'         NEXT READ WILL NOT OPEN FILE.
         BLDVRP BUFFERS=(4096(1100)),                                  +
               KEYLEN=4,                                               +
               RMODE31=ALL,                                            +
               SHRPOOL=2,                                              +
               MODE=31,                                                +
               STRNO=1,                                                +
               TYPE=(LSR,DATA)
         LTR   R15,R15
         BZ    BLDVRP2
*------\|/------------------------------------------------------------*
         ST    R15,FREDXX+8+11
FREDXX   WTO   'READACCT - .........',                                 +
               DESC=(7),ROUTCDE=(11)
*------/|\------------------------------------------------------------*
         WTO   'READACCT - DATA BLDVRP FAILED FOR ACCTNO1',            +
               DESC=(7),ROUTCDE=(11)
         ABEND 4000,DUMP
BLDVRP2  BLDVRP BUFFERS=(2048(20)),                                    +
               KEYLEN=4,                                               +
               RMODE31=ALL,                                            +
               SHRPOOL=2,                                              +
               MODE=31,                                                +
               STRNO=1,                                                +
               TYPE=(LSR,INDEX)
         LTR   R15,R15
         BZ    BLDVRPOK
         WTO   'READACCT - INDEX BLDVRP FAILED FOR ACCTNO1',           +
               DESC=(7),ROUTCDE=(11)
         ABEND 4001,DUMP
BLDVRPOK OPEN  (ACCTNO1),MODE=31
         LTR   R15,R15              VSAM OPEN OKAY?
         BZ    READ                 YES.
         WTO   'READACCT - VSAM OPEN FAILED FOR ACCTNO1',              +
               DESC=(7),ROUTCDE=(11)
         ABEND 4002,DUMP
         EJECT
READ     MODCB RPL=RPL,AREA=(R3),ARG=(R2)
         GET   RPL=RPL
         LTR   R15,R15             VSAM READ OKAY?
         BZ    QUIT                YES.
         CH    R15,=H'8'           RECORD NOT FOUND.
         BNE   ERROR
         SHOWCB RPL=RPL,FIELDS=FDBK,AREA=DFDBK,LENGTH=4
         LA    R4,16
         C     R4,DFDBK
         BE    NONE
ERROR    WTO   'READACCT - READ FAILED FOR ACCTNO1',                   +
               DESC=(7),ROUTCDE=(11)
         ABEND 4003,DUMP
NONE     MVC 0(174,R3),DHIGHVAL        RECORD NOT FOUND.
QUIT     LA  R1,QUITX
         O     R1,MODESAVE
         BSM   0,R1
QUITX    L   R13,SAVE+4
         RETURN (14,12),T,RC=0
         EJECT
ACCTNO1  ACB   DDNAME=ACCTNO1,RMODE31=ALL,SHRPOOL=2,STRNO=1,           +
               MACRF=(KEY,DIR,IN,LSR)
RPL      RPL   ACB=ACCTNO1,                                            +
               ARG=DKEY,                                               +
               OPTCD=(KEY,DIR,MVE,SYN,NUP),                            +
               AREA=DRECORD,                                           +
               AREALEN=174
DKEY     DS    0D
DRECORD  DS    0D
DFDBK    DS    F
MODESAVE DS    F
SAVE     DS    18F
DHIGHVAL DC    174X'FF'       USED FOR INDICATING RECORD NOT ON FILE.
         LTORG
         REGEQU
         END
