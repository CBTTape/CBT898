*
**       TITLE:    CONVERT ISPF PANELS TO "SBA" DATA STRINGS FOR TPUT
**       SYSTEM:   ISPF/MONITOR 1.0.0
**       MODULE:   BUFFATTR
**       PROGRAM:  BUFFISPF
**       FUNCTION: SET UP THE ATTRIBUTE DEFINITIONS FOR LATER
**                 TRANSLATION.
**       AUTHOR:   ?
*
BUFFATTR KICKOFF
         LR    R10,R1
         USING BUFFCOMA,R10
START    DS    0H
*---------------------------------------------------------------------*
         LA    R2,INREC             POINTER TO INPUT RECORD
         CLI   FIRSTIME,C'Y'        Q.  FIRST TIME THROUGH
         BNE   ATTRCHAR                 N. PROCESS ATTRIBUTE CHARACTER
         BAL   R4,ATTRSECT              Y. DEFINE DEFAULT ATTRIBUTES
*---------------------------------------------------------------------*
ATTRCHAR DS    0H                   GET SELF DEFINED ATTRS
         L     R6,TABLEPTR          LOAD TABLE POINTER
         LA    R8,80(R0)            SET R8 AS COLUMN LIMIT
ACHARLOP CLI   0(R2),C' '           IS CHAR A SPACE
         BE    ACHARINC                Y. GET NEXT CHAR
         MVC   0(1,R6),0(R2)           N. STORE ATTR CHAR
         B     ATTRTYPE             FIND ATTR TYPE
ACHARINC DS    0H
         LA    R2,1(R2)             POSITION ON FIRST CHAR OF TYPE
         BCT   R8,ACHARLOP
         B     GOODEXIT             GET NEXT RECORD (BLANK LINE)
ATTRTYPE DS    0H                   GET SELF DEFINED ATTRS TYPE
         LA    R8,80(R0)            SET R8 AS COLUMN LIMIT
         LA    R2,INREC             REPOSITION R2 TO START
         OC    INREC(80),SPACES     TRANSLATE UPPER  TO LOWER
ATYPELOP CLC   0(5,R2),TYPE         IS TYPE DEFINED
         BE    ATYPEDEF                Y. ESTABLISH TYPE
         LA    R2,1(R2)             POSITION R2 ON NEXT CHAR
         BCT   R8,ATYPELOP             N. TRY NEXT STRING
         MVC   1(1,R6),=CL1'T'      SET OUTPUT DEFAULT TYPE
         B     ATTRINTE             FIND ATTR INTENSITY
ATYPEDEF DS    0H
         LA    R2,5(R2)             POSITION ON FIRST CHAR OF TYPE
         MVC   1(1,R6),0(R2)        SET ATTR TYPE
ATTRINTE DS    0H                   GET SELF DEFINED ATTRS INTENSITY
         LA    R2,INREC             REPOSITION R2 TO START
         LA    R8,80(R0)            SET R8 AS COLUMN LIMIT
AINTELOP CLC   0(7,R2),INTENS       IS INTENSITY DEFINED
         BE    AINTEDEF                Y. ESTABLISH INTENSITY
         LA    R2,1(R2)             POSITION R2 ON NEXT CHAR
         BCT   R8,AINTELOP             N. TRY NEXT STRING
         MVC   2(1,R6),=CL1'L'      SET LOW DEFAULTS INTENSITY
         B     ATTRHILI             FIND ATTR HILITE
AINTEDEF DS    0H
         LA    R2,7(R2)             POSITION ON FIRST CHAR OF INTENSITY
         MVC   2(1,R6),0(R2)        SET ATTR INTENSITY
ATTRHILI DS    0H                   GET SELF DEFINED ATTRS HILITE
         LA    R2,INREC             REPOSITION R2 TO START
         LA    R8,80(R0)            SET R8 AS COLUMN LIMIT
AHILILOP CLC   0(7,R2),HILITE       IS HILITE DEFINED
         BE    AHILIDEF                Y. ESTABLISH HILITE
         LA    R2,1(R2)             POSITION R2 ON NEXT CHAR
         BCT   R8,AHILILOP             N. TRY NEXT STRING
         MVC   4(1,R6),=CL1'O'      SET OFF DEFAULT HILITE
         B     ATTRCOLO             FIND ATTR COLOR
AHILIDEF DS    0H
         LA    R2,7(R2)             POSITION ON FIRST CHAR OF HILITE
         MVC   4(1,R6),0(R2)        SET ATTR HILITE
ATTRCOLO DS    0H                   GET SELF DEFINED ATTRS COLOR
         LA    R2,INREC             REPOSITION R2 TO START
         LA    R8,80(R0)            SET R8 AS COLUMN LIMIT
ACOLOLOP CLC   0(6,R2),COLOR        IS COLOR DEFINED
         BE    ACOLODEF                Y. ESTABLISH INTENSITY
         LA    R2,1(R2)             POSITION R2 ON NEXT CHAR
         BCT   R8,ACOLOLOP             N. TRY NEXT STRING
         B     ACOLOSET             UNDEFINED COLOR
ACOLODEF DS    0H
         LA    R2,6(R2)             POSITION ON FIRST CHAR OF COLOR
         MVC   3(1,R6),0(R2)        SET ATTR COLOR
         B     STOTRT#              INCREMENT POINTER
ACOLOSET DS    0H                   SET UNDEFINED COLOR
TESTTEXT CLC   1(1,R6),=CL1'T'      IS ATTR TYPE = TEXT
         BNE   TESTINPU                N. TRY INPUT
TESTINTH CLC   2(1,R6),=CL1'H'      IS ATTR INTENSITY = HIGH
         BNE   TESTINTL                N. TRY LOW
         MVC   3(1,R6),=CL1'W'      SET ATTR COLOR = WHITE
         B     STOTRT#              INCREMENT POINTER
TESTINTL CLC   2(1,R6),=CL1'L'      IS ATTR INTENSITY = LOW
         BNE   STOTRT#                 N. INCREMENT TABLE POINTER
         MVC   3(1,R6),=CL1'B'      SET ATTR COLOR = WHITE
TESTINPU CLC   1(1,R6),=CL1'I'      IS ATTR TYPE = INPUT
         BNE   TESTOUTP                N. MUST BE OUTPUT
         MVC   3(1,R6),=CL1'R'      SET ATTR COLOR = RED
         B     STOTRT#              INCREMENT POINTER
TESTOUTP MVC   3(1,R6),=CL1'B'      SET ATTR COLOR = WHITE
         CLC   2(1,R6),=CL1'H'      IS ATTR INTENSITY = HIGH
         BNE   STOTRT#                 N. INCREMENT TABLE POINTER
         MVC   3(1,R6),=CL1'W'      SET ATTR COLOR = WHITE
         B     STOTRT#              INCREMENT POINTER
*---------------------------------------------------------------------*
STOTRT## DS    0H                   UPDATE TRT WITH DEFAULT ATTRS
         LA    R8,TRTABLE           STORE START OF TRANSLATE TABLE
         XR    R3,R3                INIT R3
         IC    R3,0(R6)             USE ATTR CHAR AS ADDRESS QUALIFIER
         AR    R8,R3                ESTABLIST ATTR CHAR ADDRESS IN TRT
         STC   R3,0(R8)             SET TRTABLE ATTR CHAR ON
         BR    R4                   RETURN TO WHERE CALLED FROM
*---------------------------------------------------------------------*
*    FIRST TIME THROUGH - EXPLICIT OR IMPLICIT DEFAULTS               *
*---------------------------------------------------------------------*
ATTRSECT DS    0H
         LA    R6,TABLE                LOAD TABLE ADDRESS
         ST    R6,TABLEPTR             SAVE CURRENT TABLE POINTER
         MVI   FIRSTIME,C'N'           SET FIRST TIME THRU FLAG OFF
         MVI   ATTRFLAG,C'Y'           ATTRIBUTES DEFINE = YES
         CLC   INREC+5(75),=CL75' '    USING DEFAULT ATTRIBUTES
         BE    SETDEFS                 BRANCH TO FIND KEY
DEFAULTU DS    0H                      REDEFINE DEFAULTS
         LA    R2,6(R2)                INC CARD POINTER PAST ")ATTR "
         LA    R8,74(R0)               SET R8 TO COLUMN LIMIT
ATTRLOOP DS    0H                      SOMETHING ON "ATTR" LINE
         MVC   TARGET(8),0(R2)         MOVE SOURCE FOR UPPER CONVERSION
         OC    TARGET(8),SPACES        CONVERT LOWER TO UPPER CASE
         CLC   DEFAULT(8),TARGET       Q. ARE DEFAULTS DEFINED
         BE    DEFSTART                   Y. GO TO EXPLICIT DEFAULTS
         LA    R2,1(R2)                   N. NEXT CHARACTER
         BCT   R8,ATTRLOOP             Q. CARD LIMIT REACHED, N - LOOP
         B     ERR1EXIT                   Y. ERROR (SOMETHING NOT DEF)
*---------------------------------------------------------------------*
*    EXPLICIT  DEFAULTS                                               *
*---------------------------------------------------------------------*
DEFSTART DS    0H
         LA    R2,8(R2)                SET CARD PTR PAST "DEFAULT("
         MVC   0(1,R6),0(R2)           SET TEXT HIGH DEFAULTS ATTR
         BAL   R4,STOTRT##             STORE TRTABLE ENTRY
         MVC   1(1,R6),=CL1'T'         SET TEXT HIGH DEFAULT TYPE
         MVC   2(1,R6),=CL1'H'         SET TEXT HIGH DEFAULT INTENSITY
         MVC   3(1,R6),=CL1'W'         SET TEXT HIGH DEFAULT COLOR
         MVC   4(1,R6),=CL1'O'         SET TEXT HIGH DEFAULT HILITE
         LA    R2,1(R2)                NEXT DEFAULT ATTRIBUTE CHAR
         LA    R6,5(R6)                POINT R6 TO NEXT SET OF ATTR
         MVC   0(1,R6),0(R2)           SET TEXT LOW DEFAULTS ATTR
         BAL   R4,STOTRT##             STORE TRTABLE ENTRY
         MVC   1(1,R6),=CL1'T'         SET TEXT LOW DEFAULTS TYPE
         MVC   2(1,R6),=CL1'L'         SET TEXT LOW DEFAULTS INTENSITY
         MVC   3(1,R6),=CL1'B'         SET TEXT LOW DEFAULTS COLOR
         MVC   4(1,R6),=CL1'O'         SET TEXT LOW DEFAULT HILITE
         LA    R2,1(R2)                NEXT DEFAULT ATTRIBUTE CHAR
         LA    R6,5(R6)                POINT R6 TO NEXT SET OF ATTR
         MVC   0(1,R6),0(R2)           SET TEXT INPUT DEFAULTS ATTR
         BAL   R4,STOTRT##             STORE TRTABLE ENTRY
         MVC   1(1,R6),=CL1'I'         SET TEXT INPUT DEFAULTS TYPE
         MVC   2(1,R6),=CL1'H'         SET TEXT INPUT DEFAULTS INTENS
         MVC   3(1,R6),=CL1'R'         SET TEXT INPUT DEFAULTS COLOR
         MVC   4(1,R6),=CL1'O'         SET TEXT INPUT DEFAULT HILITE
         B     STOTRT#                 INCREMENT TABLE POINTER
*---------------------------------------------------------------------*
*    IMPLICIT  DEFAULTS                                               *
*---------------------------------------------------------------------*
SETDEFS  DS    0H                      SET DEFAULT ATTRIBUTES '%+_'
         MVC   0(1,R6),=CL1'%'         SET TEXT HIGH DEFAULTS ATTR
         BAL   R4,STOTRT##             STORE TRTABLE ENTRY
         MVC   1(1,R6),=CL1'T'         SET TEXT HIGH DEFAULTS TYPE
         MVC   2(1,R6),=CL1'H'         SET TEXT HIGH DEFAULTS INTENS
         MVC   3(1,R6),=CL1'W'         SET TEXT HIGH DEFAULTS COLOR
         MVC   4(1,R6),=CL1'O'         SET TEXT HIGH DEFAULT HILITE
         LA    R6,5(R6)                POINT R6 TO NEXT SET OF ATTR
         MVC   0(1,R6),=CL1'+'         SET TEXT LOW DEFAULTS ATTR
         BAL   R4,STOTRT##             STORE TRTABLE ENTRY
         MVC   1(1,R6),=CL1'T'         SET TEXT LOW DEFAULTS TYPE
         MVC   2(1,R6),=CL1'L'         SET TEXT LOW DEFAULTS INTENS
         MVC   3(1,R6),=CL1'B'         SET TEXT LOW DEFAULTS COLOR
         MVC   4(1,R6),=CL1'O'         SET TEXT LOW DEFAULT HILITE
         LA    R6,5(R6)                POINT R6 TO NEXT SET OF ATTR
         MVC   0(1,R6),=CL1'_'         SET TEXT INPUT DEFAULTS ATTR
         BAL   R4,STOTRT##             STORE TRTABLE ENTRY
         MVC   1(1,R6),=CL1'I'         SET TEXT INPUT DEFAULTS TYPE
         MVC   2(1,R6),=CL1'H'         SET TEXT INPUT DEFAULTS INTENS
         MVC   3(1,R6),=CL1'R'         SET TEXT INPUT DEFAULTS COLOR
         MVC   4(1,R6),=CL1'O'         SET TEXT INPUT DEFAULT HILITE
         B     STOTRT#                 INCREMENT TABLE POINTER
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
ERR1EXIT DS    0H
         MVC   LINEIO(50),ERROR1       MOVE ERROR 1 TO LINE OUTPUT
         MVI   RCODE,C'1'              SET ERRORNEAOUS RETURN CODE
         B     EXITPROG
*---------------------------------------------------------------------*
STOTRT#  DS    0H                      UPDATE TRT WITH ATTR
         LA    R8,TRTABLE              STORE START OF TRANSLATE TABLE
         XR    R3,R3                   INIT R3
         IC    R3,0(R6)                USE ATTR CHAR AS ADDRESS QUAL
         AR    R8,R3                   ESTAB ATTR CHAR ADDRESS IN TRT
         STC   R3,0(R8)                SET TRTABLE ATTR CHAR ON
INCRPTR  DS    0H                      INCREMENT POINTER
         LA    R6,5(R6)                POSITION R6 TO NEXT SET OF ATTRS
         ST    R6,TABLEPTR             STORE CURRENT TABLE POINTER
*---------------------------------------------------------------------*
GOODEXIT DS    0H
         MVI   RCODE,C'0'              SET GOOD RETURN CODE
*---------------------------------------------------------------------*
EXITPROG RACKOFF
*=====================================================================*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
TARGET   DS    CL8
FIRSTIME DC    C'Y'                    FIRST TIME THROUGH FLAG
TABLEPTR DS    A                       TABLE POINTER
BUFFCOMA DSECT
         COPY  BUFFCOMA
         END
