*
**       TITLE:    CONVERT ISPF PANELS TO "SBA" DATA STRINGS FOR TPUT
**       SYSTEM:   ISPF/MONITOR 1.0.0
**       MODULE:   BUFFEXPD
**       PROGRAM:  BUFFISPF
**       FUNCTION: PARSE THE LINE GENERATED AND FIND IF IT IS TO BE
**                 EXPANDED....
**       AUTHOR:   A. KARA
*
BUFFEXPD KICKOFF (R11,R12)
         LR    R10,R1
         USING BUFFCOMA,R10
         B     START
SAVSUBR1 DS    18F                       SUBROUTINE 1 SAVE AREA
SAVSUBR2 DS    18F                       SUBROUTINE 2 SAVE AREA
SAVSUBR3 DS    18F                       SUBROUTINE 2 SAVE AREA
*=====================================================================*
START    DS    0H
         MVI   OUTLINX+15,C'X'      HEX TYPE DATA ID (LAST BUFF "RA")
         LA    R6,OLINE2+16         POSITION REG ON TYPE OF WRITE
         LA    R2,OUTLINX+17        OUTLINX BUFFER VARIABLE PART
TESTWCC  DS    0H
         CLC   OLINE2+10(5),=C'WCCDC' Q. DEFINE CONSTANT FOR "WCC"
         BNE   TESTIC                    N. NEXT TEST
         BAL   R14,WCCDC            GO PROCESS WCCDC
         B     EXITPROG             GO AND RETURN
TESTIC   DS    0H
         CLC   OLINE2+10(4),=C'ICDC'  Q. DEFINE CONSTANT FOR "IC"
         BNE   TESTSBA                   N. NEXT TEST
         BAL   R14,ICDC             GO PROCESS IC
         B     EXITPROG             GO AND RETURN
TESTSBA  DS    0H
         CLC   OLINE2+10(5),=C'SBADC' Q. DEFINE CONSTANT FOR "SBA"
         BNE   TESTRA                    N. NEXT TEST
         BAL   R14,SBADC            GO PROCESS SBA
         B     EXITPROG             GO AND RETURN
TESTRA   DS    0H
         CLC   OLINE2+10(4),=C'RADC'  Q. DEFINE CONSTANT FOR "RA"
         BNE   WRITDATA                  N. NOT BUFF EXPANDABLE
         BAL   R14,RADC                  Y. GO PROCESS RA
         B     EXITPROG             GO AND RETURN
WRITDATA DS    0H
         BAL   R14,PUTMACLB         GO PUT DATA
*---------------------------------------------------------------------*
EXITPROG DS    0H
         MVC   OLINE2O,SPACES       CLEAR OUTPUT BUFFER
EXITRUTN RACKOFF RC=0
*=====================================================================*
* EXPAND WRITE CHANEL COMMAND DEFINE CONSTANT                         *
*---------------------------------------------------------------------*
WCCDC    DS    0H
         ST    R14,SAVSUBR1         STORE RETURN REGISTERS
         MVC   OUTLINX(8),OLINE2    COMMENT OUT THE LINE
         MVI   OLINE2,C'*'          COMMENT OUT THE LINE
         CLI   MACROE,C'S'          Q. MACROS TO BE SUPPRESSED
         BE    TESTEWA                 Y. BYPASS MACRO PRINTING
         BAL   R14,PUTMACLB         GO PUT DATA
TESTEWA  DS    0H
*---------------------------------------------------------------------*
         CLC   0(3,R6),=C'EWA'      Q. IS REQUEST ERASE WRITE ALTERNATE
         BNE   TESTEW                  N. NEXT TEST
         LA    R6,4(R6)             POINT PAST "EWA" + DELIMITER
         MVC   0(2,R2),=C'7E'       MOVE "EWA" INDICATOR
         B     TESTMDT              GO PARSE MDT (MODIFIED DATA TAG)
TESTEW   DS    0H
         CLC   0(2,R6),=C'EW'       Q. IS REQUEST ERASE WRITE
         BNE   TESTW                   N. MUST BE W
         LA    R6,3(R6)             POINT PAST "EW" + DELIMITER
         MVC   0(2,R2),=C'F5'       MOVE "EW" INDICATOR
         B     TESTMDT              GO PARSE MDT (MODIFIED DATA TAG)
TESTW    DS    0H
         LA    R6,2(R6)             POINT PAST "E" + DELIMITER
         MVC   0(2,R2),=C'F1'       MOVE "E" INDICATOR
TESTMDT  DS    0H
         CLI   1(R6),C' '           Q. MDT PARAMETER MISSING
         BE    WCCNYN                  Y. GO TO DEFAULT "NYN"
WCCYYY   DS    0H
         CLC   1(3,R6),=C'YYY'      Q. MTD PARAMETER "YYY"
         BNE   WCCYYN                  N. NEXT TEST
         MVC   2(2,R2),=C'C7'       MOVE APPROPRIATE MTD ID
         B     RETWCCDC             RETURN
WCCYYN   DS    0H
         CLC   1(3,R6),=C'YYN'      Q. MTD PARAMETER "YYY"
         BNE   WCCYNY                  N. NEXT TEST
         MVC   2(2,R2),=C'C6'       MOVE APPROPRIATE MTD ID
         B     RETWCCDC             RETURN
WCCYNY   DS    0H
         CLC   1(3,R6),=C'YNY'      Q. MTD PARAMETER "YYY"
         BNE   WCCYNN                  N. NEXT TEST
         MVC   2(2,R2),=C'C5'       MOVE APPROPRIATE MTD ID
         B     RETWCCDC             RETURN
WCCYNN   DS    0H
         CLC   1(3,R6),=C'YNN'      Q. MTD PARAMETER "YYY"
         BNE   WCCNYY                  N. NEXT TEST
         MVC   2(2,R2),=C'C4'       MOVE APPROPRIATE MTD ID
         B     RETWCCDC             RETURN
WCCNYY   DS    0H
         CLC   1(3,R6),=C'NYY'      Q. MTD PARAMETER "YYY"
         BNE   WCCNNY                  N. NEXT TEST
         MVC   2(2,R2),=C'C3'       MOVE APPROPRIATE MTD ID
         B     RETWCCDC             RETURN
WCCNNY   DS    0H
         CLC   1(3,R6),=C'NNY'      Q. MTD PARAMETER "YYY"
         BNE   WCCNNN                  N. NEXT TEST
         MVC   2(2,R2),=C'C1'       MOVE APPROPRIATE MTD ID
         B     RETWCCDC             RETURN
WCCNNN   DS    0H
         CLC   1(3,R6),=C'NNN'      Q. MTD PARAMETER "YYY"
         BNE   WCCNYN                  N. NEXT TEST
         MVC   2(2,R2),=C'40'       MOVE APPROPRIATE MTD ID
         B     RETWCCDC             RETURN
WCCNYN   DS    0H
         MVC   2(2,R2),=C'C2'       MOVE APPROPRIATE MTD ID (DEFAULT)
         B     RETWCCDC             RETURN
RETWCCDC DS    0H
         MVI   4(R2),C''''          MOVE CLOSING APOSTROPHE
EXITWCC  DS    0H
         BAL   R14,PUTDATA          GO PUT NEW LINE
         L     R14,SAVSUBR1         RESTORE RETURN REGISTERS
         BR    R14                  RETURN TO CELLED POINT
*---------------------------------------------------------------------*
*
*=====================================================================*
* EXPAND INSERT CURSOR DEFINE CONSTANT                                *
*---------------------------------------------------------------------*
ICDC     DS    0H
         ST    R14,SAVSUBR1         STORE RETURN REGISTERS
         MVI   OLINE2,C'*'          COMMENT OUT THE LINE
         CLI   MACROE,C'S'          Q. MACROS TO BE SUPPRESSED
         BE    SETICSBA                Y. BYPASS MACRO PRINTING
         BAL   R14,PUTMACLB         GO PUT DATA
*---------------------------------------------------------------------*
SETICSBA DS    0H
         MVC   0(2,R2),=C'11'       MOVE "SBA" INDICATOR
         BAL   R14,ROWCOL           GO CALCULATE ROW COLUMN VALUES
         MVC   2(2,R2),CHARROW      MOVE TWO BYTE ROW INDICATOR
         MVC   4(2,R2),CHARCOL      MOVE TWO BYTE COL INDICATOR
         MVC   6(2,R2),=C'13'       MOVE "IC" INDICATOR
         MVI   8(R2),C''''          MOVE CLOSING APOSTROPHE
         BAL   R14,PUTDATA          GO PUT NEW LINE
         L     R14,SAVSUBR1         RESTORE RETURN REGISTERS
         BR    R14                  RETURN TO CELLED POINT
*---------------------------------------------------------------------*
*
*=====================================================================*
* EXPAND SET BUFFER ADDRESS DEFINE CONSTANT                           *
*---------------------------------------------------------------------*
SBADC    DS    0H
         ST    R14,SAVSUBR1         STORE RETURN REGISTERS
         MVI   OLINE2,C'*'          COMMENT OUT THE LINE
         CLI   MACROE,C'S'          Q. MACROS TO BE SUPPRESSED
         BE    BYPMAC1                 Y. BYPASS MACRO PRINTING
         BAL   R14,PUTMACLB         GO PUT DATA
BYPMAC1  DS    0H
*---------------------------------------------------------------------*
         CLC   15(6,R6),=C'CONT.D'  Q. SBA CONTINUED DIRECT FROM PREV
         BNE   SBAHASBA                N. SBA HAS SBA SETUP
         CLI   MACROE,C'S'          Q. MACROS TO BE SUPPRESSED
         BE    CONT#2                  Y. BYPASS MACRO PRINTING
         BAL   R14,PUTDATA1         GO PUT DATA
         B     CONT#2               LEAVE WITHOUT SETTING SBA
*---------------------------------------------------------------------*
SBAHASBA DS    0H
         CLI   LINE#ON,C'N'         Q. LINE NUMBER MODE OFF
         BE    CONT#0                  Y. BYPASS
         MVC   OUTLINX(4),=C'LINE'  MOVE LITERAL
         MVC   OUTLINX+4(2),0(R6)   MOVE ROW  NUMBER
         MVC   OUTLINX+6(2),3(R6)   MOVE COL  NUMBER
CONT#0   DS    0H
         MVC   0(2,R2),=C'11'       MOVE "SBA" INDICATOR
         BAL   R14,ROWCOL           GO CALCULATE ROW COLUMN VALUES
         MVC   2(2,R2),CHARROW      MOVE TWO BYTE ROW INDICATOR
         MVC   4(2,R2),CHARCOL      MOVE TWO BYTE COL INDICATOR
         LA    R2,6(R2)             POINT TO NEXT INPUT ADDRESS
         MVC   0(2,R2),=C'1D'       IF NO EXTENDED ATTRS
         CLI   EXTATR,C'Y'          Q. ARE EXTENDED ATTRIBS ON
         BNE   CONT#01                 N. BYPASS EXT ATTRIB
         MVC   0(4,R2),=C'2902'     EXTENDED ATTRIB + ORDERED PAIRS CTR
         LA    R2,4(R2)             POINT TO NEXT INPUT ADDRESS
         MVC   0(2,R2),=C'C0'       CHARACTER ATTRIBITE ID
CONT#01  DS    0H
*---------------------------------------------------------------------*
         LA    R6,6(R6)             POINT PAST THE ROW/COL + DELIMS
*---------------------------------------------------------------------*
* 3 BYTE CHARACTER ATTRIBUTE CHARACTER DESCRIPTION:                   *
*          1ST. CHAR : PROTECTION  - U(NPROTECTED), P(ROTECTED)       *
*                                    (FOR PROTECTED FIELDS, AUTOSKIP  *
*                                    IS TURNED ON.)                   *
*          2ND. CHAR : INTENSITY   - H(IGH), L(OW), N(ON)             *
*          3ND. CHAR : MDT ON FLAG - Y(ES), N(O)                      *
*---------------------------------------------------------------------*
FAUHY    DS    0H
         CLC   0(3,R6),=C'UHY'      Q. CHARACTER ATTRIBUTES "UHY"
         BNE   FAUHN                   N. GO TEST FOR NEXT MIX
         MVC   2(2,R2),=C'C9'       MOVE APPROPRIATE CHAR ATTRIB ID
         B     DEFCOL               GO DEFINE COLOR
FAUHN    DS    0H
         CLC   0(3,R6),=C'UHN'      Q. CHARACTER ATTRIBUTES "UHY"
         BNE   FAULY                   N. GO TEST FOR NEXT MIX
         MVC   2(2,R2),=C'C8'       MOVE APPROPRIATE CHAR ATTRIB ID
         B     DEFCOL               GO DEFINE COLOR
FAULY    DS    0H
         CLC   0(3,R6),=C'ULY'      Q. CHARACTER ATTRIBUTES "UHY"
         BNE   FAULN                   N. GO TEST FOR NEXT MIX
         MVC   2(2,R2),=C'C5'       MOVE APPROPRIATE CHAR ATTRIB ID
         B     DEFCOL               GO DEFINE COLOR
FAULN    DS    0H
         CLC   0(3,R6),=C'ULN'      Q. CHARACTER ATTRIBUTES "UHY"
         BNE   FAUNY                   N. GO TEST FOR NEXT MIX
         MVC   2(2,R2),=C'C4'       MOVE APPROPRIATE CHAR ATTRIB ID
         B     DEFCOL               GO DEFINE COLOR
FAUNY    DS    0H
         CLC   0(3,R6),=C'UNY'      Q. CHARACTER ATTRIBUTES "UHY"
         BNE   FAUNN                   N. GO TEST FOR NEXT MIX
         MVC   2(2,R2),=C'4D'       MOVE APPROPRIATE CHAR ATTRIB ID
         B     DEFCOL               GO DEFINE COLOR
FAUNN    DS    0H
         CLC   0(3,R6),=C'UNN'      Q. CHARACTER ATTRIBUTES "UHY"
         BNE   FAPHY                   N. GO TEST FOR NEXT MIX
         MVC   2(2,R2),=C'4C'       MOVE APPROPRIATE CHAR ATTRIB ID
         B     DEFCOL               GO DEFINE COLOR
FAPHY    DS    0H
         CLC   0(3,R6),=C'PHY'      Q. CHARACTER ATTRIBUTES "UHY"
         BNE   FAPHN                   N. GO TEST FOR NEXT MIX
         MVC   2(2,R2),=C'F9'       MOVE APPROPRIATE CHAR ATTRIB ID
         B     DEFCOL               GO DEFINE COLOR
FAPHN    DS    0H
         CLC   0(3,R6),=C'PHN'      Q. CHARACTER ATTRIBUTES "UHY"
         BNE   FAPLY                   N. GO TEST FOR NEXT MIX
         MVC   2(2,R2),=C'F8'       MOVE APPROPRIATE CHAR ATTRIB ID
         B     DEFCOL               GO DEFINE COLOR
FAPLY    DS    0H
         CLC   0(3,R6),=C'PLY'      Q. CHARACTER ATTRIBUTES "UHY"
         BNE   FAPLN                   N. GO TEST FOR NEXT MIX
         MVC   2(2,R2),=C'F5'       MOVE APPROPRIATE CHAR ATTRIB ID
         B     DEFCOL               GO DEFINE COLOR
FAPLN    DS    0H
         MVC   2(2,R2),=C'F4'       MOVE APPROPRIATE CHAR ATTRIB ID
*---------------------------------------------------------------------*
DEFCOL   DS    0H
         LA    R2,4(R2)             POINT PAST CHAR ATTRIB
         CLI   EXTATR,C'Y'          Q. ARE EXTENDED ATTRIBS ON
         BNE   CONT#1                  N. BYPASS COLOR + HILITE
         LA    R6,4(R6)             POINT PAST CHAR ATTRIB ID
         MVC   0(2,R2),=C'42'       MOVE APPROPRIATE COLOR
COLORR   DS    0H
         CLI   0(R6),C'R'           Q. IS IT THIS COLOR
         BNE   COLORP                  N. GO TEST FOR NEXT COLOR
         MVC   2(2,R2),=C'F2'       MOVE APPROPRIATE COLOR
         B     DEFHILI              GO DEFINE HILITE
COLORP   DS    0H
         CLI   0(R6),C'P'           Q. IS IT THIS COLOR
         BNE   COLORG                  N. GO TEST FOR NEXT COLOR
         MVC   2(2,R2),=C'F3'       MOVE APPROPRIATE COLOR
         B     DEFHILI              GO DEFINE HILITE
COLORG   DS    0H
         CLI   0(R6),C'G'           Q. IS IT THIS COLOR
         BNE   COLORT                  N. GO TEST FOR NEXT COLOR
         MVC   2(2,R2),=C'F4'       MOVE APPROPRIATE COLOR
         B     DEFHILI              GO DEFINE HILITE
COLORT   DS    0H
         CLI   0(R6),C'T'           Q. IS IT THIS COLOR
         BNE   COLORY                  N. GO TEST FOR NEXT COLOR
         MVC   2(2,R2),=C'F5'       MOVE APPROPRIATE COLOR
         B     DEFHILI              GO DEFINE HILITE
COLORY   DS    0H
         CLI   0(R6),C'Y'           Q. IS IT THIS COLOR
         BNE   COLORW                  N. GO TEST FOR NEXT COLOR
         MVC   2(2,R2),=C'F6'       MOVE APPROPRIATE COLOR
         B     DEFHILI              GO DEFINE HILITE
COLORW   DS    0H
         CLI   0(R6),C'W'           Q. IS IT THIS COLOR
         BNE   COLORB                  N. GO TEST FOR NEXT COLOR
         MVC   2(2,R2),=C'F7'       MOVE APPROPRIATE COLOR
         B     DEFHILI              GO DEFINE HILITE
COLORB   DS    0H
         MVC   2(2,R2),=C'F1'       MOVE APPROPRIATE COLOR (DEFAULT)
*---------------------------------------------------------------------*
DEFHILI  DS    0H
         LA    R6,2(R6)             POINT PAST CHAR ATTRIB + DELIM
         LA    R2,4(R2)             POINT PAST CHAR ATTRIB
         CLI   0(R6),C'O'           Q. IS HILITE OMITTED
         BNE   HILITEB                 N. TEST NEXT TYPE
         B     CONT#1               GO TO FINISH
HILITEB  DS    0H
         MVC   OUTLINX+25(2),=C'03' SET TO 3 ORDERED PAIRS
         MVC   0(2,R2),=C'41'       MOVE HILITE ID
         CLI   0(R6),C'B'           Q. IS IT THIS ID
         BNE   HILITER                 N. TEST NEXT TYPE
         MVC   2(2,R2),=C'F1'       MOVE THIS ID
         B     FINSBA               GO TO FINISH
HILITER  DS    0H
         CLI   0(R6),C'R'           Q. IS IT THIS ID
         BNE   HILITEU                 N. TEST NEXT TYPE
         MVC   2(2,R2),=C'F2'       MOVE THIS ID
         B     FINSBA               GO TO FINISH
HILITEU  DS    0H
         MVC   2(2,R2),=C'F4'       MUST BE "U"
FINSBA   DS    0H
         LA    R2,4(R2)             POINT PAST DATA MOVED
CONT#1   DS    0H
         MVI   0(R2),C''''          MOVE CLOSING APOSTROPHE
         BAL   R14,PUTDATA          GO PUT NEW LINE
CONT#2   DS    0H
*---------------------------------------------------------------------*
         L     R14,SAVSUBR1         RESTORE RETURN REGISTERS
         BR    R14                  RETURN TO CELLED POINT
*---------------------------------------------------------------------*
*
*=====================================================================*
* EXPAND SET BUFFER ADDRESS DEFINE CONSTANT                           *
*---------------------------------------------------------------------*
RADC     DS    0H
         ST    R14,SAVSUBR3         STORE RETURN REGISTERS
*---------------------------------------------------------------------*
         MVC   SAVERADC(8),14(R6)   SAVE REPT TO ADDR AS WILL BE O/WRIT
         CLI   23(R6),C' '          Q. RBA CONTINUED DIRECT FROM SBA
         BE    RAHASBA                 N. RA HAS SBA SETUP
         MVI   OLINE2,C'*'          COMMENT OUT THE LINE
         CLI   MACROE,C'S'          Q. MACROS TO BE SUPPRESSED
         BE    CONTRA                  Y. BYPASS MACRO PRINTING
         BAL   R14,PUTMACLB         GO PUT DATA
         BAL   R14,PUTDATA1         GO PUT DATA
         B     CONTRA               BYPASS SBA PARSING
RAHASBA  DS    0H
         BAL   R14,SBADC            GO PROCESS AS IF SBA
         LA    R2,OUTLINX+17        POINT TO NEW INPUT AREA
CONTRA   DS    0H
         LA    R6,SAVERADC          RESTORE SAVED RA VALUES
         MVC   0(2,R2),=C'3C'       MOVE "SBA" INDICATOR
         BAL   R14,ROWCOL           GO CALCULATE ROW COLUMN VALUES
         MVC   2(2,R2),CHARROW      MOVE TWO BYTE ROW INDICATOR
         MVC   4(2,R2),CHARCOL      MOVE TWO BYTE COL INDICATOR
         MVI   6(R2),C''''          MOVE CLOSING APOSTROPHE
         BAL   R14,PUTDATA          GO PUT DATA
         MVI   OUTLINX+15,C'C'      MOVE CHARACTER INDICATOR
         LA    R2,OUTLINX+17        POINT TO NEW INPUT AREA
         MVC   0(1,R2),7(R6)        MOVE REPEAT CHAR (AFT ADDR + DELIM)
         MVI   1(R2),C''''          MOVE CLOSING APOSTROPHE
         BAL   R14,PUTDATA          GO PUT DATA
*---------------------------------------------------------------------*
         L     R14,SAVSUBR3         RESTORE RETURN REGISTERS
         BR    R14                  RETURN TO CELLED POINT
*---------------------------------------------------------------------*
*
*=====================================================================*
* CALCULATE ROW/COLUMN FROM CHARACTER "RR,CC" POINTED TO BY R2.       *
* RESULTANT 4 BYTE HEX REPRESENTATION IS PLACED IN TARGET.            *
*---------------------------------------------------------------------*
ROWCOL   DS    0H
         STM   R0,R14,SAVSUBR2      STORE REGISTERS
         PACK  DWORK8(8),0(2,R6)    PACK ROW VALUE
         CVB   R3,DWORK8            CONVERT TO BINARY
         PACK  DWORK8(8),3(2,R6)    PACK COL VALUE (PAST ROW + DELIM)
         CVB   R4,DWORK8            CONVERT TO BINARY
*---------------------------------------------------------------------*
         BCTR  R3,0                 DECREMENT ROW # BY 1                01-SBA
         BCTR  R4,0                 DECREMENT COLUMN # BY 1             01-SBA
         MH    R3,=H'80'            MULTIPLY ROWS BY COLUMN LIMIT       01-SBA
         AR    R3,R4                RESULT IN R3                        01-SBA
         LH    R5,=H'64'            LOAD TABLE LENGTH                   01-SBA
         XR    R2,R2                CLEAR R2                            01-SBA
         DR    R2,R5                DIVIDE RELATIVE DISP BY 64          01-SBA
         LA    R5,TRROWCOL          START OF TABLE                      01-SBA
         AR    R5,R3                R3 ROW DISPALCEMENT IN TABLE        01-SBA
         LA    R6,TRROWCOL                                              01-SBA
         AR    R6,R2                R2 COL DIPLACEMENT IN TABLE         01-SBA
         MVC   HEXROW(1),0(R5)      MOVE ROW VALUE IN HEX               01-SBA
         MVC   HEXCOL(1),0(R6)      MOVE COL VALUE IN HEX               01-SBA
         HEXCHMAC HEXROW,CHARROW,1  CONVERT HEX TO CHAR LENGTH = 1
         HEXCHMAC HEXCOL,CHARCOL,1  CONVERT HEX TO CHAR LENGTH = 1
*---------------------------------------------------------------------*
         LM    R0,R14,SAVSUBR2      RESTORE RETURN REGISTERS
         BR    R14                  RETURN TO CELLED POINT
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
*     COMMON PUT "PRLINE" TO PRINTER1 ROUTINE                        -*
*---------------------------------------------------------------------*
PUTDATA  DS    0H
         STM   R14,R1,PUTSAVR1
         MVC   OLINE2(80),OUTLINX
         PUT   MACLIB,OLINE2        WRITE PREV SETUP LINE
         PUT   PRINTER2,OLINE2O
         MVC   OUTLINX(8),SPACES    COMMENT OUT THE LINE
         MVC   OUTLINX+17(63),SPACES
PUTPR1X  DS    0H
         LM    R14,R1,PUTSAVR1
         BR    R14
*---------------------------------------------------------------------*
PUTSAVR1 DS    4F
*---------------------------------------------------------------------*
*     MESSAGE PUT "PRLINE" TO PRINTER1 ROUTINE                       -*
*---------------------------------------------------------------------*
PUTDATA1 DS    0H
         STM   R14,R1,PUTSAVR2      STORE REGISTERS
         MVC   OLINE2(80),NOSBAMSG  SET UP MESSAGE
         PUT   MACLIB,OLINE2        WRITE PREV SETUP LINE
         PUT   PRINTER2,OLINE2O     WRITE PRINTER MESSAGE
PUTPR2X  DS    0H
         LM    R14,R1,PUTSAVR2      RESTORE REGISTERS
         BR    R14                  RETURN TO CALLER
*---------------------------------------------------------------------*
PUTSAVR2 DS    4F
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
*     COMMON PUT "PRLINE" TO PRINTER1 ROUTINE                        -*
*---------------------------------------------------------------------*
PUTMACLB DS    0H
         STM   R14,R1,PUTSAVR3      STORE REGISTERS
         PUT   MACLIB,OLINE2        WRITE PREV SETUP LINE
         PUT   PRINTER2,OLINE2O     WRITE PRINTER MESSAGE
PUTPR3X  DS    0H
         LM    R14,R1,PUTSAVR3      RESTORE REGISTERS
         BR    R14                  RETURN TO CALLER
*---------------------------------------------------------------------*
PUTSAVR3 DS    4F
*---------------------------------------------------------------------*
*=====================================================================*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
SAVERADC DS    CL8
OUTLINX  DC    CL80'          DC   X'''
CHARROW  DS    CL2                  CHARACTER REP OF HEX ROW/COL
CHARCOL  DS    CL2                  CHARACTER REP OF HEX ROW/COL
HEXROW   DS    H                    CHARACTER REP OF HEX ROW/COL
HEXCOL   DS    H                    CHARACTER REP OF HEX ROW/COL
TRROWCOL DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'  1 TO 16             01-SBA
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F' 17 TO 32             01-SBA
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F' 33 TO 48             01-SBA
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F' 49 TO 64             01-SBA
NOSBAMSG DC    CL80'** "SBA" ENTRY OMITTED. IMMEDIATE FOLLOW-ON FROM PRE
               EVIOUS TEXT. **'
HEXTABLE DC    C'0123456789ABCDEF'
BYTES    DS    0CL2
BYTE1    DS    CL1
BYTE2    DS    CL1
         DS    0D
BUFFCOMA DSECT
         COPY  BUFFCOMA
         END
