*
**       TITLE:    CONVERT ISPF PANELS TO "SBA" DATA STRINGS FOR TPUT
**       SYSTEM:   ISPF/MONITOR 1.0.0
**       MODULE:   BUFFINIT
**       PROGRAM:  BUFFISPF
**       FUNCTION: INITIALISE AND GET RUNTIME PARAMETERS
**       AUTHOR:   A. KARA
*
BUFFINIT KICKOFF (R11,R12)
         LR    R10,R1
         USING BUFFCOMA,R10
START    DS    0H
*---------------------------------------------------------------------*
         OPEN  (SYSIN,(INPUT))      OPEN INPUT FILE
         MVI   OLINE2,C'*'
         BAL   R14,PUTMACL1         PRINT LINE
         MVC   OLINE2(37),=CL37'* THE L.B.D. U BYUTE UNPATTENTED SYS.'
         BAL   R14,PUTMACL1         PRINT LINE
         MVI   OLINE2,C'*'
         BAL   R14,PUTMACL1         PRINT LINE
         MVC   ERROR4+10(35),SPACES
         MVC   ERROR4+10(29),=CL29'NO SCRNAME PARAMETER DEFINED.'
         BAL   R14,GETREC           COMMON GET RECORD  ROUTINE
         MVC   CURSOR(8),INREC+32   GET CURSOR INIT FIELD
         MVC   TRUNC(1),INREC+40    GET TRUNCATION FIELD
         MVC   LINE#ON(1),INREC+48  SET ON LINE NUMBER MODE
         MVC   MACROE(1),INREC+49   SET ON LINE NUMBER MODE
         MVC   EXTATR(1),INREC+50   EXTENDED ATTRIBUTES (COLOR = Y)
*---------------------------------------------------------------------*
         MVC   WRITETYP(1),INREC+51 WCC TYPE
         CLC   INREC+52(2),SPACES   Q. FIRSTLINE PARAMETER MISSING
         BE    SETUNAME                Y. BYPASS SETTING VALUES
         PACK  DWORK8(8),INREC+52(2) PACK STARTING LINE
         CVB   R1,DWORK8            CONVERT TO BINARY
         STH   R1,FIRSTLIN          STORE STARTING LINE
         PACK  DWORK8(8),INREC+54(2) ENDING LINE
         CVB   R1,DWORK8            CONVERT TO BINARY
         STH   R1,LASTLIN           STORE ENDING LINE
SETUNAME DS    0H
*---------------------------------------------------------------------*
         MVC   SCRNAME(8),INREC     SET UP SCRNAME
         MVC   SCRSIZE(8),INREC+8   SET UP SCRSIZE FIELS NAME
         LA    R3,OLINE2
         MVC   OLINE2(80),SPACES    CLEAR FIELD
         MVC   SCRNAME(8),INREC     SET UP SCREEN NAME
         MVC   SCRNAME(8),INREC     SAVE SCREEN NAME
         MVC   SCRNAME+16(2),=C'W ' SET WCC TO WRITE
         LA    R9,SCRNAME+17        SET END OF PARM ADDRESS
         CLI   WRITETYP,C' '        Q. WRITE TYPE SUPPLIED
         BE    CONT#20                 N. BYPASS
         CLI   WRITETYP,C'W'        Q. WRITE TYPE SUPPLIED
         BE    CONT#20                 Y. TEST FOR TRUNCATION
         MVC   SCRNAME+16(2),=C'EW' SET WCC TO ERASE/WRITE
         LA    R9,SCRNAME+18        SET END OF PARM ADDRESS
         CLI   WRITETYP,C'E'        Q. ERASE WRITE TYPE SUPPLIED
         BE    WCCLINE              CONTINUE AS ALREADY SET
         MVC   SCRNAME+16(3),=C'EWA' SET WCC TO ERASE WRITE ALTERNATE
         LA    R9,SCRNAME+19        SET END OF PARM ADDRESS
         B     WCCLINE              CONTINUE AS ALREADY SET
CONT#20  DS    0H
         CLI   TRUNC,C'A'           Q. TRANSLATE "ASIS"
         BE    WCCLINE                 Y. BYPASS
         MVC   SCRNAME+16(2),=C'EW' SET WCC TO ERASE/WRITE (TRUNCATED)
         LA    R9,SCRNAME+18        SET END OF PARM ADDRESS
WCCLINE  DS    0H
         MVC   0(7,R9),=C',,COLOR'  SET COLOR ATTRIBUTE OFF
         CLI   EXTATR,C'Y'          Q. EXTENDED ATTRIBS
         BE    CONT#30                 Y. BYPASS
         MVC   0(7,R9),=C',,MONO '  SET COLOR ATTRIBUTE OFF
CONT#30  DS    0H
         MVC   OLINE2O,OLINE1O      MOVE LINE TO OUTPUT
         BAL   R14,PUTMACLB         COMMON DATA AREA
GETVCNT  DS    0H
         LA    R3,ZVARS
         ST    R3,ZVARPTR
         MVC   ERROR4+10(35),SPACES
         MVC   ERROR4+10(33),=CL33'NO ZVAR COUNT PARAMETER SUPPLIED.'
         BAL   R14,GETREC           COMMON GET RECORD  ROUTINE
         MVC   OVARCNT(4),INREC
         MVC   VARCOUNT(4),INREC
         PACK  DWORK8(8),VARCOUNT(4)
         CVB   R3,DWORK8
         ST    R3,VARCOUNT
         LA    R2,1
         CR    R3,R2
         BL    GETFSET
GETZVAR1 DS    0H
         MVC   ERROR4+10(35),SPACES
         MVC   ERROR4+10(27),=CL27'NO ZVAR PARAMETER SUPPLIED.'
         BAL   R14,GETREC           COMMON GET RECORD  ROUTINE
         MVC   ZVARS(80),INREC
         LA    R2,11
         CR    R3,R2
         BL    GETFSET
GETZVAR2 DS    0H
         MVC   ERROR4+10(35),SPACES
         MVC   ERROR4+10(21),=CL21'EXPECTING MORE ZVARS.'
         BAL   R14,GETREC           COMMON GET RECORD  ROUTINE
         MVC   ZVARS+80(80),INREC
GETFSET  DS    0H
         MVC   ERROR4+10(35),SPACES
         MVC   ERROR4+10(34),=CL34'NO MTDON COUNT PARAMETER SUPPLIED.'
         BAL   R14,GETREC           COMMON GET RECORD  ROUTINE
         MVC   FSETCNT(4),INREC
         PACK  DWORK8(8),FSETCNT(4)
         CVB   R3,DWORK8
         ST    R3,FSETCTR
         LA    R2,1
         CR    R3,R2
         BL    GOODEXIT
GETFSET1 DS    0H
         MVC   ERROR4+10(35),SPACES
         MVC   ERROR4+10(28),=CL28'NO MTDON PARAMETER SUPPLIED.'
         BAL   R14,GETREC           COMMON GET RECORD  ROUTINE
         MVC   FSETVARS(80),INREC
         LA    R2,11
         CR    R3,R2
         BL    GOODEXIT
GETFSET2 DS    0H
         MVC   ERROR4+10(35),SPACES
         MVC   ERROR4+10(21),=CL21'EXPECTING MORE MTDON.'
         BAL   R14,GETREC           COMMON GET RECORD  ROUTINE
         MVC   FSETVARS+80(80),INREC
         B     GOODEXIT
PARMEND  DS    0H
         MVC   LINEIO(45),ERROR4       MOVE ERROR 4 TO LINE OUTPUT
         MVI   RCODE,C'4'              SET ERRORNEAOUS RETURN CODE
         BAL   R4,PUTMACLB
         B     EXITPROG
*---------------------------------------------------------------------*
GOODEXIT DS    0H
         MVI   RCODE,C'0'              SET GOOD RETURN CODE
*---------------------------------------------------------------------*
EXITPROG RACKOFF RC=0
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
*     COMMON PUT "PRLINE" TO PRINTER1 ROUTINE                        -*
*---------------------------------------------------------------------*
PUTMACL1 DS    0H
         STM   R14,R1,PUTSAVR1
         PUT   MACLIB,OLINE2        WRITE PREV SETUP LINE
         PUT   PRINTER2,OLINE2O
         MVC   OLINE2O(81),SPACES
PUTPR1X  DS    0H
         LM    R14,R1,PUTSAVR1
         BR    R14
PUTSAVR1 DS    4F
*---------------------------------------------------------------------*
*     COMMON PUT "PRLINE" TO PRINTER1 ROUTINE                        -*
*---------------------------------------------------------------------*
PUTMACLB DS    0H
         STM   R14,R1,PUTSAVR2
         CLI   LINE#ON,C'Y'         Q. LINE NUMBER MODE
         BE    MUSTEXPD                Y. MUST EXPAND HERE (MACRO CANT
*                                         HANDLE LINE NUMBER ON
         CLI   MACROE,C'N'          Q. MACROS TO BE EXPANDED
         BE    DONTEXPD                N. BYPASS EXPANSION
MUSTEXPD DS    0H
         LA    R1,BUFFCOMA          COMMON DATA AREA
         CALL  BUFFEXPD             CALL BUFFER EXPAND
         B     PUTPR2X              BYPASS
DONTEXPD DS    0H
         PUT   MACLIB,OLINE2        WRITE PREV SETUP LINE
         PUT   PRINTER2,OLINE2O
         MVC   OLINE2O(81),SPACES
PUTPR2X  DS    0H
         LM    R14,R1,PUTSAVR2
         BR    R14
PUTSAVR2 DS    4F
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
*     COMMON GET "SYSIN" INTO INREC ROUTINE                          -*
*---------------------------------------------------------------------*
GETREC   DS    0H
         STM   R14,R1,PUTSAVR4
         GET   SYSIN,INREC          GET FIRST RECORD(PARAMETER)
         LM    R14,R1,PUTSAVR4
         BR    R14
PUTSAVR4 DS    4F
*=====================================================================*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
         DS    0H
SYSIN    DCB   DDNAME=SYSIN,                                           X
               DSORG=PS,                                               X
               MACRF=(GM),                                             X
               EODAD=PARMEND
         DS    0D
BUFFCOMA DSECT
         COPY  BUFFCOMA
         END
