*
**       TITLE:    CONVERT ISPF PANELS TO "SBA" DATA STRINGS FOR TPUT
**       SYSTEM:   ISPF/MONITOR 1.0.0
**       MODULE:   BUFFBODY
**       PROGRAM:  BUFFISPF
**       FUNCTION: INPUT DATA PARSER AND GENERAL PROCESS DRIVER.
**       AUTHOR:   A. KARA.
*
BUFFISPF KICKOFF (R11,R12)
*
INITIAL  DS    0H
         LA    R6,TABLE             POINT R6 TO START OF TABLE
OPENFILS DS    0H
         OPEN  (PRINTER1,(OUTPUT))  OPEN PRINT OF PANEL TRANSLATION
         OPEN  (PRINTER2,(OUTPUT))  OPEN PRINT OF "DSECT"
*---------------------------------------------------------------------*
*   PRINT HEADLINES ETC..........                                     *
*---------------------------------------------------------------------*
         LA    R1,BUFFCOMA          COMMON AREA
         CALL  BUFFHEAD             CALL HEADLINE MODULE
         CLI   RCODE,C'0'           Q. ZERO RETURN CODE
         BNE   ERREXIT                 N. GO TO ERROR EXIT
*---------------------------------------------------------------------*
         OPEN  (ISPPLIB,(INPUT))    OPEN INPUT FILE (INPUT PANEL)
         OPEN  (MACLIB,(OUTPUT))    OPEN OUTPUT FILE (COPY-BOOK)
*---------------------------------------------------------------------*
*   GET RUNTIME PARAMETERS ETC...                                     *
*---------------------------------------------------------------------*
         LA    R1,BUFFCOMA          COMMON AREA
         CALL  BUFFINIT             CALL INITIALISATION PROGRAM
         CLI   RCODE,C'0'           Q. ZERO RETURN CODE
         BE    GETREC                  N. GO GET NEXT RECORD
         B     ERREXIT              BRANCH TO ERROR EXIT ROUTINE
*---------------------------------------------------------------------*
GETREC   DS    0H
         GET   ISPPLIB,INREC        GET FIRST RECORD(PARAMETER)
         MVC   TARGET(5),INREC      MOVE THE FIRST 5 BYTES OF RECORD
         OC    TARGET(5),SPACES     CONVERT LOWER TO UPPER CASE
         CLI   FTIMTHRU,C'Y'        Q. FIRST TIME THRU
         BNE   TSTATTR                 N. GO TEST FOR ATTRIBUTE
         MVI   FTIMTHRU,C'N'        SET FIRST TIME THRU FLAG OFF
         CLI   TARGET,C')'          Q. FIRST LINE SECTION IDENTIFIER
         BE    TSTATTR                 Y. GO TEST FOR ATTRIBUTE
         LA    R10,BODYSECT         PROCESS BODY SECTION
         ST    R10,BRNCHST          USE R10 AS RETURN TO AFTER GETREC
         B     BPCHGBRN             GO AND PROCESS BODY SECTION
TSTATTR  CLC   ATTR(5),TARGET       Q. FIRST 5 CHAR = ")ATTR"
         BNE   TSTBODY                 N. NEXT TEST
         LA    R10,ATTRSECT         Y - PROCESS BODY SECTION
         ST    R10,BRNCHST          USE R10 AS RETURN TO AFTER GETREC
         B     ATTRSECT             PROCESS ATTR SECTION
TSTBODY  CLC   BODY(5),TARGET       N - Q. FIRST 5 CHAR = ")BODY"
         BNE   TSTINIT                  N - TEST INIT
         LA    R10,BODYSECT         Y - PROCESS BODY SECTION
         ST    R10,BRNCHST          USE R10 AS RETURN TO AFTER GETREC
         B     GETREC                   GET NEXT RECORD
TSTINIT  CLC   INIT(5),TARGET       N - Q. FIRST 5 CHAR = ")INIT"
         BNE   TSTPROC                  N - TEST INIT
         BE    INPTEND              Y - "END" OF INPUT
         LA    R10,INITSECT         Y - PROCESS INIT SECTION
         ST    R10,BRNCHST          USE R10 AS RETURN TO AFTER GETREC
         B     GETREC                   GET NEXT RECORD
TSTPROC  CLC   PROC(5),TARGET       N - Q. FIRST 5 CHAR = ")PROC"
         BNE   TSTENDX                  N - TEST END
         BE    INPTEND              Y - "END" OF INPUT
         LA    R10,PROCSECT         Y - PROCESS PROC SECTION
         ST    R10,BRNCHST          USE R10 AS RETURN TO AFTER GETREC
         B     GETREC                   GET NEXT RECORD
TSTENDX  CLC   ENDB(5),TARGET       N - Q. FIRST 5 CHAR = ")END "
         BE    INPTEND              Y - "END" OF INPUT
*                                   Q. IS BRACH STORAGE = GETREC
*                                      IE. NO ")BODY" IDENTIFIER
         L     R2,BRNCHST           LOAD 'NEXT BRANCH TO' ADDRESS
         LA    R3,GETREC            LOAD ADDRESS OF 'GETREC'
         CR    R3,R2                Q. ARE THEY THE SAME
         BNE   BPCHGBRN                N. DO NOT CHANGE BRANCH
         LA    R10,BODYSECT            Y. CHANGE TO BODYSECT
         ST    R10,BRNCHST                STORE
BPCHGBRN DS    0H
         L     R10,BRNCHST          USE R10 AS RETURN TO AFTER GETREC
         BR    R10                  N - BRANCH TO LAST TYPE OF OPER
*---------------------------------------------------------------------*
*   PROCESS BODY SECTION LINE (EXCLUDING "BODY" LINE IDENTIFIER)      *
*---------------------------------------------------------------------*
BODYSECT DS    0H                   PREMATURE EXIT
         LA    R1,BUFFCOMA          COMMON AREA
         CALL  BUFFBODY             CALL INITIALISATION PROGRAM
         B     GETREC               GET NEXT RECORD
*---------------------------------------------------------------------*
*   PROCESS ATTR SECTION LINE (INCLUDING "ATTR" LINE IDENTIFIER)      *
*---------------------------------------------------------------------*
ATTRSECT DS    0H                   PREMATURE EXIT
         LA    R1,BUFFCOMA          COMMON ADDRESS
         CALL  BUFFATTR             CALL PROCESS ATTR MODULE
         CLI   RCODE,C'0'           Q. ZERO RETURN CODE
         BNE   ERREXIT                 N. BRANCH TO ERROR EXIT ROUTINE
         B     GETREC               GO GET NEXT RECORD
*---------------------------------------------------------------------*
INITSECT DS    0H                   PREMATURE EXIT
PROCSECT DS    0H                   PREMATURE EXIT
ENDSECT  DS    0H                   PREMATURE EXIT
INPTEND  DS    0H
         CLOSE ISPPLIB              CLOSE INPUT FILES
         MVC   LINEIO(4),=CL4'VCNT'
         MVC   LINEIO+5(13),=CL13'DEFINED ZVARS'
         BAL   R14,PUTPR1                COMMON PUT ROUTINE
         MVI   PRLINE,C'-'
         MVC   PRLINE+1(132),PRLINE
         MVI   PRLINE,C' '
         MVI   PRLINE+5,C' '
         BAL   R14,PUTPR1                COMMON PUT ROUTINE
         MVC   LINEIO(4),OVARCNT
         MVC   LINEIO+5(80),ZVARS
         BAL   R14,PUTPR1                COMMON PUT ROUTINE
         MVC   LINEIO+5(80),ZVARS+80
         MVC   LINEIO(132),SPACES
         MVC   LINEIO(4),=CL4'MTD '
         MVC   LINEIO+5(13),=CL13'DEFINED MTDON'
         BAL   R14,PUTPR1                COMMON PUT ROUTINE
         MVI   PRLINE,C'-'
         MVC   PRLINE+1(132),PRLINE
         MVI   PRLINE,C' '
         MVI   PRLINE+5,C' '
         BAL   R14,PUTPR1                COMMON PUT ROUTINE
         MVC   LINEIO(4),FSETCNT
         MVC   LINEIO+5(80),FSETVARS
         BAL   R14,PUTPR1                COMMON PUT ROUTINE
         MVC   LINEIO+5(80),FSETVARS+80
         BAL   R14,PUTPR1                COMMON PUT ROUTINE
         MVC   LINEIO(10),=CL10'ATTRIBUTES'
         BAL   R14,PUTPR1                COMMON PUT ROUTINE
         MVI   PRLINE,C'-'
         MVC   PRLINE+1(132),PRLINE
         MVI   PRLINE,C' '
         BAL   R14,PUTPR1                COMMON PUT ROUTINE
         MVC   LINEIO(132),TABLE
         BAL   R14,PUTPR1                COMMON PUT ROUTINE
         MVC   LINEIO(132),TABLE+80
         BAL   R14,PUTPR1                COMMON PUT ROUTINE
         MVC   LINEIO(132),TRTABLE
         BAL   R14,PUTPR1                COMMON PUT ROUTINE
         MVC   LINEIO(132),TRTABLE+133
         BAL   R14,PUTPR1                COMMON PUT ROUTINE
         MVC   LINEIO(23),=C'PROCESSED PANEL LINES "'
         LH    R1,FIRSTLIN               LOAD START LINE
         UNPACK R1,2,LINEIO+23           UNPACK START LINE
         MVC   LINEIO+25(06),=C'" TO "'
         LH    R1,LASTLIN                LOAD LAST LINE
         UNPACK R1,2,LINEIO+31           UNPACK LAST LINE
         MVC   LINEIO+33(02),=C'".'
         BAL   R14,PUTPR1                COMMON PUT ROUTINE
*---------------------------------------------------------------------*
         CLOSE PRINTER1             CLOSE PRINTFILE
FINALISE DS    0H
         MVC   OLINE2(80),SPACES    CLEAR FIELD
         MVC   OLINE2(8),SCRSIZE
         MVC   OLINE2+9(8),=CL8'EQU   *-'
         MVC   OLINE2+17(8),SCRNAME
         BAL   R14,PUTMACLB         PRINT LINE
         BAL   R14,PUTPR2                COMMON PUT ROUTINE
         CLOSE MACLIB               CLOSE INPUT FILES
         CLOSE PRINTER2             CLOSE INPUT FILES
         B     EXIT                 BRANCH TO NORMAL EXIT
*---------------------------------------------------------------------*
*     COMMON PUT "PRLINE" TO PRINTER1 ROUTINE                        -*
*---------------------------------------------------------------------*
PUTPR1   DS    0H
         STM   R14,R1,PUTSAVR1
         PUT   PRINTER1,PRLINE
         MVC   PRLINE(133),SPACES
         LM    R14,R1,PUTSAVR1
         BR    R14
PUTSAVR1 DS    4F
*---------------------------------------------------------------------*
*     COMMON PUT "PRLINE" TO PRINTER1 ROUTINE                        -*
*---------------------------------------------------------------------*
PUTPR2   DS    0H
         STM   R14,R1,PUTSAVR2
         PUT   PRINTER2,OLINE2O
         MVC   OLINE2O(81),SPACES
         LM    R14,R1,PUTSAVR2
         BR    R14
PUTSAVR2 DS    4F
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
*     COMMON PUT "PRLINE" TO PRINTER1 ROUTINE                        -*
*---------------------------------------------------------------------*
PUTMACLB DS    0H
         STM   R14,R1,PUTSAVR3
         PUT   MACLIB,OLINE2        WRITE PREV SETUP LINE
         LM    R14,R1,PUTSAVR3
         BR    R14
PUTSAVR3 DS    4F
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
*    GOOD EXIT FILES SHOULD HAVE BEEN SYSTEMATICALLY CLOSED BY PROG   *
*---------------------------------------------------------------------*
EXIT     RACKOFF  RC=0
*---------------------------------------------------------------------*
*    ERROR EXIT (SYSTEM CLOSES FILES)                                 *
*---------------------------------------------------------------------*
ERREXIT  DS    0H
         BAL   R14,PUTPR1           GO PUT LINE
         RACKOFF RC=8
*=====================================================================*
*---------------------------------------------------------------------*
         LTORG                      LOAD LITERAL POOL HERE
*---------------------------------------------------------------------*
TARGET   DS    CL5
BUFFCOMA DS    0D
         COPY  BUFFCOMA             COMMON COPY BOOK
*---------------------------------------------------------------------*
ISPPLIB  DCB   DDNAME=ISPPLIB,                                         X
               DSORG=PS,                                               X
               MACRF=(GM),                                             X
               EODAD=INPTEND
*---------------------------------------------------------------------*
         END
