*---------------------------------------------------------------------*
*                                                                     *
* PLTPI program to be executed at CICS initialisation time to turn    *
* Auxilary Trace on after all initialisations have been done. This    *
* is to identify all activity prior to being able to logon to CICS    *
* and manually turning trace on.                                      *
*                                                                     *
* This program requires in-house macro HEXCHMAC to convert hex        *
* response codes to displayable character format.                     *
*                                                                     *
*---------------------------------------------------------------------*
* Dynamic storage                                                     *
*---------------------------------------------------------------------*
DFHEISTG DSECT
MESS1    DC    CL69' '                 Generic mesage buffer
WTODET1  DS    0F
         WTO   '                                                       +
                             ',        Length 69                       +
               ROUTCDE=(11),DESC=(7),  Programmer information          +
               MF=L

         HEXCHMAC MF=L,LIST=Y
*---------------------------------------------------------------------*
* Register equates                                                    *
*---------------------------------------------------------------------*
         DFHREGS ,                     Register equates
EIBREG1  EQU   11                      EIB  register #1
CODEREG1 EQU   12                      Base register #1
DATAREG1 EQU   13                      Data register #1
*---------------------------------------------------------------------*
* Interfacing with CICS                                               *
*---------------------------------------------------------------------*
CGUAUONX DFHEIENT CODEREG=CODEREG1,DATAREG=DATAREG1,EIBREG=EIBREG1
*
*---------------------------------------------------------------------*
* Initialise fields.                                                  *
*---------------------------------------------------------------------*
         MVC   MESS1(31),=C'CGUAUONX - Auxilary Trace start'
         MVI   MESS1+62,C' '           Ripple clear tail
         MVC   MESS1+63(7),MESS1+62    of message
         MVC   WTODET1(WTODETL),WTODETF Move constant to storage

*---------------------------------------------------------------------*
* Set Auxilary Trace on and Switch next                               *
*---------------------------------------------------------------------*
         EXEC  CICS SET TRACEDEST                                      X
               AUXSTART SWITCHNEXT                                     X
               NOHANDLE

*---------------------------------------------------------------------*
* Test if command OK.                                                 *
*---------------------------------------------------------------------*
         CLC   EIBRESP,DFHRESP(NORMAL) Q. Terminal already allocated
         BNE   ERROR1                     N. Tell of error
         MVC   MESS1+31(31),=C'ed with switch(next) activated.'
         B     RETURN                  Return
ERROR1   DS    0H
         MVC   MESS1+31(32),=C'ing error, RESP=X''....'' RESP2=X'''
         MVC   MESS1+63(6),=C'....''.'
         HEXCHMAC EIBRESP+2,MESS1+49,2,MF=E  Convert Hex to Character
         HEXCHMAC EIBRESP2+2,MESS1+63,2,MF=E Convert Hex to Character
RETURN   DS    0H
         EXEC  CICS WRITEQ TD QUEUE('CSML') FROM(MESS1) LENGTH(=H'69')
         LA    R5,WTODET1              Load address of WTO area
         MVC   WTODET1+4(69),MESS1     Move message details
         WTO   MF=(E,(R5))             Issue message

         EXEC  CICS RETURN
*=====================================================================*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
WTODETF  DS    0F
         WTO   '                                                       +
                             ',        Length 69                       +
               ROUTCDE=(11),DESC=(7),    PROGRAMMER INFORMATION        +
               MF=L
WTODETL  EQU   *-WTODETF

         END
