*ASM XOPTS(SP)
*
**       Program:  CGUDYNAB
**       Function: Dynamically Allocate/Deallocate dataset in batch.
**                 (Console command.)
**
**       Format:
**                              ----+----1----+----2----+----3
**              F CICSTOR1,DYNB IDFHCSD
**              F CICSTOR1,DYNB FDFHCSD
**              F CICSTOR1,DYNB ADFHCSD  S CICS.AKOS.DFHCSD
**
**       Parameters (to CGUDYNA1):
**              IN:
**                 1 byte  - Action (operation)
**                           'I' - Information
**                           'D' - Dealloc (online) writes TS record
**                           'F' - Free (batch) no TS record
**                           'N' - New allocation (free then allocate)
**                           'A' - Allocate
**                           'R' - Realloc
**                 8 bytes - DDNAME
**                 1 byte  - Disposition
**                           'O' - old
**                           'S' - shr
**                           'M' - mod
**                           NOTE - NEW is not available
**                 1 byte  - Rcode (return code (asis))
**                If action is "Alloc"
**                44 bytes - DSNAME
**              OUT:
**                10 bytes  - Applid
**                 2 bytes  - Seperator
**                 1 byte   - Requested action.
**                xx bytes  - Message and returned info if applicable
**
**       Author:   A. Kara.
**       Date:     14/04/2002
*
*=====================================================================*
*
FCTDSBAR EQU   3
EIBREG1  EQU   10
CODEREG1 EQU   11                  BASE REGISTER 1
CODEREG2 EQU   12                  BASE REGISTER 2
DATAREG1 EQU   13                  DYNAMIC STORAGE REGISTER
         YREGS
*---------------------------------------------------------------------*
DFHEISTG DSECT                     DYNAMIC STORAGE
PARMLIST DS    0CL55               PARAMETER LIST ADDRESSES
OPER     DS    CL1
DDNAME   DS    CL8
DISP     DS    CL1
RCODE    DS    CL1
DSNAME   DS    CL44
         ORG   DSNAME
ECODE99  DS    CL4
RCODE99  DS    CL4
         ORG
PARMLEN  EQU   *-PARMLIST
PARML2   DC    H'79'
SCRNMSGO DS    CL79                General message field
         ORG   SCRNMSGO            Redefine
APPLID   DS    CL8                 Applid
         ORG
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
* Interface with CICS                                                 *
*---------------------------------------------------------------------*
CGUDYNAB DFHEIENT CODEREG=(CODEREG1,CODEREG2),                         X
               DATAREG=DATAREG1,EIBREG=EIBREG1

         MVC   SCRNMSGO,SPACES           Initialise message area
         MVC   PARML2(2),=H'79'          Initialise receive length

*---------------------------------------------------------------------*
* Get input data (leave parsing till later)                           *
*---------------------------------------------------------------------*
GETDATA  DS    0H
         EXEC  CICS RECEIVE INTO(SCRNMSGO)                             x
              LENGTH(PARML2) NOHANDLE
*
         MVC   PARMLIST(55),SCRNMSGO+5   Move to commarea
         MVC   SCRNMSGO,SPACES           Initialise message area
         EXEC  CICS ASSIGN APPLID(APPLID)
         MVC   SCRNMSGO+12(1),PARMLIST   Move action to message area
         MVC   SCRNMSGO+13(2),=C') '     Seperator
         MVI   SCRNMSGO+10,C'-'          Seperator

         EXEC CICS LINK PROGRAM('CGUDYNA1') COMMAREA(PARMLIST)         X
              LENGTH(PARML1)
         CLI   RCODE,C'0'                Q. ZERO RETURN CODE
         BNE   RETWERR                      N. PROCESS ERROR RETUR
         MVC   SCRNMSGO+15(18),=C'Action successful.'
         CLI   OPER,C'I'                 Q. Inquiry
         BNE   SENDMAP                      N. Go send map
         MVI   SCRNMSGO+32,C':'          Seperator
         MVC   SCRNMSGO+34(44),DSNAME    Move DSNAME to message
         B     SENDMAP                   GO AND SEND ERROR
RETWERR  DS    0H
         MVC   SCRNMSGO+29(10),=C'on action.'
         CLC   ECODE99(2),=C'DA'         Q. ACTION ERROR
         BE    SYNTAXE                      Y. Signal syntax error
         MVC   SCRNMSGO+29(10),=C'on DDNAME.'
         CLC   ECODE99(2),=C'DD'         Q. DDNAME ERROR
         BE    SYNTAXE                      Y. Signal syntax error
         MVC   SCRNMSGO+29(10),=C'on DSNAME.'
         CLC   ECODE99(2),=C'DS'         Q. DSNAME ERROR
         BE    SYNTAXE                      Y. Signal syntax error
         MVC   SCRNMSGO+29(10),=C'on disp.'
         CLC   ECODE99(2),=C'DI'         Q. DISPOSITION ERROR
         BE    SYNTAXE                      Y. Signal syntax error
         MVC   SCRNMSGO+15(13),=C'Action error:'
         MVC   SCRNMSGO+29(13),=C'Error code X'''
         MVC   SCRNMSGO+32(4),ECODE99
         MVC   SCRNMSGO+46(17),=C''', reason code X'''
         MVC   SCRNMSGO+63(4),RCODE99
         MVC   SCRNMSGO+67(02),=C'''.'
         B     SENDMAP                   GO AND SEND ERROR
SYNTAXE  DS    0H
         MVC   SCRNMSGO+15(13),=C'Syntax error:'

SENDMAP  DS    0h
         EXEC  CICS SEND FROM(SCRNMSGO) LENGTH(=H'79')

RETURNX  DS    0H
         EXEC CICS RETURN

*=====================================================================*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
STARTST  DS    0D
PARML1   DC    AL2(PARMLEN)
SPACES   DC    CL256' '
         END   CGUDYNAB
