*ASM XOPTS(SP)
*
**       Program:  CGUDYOC1
**       Function: Open/close DDNAMES.
**       Parameters:
**                 In   : byte  1      Operation - (O)pen (C)lose
**                        byteS 7 - 8  DDNAME
**
**                 Out  : byte  1      ASIS
**                        byte  7 - 8  ASIS
**                        byteS 9 - 10 Return code
**                                    '00' - Operation O.K.
**                                    '04' - Operation failed
**                                    'IO' - Invalid opcode
**                                    'ID' - Invalid DDNAME
**
**                 Refer: CICS/OS/VS Customisation guide page 410.
**       Date:     19/10/89
**       Author:   A. Kara.
*
*=====================================================================*
DATAREG1 EQU   8
CODEREG1 EQU   9                   BASE REGISTER 1
CODEREG2 EQU   10                  BASE REGISTER 2
EIBREG1  EQU   11
         YREGS
*---------------------------------------------------------------------*
COMMAREA DSECT
OPER     DS    CL1                     OPERATION CODE
DDNAME   DS    CL8                     DDNAME NAME
RCODE    DS    CL2                     RETURN CODE
DFHEISTG DSECT
WSWORK1  DS    0D
DUMPCODE DS    CL4                     DUMP CODE
GETML    EQU   *-WSWORK1               WORKING STORAGE LENGTH
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
*     INTERFACING WITH CICS                                           *
*---------------------------------------------------------------------*
CGUDYOC1 DFHEIENT CODEREG=(CODEREG1,CODEREG2),                         X
               DATAREG=DATAREG1,EIBREG=EIBREG1
*
*---------------------------------------------------------------------*
*     HANDLE CONDITIONS                                               *
*---------------------------------------------------------------------*
HANDCOND DS    0H
         EXEC  CICS HANDLE CONDITION                                   X
            ERROR(DUMPIT)
         EXEC  CICS HANDLE ABEND LABEL(ABENDIT)
*
*---------------------------------------------------------------------*
*    GET PARAMETER DETAILS FROM CSA X'350' FOR 8 BYTES AND            *
*     TEST IF SHUT INVOKED FROM THE MONITOR (IE. CSA + X'350' FOR 8   *
*     SHOULD = "CICSXUT8" (CICSXUTE ZAPPED BY "8")                    *
*---------------------------------------------------------------------*
GETPARM  DS    0H
         L     R4,DFHEICAP             ADDRESS OF COMMAREA
         USING COMMAREA,R4             ADDRESABILITY TO COMMAREA
         MVC   RCODE(2),=C'00'         SET GOOD RETURN CODE
         BAL   R7,PARSEIPT             GO AND PARSE INPUT PARMS
         CLC   RCODE(2),=C'00'         Q. ZERO RETURN CODE
         BNE   EXITPROG                   N. PREMATURE RETURN
         CLI   OPER,C'O'               Q. OPEN ONLY
         BE    OPENFILE                   Y. OPEN ONLY
*---------------------------------------------------------------------*
*     CLOSE FILE                                                      *
*---------------------------------------------------------------------*
CLOSFILE DS    0H
         MVC   DUMPCODE(4),=C'CLE1'    SET DUMP CODE IN CASE
         EXEC  CICS SET FILE(DDNAME) CLOSED NOHANDLE
         CLC   EIBRESP,DFHRESP(NORMAL)       Q. Normal return code
         BNE   CLOSERR                    Y. GO TO ERROR ROUTINE
         B     EXITPROG                GO AND RETURN TO CALLER
*---------------------------------------------------------------------*
*     OPEN FILE.                                                      *
*---------------------------------------------------------------------*
OPENFILE DS    0H
         MVC   DUMPCODE(4),=C'OPE1'    SET DUMP CODE IN CASE
         EXEC  CICS SET FILE(DDNAME) OPEN NOHANDLE
         CLC   EIBRESP,DFHRESP(NORMAL)       Q. Normal return code
         BNE   OPENERR                     Y. GO TO ERROR ROUTINE
         B     EXITPROG                 GO AND RETURN TO CALLER
*---------------------------------------------------------------------*
PARSEIPT DS    0H
         CLI   OPER,C'O'                 Q. VALID OPERATION
         BE    TESTDD                       Y. CONTINUE
         CLI   OPER,C'C'                 Q. VALID OPERATION
         BE    TESTDD                       Y. CONTINUE
         MVC   RCODE(2),=C'IO'           SET BAD RETURN CODE
         B     PARSEXIT                  GO TO COMMON RETURN POINT
TESTDD   DS    0H
         CLC   DDNAME(8),=CL8' '         Q. DDNAME SUPPLIED
         BE    PARSER16                     N. SET ERROR CODE
         CLI   DDNAME,C'0'               Q. FIRST CHAR NUMERIC
         BNL   PARSER16                     Y. SET ERROR CODE
         LA    R3,8                      LOOP LIMIT
         LA    R1,DDNAME+7               LAST BYTE OF DDNAME
DDNLOOP1 DS    0H
         CLI   0(R1),C' '                Q. IS CHARACTER SPACE
         BNE   DDNLOP1E                      N. EXIT LOOP
         BCTR  R1,0                      POINT TO PREVIOUS CHARACTER
         BCT   R3,DDNLOOP1               LOOP TILL END
DDNLOP1E DS    0H
*        TRT   DDNAME(8),TRTDSNM         ALL VALID CHARACTERS
         BCTR  R3,0                      RELATIVE LENGTH
         EX    R3,TRTDDNAM               ALL VALID CHARACTERS
         BNZ   PARSER16                     N. SET ERROR CODE
         B     PARSEXIT                  GO TO COMMON RETURN POINT
PARSER16 DS    0H
         MVC   RCODE(2),=C'ID'           SET BAD RETURN CODE
PARSEXIT DS    0H
         BR    R7                        RETURN FROM CALLED ROUTINE
*---------------------------------------------------------------------*
TRTDDNAM TRT   DDNAME(0),TRTDSNM         ALL VALID CHARACTERS
*---------------------------------------------------------------------*
OPENERR  DS    0H
CLOSERR  DS    0H
         MVC   RCODE(2),=C'04'           SET ERROR RETURN CODE
         B     EXITPROG                  RETURN TO CALLER
*---------------------------------------------------------------------*
DUMPIT   DS    0H
ABENDIT  DS    0H
         EXEC  CICS ABEND ABCODE(DUMPCODE)
*---------------------------------------------------------------------*
EXITPROG DS    0H
         DROP  R4                        FINISHED ADDRESABILITY
         EXEC  CICS RETURN
*=====================================================================*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
TRTDSNM  DC    16X'40'                                00-0F
         DC    16X'40'                                10-1F
         DC    16X'40'                                20-2F
         DC    16X'40'                                30-3F
         DC    X'40404040404040404040400040404040'    40-4F
         DC    X'40404040404040404040400040404040'    50-5F
         DC    16X'40'                                60-6F
         DC    X'40404040404040404040400000404040'    70-7F
         DC    16X'40'                                80-8F
         DC    16X'40'                                90-9F
         DC    16X'40'                                A0-AF
         DC    16X'40'                                B0-BF
         DC    X'40000000000000000000404040404040'    C0-CF
         DC    X'40000000000000000000404040404040'    D0-DF
         DC    X'40400000000000000000404040404040'    E0-EF
         DC    X'00000000000000000000404040404040'    F0-FF
*---------------------------------------------------------------------*
         END
