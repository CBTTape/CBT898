*ASM XOPTS(NOPROLOG NOEPILOG)
***********************************************************************
*                             HRNZNEPC                                *
*    SAMPLE NODE ERROR PROGRAM FOR HURON/CICS DEPENDENT REGION        *
*       --- COMMAND LEVEL SUPORT FOR CICS 3.2 AND ABOVE ---           *
*                                                                @DRE6*
*  NOTE:                                                         @DRE6*
*     SEE CICS/VS 2.1.2 PROBLEM DETERMINATION GUIDE, CHAPTER 2.8 @DRE6*
*      P.221 (SC33-0516-02) FOR ERROR CODES PASSED TO DFHZNEP.   @DRE6*
*     TO ADD OR REMOVE ERROR CODES CHANGE THE "CODE" OPERAND     @DRE6*
*     IN MACRO DFHSNEP TYPE=ERRPROC. THESE ERROR CODES ARE       @DRE6*
*     APPLICABLE TO CICS/VS 3.3                                  @DRE6*
*                                                                @DRE6*
*  SAMPLE ERROR CODES                                            @DRE6*
*                                                                @DRE6*
*     X'49' SESSION TERMINATED, WHEN PLACED OUT OF SERVICE IN    @DRE6*
*           CEMT SET COMMANDS.                                   @DRE6*
*     X'57' RELEASE COMMAND ISSUED FROM CEMT, LOGOFF TERMINAL    @DRE6*
*           TIMEOUT CONDITION. RELEASE COMMAND MAY ALSO BE       @DRE6*
*           ISSUED INTERNALLY E.G. IN CUSTOMIZED AUTOINSTALL     @DRE6*
*           CODE.                                                @DRE6*
*     X'5C' SESSION FAILURE. SESSION TERMINATED IMMEDIATELY.       HS81
*     X'61' LUSTATUS RECEIVED AFTER INTERVENTION REQUIRED TYPE   @DRE6*
*           OF CONDITION SUCH AS POWER OFF, POWERN. THIS         @DRE6*
*           CONDITION IS RECOVERABLE.                            @DRE6*
*     X'D1' NONRECOVERABLE ERRORS                                @DRE6*
*                                                                @DRE6*
***********************************************************************
         DFHSNEP  TYPE=USTOR                                      @DREE
RESPONSE DS       F                                               @DREE
ERRMSG   DS       0CL(ERRLEN)                                     @DREE
ERRHDR   DS        CL10             HRNCS100E                     @DREE
PGMNAME  DS        CL8              PROGRAM NAME                  @DREE
LERROR   DS        CL7              ERROR:                        @DREE
ERROR    DS        CL8                                            @DREE
LTERMLID DS        CL8              TERMID=                       @DREE
TERMNLID DS        CL4              XXXX                          @DREE
ERRLEN   EQU       *-ERRHDR                                       @DREE
DLCARQID DS        0CL8 DLCA REQID
DLCADLTO DS        CL4              'DLTO'
DLCATERM DS        CL4 TERM ID
         DFHSNEP  TYPE=USTOREND                                   @DREE
*                                                                 @DREE
         DFHSNEP  TYPE=INITIAL
         DFHSNEP  TYPE=ERRPROC,     DECLARE GROUP1 ERROR PROCESSOR     *
               CODE=(D1,5C,57,49),                                 HS81*
               GROUP=1              ERROR GROUP INDEX FOR NODE ERROR
DFHZNEP0 CSECT    ,
*        ORG      NEPTT
*        DC       256X'01'        ALL ERRORS HANDLE BY GROUP1 PROCESSOR
         SPACE
***********************************************************************
*    GROUP 01 ERROR PROCESSOR                                         *
***********************************************************************
NEPROC01 CSECT    ,
         ST       CSVTBAR,NEPEPRS SAVE RETURN REGISTER
         USING    NEPROC01,EP1BASE  DEFINE BASE
         LR       EP1BASE,EPBAR     LOAD BASE REGISTER
         SPACE
***********************************************************************
*    PERFORM CICS SUPPLIED DUMMY NODE ERROR PROGRAM FUNCTION          *
***********************************************************************
         CLI      TWAEC,TCZSRCSP    VTAM STORAGE PROB.ERROR CODE       *
                                    IN REAL TCTTE ?
         BNE      NEP01LNK          NO, SKIP PRINTING TCTTE
         OI       TWAOPT1,TWAOTCTE  PRINT TCTTE
         SPACE
***********************************************************************
*    LINK TO HURON SUPPLIED NODE ERROR PROCESSOR PROGRAM              *
***********************************************************************
NEP01LNK DS       0H
         MVC   DLCADLTO,=C'DLTO'     SET UP REQID FOR START DLCA
         MVC   DLCATERM,TWANID       .
         EXEC  CICS START                                              *
               AFTER SECONDS(0)                                        *
               FROM         (TWANID)                                   *
               REQID        (DLCARQID)                                 *
               LENGTH       (4)                                        *
               TRANSID      ('DLCA')                                   *
               RESP         (RESPONSE)
         SPACE
***********************************************************************
*    ERROR PROCESSOR EXIT - RETURN TO NEP                             *
***********************************************************************
NEP01RET DS       0H
         L        CSVTBAR,NEPEPRS   RESTORE LINK REGISTER
         BR       CSVTBAR           RETURN TO NEP
         SPACE
         LTORG    ,                 FORCE OUT ASSEMBLY LITERALS
         SPACE
EP1BASE  EQU      R8                ERROR PROCESSOR BASE REG
         SPACE
         DFHSNEP  TYPE=FINAL
         END      DFHNEPNA
