*---------------------------------------------------------------------*
*                                                                     *
* PLTPI program to be executed at CICS initialisation time to define  *
* the type of action/non-action to be taken when a system or trans-   *
* action abends of concern are encountered during the normal course   *
* of processing.                                                      *
* There is a matrix for both types of abends that identify he         *
* abend and the number of syste/trans or both (where napplicable) are *
* to be takend when they occur.                                       *
*                                                                     *
*---------------------------------------------------------------------*
* Dynamic storage                                                     *
*---------------------------------------------------------------------*
DFHEISTG DSECT
SYSDUMP  DS    F                       System dump request CVDA
TRANDUMP DS    F                       Transaction dump request CVDA
*---------------------------------------------------------------------*
* Register equates                                                    *
*---------------------------------------------------------------------*
         DFHREGS ,                     Register equates
EIBREG1  EQU   11                      EIB  register #1
CODEREG1 EQU   12                      Base register #1
DATAREG1 EQU   13                      Data register #1
*---------------------------------------------------------------------*
* Interfacing with CICS                                               *
*---------------------------------------------------------------------*
CICSTRDX DFHEIENT CODEREG=CODEREG1,DATAREG=DATAREG1,EIBREG=EIBREG1
*
*---------------------------------------------------------------------*
* Ignore duplicate conditions                                         *
*---------------------------------------------------------------------*
         EXEC  CICS IGNORE CONDITION DUPREC
*
*---------------------------------------------------------------------*
* Process transaction table                                           *
*---------------------------------------------------------------------*
         LA    R2,TDTABLE              Point to transaction table
         USING TDDSECT,R2              Addresability to table entries
TDLOOP   DS    0H
         CLC   =X'FFFFFFFF',TDABEND    Q. End of table indicator
         BE    TDLOOPEX                   Y. Exit loop
TDTESTT  DS    0H
         MVC   TRANDUMP,DFHVALUE(NOTRANDUMP) Set transaction dump OFF
         CLI   TDTRAND,C'Y'            Q. Transaction dump required
         BNE   TDTESTS                    N. Test for system dump
         MVC   TRANDUMP,DFHVALUE(TRANDUMP) Set transaction dump ON
TDTESTS  DS    0H
         MVC   SYSDUMP,DFHVALUE(NOSYSDUMP) Set system dump OFF
         CLI   TDSYSD,C'Y'             Q. System dump required
         BNE   TDTESTX                    N. Next test (if any)
         MVC   SYSDUMP,DFHVALUE(SYSDUMP) Set system dump ON
TDTESTX  DS    0H

*---------------------------------------------------------------------*
* Issue the command to set dumpoptions for specific transaction abend *
*---------------------------------------------------------------------*
         EXEC  CICS SET TRANDUMPCODE(TDABEND) ADD MAXIMUM(TDMAX)       X
               TRANDUMPING(TRANDUMP) SYSDUMPING(SYSDUMP)

         LA    R2,TDENTRYL(R2)         Point to next table entry
         B     TDLOOP                  Go process next table entry
TDLOOPEX DS    0H

*---------------------------------------------------------------------*
* Process system table                                                *
*---------------------------------------------------------------------*
         LA    R2,SDTABLE              Point to system table
         USING SDDSECT,R2              Addresability to table entries
SDLOOP   DS    0H
         CLC   =X'FFFFFFFF',SDABEND    Q. End of table indicator
         BE    SDLOOPEX                   Y. Exit loop
SDTESTS  DS    0H
         MVC   SYSDUMP,DFHVALUE(NOSYSDUMP) Set system dump OFF
         CLI   SDSYSD,C'Y'             Q. System dump required
         BNE   SDTESTX                    N. Next test (if any)
         MVC   SYSDUMP,DFHVALUE(SYSDUMP) Set system dump ON
SDTESTX  DS    0H

*---------------------------------------------------------------------*
* Issue the command to set dumpoptions for specific system abend      *
*---------------------------------------------------------------------*
         EXEC  CICS SET SYSDUMPCODE(SDABEND) ADD MAXIMUM(SDMAX)        X
               SYSDUMPING(SYSDUMP)

         LA    R2,SDENTRYL(R2)         Point to next table entry
         B     SDLOOP                  Go process next table entry
SDLOOPEX DS    0H

         EXEC  CICS RETURN
*=====================================================================*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
TDTABLE  DS    0F                          Transaction dump table
*              Abend     #Dumps   Sys  Tran
*              --------- -------  ---- ----
         DC    CL4'ASRA',AL4(999),C'Y',C'Y' Capture all ASRA
         DC    CL4'DAID',AL4(020),C'N',C'Y' Capture all ASRA
         DC    CL4'ATNI',AL4(000),C'N',C'N'
         DC    CL4'ATZI',AL4(000),C'N',C'N'
         DC    CL4'ATZ6',AL4(000),C'N',C'N'
         DC    X'FFFFFFFF'                 End of table marker

SDTABLE  DS    0F                          Systen dump table
*              Abend       #Dumps   Sys
*              ----------- -------  ----
         DC    CL8'AP0001',AL4(002),C'Y'
*        DC    CL8'AP0002',AL4(000),C'N'
         DC    X'FFFFFFFF'                 End of table marker

TDDSECT  DSECT                             Map of trans dump table
TDABEND  DS    CL4                         Abend code
TDMAX    DS    F                           Max no. of abends to dump
TDSYSD   DS    CL1                         Gen system dump for abend
TDTRAND  DS    CL1                         Gen tran dump for abend
TDENTRYL EQU   *-TDDSECT                   Length of each entry

SDDSECT  DSECT                             Map of system dump table
SDABEND  DS    CL8                         Abend code
SDMAX    DS    F                           Max no. of abends to dump
SDSYSD   DS    CL1                         Gen system dump for abend
SDENTRYL EQU   *-SDDSECT                   Length of each entry

         END
