*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*
**       TITLE:    COPY DATASET/MEMBER ROUTINE.
**       SYSTEM:   N/A
**       MODULE:   COPY
**       PROGRAM:  COPY
**       FUNCTION: Copy a dataset/member to another dataset/member.
**                 Can process:
**                    F - F
**                    F - V
**                    V - V
**                    V - F.
**                 Limit 32760 characters.
**       PARAMETERS:
**                 INPUT  - dataset DD(INFILE)
**                        - Parameter 'ISPF' to indicate an ISPF
**                          environment. (Default TSO.)
**                 OUTPUT - dataset DD(OUTFILE)
**                        - If ISPF environment:
**                          Variable 'MESSAGE' contains # of records
**                          for on-line ISPF usage.
**                          Copied # of records obtained via:
**                              ISPEXEC VGET (MESSAGE) SHARED
**                          Alternatively you may specify a 'SYSPRINT'
**                          DD name for report RECFM=FA, LRECL=133,
**                          BLKSIZE=133.
**       AUTHOR:   ASYSTS P/L (A. Kara).
**                 Tailored 05/08/92.
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
         COPY @MACROS                  COMMON MACROS USED BY PROGRAM
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
COPY     KICKOFF (R12,R13),LIST=NO
START    EQU   *
         LR    R7,R1                   CPPL ADDR
         TM    0(R7),X'80'             Q. PARAMETER ADDRESS
         BO    TESTPARM                   Y. PROCESS AS PARMAMETER
*---------------------------------------------------------------------*
* Command processor                                                   *
*---------------------------------------------------------------------*
         USING CPPL,R7                 ADDRESABILITY TO CPPL
         L     R6,CPPLCBUF             GET COMMAND BUFFER ADDR
         LH    R1,0(R6)                TOTAL COMMAND LENGTH
         SH    R1,=H'4'                SUBTRACT LENGTH'S LENGTH
         LH    R3,2(R6)                OPERAND LENGTH
         SR    R1,R3                   SUBTRACT OPERAND LENGTH
         LTR   R1,R1                   Q. ANY PARAMETER LENGTH
         BZ    NOPARAM                    N. BYPASS WITH NO PARAMETER
         LA    R2,4(R6)                ADDR OF START OF COMMAND
         AR    R2,R3                   ADD DISPLACEMENT OF OPERAND
         B     PROCPARM                GO PROCESS COMMAND
                                                                SPACE 1
*---------------------------------------------------------------------*
* Called program                                                      *
*---------------------------------------------------------------------*
TESTPARM DS    0H
         L     R6,0(R7)             LOAD ADDRESS OR PARAMETER
         LH    R1,0(R6)             LOAD LENGTH OF PARAMETER
         LTR   R1,R1                Q. IS PARAMETER LENGTH ZERO
         BZ    NOPARAM                 Y. GO PROCESS 'SYSIN'
         LA    R2,2(R6)             POINT TO PARAMETER
                                                                SPACE 1
*---------------------------------------------------------------------*
* Process parameter                                                   *
*     R2 --> parameter pointer                                        *
*     R1 --> parameter length                                         *
*---------------------------------------------------------------------*
PROCPARM DS    0H
         CLC   =C'ISPF',0(R4)          Q. IN AN ISPF ENVIRONMENT
         BNE   NOPARAM                    N. NO PARAMETER
         ISPEXEC INIT=Y                INITIALISE ISPF INTERFACE
         ISPVDEFX INM=(MESSAGE),ANM=MESSAGE,FORMAT=CHAR,LEN=22
                                                                SPACE 1
*---------------------------------------------------------------------*
* No parameter passed processing                                      *
*---------------------------------------------------------------------*
NOPARAM  DS    0H
                                                                SPACE 1
*---------------------------------------------------------------------*
* Process after parameter processing completed (if any)               *
*---------------------------------------------------------------------*
PROCESS  DS    0H
         OPEN  (INFILE,(INPUT))        OPEN INPUT FILE
         OPEN  (OUTFILE,(OUTPUT))      OPEN OUTPUT FILE
         MVC   OUTRECL(2),OUTFILE+X'52' STORE OUTFILE LRECL
         CLEARV  WORKAREA,PAD=00       CLEAR WORK AREA
         XR    R9,R9                   INIT RECORD COUNTER
         CLC   INFILE+X'52'(2),OUTFILE+X'52' Q. RECORD FORMAT SAME
         BE    READLOOP                         N. BYPASS
         MVI   FORMCHG,C'Y'            SET FORMAT CHANGED FLAG ON
READLOOP EQU   *
         GET   INFILE,WORKAREA         READ INFILE INTO WORKAREA
         TM    INFILE+X'24',RECFMV     Q. VARIABLE LENGTH INPUT
         BNO   PUTRECFX                   N. PROCESS FIXED TO ?
         TM    OUTFILE+X'24',RECFMV    Q. VARIABLE LENGTH OUTPUT
         BNO   FXDOUT                     N. PROCESS VAR TO FIXED
PUTRECVV DS    0H                      PROCESS VAR TO VAR
         MVC   OUTFILE+X'52'(2),INFILE+X'52' MOVE LRECL TO OUTPUT
         PUT   OUTFILE,WORKAREA        WRITE OUTFILE
         B     LOOPIT                  PROCESS NEXT RECORD
FXDOUT   DS    0H                      PROCESS VAR TO FIXED
         LA    R0,WORKAREA             LOAD ADDRESS OF WORKAREA
         LH    R1,OUTFILE+X'52'        LOAD LENGTH OF OUTFILE
         SH    R1,INFILE+X'52'         SUBTRACT LENGTH OF INPUT FILE
         BNP   PUTRECVF                BRANCH TO WRITE FIXED FROM VAR
*                                        IF INPUT LENGTH > OUTPUT
         AH    R0,INFILE+X'52'         POINT PAST VARIABLE INPUT DATA
         XR    R14,R14                 CLEAR SOURCE ADDRESS
         XR    R15,R15                 CLEAR SOURCE LENGTH
         ICM   R15,B'1000',=X'40'      MOVE PAD CHARACTER
         MVCL  R0,R14                  SET TRAIL AREA TO SPACES
PUTRECVF DS    0H
         PUT   OUTFILE,WORKAREA+4      WRITE OUTFILE
         B     LOOPIT                  PROCESS NEXT RECORD
PUTRECFX DS    0H                      PROCESS FIXED TO ?
         TM    OUTFILE+X'24',RECFMV    Q. VARIABLE LENGTH OUTPUT
         BNO   PUTRECFF                   N. PROCESS FIXED TO FIXED
PUTRECFV DS    0H                      PROCESS FIXED TO FIXED
         XC    VARLEN,VARLEN           INITIALISE RECORD LENGTH FIELD
         LH    R1,INFILE+X'52'         LOAD LENGTH OF INFILE
         CH    R1,OUTRECL              Q. GREATER THAN ORIG LRECL OUTP
         BNH   ORECLOK                    N. KEEP RECORD LENGTH
         LH    R1,OUTRECL              LOAD ORIG LRECL OUTP
ORECLOK  DS    0H
         STH   R1,OUTFILE+X'52'        STORE LENGTH OF OUTFILE
         STH   R1,WORKAREA-4           STORE LENGTH OF OUTFILE
         PUT   OUTFILE,WORKAREA-4      WRITE OUTFILE
         B     LOOPIT                  GO PROCESS NEXT RECORD
PUTRECFF DS    0H                      PROCESS FIXED TO FIXED
         PUT   OUTFILE,WORKAREA        WRITE OUTFILE
LOOPIT   DS    0H
         LA    R9,1(R9)                UP RECORD COUNTER BY 1
         B     READLOOP                GET NEXT RECORD
INFEND   DS    0H
         CLOSE INFILE                  CLOSE INPUT FILE
         CLOSE OUTFILE                 CLOSE OUTPUT DISK FILE
         UNPACK R9,6,MESSAGE           CONVERT TO DISPLAY FORMAT
         LA    R10,ADYNWORK            POINTER TO ADYN WORK AREA
         ADYN  ACTION=INFO,DD=SYSPRINT,MF=E,BASE=R10,DSN=WORKAREA
         LTR   R15,R15                 Q. SYSPRINT DD ALLOCATED
         BNZ   EXITPROG                   N. BYPASS
         OPEN  (SYSPRINT,(OUTPUT))     OPEN OUTPUT FILE
         LA    R2,WORKAREA             POINT TO OUTPUT DATA AREA
         MVC   0(256,R2),SPACES        CLEAR OUTPUT AREA
         MVI   0(R2),C'1'              MOVE PRINT CONTROL CHARACTER
         MVC   2(22,R2),MESSAGE        MOVE MESSAGE TO OUTPUT
         PUT   SYSPRINT,(R2)           WRITE OUTFILE
         CLI   FORMCHG,C'N'            Q. RECORD FORMAT CHANGED
         BE    EXITPROG                   N. EXIT PROGRAM
         MVC   0(50,R2),=C'  INPUT AND OUTPUT DATASETS ARE DIFFERENT FOR
               RMATS.'
         PUT   SYSPRINT,(R2)           WRITE OUTFILE
         CLOSE SYSPRINT                CLOSE OUTPUT DISK FILE
EXITPROG DS    0H
         CLI   ISPFENV,C'Y'            Q. ISPF ENVIRONMENT
         BNE   CONT#200                   N. BYPASS
         ISPEXEC 'VPUT (MESSAGE) SHARED'
CONT#200 DS    0H
         RACKOFF RC=0                  EXIT PROGRAM
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*---------------------------------------------------------------------*
         LTORG                         LOAD LITERAL POOL HERE
*---------------------------------------------------------------------*
DWORK8   DS    0D
RECFMV   EQU   64                      VARIABLE LENGTH RECORD
MESSAGE  DC    22C'...... RECORDS COPIED.'
ISPFENV  DC    C'N'                    NOT ISPF ENVIRONMENT
*
INFILE   DCB   DDNAME=INFILE,                                          X
               DSORG=PS,                                               X
               MACRF=(GM),                                             X
               EODAD=INFEND
*
OUTFILE  DCB   DDNAME=OUTFILE,                                         X
               DSORG=PS,                                               X
               MACRF=(PM)
*
SYSPRINT DCB   DDNAME=SYSPRINT,                                        X
               DSORG=PS,                                               X
               RECFM=FA,                                               X
               LRECL=133,                                              X
               BLKSIZE=133,                                            X
               MACRF=(PM)
*
         ADYN  MF=L                    LIST FORM OF ADYN
*
SPACES   DC    CL256' '                SPACES
ADYNWORK DS    CL300                   ADYN WORK AREA
FORMCHG  DC    C'N'                    FORMAT CHANGE FLAG
OUTRECL  DS    H                       ORIGINAL OUTPUT RECORD LENGTH
*
VARLEN   DC    F'0'                    VAR OUTPUT RECORD LENGTH
WORKAREA DS    CL32760                 WORK AREA
*
         ADYN  DATA=IBM                IBM DSECTS
         IKJCPPL COMMAND           PROCESSOR PARAMETER LIST
*---------------------------------------------------------------------*
         END
