*
**       ROUTINE:  CORESORT
**       FUNCTION: CORE SORT
**                 R1 POINTS TO PARAMETER LIST
**                    AL4 A OF AREA
**                    XL2 LENGTH OF ENTRY
**                    XL1 END MARKER
**                    FIELDS
**                       XL2 DISP
**                       XL2 LENGTH
**                       CL1 TYPE A OR D
**                    XL1 END MARKER
**       AUTHOR:   MELWARE
**       DATE:     13/03/87
*
         COPY  BATCHEQU                BATCH PROGRAM EQUATES
*
LINKR    EQU   14                      LINKAGE REGISTER
BASER1   EQU   15                      BASE REGISTER 1
*
CORESORT CSECT                      CONTROL SECTION
         USING *,BASER1                ADDRESS PROGRAM
         STM   R14,R12,12(R13)         SAVE REGISTERS
         ST    R13,R13SVA              SAVE R13
***********************************************************************
*                                                                     *
*        MAINLINE                                                     *
*                                                                     *
***********************************************************************
         MVC   PARM,0(R1)              MOVE PARAMETER
         LA    R6,FIELDS               ADDRESS SORT FIELDS
         CLC   0(1,R6),END             IF NO SORT FIELDS
         BE    RETURN                     GET OUT...
         SR    R9,R9
         SR    R10,R10
         SR    R11,R11
PASS     DS    0H
         LA    R9,1(R9)
         MVI   CHANGED,C'N'            SET NO CHANGE
         L     R2,ADDRESS              POINT TO START
         LH    R3,LENGTH               LENGTH OF ENTRY
         LA    R4,0(R3,R2)             POINT TO SECOND ENTRY
         CLC   0(1,R2),END             IF NO ENTRIES
         BE    RETURN                     GET OUT...
         CLC   0(1,R4),END             IF ONE ENTRY,
         BE    RETURN                     GET OUT...
         LR    R5,R3                   FOR MOVES
DOCOMP   LA    R10,1(R10)
         LA    R6,FIELDS               ADDRESS SORT FIELDS
         CLC   0(1,R6),END             IF NO SORT FIELDS
         BE    RETURN                     GET OUT...
DOFLD    LH    R7,0(R6)                GET DISP
         LA    R8,0(R7,R4)             INTO 2ND ENTRY
         LA    R7,0(R7,R2)             INTO FIRST ENTRY
         LH    R1,2(R6)                GET LENGTH
         BCTR  R1,0                     -1
         CLI   4(R6),C'D'              IF ASCENDING
         BE    DEC                        DO DEC COMP...
         EX    R1,COMP                 COMPARE ENTRIES
         BE    NEXTF                      IF SAME NEXT FIELD...
         BL    NEXTE                      IF LOW BYPASS ENTRY
         B     SWAP                          ELSE SWAP...
DEC      EX    R1,COMP                 COMPARE ENTRIES
         BE    NEXTF                      IF SAME NEXT FIELD...
         BH    NEXTE                      IF HIGH BYPASS ENTRY
         B     SWAP                          ELSE SWAP
NEXTF    LA    R6,5(R6)                BYPASS FIELD DEF
         CLC   0(1,R6),END             IF LAST FIELD
         BE    NEXTE                      BYPASS ENTRY...
         B     DOFLD                         ELSE DO IT...
SWAP     MVI   CHANGED,C'Y'            SET CHANGED
         LA    R11,1(R11)
         LA    R6,TEMP
         LR    R7,R3
         STM   R2,R7,SAVEREGS
         MVCL  R6,R2
         LM    R2,R7,SAVEREGS
         MVCL  R2,R4
         LM    R2,R7,SAVEREGS
         MVCL  R4,R6                   SWAP ENTRIES
         LM    R2,R7,SAVEREGS
NEXTE    LR    R2,R4                   BYPASS ENTRY
         LA    R4,0(R3,R4)
         CLC   0(1,R4),END             IF NOT END
         BNE   DOCOMP                     COMPARE THEM...
         CLI   CHANGED,C'Y'            IF SOMETHING CHANGED,
         BE    PASS                       DO ANOTHER PASS...
         B     RETURN                        ELSE GET OUT...
COMP     CLC   0(0,R7),0(R8)           COMPARE ENTRIES
***********************************************************************
*                                                                     *
*              RETURN TO CALLER                                       *
*                                                                     *
***********************************************************************
RETURN   DS    0H                   RETURN TO CALLER
         L     R13,R13SVA              POINT TO SAVEAREA
         LM    R14,R12,12(R13)         RELOAD REGISTERS
         BR    R14                       AND RETURN TO CALLER...
***********************************************************************
*                                                                     *
*              DEFINITIONS AND CONTROL BLOCKS                         *
*                                                                     *
***********************************************************************
         LTORG
*
**       PARAMETER AREA
*
PARM     DS    0XL256
ADDRESS  DC    AL4(0)
LENGTH   DC    H'0'
END      DC    X'FF'
FIELDS   EQU   *
         ORG   PARM+256
*
SAVEREGS DS    XL24
*
CHANGED  DC    C'N'
R13SVA   DS    AL4
*
TEMP     DS    XL4096
         END   CORESORT
