*---------------------------------------------------------------------*
&ZPRINT  SETC 'YES'                      EXPAND ALL 'UCB' TYPE MACROS
DASDEDTL KICKOFF (R11,R12),WORKAREA,WORKAREL,LIST=N
                                                                SPACE 1
*---------------------------------------------------------------------*
* Initialisation of got storage                                       *
*---------------------------------------------------------------------*
INITIAL1 DS    0H
         MVI   TRACE,C'Y'                <-----
         ISPEXEC MF=E,INIT=Y             INITIALISE ISPF INTERFACE
         ISPEXEC 'CONTROL ERRORS RETURN'
*        ISPEXEC 'CONTROL ERRORS CANCEL'
         BAL   R14,ISPFENV               PROCESS ISPF ENVIRONMENT
REFRESH  DS    0H
*---------------------------------------------------------------------*
         LA    R9,DEVCL                  LOAD DEVICE CLASS ADDRESS
         LA    R8,L'DEVCL/9              LOAD LOOP COUNTER
         XR    R7,R7                     RECORD COUNT
DEVICEL1 DS    0H
         LA    R10,NAMELIST              NAME LIST ADDRESS
         EDTINFO RTNNAMD,                                              +
               DEVCLASS=(R9),                                          +
               NAMELIST=(R10),                                         +
               MF=(E,EDT1)
         L     R10,0(R10)                LOAD NO OF UNITS RETURNED
*    PDUMP (R10),QUIT=EXITPROG,TRACE=TRACE,MSG='IN' <- FOR DEBUGGGING
         BAL   R14,IOROUTIN              GO PROCESS I/O ROUTINE
                                                                SPACE 1
         XR    R2,R2                     INITIALISE REGISTER
         ICM   R2,B'0111',1(R10)         LOAD GOT AREA LENGTH
*    PDUMP (R10),QUIT=EXITPROG,TRACE=TRACE,MSG='OUT' <- FOR DEBUGGGING
         FREEMAIN R,LV=(R2),A=(R10)      FREE AREA ACQUIRED BY EDTINFO
NEXTDEV  DS    0H
         LA    R9,9(R9)                  BUMP POINTER
         BCT   R8,DEVICEL1               LOOP TILL END
*---------------------------------------------------------------------*
TBDISPPR DS    0H
         ISPEXEC 'TBTOP DSGENRIC'
DISPSCR1 DS    0H
         ISPEXEC 'TBDISPL DSGENRIC PANEL(DASDEDTL)',LIST=YES
         LTR   R15,R15                   Q. END ENTERED
         BZ    REFRESH                      N. REDISPLAY TABLE
                                                                SPACE 1
         ISPEXEC 'TBEND DSGENRIC'        END TABLE WITHOUT SAVE
                                                                SPACE 1
EXITPROG DS    0H
         RACKOFF RC=0
                                                                SPACE 1
*---------------------------------------------------------------------*
*=====================================================================*
                                                                SPACE 1
*=====================================================================*
* Handshaking between the ISPF environment and this program. Also     *
* handshakes with the invoking dialog...                              *
*---------------------------------------------------------------------*
ISPFENV  DS    0H
         STM   R0,14,SAVREGS1            STORE REGISTERS
         ISPEXEC 'CONTROL ERRORS RETURN'
         ISPVDEFX ANM=GC009,INM=(GC009),                               +
               LIST=NO,FORMAT=CHAR,MF=E,LEN=79
         ISPVDEFX ANM=SPFZCMD,INM=(ZCMD),                              +
               LIST=NO,FORMAT=CHAR,MF=E,LEN=79
         ISPVDEFX ANM=SPFSORT,INM=(SS),                                +
               LIST=NO,FORMAT=CHAR,MF=E,LEN=2
         ISPVDEFX ANM=SPFTYPE,INM=(ST),                                +
               FORMAT=CHAR,MF=E,LIST=NO,LEN=4
         ISPVDEFX ANM=SPFGNAME,INM=(SN),                               +
               LIST=NO,FORMAT=CHAR,MF=E,LEN=8
         ISPVDEFX ANM=SPFTYPEX,INM=(SFTX),                             +
               LIST=NO,FORMAT=CHAR,MF=E,LEN=1
         ISPVDEFX ANM=WINDOW,INM=(DASDSPCW),                           +
               LIST=NO,FORMAT=CHAR,MF=E,LEN=1
                                                                SPACE 1
         ISPEXEC 'VGET (KEEPTB DASDSPCW) SHARED'
         MVC   KEEPTB+1(1),KEEPTB        BACKUP KEEP TABLE FLAG
         MVI   KEEPTB,C' '               INITIALISE KEEP TABLE FLAG
         ISPEXEC 'VPUT KEEPTB SHARED'    TURN KEEP TABLE FLAG OFF
         BAL   R14,TABLECRT              GO CREATE TABLE
         LM    R0,14,SAVREGS1            RESTORE REGISTERS
         BR    R14                       RETURN TO CALLER
                                                                SPACE 1
*=====================================================================*
* Create the table..                                                  *
*---------------------------------------------------------------------*
TABLECRT DS    0H
         STM   R0,14,SAVREGS2            STORE REGISTERS
         ISPEXEC 'TBEND DSGENRIC'
         ISPEXEC 'TBCREATE DSGENRIC KEYS(SS) NAMES(ST SN STI) NOWRITE'
         ISPEXEC 'TBSORT DSGENRIC FIELDS(SS,C,A)'
         LM    R0,14,SAVREGS2            RESTORE REGISTERS
         BR    R14                       RETURN TO CALLER
                                                                SPACE 1
*=====================================================================*
* This section populates the table from the acquired control block(s) *
*---------------------------------------------------------------------*
IOROUTIN DS    0H
         STM   R8,R14,SAVREGS1           STORE REGISTERS
         MVC   SPFLINE(SPFLENG),SPACES   CLEAR MESSAGE AREA
         MVC   SPFTYPEX(1),0(R9)         INITIALISE TYPE FLAG
                                                                SPACE 1
         MVC   SPFTYPE(4),1(R9)          MOVE DEVICE CLASS DESCRIPTION
         L     R8,4(R10)                 LOAD LOOP COUNTER
         ST    R10,FULLWORD              SWTORE POINTER
         LTR   R8,R8                     Q. ANY PARAMETERS RETURNED
         BNZ   RETDEVLP                     Y. PROCESS LIST
         MVC   SPFGNAME(4),=C'None def'  MOVE GENERIC NAME
         LA    R8,1                      FAKE 1 ENTRY
         B     TBADDIT                   GO ADD TO TABLE
RETDEVLP DS    0H
         LA    R10,8(R10)                NEXT GENERIC ENTRY
         CLC   0(4,R10),FULLWORD         Q. POINTING TO PARMLIST (?)
         BNE   NAMEOK                       N. CONTINUE
         MVC   SPFGNAME,SPACES           INITIALISE GENERIC NAME
         MVC   SPFGNAME(4),=C'????'      MOVE GENERIC NAME
         B     TBADDIT                   GO ADD IT TO THE TABLE
NAMEOK   DS    0H
         MVC   SPFGNAME,0(R10)           MOVE GENERIC NAME
         TR    SPFGNAME,TRTABLE          TRANSLATE TO DISPLAYABLE
TBADDIT  DS    0H
         LA    R7,1(R7)                  BUMP RECORD COUNT (FOR SORT)
         STH   R7,SPFSORT                STORE SORT PARAMETER
         ISPEXEC 'TBADD DSGENRIC ORDER'
         MVC   SPFTYPE(4),=C' "  '       MOVE DEVICE CLASS DESCRIPTION
GETNEXTG DS    0H
         BCT   R8,RETDEVLP               LOOP TILL END
                                                                SPACE 1
         LM    R8,R14,SAVREGS1           RESTORE REGISTERS
         BR    R14
                                                                SPACE 1
*=====================================================================*
* Constants                                                           *
*---------------------------------------------------------------------*
         LTORG
SPACES   DC    CL256' '
DEVCL    DS    0CL63
         DC    X'80',CL8'TAPE'
         DC    X'40',CL8'COMM'
         DC    X'41',CL8'CTC '
         DC    X'20',CL8'DASD'
         DC    X'10',CL8'DISP'
         DC    X'08',CL8'UREC'
         DC    X'04',CL8'CHAR'
                                                                SPACE 1
TRTABLE  DC    16C'.'                       00-0F
         DC    16C'.'                       10-1F
         DC    16C'.'                       20-2F
         DC    16C'.'                       30-3F
         DC    C' .........¢.<(+|'          40-4F
         DC    C'&&.........!$*);¬'         50-5F
         DC    C'-/........¦,%_>?'          60-6F
         DC    C'..........:#@''="'         70-7F
         DC    C'.abcdefghi......'          80-8F
         DC    C'.jklmnopqr......'          90-9F
         DC    C'.~stuvwxyz......'          A0-AF
         DC    16C'.'                       B0-BF
         DC    C'{ABCDEFGHI......'          C0-CF
         DC    C'}JKLMNOPQR......'          D0-DF
         DC    C'\.STUVWXYZ......'          E0-EF
         DC    C'0123456789......'          F0-FF
*
                                                                SPACE 1
*=====================================================================*
* DSECTS                                                              *
*---------------------------------------------------------------------*
WORKAREA DSECT
         DS    18F                       REGISTER SAVE AREA
SAVREGS1 DS    16F                       REGISTER SAVE AREA
SAVREGS2 DS    16F                       REGISTER SAVE AREA
FULLWORD DS    F                         GENERAL PURPOSE WORK WORD
NAMELIST DS    F                         RETURNED NAME LIST ADDRESS
KEEPTB   DC    CL2'NN'                   KEEP TABLE INDICATOR
WINDOW   DS    CL1                       ADDPOP WINDOW INDICATOR
                                                                SPACE 1
*---------------------------------------------------------------------*
                                                                SPACE 1
SPFLINE  DS    0F                        FULL LINE IF OTHER OUTPUT
SPFTYPE  DC    CL4' '                       "
         DC    CL2' '
SPFGNAME DC    CL8' '                    VTOC INDEX INDICATOR
SPFSORT  DC    XL2'0000'                 SORT PARAMETER
SPFLENG  EQU   *-SPFLINE                 LINE LENGTH
SPFTYPEX DC    XL1'00'                   DEVICE CLASS
SPFIBMD  DC    CL1' '                    IBM GENERIC DEFAULT IDENT
                                                                SPACE 1
TRACE    DC    CL1'Y'                    TRACE FLAG (USED FOR 'PDUMP')
SPFZCMD  DC    CL79' '                   ISPF'S ZCMD
WORK2    DS    0CL79                     DOUBLE UP AS A WORK AREA
GC009    DC    CL79' '                   GENERIC ISPF MESSAGE AREA
                                                                SPACE 1
         ISPEXEC  MF=L,INIT=Y            INITIALISE ISPF ENVIRONMENT
         ISPVDEFX MF=L                   DEFINE A'BILITY TO ISPF VARS
                                                                SPACE 1
         EDTINFO MF=(L,EDT1)             EDTINFO
                                                                SPACE 1
*=====================================================================*
WORKAREL EQU   *-WORKAREA
                                                                SPACE 1
DEVLIST  DSECT ,                         LIST FOR EDTINFO RETURN
DEVLLEN  DS    F                         LENGTH OF GOT AREA
DEVL#    DS    F                         NUMBER OF DEVICES
DEVICES  DS    0F                        4 BYTE DEVICES X DEVL#
                                                                SPACE 1
         END
