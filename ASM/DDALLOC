*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*
**       TITLE:    'VERIFY ALLOCATION OF DDNAME'
**       SYSTEM:   N/A
**       MODULE:   DDALLOC
**       PROGRAM:  DDALLOC
**       FUNCTION: TAKES THE VARIABLE DDNAME FROM THE SHARED POOL
**                 AND ISSUES AN SVC 99 INTERROGATION ON IT.
**
**                 PARAMETERS:
**                    INPUT  - UP TO 8 CHARACTER DDNAME PASSED AS
**                             STANDARD PARAMETER (NOT COMMAND PROC)
**
**                    OUTPUT - RETURN CODE:
**                               0 - DD ALLOCATED
**                               4 - DD NOT ALLOCATED
**                              12 - MISSING PARAMETER
**
**                 PROGRAM IS RE-ENTRANT.
**
**       AUTHOR:   ASYSTS P/L (A. KARA).
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*        PRINT OFF
*        COPY  @MACROS                 COMMON MACROS
*        PRINT ON
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
DDALLOC  KICKOFF (R12),WORKAREA,WORKAREL,LIST=NO
         LR    R7,R1                   ADDRESS OF PARAMETER
         L     R7,0(R7)                LOAD ADDRESS OR PARAMETER
         LH    R9,0(R7)                LOAD LENGTH OF PARAMETER
         LTR   R9,R9                   Q. IS PARAMETER LENGTH ZERO
         BNZ   HAVEPARM                   N. PROCESS PARAMETER
         LA    R15,12                  SET SEVERE ERROR CODE
         B     EXITPROG                GOTO COMMON EXIT
HAVEPARM DS    0H
         LA    R7,2(R7)                POINT PAST PARAMETER LENGTH
         MVC   DDNAME(8),=CL8' '       CLEAR DD NAME
MOVEDD   MVC   DDNAME(0),0(R7)         MOVE PARAMETER
         BCTR  R9,0                    LESS1 FOR 'EX' COMMAND
         EX    R9,MOVEDD               MOVE DDNAME
*        PDUMP DDNAME,LIST=NO          UNCOMMENT FOR TRACE
                                                                SPACE 1
*---------------------------------------------------------------------*
* TEST FOR ALLOCATION                                                 *
*---------------------------------------------------------------------*
         LA    R10,SVC99WRK            ADDRESABILITY TO SVC99 WORK AREA
         ADYN  ACTION=INFO,DD=(DDNAME),DSN=DSNAME,BASE=R10,LIST=NO
                                                                SPACE 1
EXITPROG DS    0H
         RACKOFF RC=(R15),LIST=NO      EXIT PROGRAM
                                                                SPACE 1
*=====================================================================*
*    WORK AREAS AND CONSTANTS                                         *
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
WORKAREA DSECT
         DS    18F                     REGISTER SAVE AREA
DDNAME   DC    CL8' '                  DDNAME SUPPLIED
DSNAME   DC    CL44' '                 REQUIRED FOR ADYM MACRO SYNTAX
SVC99WRK DS    CL400                   SVC 99 PARAMETER LIST AREA
WORKAREL EQU   *-WORKAREA
         ADYN  DATA=IBM
         END
*=====================================================================*
