*
**       TITLE:    DATE CONVERSION ROUTINE
**       SYSTEM:   MISCELLANEOUS
**       MODULE:   DDD$DDMM
**       PROGRAM:  DDD$DDMM
**       FUNCTION: CONVERT JULIAN (DDD) DATE FORMAT TO DD/MM/YY
**                 INPUT PARAMETERS ARE PASSED USING VPUT:
**                    YY  - YEAR (USED FOR LEAP YEAR CALCULATIONS)
**                    DDD - JULIAN DATE
**                 OUTPUT PAREMATER OBTAINED VIA VGET:
**                    CDATE - DATE IN FORMAT DD/MM/YY
**                    DD    - DAY
**                    MM    - MONTH
**                 RETURN CODES :
**                    0 - ALL OK AND DATE AVAILABLE
**                    4 - INVALID DATE:
**                        HIGH DDD VALUE
**                        ZERO DDD VALUE
**                    8 - INVALID DATE FORMAT. (DATE NOT NUMERIC)
**       AUTHOR:   A. KARA
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
DDD$DDMM KICKOFF (R13)
         ISPINIT                  INITIALISE ISPF ENVIRONMENT
VDEFINE  EQU   *
         ISPVDEFX INM=(DDD),ANM=DDD,FORMAT=CHAR,LEN=3
         ISPVDEFX INM=(DD),ANM=DD,FORMAT=CHAR,LEN=2
         ISPVDEFX INM=(MM),ANM=MM,FORMAT=CHAR,LEN=2
         ISPVDEFX INM=(YY),ANM=YY,FORMAT=CHAR,LEN=2
         ISPVDEFX INM=(CDATE),ANM=CDATE,FORMAT=CHAR,LEN=8
VARGET   EQU   *
         ISPEXEC 'VGET (DDD YY) SHARED' GET VARIABLES
         PACK  YEAR,YY(2)     GET YEAR
         MVC   CDATE+6(2),YY      SET UP YEAR
         DP    YEAR,=P'4'         DIVIDE YEAR BY 4
         CP    YEAREM,=P'0'       IS REMAINDER 0
         BNE   NLEAPYR1              ...NO, NOT LEAP YEAR
         MVC   DATETAB+2(2),=X'029C' ...YES, FEB = 29 DAYS
         MVC   DDDLIMT,=C'366'    LEAP YEAR MAX DDD LIMIT
NLEAPYR1 EQU   *
         CLI   DDD+2,C' '         Q. ONLY ONE CHARACTER PASSED
         BNE   TESTDDD               N. BYPASS
         MVC   DDD+2(1),DDD+1     MOVE TO SECOND CHAR POSITION
         MVC   DDD+1(1),DDD       MOVE TO SECOND CHAR POSITION
         MVI   DDD,C'0'           MOVE 0 TO 1ST. CHAR POSITION
         CLI   DDD+2,C' '         Q. ONLY ONE CHARACTER PASSED
         BNE   TESTDDD               N. BYPASS
         MVC   DDD+2(1),DDD+1     MOVE TO SECOND CHAR POSITION
         MVC   DDD+1(1),DDD       MOVE TO SECOND CHAR POSITION
         MVI   DDD,C'0'           MOVE 0 TO 1ST. CHAR POSITION
TESTDDD  DS    0H
         CLI   DDD,C'0'           Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         CLI   DDD+1,C'0'         Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         CLI   DDD+2,C'0'         Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         CLC   DDD(3),=C'000'     Q. DATE ZERO
         BE    EXITPRG4              Y. EXIT WITH ERROR
         CLC   DDD(3),DDDLIMT     Q. DATE > LIMIT
         BH    EXITPRG4              Y. EXIT WITH ERROR
         LA    R9,1(R0)           SET MONTH COUNTER TO 1
         LA    R8,DATETAB         SET REG 8 AS MONTH POINTER
         PACK  DAYSREM(2),DDD(3)  PACK DAYS INTO DAYS REMAINING
LOOP1    EQU   *
         MVC   DAY(2),DAYSREM     SAVE DAYS REMAINING
         SP    DAYSREM(2),0(2,R8) SUBTRACT DAY IN MONTH FROM DAYS
         CP    DAYSREM(2),=P'0'   COMPARE IF NEGATIVE
         BL    EXLOOP1            ... YES, EXIT LOOP
         CP    DAYSREM(2),=P'0'   ... NO, COMPARE IF ZERO
         BZ    EXLOOP1                ... YES, EXIT LOOP
         LA    R8,2(R8)               ... NO, POINT TO NEXT MONTH
         LA    R9,1(R9)               ... NO, INCREMENT MONTH COUNTER
         B     LOOP1              RECALCULATE
EXLOOP1  EQU   *
         UNPK  DDD(3),DAY(2)      SAVE CURRENT DAYS
         OI    DD+1,X'F0'         CHANGE SIGN OF LAST DECIMAL BYTE
         MVC   CDATE(2),DD        MOVE 2 BYTES OF DAY TO OUT FIELD
         CVD   R9,DWORD8          CONVERT MONTH TO DECIMAL/PACKED
         UNPK  MMM(3),DWORD8+6(2) UNPACK MONTH
         OI    MM+1,X'F0'         CHANGE SIGN OF LAST BYTE OF MONTH
         MVC   CDATE+3(2),MM      MOVE MONTH TO OUT FIELD
VARPUT   EQU   *
         ISPEXEC 'VPUT (CDATE,DD,MM) SHARED'
EXITPRG0 RACKOFF RC=0
EXITPRG4 RACKOFF RC=4
EXITPRG8 RACKOFF RC=8
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
DWORD8   DC    D'0'
DAY      DC    PL2'0'
DAYSREM  DC    PL2'0'
RM       DC    CL1' '
YEAR     DS    0CL3
         DS    CL2
YEAREM   DS    CL1
MMM      DS    0CL3
         DS    CL1
MM       DS    CL2
DDD      DS    0CL3
         DS    CL1
DD       DS    CL2
YY       DS    CL2
CDATE    DS    0CL8
         DS    CL2
         DC    CL1'/'
         DS    CL2
         DC    CL1'/'
         DS    CL2
DDDLIMT  DC    CL3'365'           YEAR LIMIT
*---------------------------------------------------------------------*
DATETAB  DS    0CL24              DAYS OF MONTH IN YEAR TABLE
         DC    P'31'              J
         DC    P'28'              F
         DC    P'31'              M
         DC    P'30'              A
         DC    P'31'              M
         DC    P'30'              J
         DC    P'31'              J
         DC    P'31'              A
         DC    P'30'              S
         DC    P'31'              O
         DC    P'30'              N
         DC    P'31'              D
*---------------------------------------------------------------------*
         END
