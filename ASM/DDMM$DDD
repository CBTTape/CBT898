*
**       TITLE:    DATE CONVERSION ROUTINE
**       SYSTEM:   MISCELLANEOUS
**       MODULE:   DDMM$DDD
**       PROGRAM:  DDMM$DDD
**       FUNCTION: CONVERT DD/MM DATE FORMAT TO JULIAN (DDD)
**                 INPUT PARAMETERS ARE PASSED USING VPUT:
**                    DD - DAY
**                    MM - MONTH
**                    YY - YEAR (USED FOR LEAP YEAR CALCULATIONS)
**                 OUTPUT PAREMATER OBTAINED VIA VGET:
**                    DDD - JULIAN DATE
**                 RETURN CODES :
**                    0 - ALL OK AND JULIAN DATE AVAILABLE
**                    4 - INVALID DATE:
**                        29TH. OF FEB NOT PERMITTED. (NOT LEAP YEAR).
**                        HIGH MONTH VALUE
**                        ZERO DAY/MONTH VALUE
**                    8 - INVALID DATE FORMAT. (DATE NOT NUMERIC)
**       AUTHOR:   A. KARA
*
DDMM$DDD KICKOFF (R12,R13)
INIT     EQU   *
         ISPINIT                  INITIALISE THE ISPF ENV ADDRESSES
VDEFINE  EQU   *
         ISPVDEFX INM=(DD),ANM=DD,FORMAT=CHAR,LEN=2
         ISPVDEFX INM=(MM),ANM=MM,FORMAT=CHAR,LEN=2
         ISPVDEFX INM=(YY),ANM=YY,FORMAT=CHAR,LEN=2
         ISPVDEFX INM=(DDD),ANM=DDD,FORMAT=CHAR,LEN=3
VARGET   EQU   *
         ISPEXEC 'VGET (DD MM YY) SHARED'  GET VARIABLES
         CLI   YY+1,C' '          Q. ONLY ONE CHARACTER PASSED
         BNE   TESTYY                N. BYPASS
         MVC   YY+1(1),YY         MOVE TO SECOND CHAR POSITION
         MVI   YY,C'0'            MOVE 0 TO 1ST. CHAR POSITION
TESTYY   DS    0H
         CLI   YY,C'0'            Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         CLI   YY+1,C'0'          Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         PACK  YEAR,YY(2)         GET YEAR
         DP    YEAR,=P'4'         DIVIDE YEAR BY 4
         CP    YEAREM,=P'0'       Q. IS REMAINDER 0
         BNE   NLEAPYR1              N. NOT LEAP YEAR
         MVC   DATETAB+2(2),=X'029C' Y. FEB = 29 DAYS
NLEAPYR1 EQU   *
         CLI   DD+1,C' '          Q. ONLY ONE CHARACTER PASSED
         BNE   TESTDD                N. BYPASS
         MVC   DD+1(1),DD         MOVE TO SECOND CHAR POSITION
         MVI   DD,C'0'            MOVE 0 TO 1ST. CHAR POSITION
TESTDD   DS    0H
         CLI   DD,C'0'            Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         CLI   DD+1,C'0'          Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         CLC   DD(2),=C'00'       Q. DATE ZERO
         BE    EXITPRG4              Y. EXIT WITH ERROR
         CLI   MM+1,C' '          Q. ONLY ONE CHARACTER PASSED
         BNE   TESTMM                N. BYPASS
         MVC   MM+1(1),MM         MOVE TO SECOND CHAR POSITION
         MVI   MM,C'0'            MOVE 0 TO 1ST. CHAR POSITION
TESTMM   DS    0H
         CLI   MM,C'0'            Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         CLI   MM+1,C'0'          Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         CLC   MM(2),=C'00'       Q. DATE ZERO
         BE    EXITPRG4              Y. EXIT WITH ERROR
         CLC   MM(2),=C'12'       Q. DATE > 12
         BH    EXITPRG4              Y. EXIT WITH ERROR
VALDDATE DS    0H
         LA    R7,DATETAB         POINT TO START OF TABLE
         PACK  DWORD8(8),MM(2)    PACK MONTH
         CVB   R9,DWORD8          SET MONTH AS LOOP COUNTER
         BCT   R9,LOOP1           DONT WANT FULL VALUE OF CURRENT MONTH
         B     EXLOOP1            BYPASS FOR MONTH OF JAN
LOOP1    EQU   *
         AP    ACCUM(2),0(2,R7)   ADD DAY IN MONTH
         LA    R7,2(R7)           POINT TO NEXT MONTH
         BCT   R9,LOOP1           DONT WANT FULL VALUE OF CURRENT MONTH
EXLOOP1  EQU   *
         PACK  DWORD8(2),DD(2)    PACK DAYS
         NI    DWORD8+1,X'FC'     CHANGE SIGN OF LAST BYTE TO 'C'
         CLC   DWORD8(2),0(R7)    Q. DAYS NOT GT MONTH LIMIT
         BH    EXITPRG8              Y. EXIT WITH ERROR
         AP    ACCUM(2),DWORD8(2) ADD DAY OF CURRENT MONTH
         UNPK  DDD(3),ACCUM(2)    UNPACK DAYS
         OI    DDD+2,X'F0'        CHANGE SIGN OF LAST BYTE OF DDD
VARPUT   EQU   *
         ISPEXEC 'VPUT (DDD) SHARED'  PUT VARS
EXITPRG0 RACKOFF RC=0
EXITPRG4 RACKOFF RC=4
EXITPRG8 RACKOFF RC=8
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
TARGET   DS    CL20
DWORD8   DC    D'0'
YEAR     DS    0CL3
         DS    CL2
YEAREM   DS    CL1
DD       DC    CL2' '
MM       DC    CL2' '
YY       DC    CL2' '
DDD      DS    CL3
ACCUM    DC    PL2'0'
*---------------------------------------------------------------------*
DATETAB  DS    0CL24              DAYS OF MONTH IN YEAR TABLE
         DC    P'31'              J
         DC    P'28'              F
         DC    P'31'              M
         DC    P'30'              A
         DC    P'31'              M
         DC    P'30'              J
         DC    P'31'              J
         DC    P'31'              A
         DC    P'30'              S
         DC    P'31'              O
         DC    P'30'              N
         DC    P'31'              D
*---------------------------------------------------------------------*
         END
