*
**       TITLE:    DATE CONVERSION ROUTINE
**       SYSTEM:   MISCELLANEOUS
**       MODULE:   DDMM$DOW
**       PROGRAM:  DDMM$DOW
**       FUNCTION: FROM DD/MM/YY DATE FORMAT TELLS DAY OF WEEK
**                 INPUT PARAMETERS ARE PASSED USING VPUT:
**                    "DOWDATE" : FORMAT DD/MM/YY
**                                DEFAULT IS TODAY
**                 OUTPUT PAREMATER OBTAINED VIA VGET:
**                    "DOW"     : DAY-OF-WEEK EG. 'MONDAY'
**                    "DOW#"    : DAY-OF-WEEK NUMBER EG.
**                                MON - 1
**                                .......
**                                SUN - 7
**                 RETURN CODES :
**                    0 - ALL OK AND VARIABLES "DOW" AND "DOW#" PUT
**                        IN THE SHARED POOL.
**                    8 - INVALID DATE FORMAT. (DATE NOT NUMERIC)
**       AUTHOR:   A. KARA
*
*---------------------------------------------------------------------*
DDMM$DOW KICKOFF (R12)
INITLIZE DS    0H
         ISPINIT
         ISPVDEFX INM=(ZDATE),ANM=ZDATE,FORMAT=CHAR,LEN=8
         ISPVDEFX INM=(DOWDATE),ANM=DOWDATE,FORMAT=CHAR,LEN=8
         ISPVDEFX INM=(YY),ANM=YY,FORMAT=CHAR,LEN=2
         ISPVDEFX INM=(MM),ANM=MM,FORMAT=CHAR,LEN=2
         ISPVDEFX INM=(DD),ANM=DD,FORMAT=CHAR,LEN=2
         ISPVDEFX INM=(DDD),ANM=DDD,FORMAT=CHAR,LEN=3
         ISPVDEFX INM=(DOW#),ANM=DOW#,FORMAT=CHAR,LEN=1
         ISPVDEFX INM=(DOW),ANM=DOW,FORMAT=CHAR,LEN=9
         ISPEXEC 'VGET (DOWDATE) SHARED' GET YEAR FROM SHARED VARIABLE
         ISPEXEC 'VGET (ZDATE)'        GET DATE FROM POOL
         CLC   DOWDATE,SPACES          Q. DATE PASSED AS A PARAMETER
         BNE   GOTDATE                    Y. BYPASS CONVERT TODAY
         MVC   YY,ZDATE                MOVE YEAR
         MVC   MM,ZDATE+3              MOVE MONTH
         MVC   DD,ZDATE+6              MOVE DAY
GOTDATE  DS    0H
         CLI   YY+1,C' '          Q. ONLY ONE CHARACTER PASSED
         BNE   TESTYY                N. BYPASS
         MVC   YY+1(1),YY         MOVE TO SECOND CHAR POSITION
         MVI   YY,C'0'            MOVE 0 TO 1ST. CHAR POSITION
TESTYY   DS    0H
         CLI   YY,C'0'            Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         CLI   YY+1,C'0'          Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         PACK  YEAR,YY(2)         GET YEAR
         DP    YEAR,=P'4'         DIVIDE YEAR BY 4
         CP    YEAREM,=P'0'       Q. IS REMAINDER 0
         BNE   NLEAPYR1              N. NOT LEAP YEAR
         MVC   DATETAB+2(2),=X'029C' Y. FEB = 29 DAYS
NLEAPYR1 EQU   *
         CLI   DD+1,C' '          Q. ONLY ONE CHARACTER PASSED
         BNE   TESTDD                N. BYPASS
         MVC   DD+1(1),DD         MOVE TO SECOND CHAR POSITION
         MVI   DD,C'0'            MOVE 0 TO 1ST. CHAR POSITION
TESTDD   DS    0H
         CLI   DD,C'0'            Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         CLI   DD+1,C'0'          Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         CLC   DD(2),=C'00'       Q. DATE ZERO
         BE    EXITPRG8              Y. EXIT WITH ERROR
         CLI   MM+1,C' '          Q. ONLY ONE CHARACTER PASSED
         BNE   TESTMM                N. BYPASS
         MVC   MM+1(1),MM         MOVE TO SECOND CHAR POSITION
         MVI   MM,C'0'            MOVE 0 TO 1ST. CHAR POSITION
TESTMM   DS    0H
         CLI   MM,C'0'            Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         CLI   MM+1,C'0'          Q. LESS THAN ZERO
         BL    EXITPRG8              Y. EXIT WITH ERROR
         CLC   MM(2),=C'00'       Q. DATE ZERO
         BE    EXITPRG8              Y. EXIT WITH ERROR
         CLC   MM(2),=C'12'       Q. DATE > 12
         BH    EXITPRG8              Y. EXIT WITH ERROR
VALDDATE DS    0H
         LA    R7,DATETAB         POINT TO START OF TABLE
         PACK  DWORD8(8),MM(2)    PACK MONTH
         CVB   R9,DWORD8          SET MONTH AS LOOP COUNTER
         BCT   R9,LOOP1           DONT WANT FULL VALUE OF CURRENT MONTH
         B     EXLOOP1            BYPASS FOR MONTH OF JAN
LOOP1    EQU   *
         AP    ACCUM(2),0(2,R7)   ADD DAY IN MONTH
         LA    R7,2(R7)           POINT TO NEXT MONTH
         BCT   R9,LOOP1           DONT WANT FULL VALUE OF CURRENT MONTH
EXLOOP1  EQU   *
         PACK  DWORD8(2),DD(2)    PACK DAYS
         NI    DWORD8+1,X'FC'     CHANGE SIGN OF LAST BYTE TO 'C'
         CLC   DWORD8(2),0(R7)    Q. DAYS NOT GT MONTH LIMIT
         BH    EXITPRG8              Y. EXIT WITH ERROR
         AP    ACCUM(2),DWORD8(2) ADD DAY OF CURRENT MONTH
         UNPK  DDD(3),ACCUM(2)    UNPACK DAYS
         OI    DDD+2,X'F0'        CHANGE SIGN OF LAST BYTE OF DDD
         PACK  DWORD8(8),YY(2)         PACK YEAR
         CVB   R3,DWORD8               MOVE INTO ODD REG
         BCTR  R3,0                    DECREASE BY 1
         LR    R8,R3                   LOAD YEAR
         MH    R8,=H'365'              MULTIPLY BY DAYS OF YEAR
         ZAP   DWORD8(8),ACCUM(2)      MOVE INTO LARGER FIELD
         CVB   R1,DWORD8               CONVERT TO BINARY
         AR    R8,R1                   ADD DAYS OF THIS YEAR
         LA    R9,4                    LOAD DIVISOR
         XR    R2,R2                   INITIALISE EVEN OF PAIR
         DR    R2,R9                   DIVIDE TO GET # OF LEAPYEARS
         AR    R8,R3                   ADD EXTRA LEAP YEAR DAYS
SUBTRACT DS    0H
         S     R8,MODEL                SUBTRACT MODEL FROM IT
         LPR   R7,R8                   LOAD INTO ODD REGISTER
         LA    R9,7                    LOAD DIVISOR
         XR    R6,R6                   INITIALISE EVEN OF PAIR
         DR    R6,R9                   DIVIDE BY 7 TO GET DAYS OVERM
         C     R8,=F'0'                Q. LESS THAN ZERO
         BH    NOWPOSTV                   N. BYPASS
         MH    R6,=H'-1'               NEGATE REMAINDER
NOWPOSTV DS    0H
         AH    R6,MDOW#             THIS ALLOWS FOR BCT STATEMENT
         C     R6,=F'1'             Q. IS IT LESS THAN 1
         BNL   NOTBIGR                 N. BYPASS
         AH    R6,=H'7'             ADD 7
         B     GOTIT                CONTINUE
NOTBIGR  DS    0H
         C     R6,=F'7'             Q. IS IT LESS > 7
         BNH   GOTIT                   N. BYPASS
         SH    R6,=H'7'             SUBTRACT 7
GOTIT    DS    0H
         UNPACK R6,1,DOW#           UNPACK IT INTO DAY NUMBER
         MH    R6,=H'9'             MULTIPLY BY LENGTH OF TAB ENTRY
         LA    R3,TABLE-9           POINT TO RELATIVE START
         AR    R3,R6                DISPLACEMENT INTO TABLE
         MVC   DOW(9),0(R3)         MOVE DAY
         ISPEXEC 'VPUT (DOW# DOW) SHARED'  RETURN VARIABLE
EXITPRG0 RACKOFF RC=0
EXITPRG4 RACKOFF RC=4
EXITPRG8 RACKOFF RC=8
*=====================================================================*
DWORD8   DS    D
ZDATE    DS    CL8
DOWDATE  DS    0CL8
DD       DS    CL2
         DS    CL1
MM       DS    CL2
         DS    CL1
YY       DS    CL2
DDD      DS    CL3
DOW#     DS    CL1
DOW      DS    CL9
MDOW#    DC    H'1'
MODEL    DC    F'32627'          MODEL DATE 30/04/90
TABLE    DS    0CL63
         DC    CL9'MONDAY'
         DC    CL9'TUESDAY'
         DC    CL9'WEDNESDAY'
         DC    CL9'THURSDAY'
         DC    CL9'FRIDAY'
         DC    CL9'SATURDAY'
         DC    CL9'SUNDAY'
SPACES   DC    CL8' '
YEAR     DS    0CL3
         DS    CL2
YEAREM   DS    CL1
ACCUM    DC    PL2'0'
*---------------------------------------------------------------------*
DATETAB  DS    0CL24              DAYS OF MONTH IN YEAR TABLE
         DC    P'31'              J
         DC    P'28'              F
         DC    P'31'              M
         DC    P'30'              A
         DC    P'31'              M
         DC    P'30'              J
         DC    P'31'              J
         DC    P'31'              A
         DC    P'30'              S
         DC    P'31'              O
         DC    P'30'              N
         DC    P'31'              D
*---------------------------------------------------------------------*
         END
