*---------------------------------------------------------------------*
*  PROGRAM:  DUMPAUTO                                                 *
*  FUNCTION: ISSUE COMMAND 'CEMT S DUMP AUTO' FROM PLTPI              *
*  DATE:     11/09/95                                                 *
*  AUTHOR:   ASYSTS PTY. LTD. (A. KARA)                               *
*---------------------------------------------------------------------*
DFHEISTG DSECT                         DYNAMIC STORAGE
CEMTPARM DS    0F                      CEMT PARAMETER LIST ADDRESSES
CMDADRS  DS    F                       CEMT COMMAND'S ADDRESS
CMDLENG  DS    F                       CEMT COMMAND LENGTH'S ADDRESS
CALLTYPE DS    F                       CEMT CALL TYPE'S ADDRESS
RMESSG   DS    F                       CEMT RETURN MESSAGE'S ADDRESS
RMESSGL  DS    F                       CEMT RETURN MSG LENGTH'S ADDR
PARML    EQU   *-CEMTPARM              LENGTH OF GETMAIN
                                                                SPACE 1
TEXTV    DS    0CL80
CICSID1  DS    CL08
TEXTV_F1 DC    CL03' : '
MSGTEXT  DS    CL69
MESSAGE  DS    CL80                    RETURN MESSAGE
                                                                SPACE 1
WTODET1  DS    0F
         WTO   '                                                       *
                                        ',                             *
               ROUTCDE=(11),DESC=(7),                                  *
               MF=L
                                                                SPACE 1
                                                                SPACE 1
GETML    EQU   *-DFHEISTG              LENGTH OF GETMAIN
*
*---------------------------------------------------------------------*
*     INTERFACING WITH CICS                                           *
*---------------------------------------------------------------------*
CICSSHUT DFHEIENT CODEREG=(R10),                                       X
               DATAREG=R9,EIBREG=R11
                                                                SPACE 1
*---------------------------------------------------------------------*
*     INITIALISE FIELDS AND ERROR CONDITIONS                          *
*---------------------------------------------------------------------*
*        EXEC  CICS ASSIGN APPLID(CICSID1)
         L     R1,X'218'               LOAD CURRENT TCB
         L     R1,X'0C'(R1)            POINT TO TIOT
         MVC   CICSID1(8),0(R1)        MOVE CICS JOBNAME
                                                                SPACE 1
         MVC   TEXTV_F1(3),=C' : '
         MVI   MSGTEXT,C' '            RIPPLE CLEAR FIELD
         MVC   MSGTEXT+1(L'MSGTEXT-1),MSGTEXT
         MVC   MSGTEXT(36),=C'AUTOSWITCH ENABLED FOR DUMP DATASETS'
         MVC   WTODET1(WTODETL),WTODETF   MOVE CONSTANT TO DSECT
                                                                SPACE 1
*---------------------------------------------------------------------*
*     LOAD PARAMETER ADDRESSES                                        *
*---------------------------------------------------------------------*
*
         LA    R1,SDUMPAU              OUT MESSAGES ADDRESS
         ST    R1,CMDADRS              STOREC COMMAND'S ADDRESS
         LA    R1,LEN11                LENGTH OF OUTPUT ('P SHUT')
         ST    R1,CMDLENG              STORE LENGTH OF OUTPUT'S ADDRESS
         LA    R1,ATNOWH               NO DISPLAY INDICATOR
         ST    R1,CALLTYPE             STORE PHANTOM'S ADDRESS
         LA    R1,LEN80                LENGTH OF INPUT (FROM "CEMT")
         ST    R1,RMESSGL              STORE LENGTH OF INPUT'S ADDRESS
         LA    R1,MESSAGE              RETURN MESSAGE'S ACCEPT
         ST    R1,RMESSG               RETURN MESSAGE ACCEPT ADDRESS
*                                                               SPACE 1
         EXEC  CICS LINK    ,          EXECUTE 'CEMT' COMMAND          +
               PROGRAM('DFHEMTA')                                      +
               COMMAREA(CEMTPARM)
                                                                SPACE 1
         LA    R5,WTODET1              POINT TO WTO AREA FOR TRACE
         MVC   WTODET1+4(80),TEXTV     MOVE TO WTO AREA
         WTO   MF=(E,(R5))             WTO IT FOR TRACE
                                                                SPACE 1
*---------------------------------------------------------------------*
EXITPROG DS    0H
         EXEC  CICS RETURN
                                                                SPACE 1
*#####################################################################*
         LTORG
*---------------------------------------------------------------------*
SDUMPAU  DC    CL11'S DUMP AUTO'
LEN11    DC    H'11'                   LENGTH OF ABOVE COMMAND
LEN80    DC    H'80'                   LENGTH OF 80
ATTERM   DC    X'80'                   SEND MESSAGE TO TERMINAL(CONS)
ATNOWH   DC    X'00'                   OUTPUT IS NOT DISPALYED
                                                                SPACE 1
WTODETF  DS    0F
         WTO   '                                                       *
                                        ',                             *
               ROUTCDE=(11),DESC=(7),                                  *
               MF=L
WTODETL  EQU   *-WTODETF
                                                                SPACE 1
         YREGS
         END
