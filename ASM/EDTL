*---------------------------------------------------------------------*
EDTL     KICKOFF (R12)
*---------------------------------------------------------------------*
         LA    R10,DASD                  Load device we want
         EDTINFO RTNNAMD,                Get device names for DISK     +
               DEVCLASS=(R10),                                         +
               NAMELIST=(R10)
         L     R10,0(R10)                Load address of returned data
*
         XR    R2,R2                     Initialise Register
         ICM   R2,B'0111',1(R10)         Load length of got area
         ST    R2,VL                     Save length for REXXvar inter
         LR    R1,R2                     Load length into work reg
         BCTR  R1,0                      Prepare for 'EX' command
MOVELIST MVC   EDTLIST(0),0(R10)         Move data
         EX    R1,MOVELIST               Do it
         FREEMAIN R,LV=(R2),A=(R10)      Free area acquired by EDTINFO
*
         MVC   NAME,=CL7'EDTLIST'        Move variable name
         MVC   NL,=F'7'                  Move variable name's length
         LA    R1,EDTLIST                Load address of variable value
         ST    R1,VP                     Store variable pointer
         LINK  EP=IKJCT441,PARAM=(ECU,NP,NL,VP,VL,TK),VL=1
*
EXITPROG DS    0H
         RACKOFF RC=0,LIST=YES           Get out
*
*=====================================================================*
* Constants                                                           *
*---------------------------------------------------------------------*
         LTORG
DEVCL    DS    0CL63
         DC    X'80',CL8'TAPE'
         DC    X'40',CL8'COMM'
         DC    X'41',CL8'CTC '
DASD     EQU   *
         DC    X'20',CL8'DASD'
         DC    X'10',CL8'DISP'
         DC    X'08',CL8'UREC'
         DC    X'04',CL8'CHAR'
                                                                SPACE 1
*
EDTLIST  DC    CL256' '
*=====================================================================*
                                                                SPACE 1
NAME     DS    CL12                    Variable's name
NP       DC    A(NAME)                 Variable name pointer
NL       DS    F                       Name length
VL       DS    F                       Variable length
VP       DS    F                       Variable pointer
TK       DC    F'0'                    ??
ECR      DC    A(TSVERETR)             RETRIEVE function
ECU      DC    A(TSVEUPDT)             UPDATE function
*
*---------------------------------------------------------------------*
         IKJTSVT
         IHASDWA DSECT=YES             ENTRY : SDWA
         END
*---------------------------------------------------------------------*
