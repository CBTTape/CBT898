*
**       PROGRAM:  GAFOPCL1
**       FUNCTION: OPEN/CLOSE DDNAMES.
**       PARAMETERS:
**                 IN   : BYTE  1      OPERATION, (O)PEN (C)LOSE
**                 IN   : BYTES 7 - 8  DDNAME
**
**                 OUT  : BYTES 1      ASIS
**                        BYTE  7 - 8  ASIS
**                        BYTES 9 - 10 RETURN CODE
**                                    '00' OPERATION O.K.
**                                    '04' OPERATION FAILED
**                                    'IO' INVALID OPCODE
**                                    'ID' INVALID DDNAME
**
**                 REFER: CICS/OS/VS CUSTOMISATION GUIDE PAGE 410.
**       DATE:     19/10/89
**       AUTHOR:   A. KARA.
*
*=====================================================================*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
DATAREG1 EQU   8
R9       EQU   9
CODEREG1 EQU   9                   BASE REGISTER 1
R10      EQU   10
CODEREG2 EQU   10                  BASE REGISTER 2
R11      EQU   11
EIBREG1  EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*---------------------------------------------------------------------*
COMMAREA DSECT
OPER     DS    CL1                     OPERATION CODE
DDNAME   DS    CL8                     DDNAME NAME
RCODE    DS    CL2                     RETURN CODE
DFHEISTG DSECT
WSWORK1  DS    0D
PARMLIST DS    CL16                    PARAMETER LIST ADDRESSES
DUMPCODE DS    CL4                     DUMP CODE
GETML    EQU   *-WSWORK1               WORKING STORAGE LENGTH
*---------------------------------------------------------------------*
         PRINT OFF
         COPY  DFHCSADS                CSA DSECT
         COPY  DFHTCADS                TCA DSECT
         PRINT ON
*
*---------------------------------------------------------------------*
*     INTERFACING WITH CICS                                           *
*---------------------------------------------------------------------*
GAFOPCL1 DFHEIENT CODEREG=(CODEREG1,CODEREG2),                         X
               DATAREG=DATAREG1,EIBREG=EIBREG1
*
*---------------------------------------------------------------------*
*     GET CICS CSA ADDRESS                                            *
*---------------------------------------------------------------------*
         EXEC  CICS ADDRESS CSA(R13)
*
*---------------------------------------------------------------------*
*     HANDLE CONDITIONS                                               *
*---------------------------------------------------------------------*
HANDCOND DS    0H
         EXEC  CICS HANDLE CONDITION                                   X
            ERROR(DUMPIT)
         EXEC  CICS HANDLE ABEND LABEL(ABENDIT)
*
*---------------------------------------------------------------------*
*    GET PARAMETER DETAILS FROM CSA X'350' FOR 8 BYTES AND            *
*     TEST IF SHUT INVOKED FROM THE MONITOR (IE. CSA + X'350' FOR 8   *
*     SHOULD = "CICSXUT8" (CICSXUTE ZAPPED BY "8")                    *
*---------------------------------------------------------------------*
GETPARM  DS    0H
         L     R4,DFHEICAP             ADDRESS OF COMMAREA
         USING COMMAREA,R4             ADDRESABILITY TO COMMAREA
         MVC   PARMLIST(16),OPCLLIST   MOVE DDNAME TO COMMONAREA
         MVC   PARMLIST(8),DDNAME      MOVE DDNAME TO COMMONAREA
         MVC   RCODE(2),=C'00'         SET GOOD RETURN CODE
         BAL   R7,PARSEIPT             GO AND PARSE INPUT PARMS
         CLC   RCODE(2),=C'00'         Q. ZERO RETURN CODE
         BNE   EXITPROG                   N. PREMATURE RETURN
         CLI   OPER,C'O'               Q. OPEN ONLY
         BE    OPENFILE                   Y. OPEN ONLY
*---------------------------------------------------------------------*
*     CLOSE FILE                                                      *
*---------------------------------------------------------------------*
CLOSFILE DS    0H
         MVC   DUMPCODE(4),=C'CLE1'    SET DUMP CODE IN CASE
         DFHOC TYPE=CLOSE,DATASET=DATABASE,CHECK=CLOSERR,              X
               SYMBADR=PARMLIST
         CLI   TCAOCTR,X'00'           Q. ANY CLOSE ERRORS
         BNE   CLOSERR                    Y. GO TO ERROR ROUTINE
         B     EXITPROG                GO AND RETURN TO CALLER
*---------------------------------------------------------------------*
*     OPEN FILE.                                                      *
*---------------------------------------------------------------------*
OPENFILE DS    0H
         MVC   DUMPCODE(4),=C'OPE1'    SET DUMP CODE IN CASE
         DFHOC TYPE=OPEN,DATASET=DATABASE,CHECK=OPENERR,               X
               SYMBADR=PARMLIST
         CLI   TCAOCTR,X'00'            Q. ANY OPEN ERRORS
         BNE   OPENERR                     Y. GO TO ERROR ROUTINE
         B     EXITPROG                 GO AND RETURN TO CALLER
*---------------------------------------------------------------------*
PARSEIPT DS    0H
         CLI   OPER,C'O'                 Q. VALID OPERATION
         BE    TESTDD                       Y. CONTINUE
         CLI   OPER,C'C'                 Q. VALID OPERATION
         BE    TESTDD                       Y. CONTINUE
         MVC   RCODE(2),=C'IO'           SET BAD RETURN CODE
         B     PARSEXIT                  GO TO COMMON RETURN POINT
TESTDD   DS    0H
         CLC   DDNAME(8),=CL8' '         Q. DDNAME SUPPLIED
         BE    PARSER16                     N. SET ERROR CODE
         CLI   DDNAME,C'0'               Q. FIRST CHAR NUMERIC
         BNL   PARSER16                     Y. SET ERROR CODE
         LA    R3,8                      LOOP LIMIT
         LA    R1,DDNAME+7               LAST BYTE OF DDNAME
DDNLOOP1 DS    0H
         CLI   0(R1),C' '                Q. IS CHARACTER SPACE
         BNE   DDNLOP1E                      N. EXIT LOOP
         BCTR  R1,0                      POINT TO PREVIOUS CHARACTER
         BCT   R3,DDNLOOP1               LOOP TILL END
DDNLOP1E DS    0H
*        TRT   DDNAME(8),TRTDSNM         ALL VALID CHARACTERS
         BCTR  R3,0                      RELATIVE LENGTH
         EX    R3,TRTDDNAM               ALL VALID CHARACTERS
         BNZ   PARSER16                     N. SET ERROR CODE
         B     PARSEXIT                  GO TO COMMON RETURN POINT
PARSER16 DS    0H
         MVC   RCODE(2),=C'ID'           SET BAD RETURN CODE
PARSEXIT DS    0H
         BR    R7                        RETURN FROM CALLED ROUTINE
*---------------------------------------------------------------------*
TRTDDNAM TRT   DDNAME(0),TRTDSNM         ALL VALID CHARACTERS
*---------------------------------------------------------------------*
OPENERR  DS    0H
CLOSERR  DS    0H
         MVC   RCODE(2),=C'04'           SET ERROR RETURN CODE
         B     EXITPROG                  RETURN TO CALLER
*---------------------------------------------------------------------*
DUMPIT   DS    0H
ABENDIT  DS    0H
         EXEC  CICS ABEND ABCODE(DUMPCODE)
*---------------------------------------------------------------------*
EXITPROG DS    0H
         DROP  R4                        FINISHED ADDRESABILITY
         EXEC  CICS RETURN
*=====================================================================*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
OPCLLIST DS    0D                                              @BB6A05K
         DC    CL8'        '       DDNAME ID                   @BD6A05K
OPLSTDCT DC    F'0'                ADDRESS OF DCT AFTER OP     @BD6A05K
         DC    4X'FF'              END OF LIST-INDICATOR       @BD6A05K
*---------------------------------------------------------------------*
TRTDSNM  DC    16X'40'                                00-0F
         DC    16X'40'                                10-1F
         DC    16X'40'                                20-2F
         DC    16X'40'                                30-3F
         DC    X'40404040404040404040400040404040'    40-4F
         DC    X'40404040404040404040400040404040'    50-5F
         DC    16X'40'                                60-6F
         DC    X'40404040404040404040400000404040'    70-7F
         DC    16X'40'                                80-8F
         DC    16X'40'                                90-9F
         DC    16X'40'                                A0-AF
         DC    16X'40'                                B0-BF
         DC    X'40000000000000000000404040404040'    C0-CF
         DC    X'40000000000000000000404040404040'    D0-DF
         DC    X'40400000000000000000404040404040'    E0-EF
         DC    X'00000000000000000000404040404040'    F0-FF
*---------------------------------------------------------------------*
         END
