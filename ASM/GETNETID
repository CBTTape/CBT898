***********************************************************************
*                                                                     *
*    Title:    PLACE TERMID IN ISPF SHARESD POOL                      *
*    System:   N/A                                                    *
*    Module:   GETNETID                                               *
*    Program:  GETNETID                                               *
*    Function: The purpose of this program is return the sessions     *
*              termid in the variable TERMID in the ISPF shared pool. *
*                                                                     *
*              The program needs to be in supervisor state to access  *
*              the appropriate control block.                         *
*              To permit ISPF interface, the program cannot be        *
*              authoriased, consequently it invokes SVC 247 (IGC0024G)*
*              to acqire authority as required and the relinqueshes   *
*              it.                                                    *
*                                                                     *
*              Program return code:                                   *
*                 0  - Always set to zero.                            *
*                                                                     *
*              Reserved register:                                     *
*                R12 - Base                                           *
*                R13 - Register save area                             *
*                                                                     *
*    Macros:   The following IBM macros have been used                *
*                - CVT                                                *
*                - IHAASCB                                            *
*                - IKJTSB                                             *
*                                                                     *
*    Notes:    Program is not re-entrant                              *
*              AMODE 31, RMODE any.                                   *
*    Author:   ASYSTS Pty. Ltd. (Alex Kara)                           *
*    Date:     8th September 1993                                     *
*                                                                     *
***********************************************************************
*        PRINT OFF
*        COPY  @MACROS
*        PRINT ON
GETNETID KICKOFF (R12),AMODE=31
         ISPINIT                       INITIALISE THE ISPF ENVIRONMENT
         ISPVDEFX LIST=NO,FORMAT=CHAR,                                 +
               INM=(TERMID),ANM=TERMID,LEN=8
ADDRCVT  L     R2,CVTPTR               POINT  TO   CVT
         USING CVTMAP,R2               ADDRESS CVT
         L     R2,CVTTCBP              POINT TO TCBWORDS
         DROP  R2
ADDRASCB L     R3,12(R2)               POINT TO MY ASCB
         USING ASCB,R3                 ADDRESS    ASCB
ADDRTSB  L     R2,ASCBTSB              POINT TO MY TSB
         USING TSB,R2                  ADDRESS MY TSB
         LA    R0,1                    AUTHORIZE MY TSO SESSION
         SVC   247                     CALL AUTHORIZATION SVC
         MODESET KEY=ZERO,MODE=PROB    GET INTO KEY 0
         MVC   TERMID,TSBTRMID         GET MY NETID (CSA PROTEC KEY 6)
         MODESET KEY=NZERO,MODE=PROB   GET BACK INTO MY KEY
         LA    R0,2                    RESET AUTHORIZATION
         SVC   247                     CALL AUTHORIZATION SVC
         ISPEXEC 'VPUT TERMID SHARED'
THEEND   RACKOFF RC=0                  RETURN TO CALLER
*---------------------------------------------------------------------*
         LTORG
TERMID   DS    CL8
*---------------------------------------------------------------------*
*        REQUIRED  DSECTS   (FOUND   IN SYSI.MACLIB)
*---------------------------------------------------------------------*
         CVT      DSECT=YES,LIST=NO    CVT
         IHAASCB DSECT=YES             ASCB
         IKJTSB                        TSB
         END
*---------------------------------------------------------------------*
