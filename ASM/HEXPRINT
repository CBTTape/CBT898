*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*
**       TITLE:    PRINT HEX VALUES OF DATA
**       SYSTEM:   N/A
**       MODULE:   HEXPRINT
**       PROGRAM:  HEXPRINT
**       FUNCTION: PRINT HEX FORMAT OF A MEMBER BEING EDITED
**                 MAXIMUM LIMIT IS 132 BYTES
**       PARAMETERS:
**                 INPUT:
**                      1 - ISPF VARIABLE 'LINE#' INDICATING LINE
**                          NUMBERS. IGNORED IF 125 > LRECL
**                 OUTPUT - DATASET DD(OUTFILE)
**                        - VARIABLE 'MESSAGE' CONTAINS # OF RECORDS
**                          COPIED. OBTAINED VIA:
**                              ISPEXEC VGET (MESSAGE) SHARED
**       AUTHOR:   A. KARA.
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
HEXPRINT KICKOFF (R12,R13),LIST=YES
START    EQU   *
         ISPINIT                       INITIALISE ISPF INTERFACE
         ISPVDEFX INM=(MESSAGE),ANM=MESSAGE,FORMAT=CHAR,LEN=79
         ISPVDEFX INM=(LINE#),ANM=LINE#,FORMAT=CHAR,LEN=1
         ISPVDEFX INM=(CLASS),ANM=CLASS,FORMAT=CHAR,LEN=1
         ISPEXEC 'VGET (CLASS LINE#) SHARED'
         OI    LINE#,C' '              CONVERT TO UPPER CASE
         OPEN  (INFILE,(INPUT))        OPEN INPUT FILE
         LH    R2,INFILE+X'52'         LOAD LRECL
         CH    R2,=H'248'              Q. LENGTH > 248
         BNH   CONT#000                   N. CONTINUE
         CLI   LINE#,C'Y'              Q. LINE NUMBER ON
         BNE   CONT#000                   N. CONTINUE
         MVC   MESSAGE(51),=C'LINE NUMBERING TURNED OFF BECAUSE OF RECOR
               RD LENGTH.'
         MVI   LINE#,C'N'              TURN LINE NUMBER OFF
CONT#000 DS    0H
         CH    R2,=H'256'              Q. LENGTH > 248
         BNH   CONT#100                   N. CONTINUE
         LA    R2,256                  LOAD NEW PRINT LENGT
         CLC   MESSAGE,SPACES          Q. MESSAGE ALREADY SET
         BE    CONT#050                   N. CONTINUE
         MVC   MESSAGE(49),=C'PRINT TRUNCATED TO 256 CHARACTERS.'
         MVC   MESSAGE+48(23),=C' AND LINE # TURNED OFF.'
         B     CONT#100                CONTINUE
CONT#050 DS    0H
         MVC   MESSAGE(49),=C'PRINT TRUNCATED TO 256 CHARACTERS.'
CONT#100 DS    0H
         STH   R2,RECLEN               STORE RECORD LENGTH
         OPEN  (OUTFILE,(OUTPUT))      OPEN OUTPUT FILE
         XR    R9,R9                   INITIALISE LOOP COUNTER
READLOOP EQU   *
         GET   INFILE,WORKAREA         READ INFILE INTO WORKAREA
*---------------------------------------------------------------------*
         MVC   PRLINE,SPACES           CLEAR PRINT LINE
         XR    R14,R14                 INITIALISE EVEN PAIR
         LR    R15,R9                  LOAD NUMBER OF RECORDS PROCESSED
         D     R14,=F'14'              DIVIDE BY 60 (LINE LIMIT)
         LTR   R14,R14                 Q. ANY REMAINDER
         BNZ   CONT#300                   N. BYPASS
         MVI   PRLINE,C'1'             SET PAGE SKIP INDICATOR
         MVC   PRLINE+125(4),=C'PAGE'  MOVE PAGE HEADING
         LA    R15,1(R15)              UP PAGE BY 1
         CVD   R15,DWORK8              STORE PAGE NUMBER IN PACKED
         MVC   PRLINE+129(4),PAT1      MOVE PATTERN TO PRLINE
         ED    PRLINE+129(4),DWORK8+6  EDIT OVER PATTERN TO PRLINE
         PUT   OUTFILE,PRLINE          WRITE OUTPUT RECORD
         MVC   PRLINE,SPACES           CLEAR PRINT LINE
         CLI   LINE#,C'Y'              Q. LINE NUMBER ON
         BNE   CONT#200                   N. BYPASS
         MVC   PRLINE,COL#             MOVE COL NUMBERING TO PRLINE
         B     CONT#210                BYPASS
CONT#200 DS    0H
         MVC   PRLINE,COL#+7           MOVE COL NUMBERING TO PRLINE
CONT#210 DS    0H
         PUT   OUTFILE,PRLINE          WRITE OUTPUT RECORD
         MVC   PRLINE,SPACES           CLEAR PRINT LINE
CONT#300 DS    0H
         LA    R9,1(R9)                UP RECORD COUNTER BY 1
         LH    R2,RECLEN               LOAD LENGTH OF DATA
         HEXCHMAC WORKAREA,WORKAREA+200,(R2) CONVERT HEX TO CHARACTER
         LA    R7,WORKAREA+200         SOURCE ADDRESS
         LA    R8,WORKAREA+800         DESTINATION ADDRESS
HEXMLOP1 DS    0H
         MVC   0(1,R8),0(R7)           MOVE 1ST NIBBLE TO LINE1
         MVC   1000(1,R8),1(R7)        MOVE 2ND NIBBLE TO LINE2
         LA    R8,1(R8)                NEXT CHAR POSITION LINE1
         LA    R7,2(R7)                NEXT CHAR POSITION SOURCE
         BCT   R2,HEXMLOP1             LOOP TILL END
         LH    R2,RECLEN               LOAD LENGTH OF DATA
         BCTR  R2,0                    RELATIVE LENGTH
         LA    R8,PRLINE+1             DESTINATION
         CLI   LINE#,C'Y'              Q. LINE NUMBER ON
         BNE   CONT#400                   N. BYPASS
         UNPACK R9,6,PRLINE+1          UNPACK SEQUENCE NUMBER
         LA    R8,7(R8)                POINT PAST SEQUENCE NUMBER
CONT#400 DS    0H
         EX    R2,MOVEDAT1             MOVE LINE OF DATA
         EX    R2,TRANDAT1             TRANSLATE DATA
         PUT   OUTFILE,PRLINE          WRITE OUTPUT RECORD
         MVC   PRLINE(8),SPACES        CLEAR SEQ NO IF THERE
         EX    R2,MOVEHEX1             MOVE HEX LINE 1
         PUT   OUTFILE,PRLINE          WRITE OUTPUT RECORD
         EX    R2,MOVEHEX2             MOVE HEX LINE 1
         PUT   OUTFILE,PRLINE          WRITE OUTPUT RECORD
         CLI   LINE#,C'Y'              Q. LINE NUMBER ON
         BNE   CONT#500                   N. BYPASS
         MVC   PRLINE,COL#             MOVE COL NUMBERING TO PRLINE
         B     CONT#510                BYPASS
CONT#500 DS    0H
         MVC   PRLINE,COL#+7           MOVE COL NUMBERING TO PRLINE
CONT#510 DS    0H
         PUT   OUTFILE,PRLINE          WRITE OUTPUT RECORD
*        MVI   PRLINE,C'+'             MOVE OVERPRINT INDICATOR
*        PUT   OUTFILE,PRLINE          WRITE OUTPUT RECORD
         B     READLOOP                GET NEXT RECORD
*---------------------------------------------------------------------*
MOVEDAT1 MVC   0(0,R8),WORKAREA        MOVE DATA TO PRINTLIN
TRANDAT1 TR    0(0,R8),TRTABLE         CONVERT TO PRINTABLE CHARACTER
MOVEHEX1 MVC   0(0,R8),WORKAREA+800    MOVE HEX LINE 1
MOVEHEX2 MVC   0(0,R8),WORKAREA+1800   MOVE HEX LINE 2
*---------------------------------------------------------------------*
INFEND   DS    0H
         CLOSE OUTFILE              CLOSE OUTPUT DISK FILE
         CLC   MESSAGE,SPACES       Q. MESSAGE ALREADY SET
         BNE   CLOSINPT                Y. BYPASS MESSAGE
         UNPACK R9,6,MESSAGE        CONVERT TO DISPLAY FORMAT
         MVC   MESSAGE+7(33),=C'RECORDS PROCESSED TO OUTPUT CLASS'
         MVC   MESSAGE+41(1),CLASS
         MVI   MESSAGE+42,C'.'
CLOSINPT DS    0H
         CLOSE INFILE               CLOSE INPUT FILE
EXITPROG DS    0H
         ISPEXEC 'VPUT (MESSAGE) SHARED'
         RACKOFF RC=0               EXIT PROGRAM
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*---------------------------------------------------------------------*
         LTORG                      LOAD LITERAL POOL HERE
*---------------------------------------------------------------------*
DWORK8   DS    D
LINE#    DS    CL1
CLASS    DS    CL1
COL#     DC    C'        ----+----1----+----2----+----3----+----4----+--
               ---5----+----6----+----7----+----8----+----9----+----0---
               --+----1----+----2----+        '
PAT1     DC    X'40202120'
SPACES   DC    CL256' '
PRLINE   DC    CL133' '
MESSAGE  DC    CL79' '
RECLEN   DS    H
*---------------------------------------------------------------------*
TRTABLE  DC    16C'.'                       00-0F
         DC    16C'.'                       10-1F
         DC    16C'.'                       20-2F
         DC    16C'.'                       30-3F
         DC    C' .........¢.<(+|'          40-4F
         DC    C'&&.........!$*);¬'         50-5F
         DC    C'-/........¦,%_>?'          60-6F
         DC    C'..........:#@''="'         70-7F
         DC    C'.abcdefghi......'          80-8F
         DC    C'.jklmnopqr......'          90-9F
         DC    C'.~stuvwxyz......'          A0-AF
         DC    16C'.'                       B0-BF
         DC    C'{ABCDEFGHI......'          C0-CF
         DC    C'}JKLMNOPQR......'          D0-DF
         DC    C'\.STUVWXYZ......'          E0-EF
         DC    C'0123456789......'          F0-FF
*---------------------------------------------------------------------*
INFILE   DCB   DDNAME=INFILE,                                          X
               DSORG=PS,                                               X
               MACRF=(GM),                                             X
               EODAD=INFEND
OUTFILE  DCB   DDNAME=OUTFILE,                                         X
               DSORG=PS,                                               X
               MACRF=(PM)
*             LRECL=133,                                              X
*             BLKSIZE=1330,                                           X
*             RECFM=FBA,                                              X
WORKAREA DS    CL32760
         END
