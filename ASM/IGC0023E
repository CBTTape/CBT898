***********************************************************************
*                                                                     *
*  Title   : Authorisation SVC for restricted functions.              *
*  System  : N/A.                                                     *
*  Module  : IGC0023E                                                 *
*  Program : IGC0023E                                                 *
*  Function: SVC is invoked with request parameter loaded in reg 0,   *
*              ie.  R0 = 1 requests authorization on                  *
*                   R0 = 0 requests authorization off                 *
*            these are the only two values acceptable - anything else *
*            will cause immediate return.                             *
*            R1 must be set to auhorisation code 'AUTH'.              *
*            On exit, R15 will be set to 0 if all OK otherwise it     *
*            will be set to 8.                                        *
*            Prior to authorisation, a RACHECK is made to the         *
*            General Resource Profile class 'FACILITY' for READ       *
*            access to profile 'SVC.SVC235.AUTH'. If the user is not  *
*            authorised for the resource, authorisation change is     *
*            denied and return code of 8 is returned.                 *
*                                                                     *
*  Company : ASYSTS Pty. Ltd.                                         *
*  Author  : A. Kara                                                  *
*  Date    : 26/09/94                                                 *
*                                                                     *
***********************************************************************
                                                                SPACE 1
IGC0023E CSECT AUTHORIZATION           ON/OFF SVC
         BALR  R12,0                   SET UP BASE
         USING *,R12                   REGISTER
         C     R1,CODE                 Q. WAS PROPER CODEWORD PASSED?
         BNE   RETURN8                    N. UNAUTHORIZED, EXIT
         C     R0,=F'0'                Q. VALID PARAMETER PASSED
         BL    RETURN8                    N. ERROR EXIT
         C     R0,=F'1'                Q. VALID PARAMETER PASSED
         BH    RETURN8                    N. ERROR EXIT
*---------------------------------------------------------------------*
         STM   R14,R0,X'64'(R5)        SAVE REGISTER B/C OF RACHECK
         LA    R8,FACILITY             POINT TO FACILITY PROF VALUE
         RACHECK ENTITY=((R8)),                                        X
               CLASS='FACILITY',                                       X
               ATTR=READ
                                                                SPACE 1
         L     R14,X'64'(R5)           RESTORE RETURN REGISTER
         LTR   R15,R15                 Q. ZERO RETURN CODE FROM RACF
         BNZ   RETURN8                    N. UNAUTHORIZED, EXIT
*---------------------------------------------------------------------*
         L     R0,X'64'+8(R5)          RESTORE REG 0
         L     R2,180(R4)              ADDR OF JSCB TO R2
         BCT   R0,AUTHOFF              R0 Â¬= 1 REQUESTS AUTH OFF
AUTHON   OI    236(R2),AUTHD           SET JSCB BIT
         B     RETURN                  EXIT WITH AUTHORIZATION ON
AUTHOFF  NI    236(R2),255-AUTHD       CLEAR JSCBAUTH BIT
RETURN   XR    R15,R15                 ZERO RETURN CODE
         BR    R14
RETURN8  LA    R15,8                   ERROR RETURN
         BR    R14
                                                                SPACE 1
         LTORG ,                       LITERALS
CODE     DC    C'AUTH'                 AUTORISATION CODE VALUE
FACILITY DC    C'SVC.SVC235.AUTH '     FACILITY PROFILE VALUE
AUTHD    EQU   X'01'                   AUTHORISED FLAG
                                                                SPACE 1
         YREGS
         END   IGC0023E
