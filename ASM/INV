***********************************************************************
*                                                                     *
*    Title:    'TOGGLE EXCLUDE/NON-EXCLUDE STATUS OF LINES IN EDIT'   *
*    System:   N/A                                                    *
*    Module:   INV                                                    *
*    Program:  INV                                                    *
*    Function: The purpose of this program is to toggle the the       *
*              exclude/non-exclude staus of lines during an edit      *
*              session.                                               *
*                                                                     *
*              Routine written in assembles as it's REXX/CLIST        *
*              counterpart can be quite timely for large edit         *
*              dataset(member).                                       *
*                                                                     *
*    Notes:    Program is re-entrant                                  *
*              AMODE 31, RMODE any.                                   *
*    Author:   ASYSTS Pty. Ltd. (A. Kara)                             *
*                                                                     *
***********************************************************************
INV      KICKOFF (R12),WORKAREA,WORKAREL
*---------------------------------------------------------------------*
*   Initialise environment                                            *
*---------------------------------------------------------------------*
         ISPINIT MF=E                  INIT ISPF INTERFACE
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(STATUS),ANM=STATUS,LEN=4096
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(LINE#),ANM=LINE#,LEN=6
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(XSTAT),ANM=XSTAT,LEN=2
                                                                SPACE 1
         ISPEXEC 'ISREDIT MACRO'       IDENTYFY AS MACRO
         ISPEXEC 'ISREDIT (STATUS) = USER_STATE'
         ISPEXEC 'ISREDIT (LINE#) = LINENUM .ZLAST'
         PACK  DWORK8(8),LINE#(6)      PACK LAST LINE NUMBER
         CVB   R9,DWORK8                  AND USE AS LOOP COUNTER
         LA    R8,1                    INITIALISE LINE NUMBER POINTER
LOOP1    DS    0H
         UNPACK R8,6,LINE#,MF=E        CONVERT TO CHARACTER
         ISPEXEC 'ISREDIT (XSTAT) = XSTATUS &&LINE#',TL=32
         CLI   XSTAT,C'X'              Q. LINE EXCLUDED
         BNE   EXCLUDE                    N. GO EXCLUDE IT
         ISPEXEC 'ISREDIT XSTATUS  &&LINE# = NX',TL=28
         B     NEXTLINE                GO BUMP POINTER
EXCLUDE  DS    0H
         ISPEXEC 'ISREDIT XSTATUS  &&LINE# = X',TL=27
NEXTLINE DS    0H
         LA    R8,1(R8)                BUMP LINE NUMBER
         BCT   R9,LOOP1                LOOP TILL END
                                                                SPACE 1
EXITPR00 DS    0H
         RACKOFF RC=00                 EXIT PROGRAM WITH 12 RETURN CODE
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
         LTORG
*---------------------------------------------------------------------* ---*
WORKAREA DSECT
         DS    18F                     REGISTER SAVE AREA ETC...
DWORK8   DS    D                       DOUBLE WORD WORK FIELD
         UNPACK   MF=L                 LIST FORMAT OF UNPACK
         ISPINIT  MF=L                 LIST FORMAT OF ISPINIT
         ISPVDEFX MF=L                 LIST FORMAT OF ISPVDEFX
XSTAT    DS    CL2' '                  EXCLUDE STATUS
LINE#    DC    CL6' '                  LINE NUMBER
STATUS   DS    CL4096                  REQUIRES 1380 BYTES
WORKAREL EQU   *-WORKAREA
         END
