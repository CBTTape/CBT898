***********************************************************************
*                                                                     *
*  Title:  : 'ISPF PANEL EXIT TO CHANGE ISPF 3.4 "S" option.          *
*  System  : N/A                                                      *
*  Module  : PANELI34                                                 *
*  Program : PANELI34                                                 *
*  Function: The purpose of this ISPF panel exit is to provide a      *
*            facility to change the 'S' option under ISPF 3.4 (panel  *
*            ISRUDSL0) to any user defined function of up to 8        *
*            bytes.  This is affected by entering 'S=function' on     *
*            the command line.                                        *
*  Parms   : ISPF variable 'ZDATA'.                                   *
*  Notes   : Program is re-entrant.                                   *
*                                                                     *
*  Company : ASYSTS Pty. Ltd.                                         *
*  Author  : A. Kara                                                  *
*  Date    : 16/09/94                                                 *
*                                                                     *
***********************************************************************
*        PRINT OFF
*        COPY  @MACROS                 COMMON MACROS
*        PRINT ON
***********************************************************************
                                                                SPACE 1
ISPFSYID KICKOFF (R12),WORKAREA,WORKAREL,LIST=N,AMODE=31
         LR    R7,R1                   SAVE PARAMETR LIST POINTER
         USING ISPFPARM,R7             ADDRESABILITY TO PARAMETER LIST
         L     R2,ARRAYDIM             POINT TO NO OF VARIABLE COUNTER
         L     R2,0(R2)                LOAD NO OF VARIABLE COUNTER
*        L     R3,VARNAME              POINT TO 1ST. VARIABLE NAME
         L     R5,VARVAL               POINT TO 1ST. VARIABLES VALUE
         LR    R6,R5                   POINT TO 1ST. VARIABLES VALUE
         L     R4,VARLEN               POINT TO 1ST. VARIABLES LENGTH
         A     R6,0(R4)                ADD 1ST. VARIABLES LENGTH
         L     R10,4(R4)               LOAD 2ND. VARIABLES LENGTH
         BCTR  R10,0                   PREPARE FOR 'EX' COMMAND
         CH    R10,=H'8'               Q. SUB COMMAND > 8 BYTES
         BL    LENGTHOK                   Y. CONTINUE
         LA    R10,7                   TRUNCATE
LENGTHOK DS    0H
         LA    R5,X'A0'(R5)            POINT TO 1ST. INPUT FIELD
LOPX05   DS    0H
         CR    R5,R6                   Q. PROCESSED ALL DATA
         BNL   EXITPR00                   Y. EXIT PROGRAM
         CLI   0(R5),X'11'             Q. CHANGED BY ISPF 4.1 WHEN
*                                         ERROR DETECTED
         BNE   FIXEDX11                   N. BYPASS
         MVI   0(R5),X'05'             RESTORE TO INPUT FIELD
         MVC   1(8,R5),=CL8' '         MOVE SPACES
FIXEDX11 DS    0H
         CLI   0(R5),X'05'             Q. INPUT FIELD
         BNE   EXITPR00                   N. EXIT PROGRAM
         LA    R7,1(R5)                1ST BYTE OF INPUT FIELD
         LA    R9,8                    LOOP COUNTER
CMDLOOP  DS    0H
         CLI   0(R7),C'S'              Q. 1ST CHAR SELECT
         BE    TESTSSEL                   Y. TEST REST OF CHARACTERS
         CLI   0(R7),C's'              Q. 1ST CHAR SELECT
         BE    TESTSSEL                   Y. TEST REST OF CHARACTERS
         CLI   0(R7),C' '              Q. CHAR LEADING SPACE
         BNE   NEXTLINE                   N. PROCESS NEXT LINE
         LA    R7,1(R7)                POINT TO NEXT CHAR
         BCT   R9,CMDLOOP              LOOP TILL END
NEXTLINE DS    0H
         LA    R5,80(R5)               NEXT LINE
         B     LOPX05                  LOOP TILL END
TESTSSEL DS    0H
         CH    R9,=H'1'                Q. LAST BYTE
         BE    CHANGECH                   Y. CHANGE CHARACTER
         CLI   1(R7),C' '              Q. NEXT CHAR SPACE
         BNE   NEXTLINE                   N. NOT SELECTION
CHANGECH MVC   0(0,R7),0(R6)           MOVE IN BROWSE
         EX    R10,CHANGECH            MOVE IN SUBSTITUTE
         B     NEXTLINE                GET NEXT LINE
EXITPR00 DS    0H
         RACKOFF RC=0,LIST=NO
                                                                SPACE 1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LTORG
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
                                                                SPACE 1
WORKAREA DSECT
         DS    18F                     REGISTER SAVE AREA (D.F.W.T.)
WORKAREL EQU   *-WORKAREA
                                                                SPACE 1
ISPFPARM DSECT
EXDTA    DS    F                       -> EXIT-DATA
PANNAME  DS    F                       -> PANEL NAME
PANSECT  DS    F                       -> I-INIT, R-REINIT, P-PROC
MSGID    DS    F                       -> RETURN MESSAGEID TO ISPF
ARRAYDIM DS    F                       -> NO. OF ARRAYS
VARNAME  DS    F                       -> ARRAY OF VARIABLE NAMES (8)
VARLEN   DS    F                       -> VARIABLE LENGTHS (FULLWORDS)
VARVAL   DS    F                       -> STING OF VARIABLE VALUES
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         END
