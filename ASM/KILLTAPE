*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
**
**  TITLE:    'FILL A TAPE WITH ALL HEX 00S.
**  SYSTEM:   N/A
**  MODULE:   KILLTAPE
**  PROGRAM:  KILLTAPE
**  FUNCTION: THIS PROGRAM WRITES ALL HEX ZEROS TO A TAPE TO
**            'DESTROY' ALL PREVIOUS DATA ON IT.
**            RETURN CODE OF 20 MEANS GETMAIN FAILED.
**
**
**  NOTES:    PROGRAM IS RE-ENTRANT.
**  AUTHOR:   A. KARA.
**  DATE:     03/06/92
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
KILLTAPE KICKOFF (R11),WORKAREA,WORKAREL,HELP=YES
         MVC   SYSOUT2(SYSOUT1L),SYSOUT1 MOVE LIST AREA TO GETMAIN
         MVC   OPENWK2(OPENWK1L),OPENWK1 MOVE LIST AREA TO GETMAIN
         LA    R1,OPENWK2              LOAD LIST ADDRESS
         LA    R2,SYSOUT2              LOAD DCB ADDRESS
         OPEN  ((R2),(OUTPUT)),MF=(E,(R1)) OPEN OUTPUT FILE
         CLEARV MESSAGE,PAD=00,MF=E    MOVE HEX ZEROS TO BUFFER
*---------------------------------------------------------------------*
         XC    LOOPCNTR,LOOPCNTR       FORCE ZERO VALUE
         LA    R9,LOOPCNTR             LOAD LOOP COUNT VALUE
LOOPPUTM DS    0H
         LA    R2,SYSOUT2              LOAD DCB ADDRESS
         PUT   (R2),MESSAGE            WRITE OUTFILE
         BCT   R9,LOOPPUTM             LOOP UNTIL END
*---------------------------------------------------------------------*
* THE FOLLOWING CODE IS ONLY PROVIDED TO COMPLETE THE PROGRAM IF      *
* THERE WAS A LOGICAL END TO THE LOOP                                 *
*---------------------------------------------------------------------*
GETOUT   DS    0H
         LA    R1,OPENWK2              LOAD LIST ADDRESS
         LA    R2,SYSOUT2              LOAD DCB ADDRESS
         CLOSE (R2),MF=(E,(R1))        CLOSE OUTPUT FILE
         XR    R15,R15                 INITIALISE RETURN CODE
EXITPROG DS    0H
         RACKOFF RC=(R15)              EXIT WITH HOUSEKEEPING
*=====================================================================*
         LTORG
*---------------------------------------------------------------------*
SPACES   DC    CL256' '                SPACES FOR INITIALISATION
LOOPCNTR DC    F'0'                    LOOP COUNTER
SYSOUT1  DCB   DDNAME=TAPE,                                            X
               RECFM=F,                                                X
               LRECL=32760,                                            X
               DSORG=PS,                                               X
               MACRF=(PM)
SYSOUT1L EQU   *-SYSOUT1
OPENWK1  OPEN  (,(OUTPUT)),MF=L
OPENWK1L EQU   *-OPENWK1
*---------------------------------------------------------------------*
WORKAREA DSECT
$REGSAVE DS    18F                  LINKAGE STD REG SAVE AREA
MYARPTR1 DS    F
SYSOUT2  DCB   DDNAME=TAPE,                                            X
               RECFM=F,                                                X
               LRECL=32760,                                            X
               DSORG=PS,                                               X
               MACRF=(PM)
OPENWK2  OPEN  (,(OUTPUT)),MF=L
         CLEARV  MESSAGE,MF=L
MESSAGE  DS    CL32760
WORKAREL EQU   *-WORKAREA
*---------------------------------------------------------------------*
         END
