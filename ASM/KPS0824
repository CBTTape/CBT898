* CMN *                                                                 DPSO7027
* CMN *   DPSO7027   PACKAGE #000253   DATE 26/04/92   T66LRA           DPSO7027
* CMN *   CICSESA CONVERSION                                            DPSO7027
* CMN *                                                                 DPSO7027
         TITLE '  KPS0824  -  ASSIGN VALUE FROM CONTROL BLOCKS'
*     **X                                                               DPSO7027
***********************************************************************
*  SUBROUTINE : KPS0824 - ASSIGN VALUE FROM CONTROL BLOCKS            *
*                                                                     *
*  FUNCTION   : CALLED BY COBOL PROGRAM TO GET VALUE OF RESOURCE      *
*               FROM A CICS CONTROL BLOCK                             *
*                                                                     *
*  INTERFACE  : CALL 'KPS0824'  USING  PTR TO CWA ADDR                *
*                                      WS-KPS0824-PARM.               *
***********************************************************************
         SPACE 1
TSUTBAR  EQU   2
TSUTEAR  EQU   2
TSGIDBAR EQU   2
TSMAPBAR EQU   2
DCTCBAR  EQU   7
TIOABAR  EQU   8
TCTTEAR  EQU   9
DFHEIBR  EQU   10
EISCBAR  EQU   1                                                        DPSO7027
         SPACE 2
*        DFHTCA CICSYST=YES
*        COPY DFHTCTTE
*        COPY DFHTIOA
*        COPY DFHDCTDS
*        COPY DFHTSMDS
*        COPY  DFHEIBLK
*        DFHEIS
*        DFHTM  MF=(D,PARMLIST)                                         DPSO7027
*<=====  COPY DFHCSADS                  REPLACED BY CICS@ MACRO
         CICS@  MF=L                    REQUIRED TO GET CSA@
         PRINT OFF
         DFHTCA CICSYST=YES
         COPY DFHTCTTE
         COPY DFHTIOA
         COPY DFHDCTDS
         COPY DFHTSMDS
         COPY  DFHEIBLK
         DFHEIS
         DFHTM  MF=(D,PARMLIST)                                         DPSO7027
         PRINT ON
         EJECT
***********************************************************************
***      MAINLINE                                                   ***
***********************************************************************
KPS0824  CSECT ,
KPS0824  AMODE 31
KPS0824  RMODE ANY
         STM   14,12,12(13)             SAVE CALLING REGS
         BASR  11,0                                                     DPSO7027
         USING *,11                                                     DPSO7027
         ST    13,SAV13                 SAVE SAVE REG
         B     *+12
         DC    C'*KPS0824'
         LA    2,INIT                                                   DPSO7027
         O     2,=X'80000000'                                           DPSO7027
*
         BASSM 14,2                    SET ADCONS VARS                  DPSO7027
         N     14,=X'80000000'                                          DPSO7027
         ST    14,AMODEIN                                               DPSO7027
*
         USING DFHEIBLK,DFHEIBR
         USING BRTABDS,1
         USING P824DS,3
         LA    1,BRTAB                  ADDR BRANCH CONTROL TABLE
         LA    2,BRTABN                 NO. OF ENTRIES
         L     3,@PARM                  ADDR PARM
BRLOOP   CLC   BRTABRQ,P824REQ          MATCH REQUEST ?
         BNE   BRLOOPU                  NO ... GO BUMP TABLE
         CLC   BRTABAC,P824ACT          MATCH ACTION ?
         BNE   BRLOOPU                  NO ... GO BUMP TABLE
BRLOOPF  LH    2,BRTABO                 GET OFFSET PROC ENTRY ADDR
         B     BRTAB(2)                 GO PROCESS REQUEST
BRLOOPU  LA    1,BRTABE(1)              NEXT ENTRY
         BCT   2,BRLOOP                 LOOP ON COUNT TABLE ENTRIES
         DC    X'0000'
         DC    C'FORCED ABEND - INVALID REQUEST'
*
RETURN   EQU   *
         L     13,SAV13                 RESTORE SAVE REG
         L     14,12(13)                                                DPSO7027
         O     14,AMODEIN                                               DPSO7027
         LM    15,12,16(13)             RESTORE CALLING REGS            DPSO7027
         BSM   14,14                                                    DPSO7027
         EJECT
***********************************************************************
***      UTILITIES
***********************************************************************
P#STARTC EQU   *
         DROP  TCACBAR
         USING DFHTCADY,TCACBAR
         L     TCACBAR,@TCASYS          ADDRESS SYSTEM TCA
         TM    TCAATTSC,X'05'           STARTCODE 05 ?
         BNO   P#ST4                    NO ... TRY 4
         MVC   P824VAL,=CL8'TD'         START BY TCTTE
         B     RETURN                   RETURN
P#ST4    EQU   *
         TM    TCAATTSC,X'04'           STARTCODE 04 ?
         BNO   P#ST3                    NO ... TRY 3
         MVC   P824VAL,=CL8'U '         START BY USER REQUEST
         B     RETURN                   RETURN
P#ST3    EQU   *
         TM    TCAATTSC,X'03'           START CODE 03 ?
         BNO   P#ST2                    NO ... TRY 2
         MVC   P824VAL,=CL8'QD'         START BY TD TRIGGER LEVEL
         B     RETURN                   RETURN
P#ST2    EQU   *
         TM    TCAATTSC,X'02'           STARTCODE 02 ?
         BNO   P#ST1                    NO ... TRY 1
         MVC   P824VAL,=CL8'SD'         START BY START WITH DATA
         B     RETURN                   RETURN
P#ST1    EQU   *
         TM    TCAATTSC,X'01'           STARTCODE 01 ?
         BNO   P#ST0                    NO ... TRY 0
         MVC   P824VAL,=CL8'S '         START BY START - NO DATA
         B     RETURN                   RETURN
P#ST0    EQU   *
         TM    TCAATTSC,X'00'           STARTCODE 00 ?
         BO    RETURN                   NO ... RETURN
         MVC   P824VAL,=CL8'TI'         START BY INPUT FROM CLEAR SCR
         B     RETURN                   RETURN
P#FACLTY EQU   *
         DROP  TCACBAR
         USING DFHTCADS,TCACBAR
         L     TCACBAR,@TCAUSR          ADDRESS USER TCA
         L     6,TCAFCAAA               ADDR OF PINCIPLE FACILITY
         MVC   P824VAL(4),0(6)          PRINCIPLE FACILITY
         B     RETURN                   RETURN
P#TCTUA  EQU   *
         MVC   P824VALA,@TCTUA          ADDR OF TCTUA
         B     RETURN                   RETURN
P#TCTUAX EQU   *
         DFHTM TYPE=LOCATE,TABLE=TCTE,NAME=P824NME,MF=(N,TMPARM),      XDPSO7027
               SET=P824VALA,ERROR=TMSRCHER,DUMMY=DUMMY                  DPSO7027
         L     TCTTEAR,P824VALA
         USING DFHTCTTE,TCTTEAR
         MVC   P824RSPA,TCTTECIA        ADDR OF TCTUA                   DPSO7027
         B     RETURN                   RETURN
P#ASKTME EQU   *
         MVC   EIBDATE,CSAJYDP          DATE
         MVC   EIBTIME,CSATODP          TIME
         B     RETURN                   RETURN
P#TODB   EQU   *
         MVC   P824RSPA,CSACTODB        TIME BINARY
         B     RETURN                   RETURN
P#PCT    EQU   *
         DFHTM TYPE=LOCATE,TABLE=PCT,NAME=P824NME,MF=(N,TMPARM),       XDPSO7027
               SET=P824VALA,ERROR=TMSRCHER,DUMMY=DUMMY                  DPSO7027
         B     RETURN                   RETURN
P#RPCT   EQU   *
         BAS   14,TMSSA                 TBL MNGE STATIC STORAGE AREA    DPSO7027
         USING DFHTMSSA,2
         L     2,TMASKT2                PCTR POINTER
         LA    4,3                      LENGTH OF ENTRY - 1
         BAS   14,TMSRCH                TBL MNGE SEARCH                 DPSO7027
         B     RETURN                   RETURN
P#PPT    EQU   *
         DFHTM TYPE=LOCATE,TABLE=PPT,NAME=P824NME,MF=(N,TMPARM),       XDPSO7027
               SET=P824VALA,ERROR=TMSRCHER,DUMMY=DUMMY                  DPSO7027
         B     RETURN                   RETURN
P#PFT    EQU   *
         DFHTM TYPE=LOCATE,TABLE=PFT,NAME=P824NME,MF=(N,TMPARM),       XDPSO7027
               SET=P824VALA,ERROR=TMSRCHER,DUMMY=DUMMY                  DPSO7027
         B     RETURN                   RETURN
P#FCT    EQU   *
         DFHTM TYPE=LOCATE,TABLE=FCT,NAME=P824NME,MF=(N,TMPARM),       XDPSO7027
               SET=P824VALA,ERROR=TMSRCHER,DUMMY=DUMMY                  DPSO7027
         B     RETURN                   RETURN
P#DCT    EQU   *
         DFHTM TYPE=LOCATE,TABLE=DCT,NAME=P824NME,MF=(N,TMPARM),       XDPSO7027
               SET=P824VALA,ERROR=TMSRCHER,DUMMY=DUMMY                  DPSO7027
         B     RETURN                   RETURN
P#DCTINB EQU   *
         DFHTM TYPE=LOCATE,TABLE=DCT,NAME=P824NME,MF=(N,TMPARM),       XDPSO7027
               SET=P824VALA,ERROR=TMSRCHER,DUMMY=DUMMY                  DPSO7027
         L     7,P824VALA               ADDRESS OF TDQ
         BAS   14,DCTIND                GET INTRAPARTITION TDQ          DPSO7027
         MVC   P824VALA,TDDCTTQC        NUMBER OF ELEMENTS ON QUEUE
         MVC   P824RSPA,TDDCTDS         TOTAL ELEMENTS EVER ON QUEUE
         B     RETURN                   RETURN
P#DCTITR EQU   *
         DFHTM TYPE=LOCATE,TABLE=DCT,NAME=P824NME,MF=(N,TMPARM),       XDPSO7027
               SET=P824VALA,ERROR=TMSRCHER,DUMMY=DUMMY                  DPSO7027
         L     7,P824VALA               ADDRESS OF TDQ
         BAS   14,DCTIND                GET INTRAPARTITION TDQ          DPSO7027
         MVC   P824VALA+2(2),TDDCTDQL   TRIGGER LEVEL
         B     RETURN                   RETURN
P#DCTSTR EQU   *
         DFHTM TYPE=LOCATE,TABLE=DCT,NAME=P824NME,MF=(N,TMPARM),       XDPSO7027
               SET=P824VALA,ERROR=TMSRCHER,DUMMY=DUMMY                  DPSO7027
         L     7,P824VALA               ADDRESS OF TDQ
         BAS   14,DCTIND                GET INTRAPARTITION TDQ          DPSO7027
         MVC   TDDCTDQL,P824VALA+2      TRIGGER LEVEL
         B     RETURN                   RETURN
P#TCT    EQU   *
         DFHTM TYPE=LOCATE,TABLE=TCTE,NAME=P824NME,MF=(N,TMPARM),      XDPSO7027
               SET=P824VALA,ERROR=TMSRCHER,DUMMY=DUMMY                  DPSO7027
         B     RETURN                   RETURN
P#TCTN   EQU   *
         BAS   14,TMSSA                 TBL MNGE STATIC STORAGE AREA    DPSO7027
         USING DFHTMSSA,2
         L     2,TMASKT8                TCTN POINTER
*<=====  LA    4,3                      LENGTH OF ENTRY - 1
         LA    4,7                      LENGTH OF ENTRY - 1
         BAS   14,TMSRCH                TBL MNGE SEARCH                 DPSO7027
         B     RETURN                   RETURN
P#TCTS   EQU   *
         BAS   14,TMSSA                 TBL MNGE STATIC STORAGE AREA    DPSO7027
         USING DFHTMSSA,2
         L     2,TMASKT9                TCTS POINTER
         LA    4,3                      LENGTH OF ENTRY - 1
         BAS   14,TMSRCH                TBL MNGE SEARCH                 DPSO7027
         B     RETURN                   RETURN
TMSSA    EQU   *
         DROP 2
         L     2,CSAOPFLA               OPTIONAL FEATURES LIST
         USING CSAOPFL,2
         L     2,CSASSA                 STATIC STORAGE AREA
         DROP 2
         USING DFHSSADS,2
         L     2,SSATMP                 TABLE MNGE STAT STRGE AREA
         BR    14
TMSRCH   EQU   *
         DROP 2
         USING DFHTMSKT,2
         L     2,SKTFDEA                SKATTER TABLE
         DROP 2
TMSRCHNX EQU   *                                                        DPSO7027
         CLC   3(11,2),=C'DFHTMDIRSEG'  DIRECTORY SEGMENT ?             DPSO7027
         BNE   *+12                     NO ...                          DPSO7027
         L     5,X'24'(2)               POINT TO ELEMENT CHAIN          DPSO7027
         USING DFHTMDEL,5                                               DPSO7027
         B     *+6                                                      DPSO7027
         LR    5,2                                                      DPSO7027
TMSRCHLP EQU   *
         EX    4,TEST                   ENTRY WE ARE LOOKING FOR ?
         BNE   TMSRCHBP                 NO ... GET NEXT TABLE ENTRY
         MVC   P824VALA,DIRTEA          ADDRESS OF CONTROL BLOCK
         BR    14                       RETURN TO MAINLINE
TMSRCHBP EQU   *
         L     5,DIRGNCHN               NEXT TABLE ENTRY                DPSO7027
         LTR   5,5                      IF THIS THE LAST ENTRY ?        DPSO7027
         BNZ   TMSRCHLP                 NO ... CHECK NEXT ENTRY         DPSO7027
         CLC   3(11,2),=C'DFHTMDIRSEG'  DIRECTORY SEGMENT ?             DPSO7027
         BNER  14                                                       DPSO7027
         ICM   2,B'1111',X'10'(2)                                       DPSO7027
         BNZ   TMSRCHNX                                                 DPSO7027
         BR    14                                                       DPSO7027
TMSRCHER EQU   *                                                        DPSO7027
         XC    P824VALA,P824VALA                                        DPSO7027
         B     RETURN                                                   DPSO7027
         EJECT
TEST     CLC   DIRKEY(*-*),P824NME      DIR KEY = SEARCH KEY ?
TMPARM   DS    4F                                                       DPSO7027
@TABLE   DS    F                                                        DPSO7027
DCTIND   EQU   *
         TM    TDDCTDT,TDINDTBM         INTRAPARTITION TDQ ?
         BOR   14                       YES ... RETURN
         TM    TDDCTDT,TDINDBM          INDIRECT TDQ
         BNO   DCTINDER                 NO ... INCORRECT TDQ REQUEST
         L     7,TDDCTIDI               TDQ POINTED TO BY INDIRECT TDQ
         B     DCTIND                   GET INTRAPARTITION TDQ
DCTINDER EQU   *
         MVC   P824VALA,=F'-1'          INCORRECT TDQ REQUEST
         B     RETURN
*
*---------------------------------------------------------------------*
*  NOTE: FIELD 'CSATSMTA' NO LONGER AVAILABLE UNDER V3.3              *
*---------------------------------------------------------------------*
P#TSQ    EQU   *                        LOCATE TSQ / RETURN # ENTRIES
         XC    P824VALA,P824VALA        INITIALISE RETURN AREA
*        L     2,CSATSMTA               TS UNIT TABLE ADDRESS
* ABOVE FIELD UNAVAILABLE UNDER V3.3
         MVC   P824NME,=CL8'**ERROR*'   NO ENTRIES FOUND
         B     RETURN                   RETURN
*
P#TSQF   EQU   *                        LOCATE TSQ WITH MATCHING
*                                       FIRST 4 CHARS. (TERMID)
         MVC   P824NME,=CL8'**ERROR*'   NO ENTRIES FOUND
         B     RETURN
*---------------------------------------------------------------------*
*
P#S2HMS  EQU   *                        CONVERT SECS TO 0HHMMSS PACKED
         LR    5,3
         DROP  3
         USING P824DS,5
*
         L     1,P824VALA               ) 0/1 - SECS
         XR    0,0                      )
*
         D     0,=F'100'                CONVERT TO HUNDREDTHS SEC
         LR    4,0                      HSECS
         XR    0,0
*
         D     0,=F'60'                 CONVERT TO SECS / MINS
         LR    2,1                      SAVE MINS
*
         LR    3,2                      ) 2/3 - MINS
         XR    2,2                      )
         D     2,=F'60'                 CONVERT TO MINS / HRS
*
         MH    3,=H'100'                HH..
         AR    2,3                      HHMM
         MH    2,=H'100'                HHMM..
         AR    0,2                      HHMMSS
         CLC   P824ACT,=C'SECS'
         BE    S2HMS1
         MH    0,=H'100'                HHMMSS..
         AR    0,4                      HHMMSSHSECS
*
S2HMS1   EQU   *
         CVD   0,S2D                    CONVERT TO PACKED
         MVC   P824RSPP,S2D             MOVE RESULT TO HMS
*
         LR    3,5
         DROP  5
         USING P824DS,3
         B     RETURN                   RETURN
S2D      DC    D'0'
***********************************************************************
***      INITIALIZATION - LOCATE CICS / TASK STRUCTURES             ***
***********************************************************************
INIT     EQU   *
         DROP  1,2,3,4,5,6
         XC    POOLA(POOLL),POOLA       CLEAR ADDRESS POOL
         L     2,0(1)                   POINT BLL-CPCA
         L     13,0(2)                  POINT CWA
         CLC   0(2,13),=C'P3'           P3* CWA ?
         BE    INITCWA                  YES ... STORE CWA ADDRESS
         CLC   0(2,13),=C'D3'           D3* CWA ?
         BE    INITCWA                  YES ... STORE CWA ADDRESS
         CLC   0(2,13),=C'T3'           T3* CWA ?
         BE    INITCWA                  YES ... STORE CWA ADDRESS
         CLC   0(2,13),=C'T6'           T6* CWA ?
         BE    INITCWA                  YES ... STORE CWA ADDRESS
*<=====  A     13,=F'512'               MUST BE CSA - POINT TO CWA
         L     13,CSACWAA-DFHCSADS(13)  MUST BE CSA - POINT TO CWA
INITCWA  EQU   *
         ST    13,@CWA                  SAVE ADDRESS
*<=====  S     13,=F'512'               POINT CSA
         CICS@ CSA,BASE=13              POINT CSA
         ST    13,@CSA                  SAVE ADDRESS
*
         DROP  TCACBAR
         USING DFHTCADS,TCACBAR
         L     TCACBAR,CSACDTA          USER TCA
         ST    TCACBAR,@TCAUSR          SAVE ADDRESS
*
         L     3,4(1)                   POINT TO PARAMETER BLOCK
         ST    3,@PARM                  SAVE ADDRESS
         USING P824DS,3
         XC    P824RSP(8),P824RSP       INITIALSE RETURN VALUES
*
TERM     EQU   *
         TM    TCAFCI,TCAFCDCM          DCT FACILITY
         BO    TASK                     YES ... GET OUT
         L     TCTTEAR,TCAFCAAA         TERMINAL ENTRY
         N     TCTTEAR,=X'7FFFFFFF'                                     DPSO7027
         ST    TCTTEAR,@TCTTE           SAVE ADDRESS
         LTR   TCTTEAR,TCTTEAR          NONE ... GET OUT
         BZ    TASK
         MVC   @TCTUA,TCTTECIA          SAVE TERMINAL USER AREA
*
         L     TIOABAR,TCTTEDA          CURRENT TIOA
         ST    TIOABAR,@TIOA            SAVE ADDRESS
         LTR   TIOABAR,TIOABAR          NONE ... GET OUT
         BZ    TASK
*
         LH    1,TIOATDL                BUFFER LENGTH
         SH    1,=H'12'                   LESS SAA
         STH   1,#TIOATDL               SAVE LENGTH
         LA    2,TIOADBA                BUFFER START
         ST    2,@TIOADBA               SAVE ADDRESS BUFFER
         LA    5,0(1,2)                 ) LAST
         BCTR  5,0                      )   BYTE
         ST    5,@TIOAEND               SAVE ADDRESS LAST BYTE
*
TASK     EQU   *
         L     TCACBAR,TCASYAA          SYSTEM TCA
         USING DFHTCADY,TCACBAR
         ST    TCACBAR,@TCASYS          SAVE ADDRESS
         L     EISCBAR,TCAEISA          EXEC I'FACE STRUCTURE
         USING DFHEISDS,EISCBAR                                         DPSO7027
         L     DFHEIBR,EISEIBAD         EXEC I'FACE BLOCK              +DPSO7027
                                        VERSION 2.1 VARIABLE NAME
*        L     DFHEIBR,EIS_USER_EIB_ADDR EXEC I'FACE BLOCK             +DPSO7027
                                        VERSION 3.3 VARIABLE NAME
         ST    DFHEIBR,@EIB             SAVE ADDRESS EIB
         L     TCACBAR,@TCAUSR          USER   TCA                      DPSO7027
         USING DFHTCADS,TCACBAR                                         DPSO7027
*
         BR    14                       RETURN TO MAINLINE
         EJECT
***********************************************************************
***      BASE ADDRESS POOL AND VARIABLES                            ***
***********************************************************************
*
POOLA    DS    0F
@PARM    DS    F                        CPOXCTL PARAMETER BLOCK
@EIB     DS    F                        EXEC INTERFACE BLOCK
@TCTUA   DS    F                        TCTUA
@TCAUSR  DS    F                        TCA USER AREA
@TCASYS  DS    F                        TCA SYSTEM AREA
@CSA     DS    F                        COMMON SYSTEM AREA
@CWA     DS    F                        COMMON WORK AREA
@TCTTE   DS    F                        TERMINAL CONTROL TABLE
@TIOA    DS    F                        TERMINAL I/O AREA
@TIOADBA DS    F                        INPUT BUFFER
@TIOAEND DS    F                        LAST BYTE IN DATA STREAM
#TIOATDL DS    H                        INPUT BUFFER LENGTH
BUFTRANS DS    XL4                      INPUT CARDPAC TRANSID
BUFPAGE  DS    XL2                      INPUT CARDPAC PAGE NUMBER
POOLL    EQU   *-POOLA
SAV13    DS    F                        CALLING PROGRAM R13 SAVE AREA
TSQID    DS   0CL8                      TS QUEUE NAME - FOUND
TSQID4   DS    CL4                      FIRST 4
TSQID8   DS    CL4                      LAST  4
TSQIDI   DS   0CL8                      TS QUEUE NAME - INPUT
TSQIDI4  DS    CL4                      FIRST 4
TSQIDI8  DS    CL4                      LAST  4
AMODEIN  DS    F                                                        DPSO7027
         SPACE 3
BRTAB    DS    0F
         DC    CL8'STARTCDE',CL4'    ',AL2(P#STARTC-BRTAB)
BRTABE   EQU   *-BRTAB
         DC    CL8'FACILITY',CL4'    ',AL2(P#FACLTY-BRTAB)
         DC    CL8'TCTUA   ',CL4'    ',AL2(P#TCTUA-BRTAB)
         DC    CL8'TCTUA   ',CL4'TERM',AL2(P#TCTUAX-BRTAB)
         DC    CL8'ASKTIME ',CL4'    ',AL2(P#ASKTME-BRTAB)
         DC    CL8'PCT     ',CL4'    ',AL2(P#PCT-BRTAB)
         DC    CL8'RPCT    ',CL4'    ',AL2(P#RPCT-BRTAB)
         DC    CL8'PPT     ',CL4'    ',AL2(P#PPT-BRTAB)
         DC    CL8'PFT     ',CL4'    ',AL2(P#PFT-BRTAB)
         DC    CL8'FCT     ',CL4'    ',AL2(P#FCT-BRTAB)
         DC    CL8'DCT     ',CL4'    ',AL2(P#DCT-BRTAB)
         DC    CL8'DCT     ',CL4'INBR',AL2(P#DCTINB-BRTAB)
         DC    CL8'DCT     ',CL4'ITRG',AL2(P#DCTITR-BRTAB)
         DC    CL8'DCT     ',CL4'STRG',AL2(P#DCTSTR-BRTAB)
         DC    CL8'TCT     ',CL4'    ',AL2(P#TCT-BRTAB)
         DC    CL8'TCTN    ',CL4'    ',AL2(P#TCTN-BRTAB)
         DC    CL8'TCTS    ',CL4'    ',AL2(P#TCTS-BRTAB)
         DC    CL8'CSACTODB',CL4'    ',AL2(P#TODB-BRTAB)
         DC    CL8'SEC2HMS ',CL4'    ',AL2(P#S2HMS-BRTAB)
         DC    CL8'SEC2HMS ',CL4'SECS',AL2(P#S2HMS-BRTAB)
         DC    CL8'TSQ     ',CL4'    ',AL2(P#TSQ-BRTAB)
         DC    CL8'TSQF    ',CL4'    ',AL2(P#TSQF-BRTAB)
BRTABN   EQU   (*-BRTAB)/BRTABE
         LTORG
               EJECT
BRTABDS  DSECT                          BRANCH CONTROL TABLE ENTRY
BRTABRQ  DS   CL8                       REQUEST
BRTABAC  DS   CL4                       ACTION
BRTABO   DS   AL2                       OFFSET PROC ENTRY
                                                                SPACE 2
         COPY  KPW0825
               EJECT
DFHTMDEL DSECT
DIRTEA   DS    F
DIRHSCHN DS    F
DIROWCHN DS    F
DIRPRIME DS    F
DIRGNCHN DS    F
DIRETTC  DS    AL1
DIRSTATS DS    AL1
         DS    AL2
DIREINFO DS    0X
DIRKEY   DS    CL16
DIRESIZE DS    0X
         SPACE 4                                                        DPSO7027
DFHTMSKT DSECT
         ORG   DFHTMSKT+X'20'                                           DPSO7027
SKTFDEA  DS    F
         SPACE 4                                                        DPSO7027
DFHTMSSA DSECT
         DS    2F
TMASKT1  DS    F
         DS    6F                                                       DPSO7027
TMASKT2  DS    F
         DS    6F                                                       DPSO7027
TMASKT3  DS    F
         DS    6F                                                       DPSO7027
TMASKT4  DS    F
         DS    6F                                                       DPSO7027
TMASKT5  DS    F
         DS    6F                                                       DPSO7027
TMASKT6  DS    F
         DS    6F                                                       DPSO7027
TMASKT7  DS    F
         DS    6F                                                       DPSO7027
TMASKT8  DS    F
         DS    6F                                                       DPSO7027
TMASKT9  DS    F
         DS    6F                                                       DPSO7027
TMASKT10 DS    F
         DS    6F                                                       DPSO7027
         EJECT                                                          DPSO7027
         END
