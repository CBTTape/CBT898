***********************************************************************
*                                                                     *
* PROGRAM:  LGNP001                                                   *
*           ASSEMBLER                                                 *
*           CICS/ESA V3.3.0                                           *
*           AMODE 31, RMODE ANY                                       *
*                                                                     *
*                                                                     *
*     DESCRIPTION : THIS IS THE MAIN PROGRAM IN THE LOGON SYSTEM.     *
*     -----------   IT IS GIVEN CONTROL BY THE LOGON EXIT.            *
*                   IT CONTROLS ALL "LOGGING ON AND LOGGING OFF".     *
*                   IT DISPLAYS THE MAIN MENU SCREEN.                 *
*                   IT DISPLAYS THE HELP SCREEN.                      *
*                                                                     *
*     PROGRAM FLOW :                                                  *
*     ------------                                                    *
*                                                                     *
*                                                                     *
*    CHANGE INDEX                                                     *
*    ------------                                                     *
*    <  DATE  >  <         CHANGE DESCRIPTION                  >      *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6                       HLQ TABLE BASE REGISTER
R7       EQU   7                       CWA BASE REGISTER
R8       EQU   8                       EIB BASE REGISTER
R9       EQU   9                       DYNAMIC STORAGE BASE REGISTER
R10      EQU   10                      PROGRAM BASE REGISTERS
R11      EQU   11                        "      "       "
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
*
* ------------------------------------------------------------------- *
*        DSECTS
* ------------------------------------------------------------------- *
*
         EJECT
         COPY  DFHAID             3270 AIDS
         COPY  CWACICDS           COMMON WORK AREA
         COPY  LGNHLQDS           HLQ DSECT
         COPY  TUIDTBDS           TERMINAL/USERID TABLE
         COPY  LGNLEEDS           TERMINAL LOGON EXIT EXEMPTION TABLE
         COPY  TCTUADS            TCT USER AREA DSECT
*
         EJECT
         IHAPSA                   PSA DSECT
         EJECT
         IHAASCB                  ASCB DSECT
         EJECT
         IHAASXB                  ASXB DSECT
         EJECT
         IHAACEE                  ACEE DSECT
         EJECT
*
*
         PRINT ON,NOGEN
*
* ------------------------------------------------------------------- *
*        DYNAMIC STORAGE
* ------------------------------------------------------------------- *
*
DFHEISTG DSECT
*
COMMAREA DS    0H                 OUR COMMAREA
CMSCRFLG DS    CL1                LAST-SCREEN-SENT FLAG
SCLOGON  EQU   1                  LOGON SCREEN
SCMENU   EQU   2                  MENU SCREEN
SCHELP   EQU   3                  HELP SCREEN
SCBBRD   EQU   4                  BULLETIN BOARD SCREEN
CMHLQOFF DS    F                  OFFSET OF LAST DISPLAYED HLQ
CMLENGTH EQU   *-COMMAREA
*
* VSAM KSDS RECORD LAYOUT FOR USERID/ACCOUNT NUMBER FILE
*
USRACREC DS    0CL27              USER/ACCOUNT NUMBER RECORD
USRACKEY DS    CL8                KEY.. USERID
USRACNUM DS    CL8                LAST USED ACCOUNT NUMBER
USRACDAT DS    CL4                DATE P'00YYDDDF'
USRACTIM DS    CL4                TIME P'0HHMMSSC'
USRACFIL DS    CL3                SPARE (NULLS)
*
*
ACTNUMLN EQU   8                  MAXIMUM LENGTH OF ACCOUNT NUMBER
*
         COPY  LGNMS10            MAPSET
MAPLEN   DS    H
SELTABL  EQU   M1TABLL*14         LENGTH OF SELECTION TABLE
SELTABN  EQU   14                 NUMBER OF ENTRIES IN TABLE
*
BBDATA   DS    CL77
BBTSQI   DS    H
BBMATCH  DS    CL1
XFRTRAN  DS    CL4
WM1OPSW  DS    CL8
WM1NPSW  DS    CL8
WM1ACCT  DS    CL8
SAVE3R   DS    3F
SAVEREGS DS    7F
SECSAVE  DS    7F
SAVETERM DS    CL4
SAVENET  DS    CL8
SAVECLEN DS    H
SAVEAID  DS    CL1
SAVEPASS DS    CL8
TCTUAL   DS    H
TCTUAADD DS    F
WCVDA    DS    F
TEMPUSER DS    CL8
WUSERID  DS    CL8
ABSTIME  DS    PL8
DATETEXT DS    CL8
TIMETEXT DS    CL8
CSMTMSG  DS    CL57
ACQ      DS    F
ATI      DS    F
TTI      DS    F
REL      DS    F
TEMPTERM DS    CL4
REMOTEID DS    CL4
WHLQCDE  DS    CL3
*
WSIGNFLG DS    CL1
SIGNDON  EQU   C'Y'
SIGNDOF  EQU   C'N'
*
WSELSFLG DS    CL1
SELSYES  EQU   C'Y'
SELSNO   EQU   C'N'
*
WSCROLFG DS    CL1
SCROLYES EQU   C'Y'
SCROLNO  EQU   C'N'
*
RACFENT  DS    CL3            RACFCHEK PARM
RACFSP   DS    CL5            RACFCHEK PARM
RACFACC  DS    CL7            RACFCHEK PARM - RETURNED ACCESS
RACFRC   DS    CL2            RACFCHEK PARM - RETURN CODE
*
ATTPHS   EQU   X'F9'          PROTECTED HIGHLIGHTED AUTOSKIP
ATTPNS   EQU   X'F1'          PROTECTED NORMAL AUTOSKIP
ATTUND   EQU   X'7D'          PROTECTED UNDISPLAYABLE
*
*
WORK     DS    16F            RACF AREA
WORKRR   DS    CL512          RACF AREA
ENTITY   DS    CL17           ENTITY TO PASS TO FASTAUTH
*
*
* ------------------------------------------------------------------- *
*        MAINLINE PROCESS
* ------------------------------------------------------------------- *
*
LGNP001  AMODE 31
LGNP001  RMODE ANY
LGNP001  DFHEIENT CODEREG=(R10,R11),   BASE REGISTERS                  *
               DATAREG=(R9),           DYNAMIC STORAGE REGISTERS       *
               EIBREG=(R8)             ADDRESS OF EXEC INTERFACE BLOCK
*
*   SAVE/INITIALIZE VARIABLES
*
         MVC   SAVEAID,EIBAID          SAVE VALUES OF AID KEY PRESSED
         MVC   SAVECLEN,EIBCALEN       SAVE COMMAREA LENGTH
         MVC   SAVETERM,EIBTRMID       SAVE TERMINAL ID
         MVC   M1ACCTL,NULL20          RESET CURSOR POSITION
         MVC   M1USERL,NULL20          RESET CURSOR POSITION
         MVC   M1OPSWL,NULL20          RESET CURSOR POSITION
         MVC   M1NPSWL,NULL20          RESET CURSOR POSITION
         MVC   M1TIDO,SAVETERM         MOVE TERMID TO SCREEN VAR
*
         CLC   EIBCALEN,COMMLEN        DO WE HAVE A PASSED COMMAREA?
         BNE   MAIN0000                NO
         L     R1,DFHEICAP             YES.. LOAD COMMAREA ADDRESS
         MVC   COMMAREA(CMLENGTH),0(R1)      AND MOVE COMMAREA
MAIN0000 DS    0H
*
*   ENQ UPON THE CICS CWA
*
         EXEC  CICS                                                    +
               ENQ                                                     +
               RESOURCE(ENQRES)                                        +
               LENGTH(ENQRESL)
*
*
*   ADDRESS THE CWA AND TCTUA
*
         EXEC  CICS                                                    +
               ADDRESS                                                 +
               CWA(R7)                                                 +
               TCTUA(R1)
*
         ST    R1,TCTUAADD             SAVE THE TCTUA ADDRESS
         C     R7,NOCWA                DO WE HAVE A CWA?
         BE    MAIN0099                NO.. NOTHING CAN SAVE US NOW
         USING CWACICDS,R7             YES.. MAP THE DSECT
*
*   OBTAIN NETNAME AND USERID.  IF USER NOT YET SIGNED-ON, THEN
*   USERID WILL BE THE CICS DEFAULT OF 'CICSUSER' (SET IN THE SIT).
*
         MVI   WSIGNFLG,SIGNDOF        SET FLAG TO SIGNED-OFF
*
         EXEC  CICS                                                    +
               ASSIGN                                                  +
               NETNAME(SAVENET)                                        +
               TCTUALENG(TCTUAL)                                       +
               USERID(WUSERID)                                         +
               NOHANDLE
*
         CLC   WUSERID,DFLTUSER        DEFAULT CICS SIGNON?
         BE    MAIN0010                YES.. USER NOT YET SIGNED-ON
         MVI   WSIGNFLG,SIGNDON        NO... SET FLAG TO SIGNED-ON
*
*   CHECK FOR GENERIC USERID
*
         CLC   WUSERID(L'GENERIC),GENERIC   GENERIC SIGNED-ON?
         BE    MAIN0060                YES.. TRANSFER TO LGNP006
*
*   CHECK FOR GENERIC USERID IN TCTUA
*
MAIN0010 DS    0H
         LH    R1,TCTUAL               LENGTH OF TCT USER AREA
         CH    R1,=AL2(TCTUALEN)       COMPARE WITH DSECT LENGTH
         BL    MAIN0012                TOO SHORT.. BYPASS
         L     R1,TCTUAADD             PICK UP TCTUA ADDRESS
         C     R1,NOTCTUA              IS THERE ONE?
         BE    MAIN0012                NO.. BYPASS
         USING TCTUADS,R1              YES. MAP DSECT
         CLC   WUSERID,DFLTUSER        DEFAULT CICS SIGNON?
         BNE   MAIN0011                NO
         MVC   TCTUAACT,NULL20         YES.. MAKE SURE NO ACCOUNT
MAIN0011 DS    0H
         CLC   TCTUAGID(L'GENERIC),GENERIC  IS THERE A SAVED GENERIC?
         BE    MAIN0060                YES. TRANSFER TO LGNP006
MAIN0012 DS    0H
*
*   IF THIS THE FIRST TIME IN, THEN THERE WILL BE NO COMMAREA PASSED
*   OTHERWISE, THE LENGTH OF THE PASSED COMMAREA MUST EQUAL COMMLEN
*   (IF IT IS NOT THE SAME LENGTH, THEN TREAT AS FIRST TIME IN).
*
         CLC   SAVECLEN,COMMLEN        DO WE HAVE A PASSED COMMAREA?
         BE    MAIN0040                YES..
*
*   NO COMMAREA PASSED.
*
         CLI   WSIGNFLG,SIGNDON        ARE WE SIGNED-ON?
         BNE   SNDSGN0                 NO..  SEND SIGNON SCREEN
*
*   NO COMMAREA PASSED BUT SIGNED ON.. SET UP THE MENU SELECTIONS
*
MAIN0015 DS    0H
         MVI   WSCROLFG,SCROLNO        NO A SCROLLING REUEST
         MVC   M1MESSO,MESS01          INITIALIZE MESSAGE
         BAL   R2,SELOPT00             SET UP SELECTION MENU
         CLI   WSELSFLG,SELSYES        ARE THERE ANY SELECTIONS?
         BE    MAIN0030                YES
         MVC   M1MESSO(L'MESS05),MESS05  NO SET UP MESSAGE
MAIN0030 DS    0H
         B     SNDMNU0                 AND SEND MENU SCREEN
*
*   COMMAREA PASSED IF HERE.  WHERE HAVE WE COME FROM?
*
MAIN0040 DS    0H
         CLI   CMSCRFLG,SCLOGON        LOGON SCREEN?
         BE    PLGN000                 YES.. PROCESS LOGON SCREEN
         CLI   CMSCRFLG,SCMENU         MENU SCREEN?
         BE    PMNU000                 YES.. PROCESS MENU SCREEN
         CLI   CMSCRFLG,SCHELP         HELP SCREEN?
         BE    MAIN0050                YES.. TREAT AS NO COMMAREA
         CLI   CMSCRFLG,SCBBRD         BULLETIN BOARD SCREEN?
         BE    MAIN0015                YES.. START OVER
*
*   IF WE GET TO HERE, THEN WE HAVE A VALID COMMAREA AND ARE
*   SIGNED-ON BUT THE IDENTITY OF THE LAST SCREEN SENT IS UNKNOWN!
*   (OR WE MAY WISH TO FORCE THE NO-COMMAREA CONDITION)
*
MAIN0050 DS    0H
         MVC   SAVECLEN,=H'0'          TREAT AS NO COMMAREA AND
         B     MAIN0010                FIRST TIME IN
*
*
*   GENERIC USERID SIGNED-ON OR SAVED IN THE TCT USER AREA..
*   TRANSFER TO PROGRAM LGNP006.
*
*
MAIN0060 DS    0H
*
         EXEC  CICS                                                    C
               XCTL                                                    C
               PROGRAM('LGNP006')                                      C
               NOHANDLE
*
         B     RETURN                  JUST IN CASE XCTL FAILS
*
*
*   GROSS ERROR IF HERE.. NO CICS CWA.. NOTHING WILL WORK!
*
MAIN0099 DS    0H                      NO CWA AVAILABLE
         MVC   M1ACCTL,MINUS1          POSITION CURSOR TO ACCOUNT
         MVC   M1MESSO(L'MESS50),MESS50  MESSAGE
         B     SNDSGN20                DISPLAY AND RETURN
*
*
*
* ------------------------------------------------------------------- *
*   SET UP AND SEND SIGNON-SCREEN
* ------------------------------------------------------------------- *
*
SNDSGN0  DS    0H
*
*   FIND THE USERID IN THE TERMID/USERID TABLE WHICH IS ANCHORED
*   OF THE CWA.  (IT WAS PUT THERE BY THE AUTOINSTALL EXIT PROGRAM).
*
         MVC   M1MESSO(L'MESS03),MESS03  SIGN-ON REQUIRED MESSAGE
         MVC   M1ACCTO,SPACES20        INITIALIZE ACCOUNT
         MVC   M1USERO,SPACES20        INITIALIZE USERID
         MVC   M1OPSWO,NULL20          INITIALIZE OLD PSW
         MVC   M1NPSWO,NULL20          INITIALIZE NEW PSW
         MVC   M1ACCTL,MINUS1          POSITION CURSOR TO ACCOUNT
         L     R3,CWATUITA             ADDRESS THE TERM/UID TABLE
         LTR   R3,R3                   TABLE THERE?
         BZ    SNDSGN10                NO.. SKIP TABLE SEARCH
         USING TUIDTBDS,R3             YES. MAP THE TABLE
SNDSGN6  DS    0H                      SEARCH THE TABLE FOR OUR TERM
         CLI   TUIDTTRM,TUIDTEND       END OF TABLE?
         BE    SDNSGN9                 YES
         CLC   TUIDTTRM,SAVETERM       OUR TERM IN THIS SLOT?
         BE    SNDSGN8                 YES
         LA    R3,TUIDTNTL(,R3)        NO.. BUMP POINTER AND NEXT ONE
         B     SNDSGN6
SNDSGN8  DS    0H
         MVC   M1USERO,TUIDTUID        PICK UP THE USERID FROM TABLE
*
*    LOGICALLY DELETE THE TERM/USERID ENTRY FROM THE TABLE ANCHORED
*    OF THE CWA SO THAT THE SLOT CAN BE REUSED.
*
         MVI   TUIDTTRM,TUIDTRUS
         DROP  R3
SDNSGN9  DS    0H
         CLI   M1USERO,C' '            DID WE GET A VALID USERID?
         BE    SNDSGN10                NO.. SKIP FILE READ
*
*
*   READ VSAM KSDS FOR LAST USED ACCOUNT NUMBER OF USERID.
*   USERID IS THE KEY.  IF THE USERID IS NOT FOUND OR THE FILE
*   IS UNAVAILABLE FOR ANY REASON, THEN BYPASS THIS STEP.
*
*
         EXEC  CICS                                                    +
               READ                                                    +
               FILE('USRACTNM')                                        +
               INTO(USRACREC)                                          +
               LENGTH(USRACRLN)                                        +
               RIDFLD(M1USERO)                                         +
               EQUAL                                                   +
               NOHANDLE
*
         CLC   EIBRESP,DFHRESP(NORMAL) NORMAL REPONSE?
         BNE   SNDSGN10                NO.. SO BYPASS FILE PROCESSING
         MVC   M1ACCTO,USRACNUM        MOVE IN LAST USED ACCOUNT NUMBER
         MVC   M1ACCTL,NULL20          RESET CURSOR ON ACCOUNT
         MVC   M1OPSWL,MINUS1          POSITION CURSOR TO OLD PASSWRD
         MVC   M1MESSO(L'MESS07),MESS07  MESSAGE
         B     SNDSGN20                DISPLAY AND RETURN
*
SNDSGN10 DS    0H
         MVC   M1ACCTL,MINUS1          POSITION CURSOR TO ACCOUNT
         MVC   M1MESSO(L'MESS03),MESS03  MESSAGE
*
SNDSGN20 DS    0H
         MVC   M1STATO,NULL20          BLANK STATUS USERID
         MVC   M1TIDO,SAVETERM         MOVE IN TERMINAL ID
         MVC   M1NETO,SAVENET          MOVE IN NETNAME
         MVC   M1UHO(9),HDUSER         SET UP USER AND PASSWORD
         MVC   M1PHO(10),HDPASS        TITLES IN MAP
         MVC   M1NPHO(14),HDNPASS
         MVC   M1ACHO,HDACCT           ACCOUNT HEADER
         MVC   MAPLEN,=AL2(LGNM101L)
*
         EXEC  CICS                                                    +
               SEND                                                    +
               MAP('LGNM101')                                          +
               MAPSET('LGNMS10')                                       +
               FROM(LGNM101O)                                          +
               LENGTH(MAPLEN)                                          +
               ERASE                                                   +
               CURSOR
*
         MVI   CMSCRFLG,SCLOGON        SET FLAG TO SHOW LOGON SCREEN
         B     RETXID                  RETURN WITH TRANSID/COMMAREA
*
*
* ------------------------------------------------------------------- *
*   SET UP AND SEND MENU SELECTIONS SCREEN
* ------------------------------------------------------------------- *
*
SNDMNU0  DS    0H
         MVC   M1STATO,WUSERID         MOVE IN USERID
         MVC   M1TIDO,SAVETERM         MOVE IN TERMINAL ID
         MVC   M1NETO,SAVENET          MOVE IN NETNAME
         MVC   M1USERO,NULL20          BLANK USERID
         MVC   M1UHO,NULL20            BLANK USERID TITLE
         MVC   M1PHO,NULL20              ..  PASSWD TITLE
         MVC   M1NPHO,NULL20             ..  NEWPWD TITLE
         MVC   M1ACHO,NULL20             ..  ACCTNM TITLE
         MVC   M1OPSWO,NULL20            ..  OLDPWD DATA
         MVC   M1NPSWO,NULL20            ..  NEWPWD DATA
         MVC   M1ACCTO,SPACES20          ..  ACCTNM DATA
         MVC   M1APLO,SPACES20           ..  APLID  DATA
         MVI   M1USERA,ATTPHS          PROTECT USERID FIELD
         MVI   M1NPSWA,ATTPHS          PROTECT NEW PASSWORD FIELD
         MVI   M1OPSWA,ATTPHS          PROTECT PASSWORD FIELD
         MVI   M1ACCTA,ATTPHS          PROTECT ACCOUNT  FIELD
         MVC   M1TSELL,MINUS1          SET UP CURSOR POSITIONING
         MVC   MAPLEN,=AL2(LGNM101L)
*
         EXEC  CICS                                                    +
               SEND                                                    +
               MAP('LGNM101')                                          +
               MAPSET('LGNMS10')                                       +
               FROM(LGNM101O)                                          +
               LENGTH(MAPLEN)                                          +
               ERASE                                                   +
               CURSOR
*
         MVI   CMSCRFLG,SCMENU         SET FLAG TO SHOW MENU SCREEN
         B     RETXID                  RETURN WITH TRANSID/COMMAREA
*
*
* ------------------------------------------------------------------- *
*   PROCESS THE LOGON SCREEN
* ------------------------------------------------------------------- *
*
PLGN000  DS    0H
*
*   CHECK FOR PF5/17 SINGLE LOGOFF OR PF6/18 MULTI-LOGOFF.
*   MULTI-LOGOFF NOT ALLOWED FROM LOGON SCREEN (BECAUSE WE
*   ARE NOT YET SIGNED-ON), SO TREAT MULTI-LOGOFF REQUEST
*   AS A NORMAL, SINGLE LOGOFF REQUEST.
*
         CLI   SAVEAID,DFHPF5          SINGLE LOGOFF REQUEST?
         BE    LOGOFF                  YES
         CLI   SAVEAID,DFHPF17         SINGLE LOGOFF REQUEST?
         BE    LOGOFF                  YES
         CLI   SAVEAID,DFHPF6          MULTI-LOGOFF REQUEST?
         BE    LOGOFF                  YES
         CLI   SAVEAID,DFHPF18         MULTI-LOGOFF REQUEST?
         BE    LOGOFF                  YES
*
*   NOT A LOGOFF REQUEST.. HAS CLEAR BEEN PRESSED?  IF SO,
*   FORCE A ZERO COMMAREA LENGTH AND TREAT AS FIRST TIME IN.
*
         CLI   SAVEAID,DFHCLEAR        CLEAR SCREEN PRESSED?
         BNE   PLGN005                 NO
         MVC   SAVECLEN,=H'0'          YES SET COMMAREA LENGTH TO ZERO
         B     MAIN0010                AND GO TO START
*
PLGN005  DS    0H
*
*   AS THIS IS THE SIGNON SCREEN, NO OTHER REQUESTS ARE PROCESSED
*   (APART FROM THE LOGOFF/CLEAR SCREEN ABOVE). JUST RECEIVE SCREEN.
*
*
         EXEC  CICS                                                    +
               RECEIVE                                                 +
               MAP('LGNM101')                                          +
               MAPSET('LGNMS10')                                       +
               INTO(LGNM101I)                                          +
               NOHANDLE
*
*    CHECK FOR SPECIAL EXTERNAL USERS EG TELECOM, AND INSERT
*    THEIR DEFAULT ACCOUNT CODE IF NECESSARY.
*
         CLC   SAVETERM(L'PESTERMS),PESTERMS  POLICE/EMER SERVICES?
         BNE   PLGN010                NO... CHECK NEXT
         MVC   M1ACCTI,PESACCT        YES.. MOVE IN DEFAULT ACCOUNT
         B     PLGN015                      AND CONTINUE
PLGN010  DS    0H
         CLC   SAVETERM(L'TWXTERMS),TWXTERMS  TELECOM/MOTORLINK?
         BNE   PLGN015                NO... CONTINUE
         MVC   M1ACCTI,TELACCT        YES.. MOVE IN DEFAULT ACCOUNT
PLGN015  DS    0H
*
*    NOW CHECK THAT WE HAVE AN ACCOUNT NUMBER AT LEAST 5/6 CHARS
*
         MVC   WM1ACCT,M1ACCTI
         LA    R1,ACTNUMLN           LOOP COUNTER
         LA    R2,ACTNUMLN           MAX NUMBER OF BYTES
         LA    R3,WM1ACCT            ADDRESS OF SAVED ACCOUNT NUMBER
PLGN020  DS    0H
         CLI   0(R3),C' '            IS CHAR A SPACE?
         BE    PLGN025               YES.. END OF ACCOUNT NUMBER
         CLI   0(R3),X'00'           IS CHAR A NULL?
         BE    PLGN025               YES.. END OF ACCOUNT NUMBER
         LA    R3,1(,R3)             POINT TO NEXT BYTE
         BCT   R1,PLGN020            AND TRY AGAIN
PLGN025  DS    0H
         SR    R2,R1                 SUB FOR LENGTH OF ACCOUNT NUMBER
         C     R2,=F'1'              IS IT AT LEAST 1 BYTE?
         BNL   PLGN030               NO...
         MVC   M1ACCTL,MINUS1        POSITION CURSOR TO ACCOUNT NUMBER
         MVC   M1MESSO(L'MESS06),MESS06  YES. SEND ERROR MESSAGE
         B     SNDSGN20              AND DISPLAY/RETURN
PLGN030  DS    0H
         CLI   WM1ACCT,C'0'          IS LEADING CHAR A ZERO?
         BE    PLGN035               YES.. IT IS - CHECK FOR 5 CHARS
         C     R2,=F'6'              NO... IT IS - CHECK FOR 6 CHARS
         BNL   PLGN040               YES.. GO AND SIGN ON
         MVC   M1ACCTL,MINUS1        POSITION CURSOR TO ACCOUNT NUMBER
         MVC   M1MESSO(L'MESS10),MESS10  NO.. SEND ERROR MESSAGE
         B     SNDSGN20              AND DISPLAY/RETURN
PLGN035  DS    0H                    LEADING X'F0' - MUST BE 5 CHARS
         C     R2,=F'5'              IS IT AT LEAST 5 CHARACTERS?
         BNL   PLGN040               YES.. GO AND SIGN ON
         MVC   M1ACCTL,MINUS1        POSITION CURSOR TO ACCOUNT NUMBER
         MVC   M1MESSO(L'MESS08),MESS08  NO.. SEND ERROR MESSAGE
         B     SNDSGN20              AND DISPLAY/RETURN
*
*
*   ACCOUNT NUMBER IS IN CORRECT FORMAT.. NOW SIGN-ON THE USER
*
*
PLGN040  DS    0H
         MVI   WSIGNFLG,SIGNDOF    INITIALIZE TO NOT LOGGED ON
         MVC   M1ACCTL,NULL20      REMOVE CURSOR POSITIONING
         MVC   M1USERL,NULL20        "
         MVC   M1OPSWL,NULL20        "
         MVC   M1NPSWL,NULL20        "
*
*  MAKE SURE USERID IS SPACE FILLED TO RIGHT FOR VSAM FILE KEY
*  (CHANGE ALL NULLS TO SPACES) AND MAKE SURE AT LEAST THE FIRST
*  BYTE OF THE USERID IS NOT A BLANK.
*
         LA    R5,8                LOOP COUNTER
         LA    R4,M1USERI          START OF FIELD
PLGN050  DS    0H
         CLI   0(R4),X'00'         IS THIS BYTE A NULL?
         BNE   PLGN055             NO.. NEXT BYTE
         MVI   0(R4),C' '          YES. CHANGE IT TO A SPACE
PLGN055  DS    0H
         LA    R4,1(,R4)           POINT TO NEXT BYTE
         BCT   R5,PLGN050          AND TRY AGAIN
         MVC   WUSERID,M1USERI     SAVE IN USERID
         CLI   M1USERI,C' '        IS FIRST BYTE A SPACE?
         BE    PLGN125             YES..  NO USERID ENTERED
*
*  CHECK OLD PASSWORD TO MAKE SURE AT LEAST THE FIRST BYTE
*  IS NOT BLANK.
*
         MVC   WM1OPSW,M1OPSWI     SAVE IN OLD PASSWORD
         CLI   M1OPSWI,X'00'       FIRST BYTE A NULL?
         BE    PLGN130             YES..  NO PASSWORD ENTERED
         CLI   M1OPSWI,C' '        FIRST BYTE A SPACE?
         BE    PLGN130             YES..  NO PASSWORD ENTERED
*
*  CHECK FOR PRESENCE OF A NEW PASSWORD
*
         MVC   WM1NPSW,M1NPSWI     SAVE IN NEW PASSWORD
         CLI   M1NPSWI,X'00'       FIRST BYTE A NULL?
         BE    PLGN060             YES..  SIGNON WITHOUT NEW PASS
         CLI   M1NPSWI,C' '        FIRST BYTE A SPACE?
         BE    PLGN060             YES..  SIGNON WITHOUT NEW PASS
         B     PLGN065             NEITHER.. SIGNON WITH NEW PASS
*
*  SIGNON WITHOUT NEW PASSWORD
*
PLGN060  DS    0H
         MVC   SAVEPASS,WM1OPSW    SAVE THE PASSWORD
*
         EXEC  CICS                                                    +
               SIGNON                                                  +
               USERID(WUSERID)                                         +
               PASSWORD(WM1OPSW)                                       +
               NATLANG('E')                                            +
               NOHANDLE
*
         B     PLGN070             CHECK RETURN CODES
*
*
*  SIGNON WITH NEW PASSWORD
*
PLGN065  DS    0H
         MVC   SAVEPASS,WM1NPSW    SAVE THE PASSWORD
*
         EXEC  CICS                                                    +
               SIGNON                                                  +
               USERID(WUSERID)                                         +
               PASSWORD(WM1OPSW)                                       +
               NEWPASSWORD(WM1NPSW)                                    +
               NATLANG('E')                                            +
               NOHANDLE
*
PLGN070  DS    0H                  CHECK THE RETURN CODES
         CLC   EIBRESP,DFHRESP(NORMAL)   SIGNON SUCCESSFUL?
         BE    PLGN080             YES
         CLC   EIBRESP2,=F'1'      PASSWORD REQUIRED
         BE    PLGN130             YES
         CLC   EIBRESP2,=F'2'      PASSWORD ERROR
         BE    PLGN105             YES
         CLC   EIBRESP2,=F'3'      NEW PASSWORD REQUIRED?
         BE    PLGN090             YES
         CLC   EIBRESP2,=F'4'      NEW PASSWORD NOT ACCEPTABLE
         BE    PLGN095             YES
         CLC   EIBRESP2,=F'8'      UNKNOWN USERID
         BE    PLGN100             YES
         CLC   EIBRESP2,=F'19'     USERID REVOKED
         BE    PLGN115             YES
         CLC   EIBRESP2,=F'20'     USERID ACCESS TO GROUP REVOKED
         BE    PLGN120             YES
         B     PLGN110             VARIOUS/RACF FAILURE
*
PLGN080  DS    0H                  LOGON SUCCESSFUL
         MVI   WSIGNFLG,SIGNDON
         B     PLGN150             CONTINUE
*
PLGN090  DS    0H                  NEW PASSWORD REQUIRED
         MVC   M1NPSWO,NULL20      BLANK OUT NEW PASSWORD
         MVC   M1NPSWL,MINUS1      POSITION CURSOR ON NEW PSW
         MVC   M1MESSO(L'MESS11),MESS11   SET UP ERROR MESSAGE
         B     SNDSGN20            AND DISPLAY/RETURN
*
PLGN095  DS    0H                  NEW PASSWORD NOT ACCEPTABLE
         MVC   M1OPSWO,NULL20      BLANK OUT OLD PASSWORD
         MVC   M1NPSWO,NULL20      BLANK OUT NEW PASSWORD
         MVC   M1NPSWL,MINUS1      POSITION CURSOR ON NEW PSW
         MVC   M1MESSO(L'MESS14),MESS14   SET UP ERROR MESSAGE
         B     SNDSGN20            AND DISPLAY/RETURN
*
PLGN100  DS    0H                  UNKNOWN USERID
         MVC   M1OPSWO,NULL20      BLANK OUT OLD PASSWORD
         MVC   M1NPSWO,NULL20      BLANK OUT NEW PASSWORD
         MVC   M1USERL,MINUS1      POSITION CURSOR ON USERID
         MVC   M1MESSO(L'MESS15),MESS15  SET UP ERROR MESSAGE
         B     SNDSGN20            AND DISPLAY/RETURN
*
PLGN105  DS    0H                  OLD PASSWORD ERROR
         MVC   M1OPSWO,NULL20      BLANK OUT OLD PASSWORD
         MVC   M1NPSWO,NULL20      BLANK OUT NEW PASSWORD
         MVC   M1OPSWL,MINUS1      POSITION CURSOR ON OLD PSW
         MVC   M1MESSO(L'MESS12),MESS12   SET UP ERROR MESSAGE
         B     SNDSGN20            AND DISPLAY/RETURN
*
PLGN110  DS    0H                  VARIOUS/RACF ERROR
         MVC   M1OPSWO,NULL20      BLANK OUT OLD PASSWORD
         MVC   M1NPSWO,NULL20      BLANK OUT NEW PASSWORD
         MVC   M1USERL,MINUS1      POSITION CURSOR ON USERID
         MVC   M1MESSO(L'MESS13),MESS13   SET UP ERROR MESSAGE
         B     SNDSGN20            AND DISPLAY/RETURN
*
PLGN115  DS    0H                  USERID REVOKED
         MVC   M1OPSWO,NULL20      BLANK OUT OLD PASSWORD
         MVC   M1NPSWO,NULL20      BLANK OUT NEW PASSWORD
         MVC   M1USERL,MINUS1      POSITION CURSOR ON USERID
         MVC   M1MESSO(L'MESS16),MESS16  SET UP ERROR MESSAGE
         B     SNDSGN20            AND DISPLAY/RETURN
*
PLGN120  DS    0H                  USERID ACCESS TO GROUP REVOKED
         MVC   M1OPSWO,NULL20      BLANK OUT OLD PASSWORD
         MVC   M1NPSWO,NULL20      BLANK OUT NEW PASSWORD
         MVC   M1USERL,MINUS1      POSITION CURSOR ON USERID
         MVC   M1MESSO(L'MESS17),MESS17  SET UP ERROR MESSAGE
         B     SNDSGN20            AND DISPLAY/RETURN
*
PLGN125  DS    0H                  NULL OR BLANK USERID GIVEN
         MVC   M1OPSWO,NULL20      BLANK OUT OLD PASSWORD
         MVC   M1NPSWO,NULL20      BLANK OUT NEW PASSWORD
         MVC   M1USERL,MINUS1      POSITION CURSOR ON USERID
         MVC   M1MESSO(L'MESS04),MESS04   SET UP ERROR MESSAGE
         B     SNDSGN20            AND DISPLAY/RETURN
*
PLGN130  DS    0H                  NO PASSWORD GIVEN
         MVC   M1OPSWO,NULL20      BLANK OUT OLD PASSWORD
         MVC   M1NPSWO,NULL20      BLANK OUT NEW PASSWORD
         MVC   M1OPSWL,MINUS1      POSITION CURSOR ON PASSWORD
         MVC   M1MESSO(L'MESS07),MESS07   SET UP ERROR MESSAGE
         B     SNDSGN20            AND DISPLAY/RETURN
*
*
*    AT THIS STAGE THE USER IS SIGNED ON SUCCESSFULLY.
*    NOW CHECK THE ACCOUNT NUMBER WITH RACF TO SEE IF OK.
*
PLGN150  DS    0H
*
         EXEC  CICS                                                    *
               QUERY                                                   *
               SECURITY                                                *
               RESCLASS('ACCTNUM')                                     *
               RESID(WM1ACCT)                                          *
               RESIDLENGTH(8)                                          *
               READ(WCVDA)                                             *
               NOHANDLE
*
         CLC   WCVDA,DFHVALUE(READABLE)     READ ACCESS TO ACCOUNT?
         BE    PLGN155                YES.. CONTINUE
*                                     NO.. SO SIGN-OFF AND TRY AGAIN
         EXEC  CICS                                                    +
               SIGNOFF                                                 +
               NOHANDLE
*
         MVI   WSIGNFLG,SIGNDOF       SET LOGGED-ON FLAG TO NO
         MVC   M1ACCTL,MINUS1         POSITION CURSOR TO ACCOUNT NUMBER
         MVC   M1NPSWO,NULL20         BLANK OUT NEW PASSWORD
         MVC   M1MESSO(L'MESS09),MESS09   SET UP ERROR MESSAGE
         B     SNDSGN20               SEND MESSAGE TO USER
*
PLGN155  DS    0H
*
*    NOW PLUG THE ACCOUNT NUMBER INTO THE TCTTE USER DATA AREA
*    (IF ANY) FOR USE BY THE MONITOR.
*
*
         CLC   TCTUAL,=AL2(TCTUALEN)  COMPARE WITH DSECT LENGTH
         BL    PLGN160               TOO SHORT.. BYPASS
         L     R1,TCTUAADD           ADDRESS OF TCTUA
         C     R1,NOTCTUA            IS THERE ONE?
         BE    PLGN160               NO.. BYPASS
         USING TCTUADS,R1            YES. MAP DSECT
         MVC   TCTUAACT,WM1ACCT      AND SAVE ACCOUNT NUMBER
*
*    USER IS NOW SIGNED ON AND HAS GIVEN A VALID ACCOUNT NUMBER..
*    UPDATE THE USERID/ACCOUNT NUMBER FILE WITH THE LATEST ACCOUNT.
*    IF ANY ERRORS IN THIS STEP, THEN IGNORE.
*
PLGN160  DS    0H
*
         EXEC  CICS                                                    +
               READ                                                    +
               UPDATE                                                  +
               FILE('USRACTNM')                                        +
               INTO(USRACREC)                                          +
               LENGTH(USRACRLN)                                        +
               RIDFLD(WUSERID)                                         +
               EQUAL                                                   +
               NOHANDLE
*
         CLC   EIBRESP,DFHRESP(NOTFND) RECORD NOT FOUND?
         BE    PLGN165                 YES.. WRITE NEW ONE
         CLC   EIBRESP,DFHRESP(NORMAL) NORMAL REPONSE?
         BNE   PLGN170                 NO... BYPASS FILE ACTION
*
         MVC   USRACNUM,WM1ACCT        MOVE IN LAST USED ACCOUNT NUMBER
         MVC   USRACDAT,EIBDATE        MOVE IN DATE
         MVC   USRACTIM,EIBTIME        MOVE IN TIME
         MVC   USRACFIL,NULL20         MOVE IN FILLER
*
         EXEC  CICS                                                    +
               REWRITE                                                 +
               FILE('USRACTNM')                                        +
               FROM(USRACREC)                                          +
               LENGTH(USRACRLN)                                        +
               NOHANDLE
*
         B     PLGN170
*
PLGN165  DS    0H                      NEW RECORD REQUIRED
         MVC   USRACKEY,WUSERID        MOVE IN USERID (KEY)
         MVC   USRACNUM,WM1ACCT        MOVE IN LAST USED ACCOUNT NUMBER
         MVC   USRACDAT,EIBDATE        MOVE IN DATE
         MVC   USRACTIM,EIBTIME        MOVE IN TIME
         MVC   USRACFIL,NULL20         MOVE IN FILLER
*
         EXEC  CICS                                                    +
               WRITE                                                   +
               FILE('USRACTNM')                                        +
               FROM(USRACREC)                                          +
               LENGTH(USRACRLN)                                        +
               RIDFLD(WUSERID)                                         +
               NOHANDLE
*
*   ALL SIGNED-ON OK IF HERE.
*   CHECK THE LOGON EXIT EXEMPTION TABLE TO SEE IF OUR TERMINAL
*   IS IN IT.  THIS COULD HAPPEN IF A PREVIOUS USER WAS SIGNED-ON,
*   ENTERED THE 'CIC' SELECTION TO GET A BLANK SCREEN AND THEN DID
*   A 'CSSF LOGOFF'.  IF THIS HAPPENS, THE NEXT PERSON TO SIGN ON
*   FROM THE SAME TERMINAL COULD BE EVENTUALLY LEFT IN 'NATIVE'
*   CICS.  TO AVOID THIS HAPPENING, WE WILL CHECK THE LEE TABLE
*   AT EVERY SIGN-ON AND IF THE TERMINAL IS IN THERE, WE WILL
*   DELETE IT.
*
PLGN170  DS    0H
         L     R5,CWALEETA             ADDRESS THE LEE TABLE
         LTR   R5,R5                   IS THERE ONE?
         BZ    PLGN185                 NO.. BYPASS
         USING LGNLEEDS,R5             YES. MAP IT
PLGN175  DS    0H
         CLI   LGNLEETM,LGNLEEPE       PHYSICAL EOT?
         BE    PLGN185                 YES. TERM NOT FOUND
         CLI   LGNLEETM,LGNLEELE       FIRST NEVER-USED ENTRY?
         BE    PLGN185                 YES. TERM NOT FOUND
         CLC   LGNLEETM,SAVETERM       OUR TERM IN THIS SLOT?
         BE    PLGN180                 YES. DELETE IT
         LA    R5,LGNLEELN(,R5)        NO.. BUMP TO NEXT ENTRY
         B     PLGN175                 AND TEST IT
PLGN180  DS    0H
         MVI   LGNLEETM,LGNLEERE       MAKE SLOT RE-USABLE
*
*  LEE TABLE PROCESSING ENDED
*
PLGN185  DS    0H
*
*  BLANK OUT GENERIC USERID/PASSWORD AREA IN TCTUA AND CHECK
*  THAT THE USERID WHICH HAS JUST SUCCESSFULLY SIGNED-ON IS
*  GENERIC.  IF IT IS, THEN TRANSFER TO PROGRAM LGNP006.
*
         CLC   TCTUAL,=AL2(TCTUALEN)   COMPARE WITH DSECT LENGTH
         BL    PLGN187                 TOO SHORT.. BYPASS
         L     R1,TCTUAADD             ADDRESS TCTUA
         C     R1,NOTCTUA              IS THERE ONE?
         BE    PLGN187                 NO.. BYPASS
         USING TCTUADS,R1              YES. MAP DSECT
         MVC   TCTUAGID,SPACES20       BLANK USERID SAVE AREA
         MVC   TCTUAGPW,SPACES20       BLANK PASSWORD SAVE AREA
         CLC   WUSERID(L'GENERIC),GENERIC  GENERIC JUST SIGNED ON?
         BNE   PLGN187                 NO.. SKIP
         MVC   TCTUAGID,WUSERID        YES. SAVE GENERIC USERID
         MVC   TCTUAGPW,SAVEPASS            AND THE PASSWORD
         B     MAIN0060                TRANSFER TO LGNP006
PLGN187  DS    0H
         CLI   CWABBIND,C'Y'           BBMESS TO DISPLAY ?
         BNE   PLGN190                 NO...JUST DISPLAY/RETURN
         BAL   R2,CHECKTRM             YES..CHECK FOR TERMINAL MATCH ?
         CLI   BBMATCH,C'Y'            TERMINAL MATCH ?
         BE    BBDISP00                YES...DISPLAY BULLETIN BOARD
PLGN190  DS    0H
         MVI   WSCROLFG,SCROLNO        NOT A SCROLLING REQUEST
         MVC   M1MESSO,MESS01          INITIALIZE MESSAGE
         BAL   R2,SELOPT00             SET UP SELECTION MENU
         CLI   WSELSFLG,SELSYES        ARE THERE ANY SELECTIONS?
         BE    PLGN195                 YES
         MVC   M1MESSO(L'MESS05),MESS05  NO SET UP MESSAGE
PLGN195  DS    0H
         B     SNDMNU0                 AND SEND MENU SCREEN
*
*
* ------------------------------------------------------------------- *
*   PROCESS THE MENU SCREEN
* ------------------------------------------------------------------- *
*
PMNU000  DS    0H
*
*    CHECK TO SEE WHICH ATTENTION KEY PRESSED
*
         CLI   SAVEAID,DFHPF1          HELP
         BE    PMNU005                 YES
         CLI   SAVEAID,DFHPF13         HELP
         BE    PMNU005                 YES
         B     PMNU007                 NO
PMNU005  DS    0H
         MVC   MAPLEN,=AL2(LGNM102L)
         MVI   CMSCRFLG,SCHELP        SET FLAG TO HELP SCREEN SENT
*
         EXEC  CICS                                                    +
               SEND MAP('LGNM102')                                     +
               MAPSET('LGNMS10')                                       +
               FROM(LGNM102O)                                          +
               LENGTH(MAPLEN)                                          +
               ERASE                                                   +
               NOHANDLE
*
         B     RETXID                 RETURN TRANSID/COMMAREA
*
PMNU007  DS    0H
         CLI   SAVEAID,DFHPF5          LOGOFF
         BE    LOGOFF                  YES
         CLI   SAVEAID,DFHPF17         LOGOFF
         BE    LOGOFF                  YES
         CLI   SAVEAID,DFHPF6          MULTI-LOGOFF
         BE    MLTLGF                  YES
         CLI   SAVEAID,DFHPF18         MULTI-LOGOFF
         BE    MLTLGF                  YES
*
         CLI   SAVEAID,DFHPF10         CHANGE USERIDS
         BE    PMNU008                 YES
         CLI   SAVEAID,DFHPF22         CHANGE USERIDS
         BE    PMNU008                 YES
         B     PMNU009                 NO
PMNU008  DS    0H
*
         EXEC  CICS                                                    +
               SIGNOFF                                                 +
               NOHANDLE
*
         B     XCTL00
*
PMNU009  DS    0H
         CLI   SAVEAID,DFHCLEAR        CLEAR SCREEN PRESSED
         BNE   PMNU010                 NO..
         MVC   SAVECLEN,=H'0'          YES.. SET COMMAREA TO ZERO
         B     MAIN0010                AND GO TO START
*
PMNU010  DS    0H
         CLI   SAVEAID,DFHPF2          DISPLAY BULLETIN BOARD
         BE    PMNU020                 YES....
         CLI   SAVEAID,DFHPF14         DISPLAY BULLETIN BOARD
         BE    PMNU020                 YES....
         B     PMNU030                 NO.....CONTINUE
PMNU020  DS    0H                      CHECK FOR ANY MESSAGES FIRST
         CLI   CWABBIND,C'Y'           IS THERE ONE?
         BNE   PMNU024                 NO.. SEND MESSAGE
         BAL   R2,CHECKTRM             YES. CHECK OUT TERMINAL
         CLI   BBMATCH,C'Y'            DOES IT MATCH?
         BE    BBDISP00                YES IT DOES..DISPLAY BB
PMNU024  DS    0H                      NOTHING TO DISPLAY
         MVC   M1MESSO,MESS54          NO..ISSUE MESSAGE
         B     PMNU045                 AND DISPLAY/RETURN
*
PMNU030  DS    0H
         CLI   SAVEAID,DFHPF8          SCROLL THE SELECTION SCREEN
         BE    PMNU040                 YES...
         CLI   SAVEAID,DFHPF20         SCROLL THE SELECTION SCREEN
         BE    PMNU040                 YES
         B     PMNU050                 NO.....CONTINUE
PMNU040  DS    0H
*
*   SCROLL FORWARD
*
         MVI   WSCROLFG,SCROLYES       SCROLLING REQUEST
         MVC   M1MESSO,MESS01          INITIALIZE MESSAGE
         BAL   R2,SELOPT00             SET UP SELECTION MENU
         CLI   WSELSFLG,SELSYES        ARE THERE ANY SELECTIONS?
         BE    SNDMNU0                 YES SEND MENU SCREEN
PMNU045  DS    0H
         MVI   WSCROLFG,SCROLNO        NO.. TURN SCROLLING OFF
         BAL   R2,SELOPT00             SET UP SELECTION MENU
         B     SNDMNU0                 AND SEND MENU SCREEN
*
*   ANYTHING ELSE TREAT AS SELECT AN APPLICATION
*   RECEIVE THE SCREEN
*
PMNU050  DS    0H
*
         EXEC  CICS                                                    +
               RECEIVE                                                 +
               MAP('LGNM101')                                          +
               MAPSET('LGNMS10')                                       +
               INTO(LGNM101I)                                          +
               NOHANDLE
*
         B     XFRAPPL
*
*
*
* ------------------------------------------------------------------- *
*        PROCESS SINGLE LOGOFF REQUEST                                *
* ------------------------------------------------------------------- *
*
LOGOFF   DS    0H
*
         EXEC  CICS                                                    +
               ISSUE                                                   +
               DISCONNECT                                              +
               NOHANDLE
*
         B     RETURN
*
* ------------------------------------------------------------------- *
*        PROCESS MULTI-LOGOFF REQUEST                                 *
* ------------------------------------------------------------------- *
MLTLGF   DS    0H
*
*   CHECK TO SEE IF WE ARE ALREADY SIGNED ON.
*
         CLI   WSIGNFLG,SIGNDON            ARE WE SIGNED ON?
         BNE   LOGOFF                      NO.. DO SINGLE LOGOFF
*
*   GET DATE/TIME AND FORMAT THEM FOR CSMT MESSAGES
*
         EXEC  CICS                                                    +
               ASKTIME                                                 +
               ABSTIME(ABSTIME)
*
         EXEC  CICS                                                    +
               FORMATTIME                                              +
               ABSTIME(ABSTIME)                                        +
               DATESEP('-')                                            +
               DDMMYY(DATETEXT)                                        +
               TIME(TIMETEXT)                                          +
               TIMESEP
*
         MVC   CSMTMSG,MESS55              INIT CSMT MESSAGE
         MVC   CSMTMSG+13(8),DATETEXT      WITH DATE
         MVC   CSMTMSG+22(8),TIMETEXT      AND TIME
         MVC   CSMTMSG+31(8),WUSERID       AND USERID
         MVC   REL,DFHVALUE(RELEASED)      INITIALIZE RELEASED
*
*   START THE SCAN OF THE TCT
*
         EXEC  CICS                                                    +
               INQUIRE                                                 +
               TERMINAL                                                +
               START                                                   +
               NOHANDLE
*
MLTLGF1  DS    0H                     LOOP
*
         EXEC  CICS                                                    +
               INQUIRE                                                 +
               TERMINAL(TEMPTERM)                                      +
               ACQSTATUS(ACQ)                                          +
               ATISTATUS(ATI)                                          +
               TTISTATUS(TTI)                                          +
               REMOTESYSTEM(REMOTEID)                                  +
               USERID(TEMPUSER)                                        +
               NEXT                                                    +
               NOHANDLE
*
         CLC   EIBRESP2,=F'2'         END OF LIST?
         BE    MLTLGF2                YES...
*
         CLC   ACQ,DFHVALUE(ACQUIRED) IS IT ACQUIRED?
         BNE   MLTLGF1                NO.... SKIP TO NEXT TERMID
         CLC   ATI,DFHVALUE(ATI)      IS IT ATI TERMINAL?
         BNE   MLTLGF1                NO.... SKIP TO NEXT TERMID
         CLC   TTI,DFHVALUE(TTI)      IS IT TTI TERMINAL?
         BNE   MLTLGF1                NO.... SKIP TO NEXT TERMID
         CLI   REMOTEID,C' '          IS IT LOCAL?
         BNE   MLTLGF1                NO.... SKIP TO NEXT TERMID
         CLC   TEMPUSER,WUSERID       DOES IT BELONG TO US?
         BNE   MLTLGF1                NO.... SKIP TO NEXT TERMID
         CLC   TEMPTERM,SAVETERM      ARE WE RUNNING ON THIS TERM?
         BE    MLTLGF1                YES... LEAVE TILL LAST
*
*   MUST BE ACQUIRED, ATI, TTI, LOCAL AND OURS
*
         MVC   CSMTMSG+40(4),TEMPTERM TERMID TO CSMT MESSAGE
*
         EXEC  CICS                                                    +
               SET                                                     +
               TERMINAL(TEMPTERM)                                      +
               ACQSTATUS(REL)                                          +
               NOHANDLE
*
         EXEC  CICS                                                    +
               WRITEQ                                                  +
               TD                                                      +
               QUEUE('CSMT')                                           +
               FROM(CSMTMSG)                                           +
               LENGTH(MESS55LN)
*
         B     MLTLGF1                AND PROCESS NEXT TERMID
*
MLTLGF2  DS    0H
*
*   END OF TCT SCAN
*
         EXEC  CICS                                                    +
               INQUIRE                                                 +
               TERMINAL                                                +
               END
*
         MVC   CSMTMSG+40(4),SAVETERM TERMID TO CSMT MESSAGE
*
         EXEC  CICS                                                    +
               WRITEQ                                                  +
               TD                                                      +
               QUEUE('CSMT')                                           +
               FROM(CSMTMSG)                                           +
               LENGTH(MESS55LN)
*
         B     LOGOFF                 AND LOGOFF THIS TERMINAL
*
*
* ------------------------------------------------------------------- *
*   BULLETIN BOARD DISPLAY
* ------------------------------------------------------------------- *
*
BBDISP00 DS    0H
         MVI   CMSCRFLG,SCBBRD        SET FLAG TO BULLETIN BOARD
*
         LA    R3,1                   SET ITEM COUNT
         LA    R4,14                  SET LOOP COUNT
         LA    R5,B1MTABO             ADDRESS SCREEN STRUCTURE
BBDISP01 DS    0H
         STH   R3,BBTSQI              SET UP COUNT
*
         EXEC  CICS                                                    +
               READQ                                                   +
               TS                                                      +
               QUEUE('LGNBBTSQ')                                       +
               ITEM(BBTSQI)                                            +
               INTO(BBDATA)                                            +
               LENGTH(BBTSQL)                                          +
               NOHANDLE
*
         CLC   EIBRESP,DFHRESP(NORMAL)  NORMAL RESPONSE
         BNE   BBDISP03               NO.. PROBABLY QIDERR
*
         MVC   3(L'B1MESSI,R5),BBDATA   MOVE DATA TO SCREEN
         LA    R5,B1MTABL(,R5)          BUMP TO NEXT SCREEN ENTRY
         LA    R3,1(,R3)                INCREMENT ITEM COUNT
         BCT   R4,BBDISP01              LOOP AROUND
*
BBDISP03 DS    0H
         MVC   MAPLEN,=AL2(LGNM103L)
*
         EXEC  CICS                                                    +
               SEND                                                    +
               MAP('LGNM103')                                          +
               FROM(LGNM103O)                                          +
               MAPSET('LGNMS10')                                       +
               LENGTH(MAPLEN)                                          +
               ERASE                                                   +
               NOHANDLE
*
         B     RETXID                 RETURN TRANSID/COMMAREA
*
* ------------------------------------------------------------------- *
*              TRANSFER TO AN APPLICATION                             *
* ------------------------------------------------------------------- *
*
XFRAPPL  DS    0H
*
*   FIRST SCAN SCREEN MENU AND RE-PROTECT ANY PROTECTED ENTRIES
*   ALSO REPLACE ANY SPACE ENTERED WITH A '_'.
*
         LA    R5,M1TABLI             ADDRESS SCREEN MENU TABLE
         LA    R1,SELTABN             MAX NUMBER OF MENU ITEMS
XFR0000  DS    0H
         CLI   3(R5),X'00'            WAS IT PROTECTED?
         BE    XFR0003                YES. RE-PROTECT IT
         CLI   3(R5),C' '             HAS SPACE BEEN ENTERED?
         BNE   XFR0005                NO.. TRY NEXT ENTRY
         MVI   3(R5),C'_'             YES. REPLACE MARKER
         B     XFR0005                AND TRY NEXT ENTRY
XFR0003  DS    0H
         MVI   2(R5),ATTPNS           PROTECT, AUTOSKIP
XFR0005  DS    0H
         LA    R5,M1TABLL(,R5)        BUMP TABLE POINTER
         BCT   R1,XFR0000             AND TRY NEXT ENTRY
*
*   NEXT SCAN SCREEN MENU TO SEE IF ANYTHING SELECTED
*
         LA    R5,M1TABLI             ADDRESS SCREEN MENU TABLE
         LA    R1,SELTABN             MAX NUMBER OF MENU ITEMS
XFR0008  DS    0H
         CLI   3(R5),C'_'             WAS IT SELECTED ?
         BE    XFR0010                NO.. TRY NEXT ENTRY
         CLI   3(R5),X'00'            WAS IT PROTECTED?
         BE    XFR0010                YES. TRY NEXT ENTRY
         MVC   WHLQCDE,7(R5)          PICK UP HLQCODE
         B     XFR0020                     AND PROCESS IT
XFR0010  DS    0H
         LA    R5,M1TABLL(,R5)        BUMP TABLE POINTER
         BCT   R1,XFR0008             AND TRY NEXT ENTRY
*
*  NOTHING SELECTED FROM MENU SELECTION LIST..
*  CHECK 3 CHAR HIDDEN APPLID ON THE SELECTION
*  SCREEN WHICH MAY HAVE BEEN FILLED IN BY A PC FRONT END
*  (OR BY A PERSON).
*  A SPECIAL APLID 'NTV' CAN BE ENTERED BY AUTHORISED USERS
*  WHO ARE USING GUI FRONT ENDS AND NEED ACCESS TO A NATIVE
*  CICS SCREEN.. CHECK FOR THIS AND TREAT AS THOUGH 'CIC' HAD
*  BEEN SELECTED.
*
         CLI   M1APLI,C' '            BLANK APPL?
         BE    XFR0050                YES.. NOTHING SELECTED
         CLI   M1APLI,X'00'           NULL  APPL?
         BE    XFR0050                YES.. NOTHING SELECTED
         MVC   RACFENT,M1APLI         MOVE TO RACF ENTITY
         MVC   RACFSP,=CL5' '         BLANK OUT REST OF ENTITY
         MVC   RACFRC,=C'99'          SET RETURN CODE TO BAD
         MVC   RACFACC,SPACES20       SET ACCESS TO NONE
         BAL   R2,SECHK0              DO THE SECURITY CHECK
         CLC   RACFRC,=C'00'          GOOD CALL?
         BNE   XFR0050                NO... NOTHING SELECTED
         CLC   RACFACC,=C'NONE    '   DOES USER HAVE ACCESS?
         BE    XFR0050                NO... NOTHING SELECTED
         MVC   WHLQCDE,M1APLI         YES. SAVE APPLID
         CLC   M1APLI,=C'NTV'         WAS NTV ENTERED?
         BNE   XFR0020                NO.. JUST CONTINUE
         MVC   WHLQCDE,=C'CIC'        YES. TREAT AS 'CIC' IN MENU
*
*   ITEM SELECTED.. FIND IT IN THE HLQ TABLE TO PICK UP TRANSID
*
XFR0020  DS    0H
         L     R6,CWAHLQTA            ADDRESS HLQ TABLE
         USING LGNHLQDS,R6            MAP HLQ TABLE
XFR0030  DS    0H
         CLI   HLQSCODE,X'FF'         END OF HLQ TABLE?
         BE    XFR0050                NOTHING SELECTED
         CLC   HLQSCODE,WHLQCDE       THIS ENTRY?
         BE    XFR0040                YES.. PROCESS THE TRANSID
         LA    R6,HLQTBLLN(,R6)       BUMP TO NEXT ENTRY
         B     XFR0030                ...AND LOOP AROUND
XFR0040  DS    0H
         CLI   HLQAVAIL,C'N'          APPLICATION AVAIABLE ?
         BE    XFR0060                NO
         MVC   XFRTRAN,HLQTCODE       YES.. PICK UP TRANSID
*
*   NOW START THE TRANSACTION SELECTED
*
         EXEC  CICS                                                    +
               START                                                   +
               TRANSID(XFRTRAN)                                        +
               TERMID(SAVETERM)                                        +
               NOHANDLE
*
         CLC   EIBRESP,DFHRESP(IOERR)
         BE    XFR0070                I/O ERROR
         CLC   EIBRESP,DFHRESP(NOTAUTH)
         BE    XFR0080                NOT AUTHORIZED
         CLC   EIBRESP,DFHRESP(TERMIDERR)
         BE    XFR0090                TERMINAL ID ERROR
         CLC   EIBRESP,DFHRESP(TRANSIDERR)
         BE    XFR0100                TRANSACTION NOT KNOWN
         CLC   EIBRESP,DFHRESP(ISCINVREQ)
         BE    XFR0110                ISC INVALID REQUEST
         CLC   EIBRESP,DFHRESP(SYSIDERR)
         BE    XFR0120                SYSID ERROR
         B     RETURN                 CICS RETURN
*
*
XFR0050  DS    0H                     NOTHING SELECTED
         MVC   M1MESSO,MESS01
         B     SNDMNU0
*
XFR0060  DS    0H                     APPLICATION NOT AVAILABLE
         MVC   M1MESSO,MESS30
         B     SNDMNU0
*
XFR0070  DS    0H                     I/O ERROR
         MVC   M1MESSO,MESS24
         B     SNDMNU0
*
XFR0080  DS    0H                     NOT AUTHORIZED
         MVC   M1MESSO,MESS25
         B     SNDMNU0
*
XFR0090  DS    0H                     TERMID ERROR
         MVC   M1MESSO,MESS26
         B     SNDMNU0
*
XFR0100  DS    0H                     TRANSACTION UNKNOWN
         MVC   M1MESSO,MESS27
         B     SNDMNU0
*
XFR0110  DS    0H                     ISC INVALID REQUEST
         MVC   M1MESSO,MESS28
         B     SNDMNU0
*
XFR0120  DS    0H                     SYSID ERROR
         MVC   M1MESSO,MESS29
         B     SNDMNU0
*
*
*
*
* ------------------------------------------------------------------- *
*              SUBROUTINES                                            *
* ------------------------------------------------------------------- *
*
* ------------------------------------------------------------------- *
*      SETUP MENU SELECTION LIST                                      *
* ------------------------------------------------------------------- *
*      ON ENTRY:
*      R2 --> RETURN ADDRESS
*      ON EXIT:
*      CMHLQOFF CONTAINS ADDRESS OF LAST HLQ TABLE ENTRY DISPLAYED
*      OR IT WILL CONTAIN X'FF' IF NOTHING DISPLAYED
*
SELOPT00 DS    0H
         STM   R2,R7,SAVEREGS        SAVE REGISTERS
         L     R6,CWAHLQTA           ADDRESS THE HLQ TABLE
         USING LGNHLQDS,R6           AND MAP IT
         CLI   WSCROLFG,SCROLYES     IS THIS A SCROLL REQUEST?
         BNE   SELOPT20              NO.. LEAVE REG6 AT START
*
*   SCROLL FORWARD REQUEST
*
         CLI   CMHLQOFF,X'FF'        ANYTHING DISPLAYED PREVIOUSLY?
         BE    SELOPT20              NO.. LEAVE REG 6 UNCHANGED
         L     R1,CMHLQOFF           LOAD OFFSET OF LAST HLQ ENT
         AR    R6,R1                 ADD TO START ADDRESS
         LA    R6,HLQTBLLN(,R6)      INCREMENT FOR NEXT ADDRESS
         CLI   HLQSCODE,X'FF'        END OF TABLE?
         BNE   SELOPT20              NO.. SO USE IT
         L     R6,CWAHLQTA           YES. POINT TO START OF TABLE
SELOPT20 DS    0H
*
*   FIRST CLEAR THE SCREEN MENU TABLE
*
         XR    R4,R4                 CLEAR EVEN/ODD PAIR FOR MOVE
         XR    R5,R5
         LA    R2,M1TABLO            POINT TO START OF THE TABLE
         LA    R3,SELTABL            PICK UP TABLE ENTRY LENGTH
         MVCL  R2,R4                 CLEAR SCREEN TABLE
*
*   PROTECT ALL SELECTION FIELDS IN CASE THE SCREEN ONLY
*   PARTIALLY FILLED
*
         LA    R3,SELTABN            NUMBER OF MENU TABLE ENTRIES
         LA    R4,M1TABLO            START OF SCREEN OUTPUT TABLE
SELOPT30 DS    0H
         MVI   2(R4),ATTPHS          PROTECT ATTRIBUTE
         LA    R4,M1TABLL(,R4)       NEXT ENTRY
         BCT   R3,SELOPT30
*
*   NOW BUILD MENU OF AUTHORIZED SELECTIONS
*
         LA    R3,SELTABN            NUMBER OF MENU TABLE ENTRIES
         LA    R4,M1TABLO            START OF SCREEN OUTPUT TABLE
         MVI   WSELSFLG,SELSNO       SET FLAG TO NO-SELECTIONS
         L     R5,=X'FFFFFFFF'       INITIALIZE R5
         CLI   HLQSCODE,X'FF'        END OF HLQ TABLE?
         BE    SELOPT70              YES
*
SELOPT40 DS    0H
         MVC   RACFENT,HLQSCODE      MOVE IN HLQ FOR RACF ENTITY CHECK
         MVC   RACFSP,=CL5' '        RACFCHEK EXPECTS 8 BYTES NOT 3
         MVC   RACFRC,=C'99'         SET RETURN CODE TO BAD
         MVC   RACFACC,SPACES20      BLANK OUT ACCESS
         BAL   R2,SECHK0             PERFORM THE RACF CHECK
         CLC   RACFRC,=C'00'         WAS CALL GOOD?
         BNE   SELOPT60              NO ..TRY NEXT HLQ ENTRY
         CLC   RACFACC,=C'NONE   '   ...HAS HE GOT ACCESS TO ENTITY
         BE    SELOPT60              NO ..TRY NEXT HLQ ENTRY
*
*   USER HAS ACCESS TO THIS HLQ TABLE ITEM. PUT IT IN MENU
*
         MVI   WSELSFLG,SELSYES      SET FLAG TO SELECTIONS-YES
         MVI   2(R4),X'00'           REMOVE PROTECT ATTRIBUTE
         MVI   3(R4),C'_'            PUT IN SELECT CHARACTER
         MVC   7(3,R4),HLQSCODE      MVC IN HLQ QUALIFIER
         MVC   13(40,R4),HLQDESC     MOVE IN HLQ DESCRIPTION
         LR    R5,R6                 SAVE CURRENT ADDRESS OF HLQ
         CLI   HLQAVAIL,C'Y'         APPLICATION AVAILABLE ?
         BE    SELOPT50              YES..SKIP NEXT BIT OF CODE
         MVC   13(40,R4),MESS99      NO...PUT MESSAGE IN DESCRIPTION
         MVI   2(R4),ATTPNS          PROTECT AUTOSKIP SELECT FIELD
         MVI   3(R4),X'00'           MOVE IN NULL  TO SELECT FIELD
*
SELOPT50 DS    0H
         LA    R6,HLQTBLLN(,R6)      NEXT HLQ TABLE ENTRY
         CLI   HLQSCODE,X'FF'        END OF HLQ TABLE?
         BE    SELOPT70              YES
         LA    R4,M1TABLL(,R4)       BUMP TO NEXT OUTPUT ENTRY
         BCT   R3,SELOPT40
         B     SELOPT70              AND LEAVE
*
SELOPT60 DS    0H                    POINT TO NEXT HLQ TABLE ENTRY
         LA    R6,HLQTBLLN(,R6)
         CLI   HLQSCODE,X'FF'        END OF HLQ TABLE?
         BE    SELOPT70              YES
         B     SELOPT40              NO.. TRY NEXT ENTRY
*
SELOPT70 DS    0H
         ST    R5,CMHLQOFF           SAVE THE HLQ ADDRESS OF LAST
         CLI   CMHLQOFF,X'FF'        ANYTHING DISPLAYABLE?
         BE    SELOPT80              NO
*
*   SINCE THERE WAS AT LEAST ONE DISPLAYABLE MENU ITEM, WE MUST
*   CALCULATE ITS HLQ TABLE OFFSET INCASE THE USER WANTS TO
*   SCROLL THE NEXT SCREEN.
*
         L     R6,CWAHLQTA           ADDRESS THE HLQ TABLE START
         SR    R5,R6                 SUBTRACT LAST DISP ENTRY
         ST    R5,CMHLQOFF           AND SAVE OFFSET IN COMMAREA
SELOPT80 DS    0H
         LM    R2,R7,SAVEREGS        RESTORE REGISTERS
         BR    R2
*
*
* ------------------------------------------------------------------- *
*      SECURITY CHECKING ROUTINE                                      *
* ------------------------------------------------------------------- *
*
*
SECHK0   DS    0H
         STM   R2,R6,SECSAVE         SAVE REGISTERS
*
*      FIND OUT THE RACF USERID FOR THIS CICS REGION.
*      IN ORDER TO CHECK RACF FOR USER'S ACCESS, WE HAVE
*      TO BUILD AN ENTITY STRING TO PASS TO RACF.  THE 17 BYTE
*      STRING HAS THE FORM CCCCCCCC.HLQSSSSS WHERE CCCCCCCC IS
*      THE CICS REGION RACF USERID (MAX 8 CHARACTERS), HLQ IS
*      THE 3 CHAR CODE FROM THE HLQ TABLE AND SSSSS IS 5 SPACES.
*
         XR    R1,R1               ZERO REGISTER
         USING FLC,R1              AND MAP PSA
         L     R1,PSAAOLD          ADDRESS THE OLD ASCB
         USING ASCB,R1             MAP ASCB
         L     R1,ASCBASXB         ADDRESS ASCB EXTENSION BLOCK
         USING ASXBEGIN,R1         MAP ASXB
         L     R1,ASXBSENV         ADDRESS ACEE OF ADDRESS SPACE
         LTR   R1,R1               MAKE SURE THERE IS ONE
         BZ    SECHK8              NO ACEE.. GET OUT
         USING ACEE,R1             YES.. MAP IT
         MVI   ENTITY,C' '         SPACE OUT ENTITY
         MVC   ENTITY+1(L'ENTITY-1),ENTITY
         MVC   ENTITY(8),ACEEUSRI  MOVE ADRESS SPACE USERID TO ENTITY
         XR    R2,R2               CLEAR REGISTER
         IC    R2,ACEEUSRL         1 BYTE LENGTH OF USERID
         LA    R1,ENTITY           ADDRESS ENTITY FIELD
         AR    R1,R2               ADD LENGTH OF USERID
         MVI   0(R1),C'.'          INSERT DOT AFTER USERID PREFIX
         MVC   1(8,R1),RACFENT     MOVE HLQ NAME AFTER DOT
         XR    R6,R6               CLEAR REGISTER
         IC    R6,=X'02'           SET FOR 'READ' CHECKING
*
*   NOW GET ADDRESS OF SIGNED-ON USERS ACEE
*
         EXEC  CICS                                                    X
               ADDRESS                                                 X
               ACEE(R2)
*
         C     R2,NOACEE           IS THERE AN ACEE?
         BE    SECHK8              NO..
         USING ACEE,R2             YES.. MAP IT
         LA    R5,WORKRR
         LA    R4,ENTITY
         LA    R3,WORK
*
         RACROUTE REQUEST=FASTAUTH,                                    X
               WORKA=(R5),         MVS ROUTER WORK AREA                X
               ENTITY=(R4),        ENTITY TO BE CHECKED                X
               CLASS='TCICSTRN',   CLASS                               X
               ATTR=(R6),          R6 -> READ ATTRIBUTE                X
               ACEE=(R2),          R2 -> USER ACEE                     X
               RELEASE=1.8,        RACF VERSION                        X
               WKAREA=(R3)
*
         DS    0H
         LTR   R15,R15             CHECK RETURN CODE FROM RACF
         BNZ   SECHK8              NO READ ACCESS
         MVC   RACFRC,=C'00'       SET RETURN CODE TO '00'
         MVC   RACFACC,=C'READ   ' GOT ACCESS TO ENTITY
         B     SECHK9              AND RETURN
*
SECHK8   DS    0H                  NO READ ACCESS
         MVC   RACFRC,=C'00'       SET RETURN CODE TO '00'
         MVC   RACFACC,=C'NONE   ' NO ACCESS TO ENTITY
*
SECHK9   DS    0H
         LM    R2,R6,SECSAVE       RESTORE REGISTERS
         BR    R2
*
*
* ------------------------------------------------------------------- *
*              CHECK TERMINAL FOR MATCH SUBROUTINE                    *
* ------------------------------------------------------------------- *
*
CHECKTRM DS    0H
         STM   R3,R5,SAVE3R           SAVE REGISTERS
*
         MVI   BBMATCH,C'N'           INITIALIZE TO NO
         CLC   SAVETERM(L'PESTERMS),PESTERMS  POLICE/EMER SERVICES?
         BE    CHECKTM3               YES..DO NOT DISPLAY
         CLC   SAVETERM(L'TWXTERMS),TWXTERMS  TELECOM/MOTORLINK?
         BE    CHECKTM3               YES..DO NOT DISPLAY
         CLC   SAVETERM(L'MONTERMS),MONTERMS  TMON MONITOR?
         BE    CHECKTM3               YES..DO NOT DISPLAY
*
         LA    R1,SAVETERM            POINT TO CURRENT TERM-ID
         LA    R3,CWABBTRM            POINT TO BB CWA TERM-ID
         LA    R5,L'SAVETERM          SET TO LOOP COUNT OF 4
CHECKTM1 DS    0H
         CLI   0(R3),C'*'             IS CWABBTRM CHAR A '*'?
         BE    CHECKTM2               YES ...THEREFORE A MATCH
*
         CLC   0(1,R3),0(R1)          DOES CWABBTRM CHAR = TERM-ID?
         BNE   CHECKTM3               NO  ...THERFORE NOT A MATCH
*                                     YES ...SO TRY NEXT CHAR
         LA    R1,1(,R1)              POINT TO NEXT TERM-ID BYTE
         LA    R3,1(,R3)              POINT TO NEXT CWABBTRM BYTE
         BCT   R5,CHECKTM1            GO AND COMPARE NEXT BYTE
*                                     IF ANY LEFT, OTHERWISE MUST
*                                     HAVE MATCHED ALL 4 CHARS.
CHECKTM2 DS    0H
         MVI   BBMATCH,C'Y'           SET YES FLAG FOR MATCH
*
CHECKTM3 DS    0H
         LM    R3,R5,SAVE3R           RESTORE REGISTERS AND
         BR    R2                     RETURN
*
*
* ------------------------------------------------------------------- *
*        RETURN TO CICS                                               *
* ------------------------------------------------------------------- *
*
RETURN   DS    0H                    SIMPLE RETURN
*
*   REMOVE THE ENQ FROM THE CWA
*
         EXEC  CICS                                                    +
               DEQ                                                     +
               RESOURCE(ENQRES)                                        +
               LENGTH(ENQRESL)
*
         EXEC  CICS                                                    +
               RETURN
*
*
*
RETXID   DS    0H                    RETURN WITH TRANSID/COMMAREA
*
*   REMOVE THE ENQ FROM THE CWA
*
         EXEC  CICS                                                    +
               DEQ                                                     +
               RESOURCE(ENQRES)                                        +
               LENGTH(ENQRESL)
*
         EXEC  CICS                                                    +
               RETURN                                                  +
               TRANSID(TRANID)                                         +
               COMMAREA(COMMAREA)                                      +
               LENGTH(COMMLEN)
*
XCTL00   DS    0H                    TRANSFER TO ANOTHER PROGRAM
*
*   REMOVE THE ENQ FROM THE CWA
*
         EXEC  CICS                                                    +
               DEQ                                                     +
               RESOURCE(ENQRES)                                        +
               LENGTH(ENQRESL)
*
         EXEC  CICS                                                    +
               XCTL                                                    +
               PROGRAM(PROGID)
*
*
*
* ------------------------------------------------------------------- *
*              'LGNP001 - CONSTANTS'                                  *
* ------------------------------------------------------------------- *
MESS01   DC    CL50'<< SELECT AN APPLICATION >>'
MESS03   DC    CL50'<< SIGN ON REQUIRED >>'
MESS04   DC    CL50'<< USERID MUST BE ENTERED >>'
MESS05   DC    CL50'<< NO ACCESS ALLOWED TO ANY APPLICATION >>'
MESS06   DC    CL50'<< ACCOUNT NUMBER MUST BE ENTERED >>'
MESS07   DC    CL50'<< PASSWORD MUST BE ENTERED >>'
MESS08   DC    CL50'<< ACCOUNT NUMBER MUST BE AT LEAST 5 CHARACTERS >>'
MESS09   DC    CL50'<< YOU ARE NOT AUTHORIZED TO USE THAT ACCOUNT >>'
MESS10   DC    CL50'<< ACCOUNT NUMBER MUST BE AT LEAST 6 CHARACTERS >>'
MESS11   DC    CL50'<< NEW PASSWORD REQUIRED >>'
MESS12   DC    CL50'<< PASSWORD NOT AUTHORISED >>'
MESS13   DC    CL50'LGNP001-13F: RACF FAILURE/VARIOUS ERRORS'
MESS14   DC    CL50'<< NEW PASSWORD NOT ACCEPTABLE >>'
MESS15   DC    CL50'<< USERID UNKNOWN TO RACF >>'
MESS16   DC    CL50'<< USERID REVOKED >>'
MESS17   DC    CL50'<< USERID GROUP ACCESS REVOKED >>'
MESS24   DC    CL50'LGNP001-24F: IOERR ON EXEC CICS START COMMAND'
MESS25   DC    CL50'LGNP001-25F: RESOURCE SECURITY CHECK FAILURE'
MESS26   DC    CL50'LGNP001-26F: TERMINAL NOT DEFINED IN TCT'
MESS27   DC    CL50'LGNP001-27F: TRANSACTION NOT DEFINED IN PCT'
MESS28   DC    CL50'LGNP001-28F: ISCINVREQ ON EXEC CICS START'
MESS29   DC    CL50'LGNP001-29F: REQUESTED CICS SYSTEM NOT AVAILABLE'
MESS30   DC    CL50'LGNP001-30F: SELECTED APPLICATION NOT ACTIVE'
MESS50   DC    CL50'LGNP001-50F: NO CICS CWA-CONTACT HELP DESK'
MESS54   DC    CL50'<< * THERE IS CURRENTLY NO BULLETIN FOR YOU * >>'
MESS55   DC    CL57'LGNP001-55I: DD-MM-YY HH:MM:SS USERIDxx TERM MULTI-X
               LOGOFF'
MESS99   DC    CL40'<< ***** APPLICATION UNAVAILABLE ***** >>'
*
*
*
ENQRES   DC    C'CWACICDS'
ENQRESL  DC    Y(L'ENQRES)
         DS    0F
NOCWA    DC    XL4'FF000000'
NOACEE   DC    XL4'FF000000'
NOTCTUA  DC    XL4'FF000000'
SPACES20 DC    CL20' '
NULL20   DC    XL20'00'         NULL
MINUS1   DC    H'-1'
BBTSQL   DC    H'77'
TRANID   DC    CL4'LG01'
PROGID   DC    CL8'LGNP001'
HDACCT   DC    CL4'A/c:'
HDUSER   DC    CL9'User Id :'
HDPASS   DC    CL10'Password :'
HDNPASS  DC    CL14'New Password :'
USRACRLN DC    H'27'            RECORD LENGTH OF USERID/ACCTNUM
PESTERMS DC    CL2'ES'          POLICE AND EMERG SERV TERMS PREFIX
TWXTERMS DC    CL2'XX'          TELECOM MOTORLINK TWX TERMS PREFIX
MONTERMS DC    CL2'TM'          TMON MONITOR TERMS PREFIX
COMMLEN  DC    AL2(CMLENGTH)    LENGTH OF OUR COMMAREA
PESACCT  DC    CL8'X06000  '    POLICE AND EMERG SERV ACCOUNT
TELACCT  DC    CL8'X16000  '    TELECOM MOTORLINK TWX ACCOUNT
MESS55LN DC    H'57'
GENERIC  DC    CL2'G$'          PREFIX OF ALL GENERIC USERIDS
DFLTUSER DC    CL8'CICSUSER'    DEFAULT CICS USERID
*
*
*
         LTORG
         END
