*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
**
**      Title:     'CONTROLLED LOOP'
**      System:    N/A
**      Module:    LOOPIT
**      Program:   LOOPIT
**      Function:  Provides for a controlled looping program for
**                 problem testing without hoging CPU.
**
**                 The program issues a stimer, wait for 1 minute,
**                 every minute for one hour.
**                 The job may be cancelled any time.
**
**      Parameter: You may pass a 2 digit hour parameter to keep the
**                 program looping for. Missing or invalid parameters
**                 will default to 1 hour.
**                 Program is re-entrantr.
**
**      Author:    A. Kara.
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*        PUSH PRINT
*        COPY @MACROS                    COMMON USED MACROS
*        POP  PRINT
*---------------------------------------------------------------------*
LOOPIT   KICKOFF (R12),WORKAREA,WORKAREL,LIST=NO,AMODE=31
                                                                SPACE 1
TESTPARM DS    0H
         LR    R7,R1                ADDRESS OF PARAMETER
         L     R7,0(R7)             LOAD ADDRESS OR PARAMETER
         LH    R9,0(R7)             LOAD LENGTH OF PARAMETER
         LTR   R9,R9                Q. IS PARAMETER LENGTH ZERO
         BZ    NOPARAM                 Y. GO PROCESS 'SYSIN'
OKCONT#1 DS    0H
         LA    R7,2(R7)             POINT PAST PARAMETER LENGTH
         B     PARSEPRM             GO AND PARSE PARAMETER
NOPARAM  DS    0H
         MVC   HOURS(2),=C'00'      INITIALISE HOURS
PRPARAM  DS    0H
         PACK  DWORK8(8),HOURS(2)   PACK TIME
         CVB   R5,DWORK8            CONVERT TO BINARY
         LTR   R5,R5                Q. PARAMETER ENTERED
         BNZ   CONVMIN                 Y. CONVERT TO MINUTES
         LA    R5,60                SET TO 60 MINUTES MINIMUM
CONVMIN  DS    0H
         MH    R5,=H'60'            CONVERT TO SECONDS
                                                                SPACE 1
LOOP#01  DS    0H
         STIMER WAIT,DINTVL=VTIME   WAIT 1 SEC
         BCT   R5,LOOP#01           LOOP FOR 360 MAX SECONDS
EXITPROG RACKOFF RC=0,LIST=NO
                                                                SPACE 1
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
* Parse parameter entered                                             *
*---------------------------------------------------------------------*
PARSEPRM DS    0H
         CLI   0(R7),C'0'           Q. CHARACETER LESS THAN 1
         BL    NOPARAM                 Y. PROCESS AS NO CHARACTER
         MVC   HOURS+1(1),0(R7)     MOVE UNITS
         CH    R9,=H'1'             Q. INPUT DATA > 1 BYTE
         BNH   PRPARAM                 N. GO PROCESS DATA
         CLI   1(R7),C'0'           Q. CHARACETER LESS THAN 1
         BL    NOPARAM                 Y. PROCESS AS NO CHARACTER
         MVC   HOURS(1),HOURS+1     MOVE UNITS TO TENS
         MVC   HOURS+1(1),1(R7)     MOVE UNITS
         B     PRPARAM              GO PROCESS DATA
*=====================================================================*
         LTORG                      LOAD LITERAL POOL HERE
*---------------------------------------------------------------------*
*                  HHMMSSTH
VTIME    DC    CL8'00000100'        TIME INTERVAL
                                                                SPACE 1
WORKAREA DSECT
$REGS    DS    18F                  REGISTER SAVE AREA
DWORK8   DS    D
HOURS    DC    C'00'
WORKAREL EQU   *-WORKAREA
*---------------------------------------------------------------------*
         END
