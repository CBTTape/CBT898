***********************************************************************
* FUNC    : Dynamically add module to LPA list. Program is passed
*           using conventional parameter passing (ie. program should
*           not be used as a command processor).
*           DD(SYSUT1) should be allocated to point to the module
*           containing the source program.
* PARMS   : Program name.
* MACROs  : KICKOFF - startup, register save)
*           RACKOFF - exit, register restore)
*           LPA$MSG - invoke message processing TPUT routine
*           SVC235  - invoke SVC 235 to authorise the program.
*                     Alternatively the program may link-editted into
*                     an APF authorised library.
* RTNS    : LPAMSG  - invoked by LPA$MSG macro for processing messages
* ATTRIB  : AC(1),AMODE(24),RMODE(24)
*                                                                     *
* Source  : MVS update Issue 77, February 1993.
***********************************************************************
LPAADD   KICKOFF (R12),LIST=N
         USING CVT,R3                  (COMMUNICATIONS VECTOR TABLE)
         USING CDENTRY,R4              (CONTENTS DIRECTORY ENTRY)
         USING XTLST,R6                (EXTENT LIST)
*---------------------------------------------------------------------*
* PROCESS EXEC PARAMETER                                              *
*---------------------------------------------------------------------*
         SR    R14,R14                 CLEAR FOR INSERT
         SR    R15,R15                 CLEAR FOR INSERT
         ICM   R14,7,1(R1)             ->EXEC PARAMETER
         BZ    ERROR1                  OMITTED
         ICM   R15,3,0(R14)            -EXEC PARAMETER LENGTH
         BZ    ERROR1                  OMITTED
         CH    R15,=Y(8)               TRUNCATE IF NECESSARY
         BNH   *+8
         LA    R15,8
         BCT   R15,*+10                MOVE TO WORKAREA
         MVC   MODNAME(*-*),2(R14)
         EX    R15,*-6
*---------------------------------------------------------------------*
* CHECK AUTHORIZATION                                                 *
*---------------------------------------------------------------------*
*        TESTAUTH FCTN=1
*        LTR   R15,R15
*        BNZ   ERROR4
*---------------------------------------------------------------------*
* DETERMINE MODULE LENGTH AND AMODE                                   *
*---------------------------------------------------------------------*
         LOAD  EPLOC=MODNAME,          LOAD INTO PRIVATE               +
               ERRET=ERROR6
         N     R1,=A(X'00FFFFFF')      MASK LENGTH BYTES
         SLL   R1,3                    CONVERT LENGTH FROM DOUBLEWORDS
         ST    R1,MODLEN               SAVE FOR LATER
         ST    R0,DW                   SAVE EP ADDRESS IN WORKAREA
         TM    DW,X'80'                31 BIT?
         BZ    *+8                     (NO )
         OI    FLAG$PGM,$PGM31         (YES)
         DELETE EPLOC=MODNAME          DELETE MODULE FROM PRIVATE
*---------------------------------------------------------------------*
* BUILD CDE AND EXTENT LIST IN SQA                                    *
*---------------------------------------------------------------------*
         SVC235 ON                     SET AUTH FLAG ON
         LTR   R15,R15                 Q. AUTHORISATION SET
         BNZ   YJP$EXIT                   N. EXIT PROGRAM
         MODESET MODE=SUP,             GET AUTHORIZED                  +
               KEY=ZERO
         LA    R0,LENCDE+LENXTLST      -LENGTH OF CDE AND XTLST
         GETMAIN RC,LV=(0),            GETMAIN SQA                     +
               SP=245,LOC=BELOW
         LTR   R15,R15                 OK?
         BNZ   ERROR2                  (NO
         LR    R4,R1                   ->CDE
         XC    0(LENCDE+LENXTLST,R4),0(R4) CLEAR STORAGE
         LA    R6,LENCDE(,R4)          ->EXTENT LIST
         ST    R6,CDXLMJP              SAVE EXTENT LIST ADDRESS IN CDE
         MVC   CDNAME,MODNAME          ADD MODULE NAME TO CDE
         OI    CDATTR,CDNIP+CDREN+CDSER+CDNLR ADD ATTRIBUTES TO CDE
         OI    CDATTR2,CDXLE
         LA    R5,16                   -LENGTH OF EXTENT LIST
         ST    R5,XTLLNTH              SET IN EXTENT LIST
         LA    R5,1                    =?
         ST    R5,XTLNRFAC             SET IN EXTENT LIST
         MVI   XTLMSBLA,X'80'          SET ?
         MODESET MODE=PROB,            GET UNAUTHORIZED                +
               KEY=NZERO
*---------------------------------------------------------------------*
* GETMAIN STORAGE FOR MODULE                                          *
*---------------------------------------------------------------------*
         L     R0,MODLEN               -LENGTH OF MODULE
         TM    FLAG$PGM,$PGM31         31 BIT?
         BZ    GETM24                  (NO )
         GETMAIN RC,LV=(0),            GETMAIN ABOVE THE LINE          +
               SP=228,LOC=(ANY,ANY)
         B     CHECKGET
GETM24   GETMAIN RC,LV=(0),            GETMAIN  BELOW THE LINE         +
               SP=228,LOC=BELOW
CHECKGET LTR   R15,R15                 GETMAIN  OK?
         BNZ   ERROR3                  (NO )
         STCM  R1,15,MODADDR           SAVE FOR LATER
*---------------------------------------------------------------------*
* LOAD MODULE INTO CSA                                                *
*---------------------------------------------------------------------*
         MODESET MODE=SUP,             GET AUTHORIZED                  +
               KEY=ZERO
         ICM   R8,15,MODADDR           ->GETMAINED  AREA IN CSA
         OPEN  (DCB,INPUT)             OPEN DCB FOR LOAD
         TM    DCB+DCBOFLGS-IHADCB,X'10'
         BZ    ERROR5
         TM    FLAG$PGM,$PGM31         31 BIT MODE REQUIRED?
         BZ    LOAD                    (NO )
         LA    R14,LOAD                (YES)
         O     R14,=A(X'80000000')
         BSM   R0,R14
LOAD     LOAD  EPLOC=MODNAME,          LOAD MODULE TO CSA              +
               ADRNAPF=(R8),DCB=DCB,                                   +
               ERRET=ERROR7
         STCM  R0,15,MODEP             SAVE ENTRY POINT
         LA    R14,*+6                 SWITCH TO 24 BIT MODE
         BSM   R0,R14
         CLOSE DCB
*---------------------------------------------------------------------*
* UPDATE LPA QUEUE                                                    *
*---------------------------------------------------------------------*
         SETLOCK OBTAIN,               GET LOCAL LOCK                  +
               MODE=UNCOND,                                            +
               TYPE=LOCAL,                                             +
               REGS=USE,                                               +
               RELATED=REQUIRED
         SETLOCK OBTAIN,               GET CMS LOCK                    +
               MODE=UNCOND,                                            +
               TYPE=CMS,                                               +
               REGS=USE,                                               +
               RELATED=REQUIRED
         L     R3,CVTPTR               ->CVT
         L     R8,CVTQLPAQ             ->START OF LPA QUEUE
         ICM   R5,15,CDCHAIN-CDENTRY(R8) ->FIRST CDE ON LPAQ
         ST    R4,CDCHAIN-CDENTRY(R8)  OVERWRITE WITH OUR CDE ADDR
         STCM  R5,15,CDCHAIN           CHAIN OLD FIRST CDE FROM OURS
         L     R6,CDXLMJP              >OUR EXTENT LIST
         ICM   R1,15,MODADDR           ->START OF GETMAINED CSA
         STCM  R1,15,XTLMSBAD          SAVE IN OUR EXTENT LIST
         ICM   R1,15,MODEP             >MODULE ENTRY POINT
         STCM  R1,15,CDENTPT           SAVE IN OUR CDE
         L     R1,MODLEN               -LENGTH OF OUR MODULE
         STCM  R1,B'0111',XTLMSBLN     SAVE IN EXTENT LIST
         SETLOCK RELEASE,              RELEASE ALL LOCKS               +
               TYPE=ALL,                                               +
               REGS=USE,                                               +
               RELATED=REQUIRED
         MODESET MODE=PROB,            GET UNAUTHORIZED                +
               KEY=NZERO
         SVC235 OFF                    SET AUTH FLAG OFF
         SR    R15,R15                 SET OK RETURN CODE
*---------------------------------------------------------------------*
* ISSUE INFORMATION MESSAGES                                          *
*---------------------------------------------------------------------*
         LPA$MSG  MSG=10,TYPE=WTO,TABLE=YJP$MSGT,                      +
               FIELDS=(MODNAME,8,CHAR,MODADDR,4,HEX,MODEP,4,HEX)
         ST    R4,DW
         ST    R6,DW+4
         LPA$MSG  MSG=11,TYPE=WTO,TABLE=YJP$MSGT,                      +
               FIELDS=(DW,4,HEX,DW+4,4,HEX)
*---------------------------------------------------------------------*
* RETURN TO CALLER                                                    *
*---------------------------------------------------------------------*
YJP$EXIT DS    0H
         RACKOFF RC=(R15)
*---------------------------------------------------------------------*
* PARAMETER OMITTED                                                   *
*---------------------------------------------------------------------*
ERROR1   LPA$MSG  MSG=1,TYPE=WTO,TABLE=YJP$MSGT,                       +
               FIELDS=(DW,4,HEX)
         LA    R15,8
         B     YJP$EXIT
*---------------------------------------------------------------------*
* GETMAIN FOR SQA FAILED                                              *
*---------------------------------------------------------------------*
ERROR2   ST    R15,DW                  SAVE RETURN CODE
         MODESET MODE=PROB,            GET UNAUTHORIZED                +
               KEY=NZERO
         SVC235 OFF                    SET AUTH FLAG OFF
         LPA$MSG  MSG=2,TYPE=WTO,TABLE=YJP$MSGT,                       +
               FIELDS=(DW,4,HEX)
         LA    R15,8
         B     YJP$EXIT
*---------------------------------------------------------------------*
* GETMAIN FOR CSA FAILED                                              *
*---------------------------------------------------------------------*
ERROR3   SVC235 OFF                    SET AUTH FLAG OFF
         ST    R15,DW
         LPA$MSG  MSG=3,TYPE=WTO,TABLE=YJP$MSGT,                       +
               FIELDS=(DW,4,HEX)
         LA    R15,12
         B     YJP$EXIT
*---------------------------------------------------------------------*
* NOT RUNNING AUTHORIZED                                              *
*---------------------------------------------------------------------*
ERROR4   LPA$MSG  MSG=4,TYPE=WTO,TABLE=YJP$MSGT
         LA    R15,16
         B     YJP$EXIT
*---------------------------------------------------------------------*
* UNABLE TO OPEN SYSUT1 DDNAME                                        *
*---------------------------------------------------------------------*
ERROR5   LPA$MSG  MSG=5,TYPE=WTO,TABLE=YJP$MSGT
         LA    R15,20
         B     YJP$EXIT
*---------------------------------------------------------------------*
* LOAD INTO PRIVATE FAILED                                            *
*---------------------------------------------------------------------*
ERROR6   ST    R1,DW
         ST    R15,DW+4
         LPA$MSG  MSG=6,TYPE=WTO,TABLE=YJP$MSGT,                       +
               FIELDS=(MODNAME,8,CHAR,DW,4,HEX,DW+4,4,HEX)
         LA    R15,24
         B     YJP$EXIT
*---------------------------------------------------------------------*
* LOAD INTO CSA FAILED                                                *
*---------------------------------------------------------------------*
ERROR7   ST    R1,DW
         ST    R15,DW+4
         MODESET MODE=PROB,            GET UNAUTHORIZED                +
               KEY=NZERO
         SVC235 OFF                    SET AUTH FLAG OFF
         LPA$MSG  MSG=7,TYPE=WTO,TABLE=YJP$MSGT,                       +
               FIELDS=(MODNAME,8,CHAR,DW,4,HEX,DW+4,4,HEX)
         LA    R15,24
         B     YJP$EXIT
*---------------------------------------------------------------------*
* LITERAL POOL                                                        *
*---------------------------------------------------------------------*
         LTORG ,
*---------------------------------------------------------------------*
* DATA AREA                                                           *
*---------------------------------------------------------------------*
                                                                SPACE 1
DW       DC    D'0'                    WORKAREA
MODNAME  DC    CL8' '                  MODULE NAME
MODADDR  DC    A(0)                    MODULE START ADDRESS IN CSA
MODLEN   DC    A(0)                    MODULE LENGTH
MODEP    DC    A(0)                    MODULE ENTRY POINT ADDRESS
FLAG$PGM DC    X'00'                   PROGRAM FLAGS :
$PGM31   EQU   X'80'                   MODULE RESIDES ABOVE THE LINE
DCB      DCB   DDNAME=SYSUT1,          DCB FOR PROGRAM LOAD            +
               MACRF=GL,DSORG=PS
*---------------------------------------------------------------------*
* MAPPING MACROS                                                      *
*---------------------------------------------------------------------*
         CVT   DSECT=YES,LIST=NO       COMMUNICATIONS VECTOR TABLE
         DCBD  DSORG=(PO,PS),DEVD=DA   DATA CONTROL BLOCK
         IHACDE                        CONTENTS DIRECTORY ENTRY
LENCDE   EQU   *-CDENTRY
         IHAPSA DSECT=YES,LIST=NO      PREFIXED SAVE AREA
         IHAXTLST                      EXTENT LIST
LENXTLST EQU   *-XTLST
*---------------------------------------------------------------------*
* MESSAGE TABLE                                                       *
*---------------------------------------------------------------------*
YJP$MSGT CSECT ,
         DC    AL2(1,L'M001)
M001     DC    C'LPA001E EXEC parameter omitted'
         DC    AL2(2,L'M002)
M002     DC    C'LPA002E GETMAIN for SQA failed: RC=X''%'''
         DC    AL2(3,L'M003)
M003     DC    C'LPA003E GETMAIN for CSA failed; RC=X''%'''
         DC    AL2(4,L'M004)
M004     DC    C'LPA004E Program is not running authorized'
         DC    AL2(5,L'M005)
M005     DC    C'LPA005E Unable to OPEN SYSUTI DDNAME'
         DC    AL2(6,L'M006)
M006     DC    C'LPA006E Unable to LOAD %; ABEND=X''%'' R15=X''%'''
         DC    AL2(7,L'M007)
M007     DC    C'LPA007E Unable to LOAD % into CSA; ABEND=X''%'' R15=X'+*
               '%'''
         DC    AL2(10,L'M010)
M010     DC    C'LPA010I Module % LOADed at X''%'' Entry point X''%'''
         DC    AL2(11,L'M011)
M011     DC    C'LPA011I CDE at X''%'' XTLST at X''%'''
         DC    X'FFFFFFFF'
*---------------------------------------------------------------------*
* END OF PROGRAM                                                      *
*---------------------------------------------------------------------*
         END   ,
