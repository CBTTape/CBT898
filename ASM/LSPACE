         TITLE 'LSPACE - Obtain DASD Volume Information'
***********************************************************************
*                                                                     *
* LSPACE AC(0),AMODE(31),RMODE(ANY)                                   *
*                                                                     *
* FUNCTION   : This is a REXX external function routine that returns  *
*              DASD volume information by using the LSPACE macro.     *
*              The length of the data returned is 36 bytes, unless    *
*              an error is returned from the LSPACE macro.  This will *
*              be indicated by the length of the data returned - the  *
*              actual data in this case will itself be invalid, only  *
*              the length will be significant.                        *
*                                                                     *
* INVOCATION : Use as any normal REXX function.  For example:         *
*                                                                     *
*              DATA = LSPACE(hex_dasd_ucb_addr)                       *
*                                                                     *
***********************************************************************
*
***********************************************************************
* Housekeeping code...                                                *
***********************************************************************
LSPACE   CSECT
LSPACE   AMODE 31
LSPACE   RMODE ANY
         SAVE  (14,12),,'LSPACE.&SYSDATE'
         BASR  R12,0
         USING *,R12
         LA    R2,SAVE                      -> My savearea
         ST    R13,4(R2)                    -  Callers savearea addr
         ST    R2,8(R13)                    -  My savearea addr
         LR    R13,R2                       -> My savearea
         TITLE 'LSPACE - MAINLINE CODE'
***********************************************************************
* Get the parameter info...                                           *
***********************************************************************
         LR    R11,R1                       -> EFPL
         USING EFPL,R11
         L     R10,EFPLARG                  -> Argument table
         USING ARGTABLE_ENTRY,R10
         L     R8,EFPLEVAL                  -> Address of Evalblock
         L     R9,0(0,R8)                   -> Evalblock
         USING EVALBLOCK,R9
***********************************************************************
* Get Address Space ID...                                             *
***********************************************************************
         L     R8,ARGTABLE_ARGSTRING_PTR    -> UCB_ADDR field
         C     R8,=F'-1'                    -  End of parmlist ?
         BNE   GETUCB
         LA    R15,20                       -  Signal error
         B     EXIT                         -  Exit
GETUCB   L     R7,ARGTABLE_ARGSTRING_LENGTH -  Length of UCB field
         C     R7,=F'4'                     -  Is length equal 4 bytes
         BE    MVCUCB                       -  Yes, get UCB address
         LA    R15,20                       -  Signal error
         B     EXIT                         -  Exit
MVCUCB   ICM   R3,B'1111',0(R8)             -> UCB
         LA    R5,36                        -  Length required
         ST    R5,EVALBLOCK_EVLEN           -  Actual length of data
         LA    R4,EVALBLOCK_EVDATA          -> Output area
***********************************************************************
* Issue the LSPACE Macro...                                           *
***********************************************************************
         LSPACE UCB=(R3),DATA=(R4),MF=I
         LTR   R15,R15
         BZ    EXIT
         ST    R15,EVALBLOCK_EVLEN          -  Indicate RC in length
         LA    R15,0                        -  Reset RC to zero
         TITLE 'LSPACE - RETURN TO CALLER'
***********************************************************************
* Return to caller...                                                 *
***********************************************************************
EXIT     L     R13,SAVE+4                   -> Callers savearea
         L     R14,12(,R13)
         LM    R1,R12,24(R13)
         BSM   R0,R14                       -  Return with RC in R15
         TITLE 'LSPACE - SAVE/WORKAREA'
***********************************************************************
* Register Savearea and Workarea...                                   *
***********************************************************************
SAVE     DS    0F
         DC    C'PL1 PSA NSA RET EP  R0  R1  R2  R3  '
         DC    C'R4  R5  R6  R7  R8  R9  R10 R11 R12 '
***********************************************************************
* Literal pool...                                                     *
***********************************************************************
         LTORG
         TITLE 'LSPACE - MACROS'
***********************************************************************
* DSECTS...                                                           *
***********************************************************************
         IRXEFPL
         IRXARGTB
         IRXEVALB
         YREGS
         END
