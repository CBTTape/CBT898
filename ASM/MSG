*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
**
**       Title:    Leave a displayed text at a terminal
**       System:   N/A
**       Module:   MSG
**       Program:  MSG
**       Function: Takes the message from the command processor and
**                 places it into a template for a fullscreen TPUT.
**
**                 Use as an example of 3270 data stream character
**                 attributes.
**
**       Notes:    Program is not re-entrant.
**       Author:   A. Kara.
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*        PRINT OFF
*        COPY  @MACROS                 COMMON MACROS
*        PRINT ON
MSG      KICKOFF (R12)
         LR    R7,R1                   CPPL ADDR
         USING CPPL,R7                 ADDRESABILITY TO CPPL
         L     R6,CPPLCBUF             GET COMMAND BUFFER ADDR
         LH    R1,0(R6)                TOTAL COMMAND LENGTH
         SH    R1,=H'2'                SUBTRACT LENGTH OF ITSELF
         LH    R3,2(R6)                OPERAND LENGTH
         SR    R1,R3                   Q. ANY PARAMETER LENGTH
         BZ    DISPLAY                    N. DISPLAY WITHOUT DATA
         SH    R1,=H'2'                SUBTRACT LENGTH OF OPERAND
         LA    R2,4(R6)                ADDR OF START OF COMMAND
         AR    R2,R3                   ADD DISPLACEMENT OF OPERAND
         CH    R1,OUTLEN               Q. OPERAND LENGTH AREA
         BNH   CONT#100                   N. BYPASS
         LH    R1,OUTLEN               SET OPERAND LENGTH AREA LENGTH
CONT#100 DS    0H
         LTR   R1,R1                   Q. ANY PARAMETER SUPPLIED
         BNZ   CONT#200                   Y. PROCESS
         LA    R2,SAMPLE               POINT TO SAMPLE
         LA    R1,L'SAMPLE             LOAD SAMPLE LENGTH
CONT#200 DS    0H
         XR    R4,R4                   INIT EVEN PAIR
         LH    R5,OUTLEN               MAX LENGTH
         SR    R5,R1                   SUBTRACT LENGTH OF DATA
         D     R4,=F'2'                DIVIDE BY 2
         LA    R4,OUTPUT+3             DESTINATION
         LTR   R1,R1                   Q. ANY PARAMETER SUPPLIED
         SLL   R5,2                    MULTIPLY BY 4
         AR    R4,R5                   16-LENGTH/2
MOVLOOP  DS    0H
         MVC   0(1,R4),0(R2)           MOVE CHARACTER
         LA    R4,4(R4)                POINT TO NEXT DESTINATION
         LA    R2,1(R2)                POINT TO NEXT SOURCE
         BCT   R1,MOVLOOP              LOOP TILL END
DISPLAY  DS    0H
         TPUT  MSG1,SCRSZMSG,NOEDIT,WAIT,,BREAKIN,HIGHP
INPUTIT  DS    0H
         TGET  REPLY,L'REPLY,ASIS,WAIT
EXITPR00 DS    0H
         RACKOFF RC=0
*=====================================================================*
         LTORG
OUTLEN   DC    H'74'
         DS    0F
REPLY    DS    CL80
SAMPLE   DC    C'Hello there big boy.....'
*        COPY  MSGR1
*---------------------------------------------------------------------*
*
* THE L.B.D. U BYUTE UNPATTENTED SYS.
*
MSG1     WCCDC W,,COLOR
*        SBADC 02,14,UHN,R,O
*ZCMD    DC    CL65' '
         ICDC  01,01
         RADC  02,01,PHN,Y,R,02,80,' '
         SBADC 02,80,PHN,Y,O
         SBADC 03,01,PHN,Y,R
         DC    CL01' '
         SBADC 03,03,PHN,T,O
         DS    0CL74
OUTPUT   CADC  TEXTL='                                                 +
                                        ',                             +
               TEXTC='ybwptrgybwptrgybwptrgybwptrgybwptrgybwptrgybwptrg+
               ybwptrgybwptrgybwptrgybwp'
         SBADC 03,78,PHN,Y,R
         DC    CL01' '
         SBADC 03,80,PHN,Y,O
         RADC  04,01,PHN,Y,R,04,80,' '
         SBADC 04,80,PHN,Y,O
SCRSZMSG EQU   *-MSG1
*---------------------------------------------------------------------*
         IKJCPPL COMMAND                PROCESSOR PARAMETER LIST
         END
