***********************************************************************
*                                                                     *
*    Title:    'MESSAGE FROM EDIT SESSION'                            *
*    System:   N/A                                                    *
*    Module:   MSGE                                                   *
*    Program:  MSGE                                                   *
*    Function: The purpose of this program is to send a file as a     *
*              series of TPUTs to a specific user. Generally used to  *
*              send multi line messages in one hit.                   *
*                                                                     *
*              Tho 8 character parameters are may pe passed to this   *
*              program. The first is the destination userid and is    *
*              mandatory. The second is an optional userid to be      *
*              notified of the start and end of message transmission. *
*              This second parameter is usefull as the program is     *
*              designed to run in batch. Sending multi screen         *
*              messages on-line will lock the senders terminal until  *
*              the entire message is received which could be quite    *
*              timely if the receiver does not hit the <ENTER> key    *
*              regularly.                                             *
*              Batch jobs may have to wait for other jobs during      *
*              busy batch sessions.                                   *
*              The text to be sent, has to be allocated to 'SYSIN'    *
*              DDname.                                                *
*                                                                     *
*                                                                     *
*    Notes:    Program is re-entrant                                  *
*              AMODE 31, RMODE any.                                   *
*    Author:   ASYSTS Pty. Ltd. (A. Kara)                             *
*                                                                     *
***********************************************************************
                                                                SPACE 1
MSGEV     KICKOFF (R11,R12),WORKAREA,WORKAREL,LIST=N
*---------------------------------------------------------------------*
*   GET PARAMETER                                                     *
*---------------------------------------------------------------------*
         LR    R7,R1                   SAVE CPL POINTER
         CLI   0(R7),X'80'             Q. CALL TYPE PARAMETER LIST
         BE CALLTYPE                      Y. PROCESS SUCH
         USING CPPL,R7                 ADDRESABILITY TO CPPL
         L     R6,CPPLCBUF             GET COMMAND BUFFER ADDR
         LH    R4,0(R6)                TOTAL COMMAND LENGTH
         SH    R4,=H'2'                SUBTRACT LENGTH OF ITSELF
         LH    R3,2(R6)                OPERAND LENGTH
         SR    R4,R3                   Q. ANY PARAMETER LENGTH
         BZ    EXITPR12                   N. ERROR EXIT
         SH    R4,=H'2'                SUBTRACT LENGTH OF OPERAND
         LA    R2,4(R6)                ADDR OF START OF COMMAND
         AR    R2,R3                   ADD DISPLACEMENT OF OPERAND
         B     PROCESS                 GO PROCESS PARAMETER
CALLTYPE DS    0H
         L     R2,0(R7)                ADDRESS OF PASSED PARAMETER
         XR    R3,R3                   CLEAR REG 3
         ICM   R3,B'0011',0(R2)        GET LENGTH OF PARAMETER
         LR    R4,R3                   USE R1 TO TEST PARAMETER LENGTH
         LTR   R4,R4                   Q. ANY PARAMETER LENGTH
         BZ    EXITPR12                   N. ERROR EXIT
         LA    R2,2(R2)                POINT TO VOLSER PART
PROCESS  DS    0H
*---------------------------------------------------------------------*
         MVC   RECEIVER(16),SPACES     INITIALISE FIELDS
         EX    R4,MOVEPARM             EXECUTE MOVE PARAMETERS
         OC    RECEIVER(16),SPACES     CONVERT TO UPPER CASE
*---------------------------------------------------------------------*
*   TEST IF SYSIN DDNAME ALLOCATED                                    *
*---------------------------------------------------------------------*
         MVC   OMESSAGE,SPACES         INITIALISE DSN DESTINATION
         LA    R10,SVC99WRK            POINT TO SVC 99 WORK AREA
         ADYN  MF=E,ACTION=INFO,BASE=R10,DD=SYSIN,                     X
               PARMT=PARM,DSN=OMESSAGE,DSNR=Y
         LTR   R15,R15                 Q. ALLOCATE OK
         BNZ   NOSYSIN                    N. PROCESS AS ERROR
         CLC   =C'NULLFILE',OMESSAGE   Q. NULLFILE ALLOCATED
         BNE   INITIAL1                   N. GO PROCESS SYSIN
NOSYSIN  DS    0H
         CLC   SENDER(7),SPACES        Q. NOTIFY USERID
         BE    EXITPR12                   N. EXIT WITH ERROR CODE
         MVC   OMESSAGE(44),=C'MISSING SYSIN DDNAME OR ''NULLFILE'' ALL+
               OCATED'
         TPUT  OMESSAGE,44,EDIT,WAIT,,BREAKIN,HIGHP,USERIDL=SENDER
         B     EXITPR12                EXIT WITH ERROR CODE
*---------------------------------------------------------------------*
INITIAL1 DS    0H
         MVC   OMESSAGE,SPACES
         MVC   SYSIN2(SYSIN1L),SYSIN1    MOVE LIST AREA TO GETMAIN
         MVC   OPENWK2(OPENWK1L),OPENWK1 MOVE LIST AREA TO GETMAIN
PROCINFL DS    0H          Y. GO PROCESS SYSIN
*---------------------------------------------------------------------*
*    Open dataset                                                     *
*---------------------------------------------------------------------*
         LA    R1,OPENWK2                LOAD LIST ADDRESS
         LA    R2,SYSIN2                 LOAD DCB ADDRESS
         OPEN  ((R2),(INPUT)),MF=(E,(R1)) OPEN INPUT FILE
         TM    48(R2),X'10'              Q. DID SYSIN OPEN O.K.
         BO    READ                         Y. PROCESS RECORDS
OPENERR  DS    0H
         CLC   SENDER(7),SPACES        Q. NOTIFY USERID
         BE    EXITPR12                   N. EXIT WITH ERROR CODE
         MVC   OMESSAGE(28),=C'ERROR OPENING ''SYSIN'' DDNAME'
         TPUT  OMESSAGE,28,EDIT,WAIT,,BREAKIN,HIGHP,USERIDL=SENDER
         B     EXITPR12                EXIT WITH ERROR CODE
*---------------------------------------------------------------------*
*    PROCESS INPU DATA                                                *
*---------------------------------------------------------------------*
READ     DS    0H
         LA    R2,SYSIN2               LOAD DCB ADDRESS
         LA    R3,78                   LENGTH OF THE MOVE
         CLC   SENDER(7),SPACES        Q. NOTIFY USERID
         BE    READLOOP                   N. BYPASS
         TPUT  SMESS,LSMESS,EDIT,WAIT,,BREAKIN,HIGHP,USERIDL=SENDER
READLOOP DS    0H
         GET   (R2),OMESSAGE           READ SYSIN
*        TPUT  OMESSAGE,(3),EDIT,NOWAIT,,BREAKIN,HIGHP,USERIDL=RECEIVER
         TPUT  OMESSAGE,(3),EDIT,WAIT,,BREAKIN,HIGHP,USERIDL=RECEIVER
*        LTR   R15,R15                 Q. ZERO RETURN CODE
*        BZ    READLOOP                   Y. LOOP TILL END
         B     READLOOP                LOOP TILL END
EXITERR  DS    0H
         CLC   SENDER(7),SPACES        Q. NOTIFY USERID
         BE    CLOSFILE                   N. GO CLOSE FILE
         TPUT  EMESS,LEMESS,EDIT,WAIT,,BREAKIN,HIGHP,USERIDL=SENDER
         B     CLOSFILE                GO CLOSE FILE
*---------------------------------------------------------------------*
INFEND   DS    0H           (END of INFILE)
         CLC   SENDER(7),SPACES        Q. NOTIFY USERID
         BE    CLOSFILE                   N. BYPASS
         TPUT  FMESS,LFMESS,EDIT,WAIT,,BREAKIN,HIGHP,USERIDL=SENDER
*---------------------------------------------------------------------*
*    Close dataset                                                    *
*---------------------------------------------------------------------*
CLOSFILE DS    0H
         LA    R1,OPENWK2              LOAD LIST ADDRESS
         CLOSE (R2),MF=(E,(R1))        CLOSE INPUT FILE
EXITPR00 DS    0H
         RACKOFF RC=0                 EXIT WITH RC=0
         RACKOFF RC=8                 EXIT WITH RC=8
EXITPR12 RACKOFF RC=12                EXIT WITH RC=12
*---------------------------------------------------------------------*
*        MOVE OMESSAGE                                                *
*---------------------------------------------------------------------*
MOVEPARM MVC   RECEIVER(0),0(R2)      MOVE OMESSAGE
*---------------------------------------------------------------------*
         LTORG
*
SPACES   DC    CL256' '
SMESS    DC    C'STARTING TO SEND OMESSAGE.'
LSMESS   EQU   *-SMESS
EMESS    DC    C'OMESSAGE NOT RECEIVED.'
LEMESS   EQU   *-EMESS
FMESS    DC    C'FINISHED SENDING OMESSAGE.'
LFMESS   EQU   *-FMESS
                                                                SPACE 1
SYSIN1   DCB   DDNAME=SYSIN,                                           +
               DSORG=PS,                                               +
               MACRF=(GM),                                             +
               EODAD=INFEND
SYSIN1L EQU    *-SYSIN1
*
OPENWK1  OPEN  (,(INPUT)),MF=L
OPENWK1L EQU   *-OPENWK1
                                                                SPACE 1
WORKAREA DSECT
         DS    18F                    REGISTER SAVE AREA
RECEIVER DC    CL8'ZTSALK'
SENDER   DC    CL8'ZTSALK'
SYSIN2   DCB   DDNAME=SYSIN,                                           +
               DSORG=PS,                                               +
               MACRF=(GM),                                             +
               EODAD=INFEND
OPENWK2  OPEN  (,(INPUT)),MF=L
         ADYN  MF=L                   LIST FORMAT OF ADYN
OMESSAGE DS    CL80
SVC99WRK DS    CL400
WORKAREL EQU   *-WORKAREA
                                                                SPACE 1
         IKJCPPL COMMAND              PROCESSOR PARAMETER LIST
         ADYN  DATA=IBM               IBM SUPPLIED DSECTS
         END
