*
**       TITLE:    NON-REUSABLE ADDRESS SPACES
**       SYSTEM:   N/A
**       MODULE:   NONRASCB
**       PROGRAM:  NONRASCB
**       FUNCTION: ACCUMULATE ALL THE NON-REUSABLE ADDRESS SPACES
**                 AND DISPLAY IN NEXT ISPF PANEL ON THE MESSAGE LINE.
**                 NON-REUSABLE ADDRESSES ARE CAUSED BY DB2 COMMING
**                 DOWN WITH CROSS MEMORY FACILITIES BETWEEN CICS AND
**                 DB2 NOT BEING CLOSED. THE ADDRESS SPACE IS MARKED
**                 NON-REUSABLE IN ORDER TO PREVENT ANOTHER TASK USING
**                 THAT ADDRESS SPACE WITH CROSS MEMORY STILL OPEN TO
**                 CICS.
**
**                 PARAMETERS:
**                     ENVIRONMENT INDICATOR (1 CHAR)
**                        I - ISPF  (DISPLAY ON ISPF MESSAGE LINE)
**                        B - BATCH (DISPLAY ON CONSOLE)
**                        ANYTHIN ELSE IS TREATED AS NON-ISPF AND
**                          'TPUT' USED
**                        (DEFAULT IS NON-ISPF)
**                     HIGH WATER MARK       (UP TO 3 NUMERIC CHARS)
**                         MUST BE LESS THAN PERMISSABLE ADDRESS
**                         SPACES. IF HIGHER, THE APPROPRIATE TYPE
**                         OF DISPLAY IS USED FOR THE ERROR MESSAGE.
**
**       AUTHOR:   A. KARA.
**       DATE:     09/04/91.
*
*=====================================================================*
NONRASCB KICKOFF (R12),WORKAREA,WORKAREL
         LR    R7,R1                ADDRESS OF PARAMETER
         L     R7,0(R7)             LOAD ADDRESS OR PARAMETER
         LH    R9,0(R7)             LOAD LENGTH OF PARAMETER
         MVC   GC009,GC009S         INITIALISE MESSAGE
         MVC   HWM(3),=C'000'       INITIALISE HIGH WATER MARK
         MVI   ENVMT,C'N'           INITIALISE FOR NON ISPF ENVIRONMENT
         LTR   R9,R9                Q. IS PARAMETER LENGTH ZERO
         BZ    NOPARAM                 Y. GO PROCESS 'SYSIN'
OKCONT#1 DS    0H
         LA    R7,2(R7)             POINT PAST PARAMETER LENGTH
         MVC   ENVMT(1),0(R7)       MOVE ENVIRONMENTN
         LA    R2,HWM+3             LAST BYTE OF DESTINATION
         BCTR  R9,0                 LESS 1 FOR ENVIRONMENT INDICATOR
         SR    R2,R9                SUBTRACT LEGTH OF PARAMETER
         BCTR  R9,0                 LESS 1 FOR EX COMMAND
         EX    R9,MOVEHWM           MOVE HIGH WATER MARK
         CLI   HWM,C'0'             Q. VALID BYTE 1
         BL    INVHWM                  N. DISPLAY INVALID MESSAGE
         CLI   HWM+1,C'0'           Q. VALID BYTE 2
         BL    INVHWM                  N. DISPLAY INVALID MESSAGE
         CLI   HWM+2,C'0'           Q. VALID BYTE 3
         BNL   NOPARAM                 Y. PROCESS PARAMETER
INVHWM   DS    0H
         MVC   GC009,SPACES         CLEAR MESSAGE AREA
         MVC   GC009(51),=C'INVALID HIGH-WATER-MARK PARAMETER. MUST BE N
               NUMERIC.'
         B     SETMSG               GO DISPLAY MESSAGE
*---------------------------------------------------------------------*
MOVEHWM  MVC   0(0,R2),1(R7)        MOVE HWM
*---------------------------------------------------------------------*
NOPARAM  DS    0H
         PACK  DWORK8(8),HWM(3)     PACK HIGH WATER MARK
         CVB   R1,DWORK8            CONVERT TO BINARY
         STH   R1,HWM               STORE AS HALF WORD
*---------------------------------------------------------------------*
*                                                                     *
* GET THE "ASVT":                                                     *
*   1. 0 + X'10'   => CVT @                                           *
*   2. CVT + X'22C' => ASVT @                                         *
*   3. ASVT + X'204' => NO. OF ASVT ENTRIES                           *
*   4. ASVT + X'210' => FISRST ACSB POINTER                           *
*                                                                     *
*   X'80' IN THE HIGH ORDER BYTE OF THE ASCB POINTER INDICATES A NON- *
*   REUSABLE ASCB.                                                    *
*---------------------------------------------------------------------*
         L     R3,X'10'             CVT POINTER
         L     R3,X'22C'(R3)        ASVT POINTER
         L     R8,X'204'(R3)        # ASVT ENTRIES
         CH    R8,HWM               Q. HWM < # ADDRESS SPACES
         BH    HWMOK                   Y. BYPASS ERROR
         UNPACK R8,3,GC009+7,MF=E   UNPACK TOTAL ADDRESS SPACES
         MVC   GC009,SPACES         CLEAR MESSAGE AREA
         MVC   GC009(72),=C'HIGH-WATER-MARK PARAMETER MUST BE LESS THAN+
                NO. OF ADDRESS SPACES (...).'
         UNPACK R8,3,GC009+67,MF=E  UNPACK TOTAL ADDRESS SPACES
         B     SETMSG               GO DISPLAY MESSAGE
HWMOK    DS    0H
         UNPACK R8,3,GC009+7,MF=E   UNPACK TOTAL ADDRESS SPACES
         LA    R3,X'210'(R3)        1ST. ASVT ENTRY
         ST    R3,NRAADRS           STORE NON REUSABLE ADDRESS
         OI    NRAADRS,X'80'        SET NON-REUSABLE FLAG MASK
         XR    R7,R7                INITIALISE COUNTER
ASVTLOOP DS    0H
         CLC   0(4,R3),NRAADRS      Q. NON-REUSABLE ADDRESS
         BNE   NEXTASVT                N. GET NEXT ASVT
         LA    R7,1(R7)             UP NO REUSABLE COUNTER
NEXTASVT DS    0H
         LA    R3,4(R3)             POINT TO NEXT ASVT
         BCT   R8,ASVTLOOP          LOOP TILL END
ASVTEEND DS    0H
         CH    R7,HWM               Q. PASSED HIGH WATER MARK
         BL    EXITPROG                N. EXIT PROGRAM
*---------------------------------------------------------------------*
         UNPACK R7,3,GC009,MF=E     UNPACK NON-REUSE COUNT
         CLI   GC009,C'0'           Q. LEADING ZERO
         BNE   SETMSG                  N. BYPASS
         LA    R1,0                 INITIALISE COUNTER
         CLI   GC009+1,C'0'         Q. LEADING ZERO
         BNE   REMZEROS                N. REMOVE ZERO
         LA    R1,1(R1)             UP COUNTER BY 1
REMZEROS DS    0H
         LA    R2,79                LENGTH OF ENTRY
         SR    R2,R1                SUBTRACT ZERO LENGTHS
         LA    R3,GC009             START OF FIELD
         AR    R3,R1                START OF NON ZERO CHAR
         EX    R2,RZEROS            REMOVE ZEROS
         LA    R3,GC009             START OF FIELD
         AR    R3,R2                LAST CHARACTER OF FIELD
         EX    R1,MSPACES           ADD TRAILIN SPACES
SETMSG   DS    0H
         CLI   ENVMT,C'I'           Q. ISPF ENVIRONMENT
         BNE   WTOMSG                  N. NEXT TEST
         ISPINIT  MF=E              INITIALISE ISPF ENVIRONMENT
         ISPVDEFX INM=(GC009),ANM=GC009,FORMAT=CHAR,LEN=79,MF=E
         ISPEXEC 'SETMSG MSG(GC009)'
         B     EXITPROG             EXIT PROGRAM
WTOMSG   DS    0H
         CLI   ENVMT,C'B'           Q. BATCH ENVIRONMENT
         BNE   TPUTMSG                 N. NEXT TEST
         MVC   WTOWORK(WTOSKELL),WTOSKEL
         LA    R2,WTOWORK              LOAD ADDRESS OF WTO AREA
         MVC   4(79,R2),GC009       MOVE BUFFER DETAILS
         WTO   MF=(E,(2))              ISSUE MSG
         MVC   4(79,R2),=CL79'PLEASE INFORM TECHNICAL SUPPORT ABOUT THIS
               S MESSAGE.'
         WTO   MF=(E,(2))              ISSUE MSG
         B     EXITPROG             EXIT PROGRAM
TPUTMSG  DS    0H
         MVC   TPUTWORK(TPUTSKLL),TPUTSKL
         LA    R1,TPUTWORK          LOAD DSECT ADDRESS
         LA    R2,GC009             POINT TO MESSAGE
         LA    R3,L'GC009           LOAD LENGTH OF MESSAGE
         TPUT  (R2),(R3),EDIT,WAIT,,BREAKIN,HIGHP,MF=(E,(R1))
EXITPROG DS    0H
         RACKOFF RC=0
RZEROS   MVC   GC009(0),1(R3)       REMOVE ZEROS
MSPACES  MVC   1(0,R3),SPACES       ADD TRAILING SPACES
*=====================================================================*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
SPACES   DC    CL256' '
GC009S   DC    CL79'... OF ... ADDRESS SPACES ARE MARKED AS NON-REUSABLE
               E.'
WTOSKEL  DS    0F
         WTO   '                                                       *
                                       ',                              *
               ROUTCDE=(2),DESC=(11),                                  *
               MF=L
WTOSKELL EQU   *-WTOSKEL
TPUTSKL  TPUT  MF=L
TPUTSKLL EQU   *-TPUTSKL
*---------------------------------------------------------------------*
WORKAREA DSECT
REGSAVER DS    18F
DWORK8   DS    D
ASVTADRS DS    F
NRAADRS  DS    F
HWM      DS    F
         ORG   HWM+3
ENVMT    DSG   CL1
         ORG
GC009    DS    CL79
         UNPACK   MF=L              UNPACK LIST FORMAT
         ISPINIT  MF=L              UNPACK LIST FORMAT
         ISPVDEFX MF=L              UNPACK LIST FORMAT
WTOWORK  DS    0F
         WTO   '                                                       *
                                       ',                              *
               ROUTCDE=(2),DESC=(11),                                  *
               MF=L
TPUTWORK TPUT  MF=L
WORKAREL EQU   *-WORKAREA
*---------------------------------------------------------------------*
         END
