*
**       Title:    OPERATOR COMMANDS
**       Module:   OPERCMD
**       Program:  OPERCMD
**       Function: Issue operator commands from batch jobs.
**                 Parameters may be passed via "SYSIN" DD, parameter
**                 list or command processor.
**
**                 In SYSIN parameters, you may pass a WAIT command
**                 to force a periodic delay before issuing the next
**                 command. The format is:
**                   WAIT=sss  - where 'sss' is a range of 1 to 999
**                               seconds.
**                               If long wait periods are required,
**                               use a combination of multiple WAIT
**                               commands totaling to the required
**                               time.
**                               The 'sss' will be converted to
**                               'mmss' format to feed the STIMER,
**                   '*'       - In column 1 is treated as commnet.
**
**                 Return codes:
**                    0 - Normal completion
**                    8 - Program not authorised
**
**       Note:     Program must be authorised using link-edit parm of:
**                      'SETCODE AC(1)'
**                 Link-edit into an APF authorised dataset.
**       Author:   A. Kara - ASYSTS P/L(With inspiration from R. Hann).
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*        PRINT OFF
         COPY  @MACROS              MISCELLANEOUS MACROS
*        PRINT ON
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
OPERCMD  KICKOFF (R11,R12)
         LR    R7,R1                ADDRESS OF PARAMETER
         TESTAUTH ,                 TEST AUTHORISATION
         LTR   R15,R15              Q. AUTHORISED
         BNZ   EXITRC08                N. EXIT RETURN CODE 08
INIT#1   DS    0H
         L     R7,0(R7)             LOAD ADDRESS OR PARAMETER
         LH    R9,0(R7)             LOAD LENGTH OF PARAMETER
         LTR   R9,R9                Q. IS PARAMETER LENGTH ZERO
         BNZ   OKCONT#1                N. GO PROCESS PARAMETER
         BAL   R14,NOPARAM          GO AND PROCESS 'SYSIN'
         B     EXITRC00             EXIT PROGRAM
OKCONT#1 DS    0H
         LA    R7,2(R7)             POINT PAST PARAMETER LENGTH
         BAL   R14,SENDCMD          GO AND SEND COMMAND
EXITRC00 RACKOFF RC=0               EXIT PROGRAM RETURN CODE '0'
EXITRC08 RACKOFF RC=8               EXIT PROGRAM RETURN CODE '8'
                                                                SPACE 1
*=====================================================================*
* No parameter passed. Assume using //SYSIN DD statement              *
*---------------------------------------------------------------------*
NOPARAM  DS    0H
         STM   R0,R14,SAVSUBR1      STORE REGISTERS
         LA    R1,BUFFER            LOAD BUFFER ADDRESS
         ST    R1,BUFFER            STORE BUFFER ADDRESS
         MVC   HERE1(3),BUFFER+1    MOVE ADDRESS
         STCM  R1,B'0111',HERE1     STORE BUFFER ADDRESS IN 'EXLST'
         OPEN  (SYSIN,(INPUT))      OPEN SYSIN FILE
         RDJFCB (SYSIN)             GET FILE INFO ON PRINTER FILE
         LTR   R15,R15              Q. RETURN CODE 0
         BNZ   CLOSFILE                N. GO TO ERROR IN DCB INFO
                                                                SPACE 1
GETRECRD DS    0H
         GET   SYSIN,BUFFER         GET LINE OF DATA
                                                                SPACE 1
         CLC   BUFFER(80),SPACES    Q. DATA BLANK LINE
         BE    GETRECRD                Y. GET NEXT LINE OF DATA
         LA    R9,73                LOOP COUNTER
         LA    R8,BUFFER            POINT TO START OF BUFFER
LOOP#100 DS    0H
         CLI   0(R8),C'*'           Q. Comment
         BE    GETRECRD                Y. GET NEXT LINE OF DATA
         CLC   =C'WAIT',0(R8)       Q. DELAY REQUESTED
         BNE   LOOP#10C                N. NEXT CHAR
         BAL   R14,WAITPROC         GO AND PROCESS WAIT COMMAND
         B     GETRECRD             GET NEXT LINE OF DATA
LOOP#10C DS    0H
         LA    R8,1(R8)             POINT TO NEXT CHARACTER
         BCT   R9,LOOP#100          LOOP TILL NO MORE TRAILING SPACES
                                                                SPACE 1
NOTDELAY DS    0H
         LA    R9,80                LOOP COUNTER
         LA    R8,BUFFER+79         POINT TO LAST CHAR OF DATA
LOOP#200 DS    0H
         CLI   0(R8),C' '           Q. CHARACTER A SPACE
         BNE   LOOP#20E                N. EXIT LOOP
         BCTR  R8,0                 POINT TO PREV CHARACTER
         BCT   R9,LOOP#200          LOOP TILL NO MORE TRAILING SPACES
LOOP#20E DS    0H
         LA    R7,BUFFER            ADDRESS OF PARAMETER
         BAL   R14,SENDCMD          GO AND SEND COMMAND
         B     GETRECRD             GET NEXT LINE OF DATA
                                                                SPACE 1
CLOSFILE DS    0H
         CLOSE SYSIN                CLOSE PARAMETER FILE
DCBABEX1 DS    0H
         LM    R0,R14,SAVSUBR1      RESTORE REGISTERS
         BR    R14                  RETURN TO CALLER ROUTINE
                                                                SPACE 1
*=====================================================================*
*  Called subroutines                                                 *
*---------------------------------------------------------------------*
*  Send the command using SVC 34                                      *
*---------------------------------------------------------------------*
SENDCMD  DS    0H
         STM   R0,R14,SAVSUBR2      STORE REGISTERS
         BCTR  R9,R0                SET MOVE LENGTH
         MVC   MESSAGE,SPACES       CLEAR MESSAGE AREA
         EX    R9,MOVE#1            GET COMMAND DATA
         AH    R9,=H'5'             SET LENGTH FOR MVC (BCTR + LENGTH)
         STH   R9,MESSAGEL          STORE MESSAGE LENGTH
         MODESET KEY=ZERO,MODE=SUP  SUPERVISOR STATE FOR SVC CALL
         LA    R1,MESSAGEL          SET REGISTERS
         XR    R0,R0                     FOR SVC
         SVC   34                   ISSUE COMMAND
         MODESET KEY=NZERO,MODE=PROB RETURN TO PROBLEM STATE
         LM    R0,R14,SAVSUBR2      RESTORE REGISTERS
         BR    R14                  RETURN TO CALLER ROUTINE
                                                                SPACE 1
*---------------------------------------------------------------------*
*  Test WAIT time parameter and wait for designated time if valid.    *
*  In either case inform using WTO...                                 *
*---------------------------------------------------------------------*
WAITPROC DS    0H
         STM   R0,R14,SAVSUBR2      STORE REGISTERS
         MVC   TIMEO(3),=C'000'     SET TIME TO ZERO
         LA    R8,5(R8)             POINT PAST COMMAND IDENTIFIER
         CLI   0(R8),C' '           Q. FIRST CHARACTER SPACE
         BE    TEST2NDC                Y. GET NEXT CHARACTER
         CLI   0(R8),C'0'           Q. FIRST CHARACTER NUMERIC
         BL    INVTIME                 N. INVALID TIME
         MVC   TIMEO+2(1),0(R8)     MOVE TIME TO LAST BYTE
TEST2NDC DS    0H
         CLI   1(R8),C' '           Q. NEXT SPACE
         BE    ISSUWAIT                Y. HAVETIME
         CLI   1(R8),C'0'           Q. 2ND. CHARACTER NUMERIC
         BL    INVTIME                 N. INVALID TIME
         MVC   TIMEO+1(1),TIMEO+2   RIPLE DECIMAL DIGIT TO LEFT
         MVC   TIMEO+2(1),1(R8)     MOVE TIME TO LAST BYTE
TEST3RDC DS    0H
         CLI   2(R8),C' '           Q. NEXT SPACE
         BE    ISSUWAIT                Y. HAVETIME
         CLI   2(R8),C'0'           Q. 2ND. CHARACTER NUMERIC
         BL    INVTIME                 N. INVALID TIME
         MVC   TIMEO(2),TIMEO+1     RIPLE DECIMAL DIGIT TO LEFT
         MVC   TIMEO+2(1),2(R8)     MOVE TIME TO LAST BYTE
         B     ISSUWAIT             CONTINUE
INVTIME  DS    0H
         MVC   WTOERRP+32(3),0(R8)
         B     WTOERRP              BRANCH TO FULL WORD ALIGNMENT
WTOERRP  WTO   'Invalid wait parameter ''...'' entered',               *
               ROUTCDE=(11),DESC=(7)
         B     RETWAIT              GO TO COMMON RETURN POINT
                                                                SPACE 1
ISSUWAIT DS    0H
         MVC   WTOWAIT+17(3),TIMEO  MOVE TIME TO DISPLAY POSAITION
*---------------------------------------------------------------------*
         PACK  DWORK8,TIMEO         PACK THE SECONDS
         XR    R0,R0                INITIALISE EVEN REGISTER
         CVB   R1,DWORK8            CONVERT TO BINARY
         D     R0,=F'60'            CONVERT TO MM:SS
         UNPACK R0,2,TIMEO+1,LIST=Y UNPACK SS TO DISPLAYABLE FORMAT
         UNPACK R1,2,TIMEO-1,LIST=Y UNPACK MM TO DISPLAYABLE FORMAT
*---------------------------------------------------------------------*
         B     WTOWAIT              BRANCH TO FULL WORD ALIGNMENT
WTOWAIT  WTO   'Waiting ''...'' seconds before next command',          *
               ROUTCDE=(11),DESC=(7)
         STIMER WAIT,DINTVL=VTIME   WAIT 1/100 OF A SEC
RETWAIT  DS    0H
         LM    R0,R14,SAVSUBR2      RESTORE REGISTERS
         BR    R14                  RETURN TO CALLER ROUTINE
                                                                SPACE 1
*---------------------------------------------------------------------*
MOVE#1   MVC   MESSAGE(0),0(R7)     'EX' FORM OF MOVE
*---------------------------------------------------------------------*
*=====================================================================*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
SAVSUBR1 DS    16F                  SUBROUTINE REGISTER SAVE AREA
SAVSUBR2 DS    16F                  SUBROUTINE REGISTER SAVE AREA
MESSAGEL DS    AL2                  LENGTH OF COMMAND
         DC    AL2(0)               LENGTH OF COMMAND
MESSAGE  DS    0CL100               MESSAGE
         DS    CL100
SPACES   DC    CL256' '
DWORK8   DS    D                    DOUBLE WORD WORK AREA
*                hhmmsstt                                             *
VTIME    DC    D'00000000'          VARIABLE TIME INTERVAL
         ORG   VTIME
         DS    CL3
TIMEO    DS    CL3
         ORG
BUFFER   DS    CL200
*---------------------------------------------------------------------*
SYSIN    DCB   DDNAME=SYSIN,                                           X
               DSORG=PS,                                               X
               MACRF=(GM),                                             X
               EXLST=CAEXLST1,                                         X
               EODAD=CLOSFILE
CAEXLST1 DS    0F
         DC    X'07'
HERE1    DS    AL3
         DC    X'11'
         DC    AL3(DCBABEX1)
         DC    X'80'
*---------------------------------------------------------------------*
         END
