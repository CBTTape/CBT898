***********************************************************************
*                                                                     *
*  Title:  : 'ISPF PANEL EXIT FOR DELETE CONFIRMATION PROMPTS'        *
*  System  : N/A                                                      *
*  Module  : PANELDPT                                                 *
*  Program : PANELDPT                                                 *
*  Function: The purpose of this ISPF panel exit is to provide a      *
*            prompt for ISPF panels where members can be deleted      *
*            however no confirmation screen is displayed.             *
*            As ISPF services cannot be invoked from a panel exit,    *
*            this program uses 3270 datastreams to display            *
*            confirmation requests.                                   *
*            NOTE - If a delete is detected, a TPUT is issued to      *
*                   force a screen refresh.                           *
*            The following ISPF panels are catered for:               *
*                ISRUDSM                                              *
*                ISRUDMM                                              *
*  Parms   : ISPF variable ZDATA, ZDSN.                               *
*  Notes   : Program is re-entrant.                                   *
*                                                                     *
*  Company : ASYSTS Pty. Ltd.                                         *
*  Author  : A. Kara                                                  *
*  Date    : 16/09/94                                                 *
*                                                                     *
***********************************************************************
*        PRINT OFF
*        COPY  @MACROS                 COMMON MACROS
*        PRINT ON
***********************************************************************
                                                                SPACE 1
PANELDPT KICKOFF (R12),WORKAREA,WORKAREL,LIST=N,AMODE=31
         LR    R7,R1                   SAVE PARAMETR LIST POINTER
         USING ISPFPARM,R7             ADDRESABILITY TO PARAMETER LIST
         XC    FLAG,FLAG               INITIALISE FLAG
*---------------------------------------------------------------------*
* Establish PANELID and thereby the selection field size              *
*---------------------------------------------------------------------*
         LA    R9,TABLENT              LOAD # OF TABLE ENTRIES
         L     R4,PANNAME              LOAD POINTER TO PANELNAME
         LA    R5,TABLE                POINT TO TABLE
TABLOOP  DS    0H
         CLC   0(8,R4),2(R5)           Q. CATERED FOR PANEL
         BE    HITTABLE                   Y. PROCESS IT'S OFFSETS
         LA    R5,10(R5)               POINT TO NEXT TABLE ENTRY
         BCT   R9,TABLOOP              LOOP TILL END
         B     EXITPR00                EXIT FOR UNCATERED PANEL
HITTABLE DS    0H
         MVC   SELLEN(2),0(R5)         STORE SELECTION FIELD LENGTH
         L     R4,VARLEN               POINT TO 1ST. VARIABLES LENGTH
         L     R4,0(R4)                LOAD 1ST. VARIABLES LENGTH
         L     R5,VARVAL               POINT TO 1ST. VARIABLES ADDRESS
         LA    R5,0(R5)                POINT TO 1ST. VARIABLES VALUE
         BCT   R4,EXCMD1               DECREMENT POINTER AND BRANCH
MOVEDSN  MVC   DATASET(0),0(R5)        MOVE DATASET
EXCMD1   MVC   DATASET,SPACES          CLEAR DATASET NAME
         EX    R4,MOVEDSN              MOVE DATASET
*---------------------------------------------------------------------*
* Check each line for a 'D'                                           *
*---------------------------------------------------------------------*
         L     R4,VARLEN               POINT TO 1ST. VARIABLES LENGTH
         L     R1,0(R4)                LOAD 1ST. VARIABLES LENGTH
         AR    R5,R1                   POINT PAST 1ST. VARIABLE DATA
         LR    R6,R5                   POINT TO THE END
         A     R6,4(R4)                    OF 2ND. VARIABLE DATAH
LOPX05   DS    0H
         CR    R5,R6                   Q. PROCESSED ALL DATA
         BNL   EXITPROG                   Y. EXIT PROGRAM
         CLI   0(R5),X'01'             Q. INPUT FIELD
         BNE   EXITPROG                   N. EXIT PROGRAM
         LA    R7,1(R5)                1ST BYTE OF INPUT FIELD
         LH    R9,SELLEN               LOOP COUNTER
CMDLOOP  DS    0H
         CLI   0(R7),C'D'              Q. 1ST CHAR SELECT
         BE    TESTSSEL                   Y. TEST REST OF CHARACTERS
         CLI   0(R7),C'd'              Q. 1ST CHAR SELECT
         BE    TESTSSEL                   Y. TEST REST OF CHARACTERS
         CLI   0(R7),C' '              Q. CHAR LEADING SPACE
         BNE   NEXTLINE                   N. PROCESS NEXT LINE
         LA    R7,1(R7)                POINT TO NEXT CHAR
         BCT   R9,CMDLOOP              LOOP TILL END
NEXTLINE DS    0H
         LA    R5,80(R5)               NEXT LINE
         B     LOPX05                  LOOP TILL END
TESTSSEL DS    0H
         CH    R9,=H'1'                Q. LAST BYTE
         BE    PROMPTDL                   Y. CHANGE CHARACTER
         CLI   1(R7),C' '              Q. NEXT CHAR SPACE
         BNE   NEXTLINE                   N. NOT SELECTION
*---------------------------------------------------------------------*
* Prompt for delete confirmation                                      *
*---------------------------------------------------------------------*
PROMPTDL DS    0H
         LA    R9,2(R5)                ALLOW FOR 2 ATTRIBUTE BYTES
         AH    R9,SELLEN               ADD SELECTION LENGTH
         MVC   MEMBER(8),0(R9)         MOVE MEMBER
         LA    R2,PANELDP1             SOURCE OF 3270 DATASTREAM
         LA    R3,PANSZ100             LOAD LENGTH
         OI    FLAG,CLEARSCR           SET CLEAR SCREEN REQ'D LATER
         MVC   ERRMSG,SPACES           CLEAR ERROR MESSAGE LINE
SCREEN   DS    0H
         TPUT  (R2),(R3),NOEDIT,WAIT,,BREAKIN,HIGHP
         TGET  REPLY,40,ASIS,WAIT
         CLI   REPLY,X'F3'             Q. END ENTERED
         BE    CLEARDEL                   Y. REMOVE DELETE
         CLI   REPLY,X'7D'             Q. <ENTER> ENTERED
         BNE   INVALENT                   Y. GET NEXT LINE
         OC    REPLY+6(30),SPACES      CONVERT TO UPPER CASE
         CLC   REPLY+6(30),SPACES      Q. NO COMMAND ENETERD
         BE    NEXTLINE                   Y. GET NEXT LINE
         CLC   REPLY+6(4),=C'END '     Q. END ENTERED
         BE    CLEARDEL                   Y. REMOVE DELETE
INVALENT DS    0H
*    PDUMP REPLY,MSG='REPLY-2'
         MVC   ERRMSG(14),=C'Invalid entry.'
         B     SCREEN                  REDISPLAY SCREEN
*---------------------------------------------------------------------*
* Change the 'D' selection to a space                                 *
*---------------------------------------------------------------------*
CLEARDEL DS    0H
         LH    R9,SELLEN               LOAD SELECTION LENGTH
         BCTR  R9,0                    PREPARE FOR 'EX' COMMAND
         EX    R9,MOVESPCD             CLEAR DELETE CHARACTERS
         B     NEXTLINE                GET NEXT LINE
MOVESPCD MVC   1(0,R5),SPACES          CLEAR DELETE CHARACTERS
EXITPROG DS    0H
         TM    FLAG,CLEARSCR           Q. CLEAR SCREEN
         BNO   EXITPR00                   N. EXIT PROGRAM
*---------------------------------------------------------------------*
* Restore terminal display environment then exit                      *
*---------------------------------------------------------------------*
*        STTMPMD  ON,KEYS=NO           SET DISPLAY TERMINAL MANAGER OFF
*        STFSMODE OFF                  TURN OFF FULL SCREEN MODE
*        STLINENO LINE=1               CLEAR THE SCREEN
*        TPUT =C'Hit <ENTER> to refresh screen...',32
*=====================================================================*
EXITPR00 DS    0H
         RACKOFF RC=0,LIST=NO
                                                                SPACE 1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LTORG
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
SPACES   DC    CL80' '
*        DC    H'INPUT_FIELD_LENGTH',CL8'PANEL_ID'
TABLE    DC    H'09',CL8'ISRUDSM '
         DC    H'01',CL8'ISRUDMM '
TABLENT  EQU   (*-TABLE)/10
SELLEN   DS    H                       SELECTION FIELD'S LENGTH
         COPY  PANELDP1                PROMPT PANEL
                                                                SPACE 1
WORKAREA DSECT
         DS    18F                     REGISTER SAVE AREA (D.F.W.T.)
FLAG     DS    CL1                     TPUT REPLY
CLEARSCR EQU   X'01'
REPLY    DS    CL60                    TPUT REPLY
WORKAREL EQU   *-WORKAREA
                                                                SPACE 1
ISPFPARM DSECT
EXDTA    DS    F                       -> EXIT-DATA
PANNAME  DS    F                       -> PANEL NAME
PANSECT  DS    F                       -> I-INIT, R-REINIT, P-PROC
MSGID    DS    F                       -> RETURN MESSAGEID TO ISPF
ARRAYDIM DS    F                       -> NO. OF ARRAYS
VARNAME  DS    F                       -> ARRAY OF VARIABLE NAMES (8)
VARLEN   DS    F                       -> VARIABLE LENGTHS (FULLWORDS)
VARVAL   DS    F                       -> STING OF VARIABLE VALUES
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         END
