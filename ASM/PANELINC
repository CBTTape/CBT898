*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*                                                                     *
*        Title:    'ISPF PANEL EXIT TO INCREMENT AN 8 BYTE COUNTER'   *
*        System:   N/A                                                *
*        Module:   PANELINC                                           *
*        Program:  PANELINC                                           *
*        Function: The purpose odf this ISPF panel exit is to         *
*                  increment a counter used in the panel and the      *
*                  appropriate variable cannot be accessed by the     *
*                  application because it is externaly controlled.    *
*                  The routine will increment the value by 1.         *
*                                                                     *
*        Parameters:                                                  *
*                  Two parameters may be used:                        *
*                  1. Numeric field initialised to 8 bytes with       *
*                     leading zeros.                                  *
*                  2. Eight byte field that can house a copy of the   *
*                     retained value left justified with the leading  *
*                     zeros removed. This field is optional.          *
*                                                                     *
*        Notes:    Program not is re-entrant.                         *
*        Author:   A. Kara (ASYSTS P/L).                              *
*                                                                     *
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
PANELINC KICKOFF (R13),LIST=NO
         LR    R7,R1                   SAVE PARAMETR LIST POINTER
         USING ISPFPARM,R7             ADDRESABILITY TO PARAMETER LIST
         L     R2,ARRAYDIM             POINT TO NO OF VARIABLE COUNTER
         L     R2,0(R2)                LOAD NO OF VARIABLE COUNTER
         LTR   R2,R2                   Q. ANY PARAMETER PASSED
         BZ    EXITPR00                   N. EXIT
*        L     R3,VARNAME              POINT TO 1ST. VARIABLE NAME
         L     R4,VARLEN               POINT TO 1ST. VARIABLES LENGTH
         L     R5,VARVAL               POINT TO 1ST. VARIABLES VALUE
         L     R9,0(R4)                LOAD 1ST. VARIABLES LENGTH
         ZAP   DWORK8(8),=PL8'0'       ZAP DATA
         LTR   R9,R9                   Q. ZERO LENGTH
         BZ    INCREMNT                   Y. BYPASS CONVERSION
         BAL   R14,PACKDATA            GO AND PACK DATA
INCREMNT DS    0H
         AP    DWORK8(8),=PL1'1'       BUMP COUNTER
         OI    DWORK8+7,X'0F'          FIX RIGHT NIBBLE                 01-UNPAC
         MVC   CURRENT#,DWORK8         STORE CURRENT COUNTER
         UNPK  TARGET,DWORK8+5(3)      UNPACK FOR TRANSFER
         MVC   0(8,R5),TARGET          SET VALUE
         AR    R5,R9                   ADD 1ST. VARIABLES LENGTH
         LA    R4,4(R4)                POINT TO NEXT VAR LENGTH
         L     R9,0(R4)                POINT TO NEXT VAR LENGTH
         MVC   0(8,R5),SPACES          CLEAR FIELD
         BAL   R14,REMLZERO            REMOVE LEADING ZEROS
TESTTARG DS    0H
         AR    R5,R9                   ADD 1ST. VARIABLES LENGTH
         LA    R4,4(R4)                POINT TO NEXT VAR LENGTH
         L     R9,0(R4)                LOAD 2ND. VARIABLES LENGTH
         LTR   R9,R9                   Q. ZERO LENGTH
         BZ    EXITPR00                   Y. EXIT PROGRAM
         BAL   R14,PACKDATA            GO AND PACK DATA
         MVC   0(8,R5),SPACES          CLEAR FIELD
         BAL   R14,REMLZERO            REMOVE LEADING ZEROS
COMPARET DS    0H
         SP    DWORK8(8),=PL1'1'       BUMP COUNTER
         CP    DWORK8(8),CURRENT#      Q. TARGET HIGHER THAN POINTER
         BNH   EXITPR00                   N. EXIT PROGRAM               01-UNPAC
         AR    R5,R9                   ADD 2ND. VARIABLES LENGTH
         LA    R4,4(R4)                POINT TO NEXT VAR LENGTH
         MVI   0(R5),C'Y'              SET SKIP FLAG
         LA    R1,1                    SET LENGTH
EXITPR00 DS    0H
         RACKOFF RC=0,LIST=NO
                                                                SPACE 1
*---------------------------------------------------------------------*
*
*
*---------------------------------------------------------------------*
PACKDATA DS    0H
         ST    R14,R14SAVER            STORE RETURN ADDRESS
         MVC   TARGET,ZEROS            INITIALISE TARGET
         LA    R15,TARGET+7            POINT TO END OF DATA
         LR    R1,R9                   STORE LENGTH
         BCTR  R1,0                    RELATIVE LENGTH
         SR    R15,R1                  SUBTRACT LENGTH
         EX    R1,MOVEDATA             MOVE DATA
         PACK  DWORK8(8),TARGET(8)     PACK DATA
         L     R14,R14SAVER            RESTORE RETURN ADDRESS
         BR    R14                     RETURN TO CALLER
                                                                SPACE 1
*---------------------------------------------------------------------*
*
*
*---------------------------------------------------------------------*
REMLZERO DS    0H
         ST    R14,R14SAVER            STORE RETURN ADDRESS
         LA    R1,8                    MAXIMUM LENGTH
         LA    R6,TARGET               POINT TO 1ST. VARIABLES VALUE
LOPZERO  DS    0H
         CLI   0(R6),C'0'              Q. ZERO LENGTH
         BNE   LOPZEROE                   Y. EXIT LOOP
         LA    R6,1(R6)                BUMP POINTER
         BCT   R1,LOPZERO              DECREMENT LENGTH COUNTER & LOOP
         LA    R1,1                    ENSURE LAST BYTE MOVED
         BCTR  R6,0                    BACK TO LAST BYTE
LOPZEROE DS    0H
         BCTR  R1,0                    PREPARE FOR 'EX' COMMAND
         EX    R1,REMZEROS             REMOVE LEADING ZEROS
         L     R14,R14SAVER            RESTORE RETURN ADDRESS
         BR    R14                     RETURN TO CALLER
                                                                SPACE 1
*---------------------------------------------------------------------*
MOVEDATA MVC   0(0,R15),0(R5)          MOVE DATA
REMZEROS MVC   0(0,R5),0(R6)           MOVE WITHOUT LEADIN ZEROS
*---------------------------------------------------------------------*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LTORG
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
DWORK8   DC    D'0'
CURRENT# DC    D'0'
R14SAVER DS    F
TARGET   DC    CL8'000000000'
         DC    CL8'<--------'
ZEROS    DC    CL8'000000000'
SPACES   DC    CL256' '
                                                                SPACE 1
ISPFPARM DSECT
EXDTA    DS    F
PANNAME  DS    F                       PANEL NAME
PANSECT  DS    F                       I-INIT, R-REINIT, P-PROC
MSGID    DS    F                       I-INIT, R-REINIT, P-PROC
ARRAYDIM DS    F                       NO OF VARNAME/VARLEN ARRAYS
VARNAME  DS    F                       VARIABLE NAME (8)
VARLEN   DS    F                       VARIABLE LENGTH (FULLWORD)
VARVAL   DS    F                       VARIABLE VALUE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         END
