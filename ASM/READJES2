*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
**
**       TITLE:    'READ THE JES2 SYSOUT DATA'
**       SYSTEM:   N/A
**       MODULE:   READJES2
**       PROGRAM:  READJES2
**       FUNCTION: ................................................
**                 ................................................
**       AUTHOR:   A. KARA.
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
READJES2 KICKOFF (R12)
*---------------------------------------------------------------------*
START    DS    0H
         LA    R10,SSOBZ               ADDRESS SSOB AREA
         USING SSOB,R10                ESTABLISH ADDRESSABILITY
         XC    SSOBEGIN(SSOBLEN1),SSOBEGIN  CLEAR
         MVC   SSOBID,=C'SSOB'         AREA IDENTIFIER
         LA    R4,SSOBHSIZ             HEADER SIZE
         STH   R4,SSOBLEN              STORE INTO BLOCK
         LA    R4,SSSOSIZE             EXTENSION SIZE
         STH   R4,SSSOLEN              STORE INTO BLOCK
         LA    R4,SSSOBGN              EXTENSION ADDRESS
         ST    R4,SSOBINDV             STORE INTO HEADER
         MVC   SSSOPGMN,=C'ZTSALKA'    IDENTIFY THIS PROGRAM
         MVI   SSSOPGMN+7,X'00'            <=====
         MVI   SSOBFUNC+1,SSOBSOUT     FUNCTION = SELECT SYSOUT (SO)
         OI    SSSOFLG1,SSSOSCLS+SSSODST CLASS AND DEST CRITERIA
         MVC   SSSODEST,=CL8'LOCAL'    SET DEST NAME (DEST FRED ONLY)
         MODESET KEY=ZERO,MODE=SUP     KEY ZERO, SUPERVISOR MODE
         LA    R1,ASSOB                ADDRESS REQUEST
         IEFSSREQ                      SUB-SYSTEM REQUEST
         LTR   R15,R15                 Q. ZERO RETURN CODE
         BNZ   FAILSSR                    N. PROCESS REQUEST ERROR
         MODESET KEY=NZERO,MODE=PROB   KEY NONZERO, PROBLEM STATE
         MVC   MESSAGEG(02),SSOBRETN   MOVE RETURN CODE
         MVC   MESSAGEG+2(08),SSSOJOBN MOVE JOBNAME
         MVC   MESSAGEG+12(44),SSSODSN MOVE SUBSYSTEM DATASET NAME
         PDUMP MESSAGEG,MSG='R10=SSOBZ'    <=====
         BAL   R14,WTORTN
EXITPR00 DS    0H
         RACKOFF RC=0                  EXIT ZERO RETURN CODE
FAILSSR  DS    0H
         MODESET KEY=NZERO,MODE=PROB   KEY NONZERO, PROBLEM STATE
         PDUMP MSG='FAILSCR'     <====
EXITPR12 DS    0H
         RACKOFF RC=12                 EXIT ERROR RETURN CODE
*---------------------------------------------------------------------*
WTORTN   DS    0H
         STM   R14,R12,SAVSUBR1        SAVE REGISTERS
         LA    R5,WTODET1              LOAD ADDRESS OF WTO AREA
         MVC   WTODET1+4(73),MESSAGEG  MOVE MESSAGE DETAILS
         WTO   MF=(E,(R5))             ISSUE MSG
         MVC   MESSAGEG,SPACES         CLEAR MESSAGE AREA
         LM    R14,R12,SAVSUBR1        RESTORE REGISTERS
         BR    R14                     RETURN TO CALLER
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
         LTORG
*---------------------------------------------------------------------*
SPACES   DC    CL256' '
SSOBZ    DC    150X'00'
ASSOB    DC    AL1(X'80'),AL3(SSOBZ)
*---------------------------------------------------------------------*
$REGSAVE DS    18F                  LINKAGE STD REG SAVE AREA
SAVSUBR1 DS    15F                  SUBROUTINE REGISTER SAVE AREA
SAVSUBR2 DS    15F                  SUBROUTINE REGISTER SAVE AREA
MESSAGEG DS    CL79                 GENERAL MESSAGE LINE
WTODET1  DS    0F
         WTO   '                                                       *
                                 ',                                    *
               ROUTCDE=(2,12),DESC=(7),                                *
               MF=L
*---------------------------------------------------------------------*
         CVT   DSECT=YES
         IEFJESCT TYPE=DSECT,MNTBL=YES
         IEFJSSOB (SO,AG,AL),CONTIG=YES
*---------------------------------------------------------------------*
         END
