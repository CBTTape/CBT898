*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
**
**       Title   : 'Return related zone id'
**       System  : N/A
**       Module  : RELZONE
**       Program : RELZONE
**       Function: Reasd a SMP/E TARGET/DLIB zone (CSI) and return
**                 it's related zone name in ISPF variable 'RZONE'
**                 placed in the shared pool.
**
**                 The approriate CSI will need to be allocated to a
**                 DDNAME whose value is placed in ISPF variable
**                 'DDVSAM' in the shared pool.
**
**                 The program processes the CSI and extracts the
**                 related zone from record #4 position 17-24.
**
**                 If all processing is sucessful, the related zone
**                 is placed in variable RZONE and the return code is
**                 set to 0.
**
**                 If a processing error is detected, an error message
**                 is set in variable 'MESSAGE' (79 bytes long) and the
**                 return code is set to 8.
**
**                 Program is not re-entrant.
**
**       Author  : A. Kara (ASYSTS P/L) for Vic. Roads...
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*        PRINT OFF
*        COPY  @MACROS                  COMMON MACROS USE BY PROGRAM
*        PRINT ON
*---------------------------------------------------------------------*
RELZONE  KICKOFF (R11,R12),AMODE=31,LIST=NO
         ISPINIT MF=E,LIST=NO           INITIALISE ISPF ENVIRONMENT
         ISPVDEFX INM=(DDVSAM),ANM=DDVSAM,FORMAT=CHAR,LEN=8,MF=E,      X
               LIST=NO
         ISPVDEFX INM=(RZONE),ANM=RZONE,FORMAT=CHAR,LEN=8,MF=E,        X
               LIST=NO
         ISPVDEFX INM=(MESSAGE),ANM=MESSAGE,FORMAT=CHAR,LEN=79,MF=E,   X
               LIST=NO
         ISPEXEC 'VGET (DDVSAM) SHARED' GET DDNAME FROM SHARED POOL
                                                                SPACE 1
         MODCB ACB=ACB1,DDNAME=(*,DDVSAM) SET DDNAME IN ACB
OPEN     DS    0H
         OPEN  ACB1                    OPEN VSAM DATASET
         LTR   R15,R15                 Q. ZERO RETURN CODE
         BZ    SHOWCB                     Y. GO SHOW CB
OPENERR1 DS    0H
         SHOWCB ACB=ACB1,AREA=ERROR,LENGTH=4,FIELDS=ERROR
         LA    R3,116
         C     R3,ERROR                Q. VERIFY NEEDED
         BE    VERIFY                     Y. GO VERIFY
         BNE   STMSGOPN                   N. PROCESS OTHER ERROR
         LA    R3,160
         C     R3,ERROR                Q. EMPTY DATASET
         BE    EMPTY                      Y. GO VERIFY
         B     STMSGOPN                PROCESS OTHER ERROR
VERIFY   DS    0H
         VERIFY RPL=RPLVER,ACTION=REFRESH
         LA    R3,1
         X     R3,OPNAGAIN             SET VERIFIED FLAG ON
         ST    R3,OPNAGAIN             LOAD REOPEN FALG
         BNZ   CLOSE                      N. GOTO CLOSE + RETRY OPEN
STMSGOPN DS    0H
         MVC   MESSAGE(21),=C'ERROR OPENING DATASET'
         B     CLOSE                   GO TO CLOSE ROUTINE
SHOWCB   DS    0H
         SHOWCB ACB=ACB1,AREA=NLOGR,LENGTH=16,                         X
               FIELDS=(NLOGR,LRECL,KEYLEN,RKP)
         L     R3,NLOGR                LOAD NO OF LOGICAL RECORDS
         LTR   R3,R3                   Q. EMPTY DATASET
         BNZ   NOTEMPTY                   N. CONTINUE PROCESSING
EMPTY    DS    0H
         MVC   MESSAGE(21),=C'EMPTY VSAM DATASET'
         B     CLOSE                   GO TO CLOSE ROUTINE
NOTEMPTY DS    0H
         TESTCB ACB=ACB1,ATRB=KSDS     TEST DATASET
         BE    GETRECD                    Y. GO TEST FOR SORT
NOTKSDS  DS    0H
         MVC   MESSAGE(21),=C'NOT KSDS VSAM DATASET'
         B     CLOSE                   GO TO CLOSE ROUTINE
*---------------------------------------------------------------------*
GETRECD  DS    0H
         GET   RPL=RETRVE              DUMMY 1ST.RECORD
         GET   RPL=RETRVE              ZONE NAME
         GET   RPL=RETRVE              ZONE TYPE (TARGET/DLIB)
         GET   RPL=RETRVE              RELATED ZONE
         MVC   RZONE(8),AREA1+16       MOVE R-ZONE NAME
         XC    RCODE,RCODE             SET ZERO RETURN CODE
         B     CLOSE                   GO CLOSE DATASET
*---------------------------------------------------------------------*
EODAD1   DS    0H
         MVC   MESSAGE(21),=C'NOT ENOUGH RECORDS'
         B     CLOSE
CLOSE    DS    0H
         CLOSE ACB1                    CLOSE VSAM DATA SET
         L     R3,OPNAGAIN             LOAD RE-OPEN FLAG
         LTR   R3,R3                   Q. ALREADY RETRIED
         BNZ   OPEN                       Y. RETRY OPEN
EXITPR00 DS    0H
         ISPEXEC 'VPUT (RZONE MESSAGE) SHARED' PUT VARS IN SHR POOL
         LH    R15,RCODE               LOAD RETURN CODE
         RACKOFF RC=(R15)              RETURN
*=====================================================================*
*    VSAM CONTROL BLOCKS                                              *
*=====================================================================*
ACB1     ACB   EXLST=EXLST1,                                           X
               MACRF=(ADR,SEQ,IN),                                     X
               BUFND=5,                                                X
               BUFNI=2,                                                X
               RMODE31=ALL
EXLST1   EXLST EODAD=(EODAD1,A)
RETRVE   RPL   ACB=ACB1,                                               X
               AREA=AREA1,                                             X
               AREALEN=32760,                                          X
               OPTCD=SEQ
RPLVER   RPL   ACB=ACB1,                                               X
               AREA=AREA1,                                             X
               AREALEN=32760,                                          X
               OPTCD=CNV
*=====================================================================*
*    WORK AREAS AND CONSTANTS                                         *
*=====================================================================*
         LTORG
RSAVE    DC    18F'0'               REGISTER SAVE AREA
*
DDVSAM   DC    CL8' '
RZONE    DC    CL8' '
*
OPNAGAIN DC   F'0'
         ISPINIT  MF=L
         ISPVDEFX MF=L
MESSAGE  DS   CL79
RCODE    DC   H'8'
NLOGR    DC   F'0'
LRECL    DC   F'4096'
KEYLEN   DC   F'0'
KEYPOS   DC   F'0'
ERROR    DC   F'0'
AREA1    DS   CL32760
*---------------------------------------------------------------------*
         END
*=====================================================================*
