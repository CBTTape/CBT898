RVSAM    KICKOFF (R11,R12),AMODE=31,LIST=NO
         LR    R7,R1                ADDRESS OF PARAMETER
         L     R7,0(R7)             LOAD ADDRESS OR PARAMETER
         LH    R9,0(R7)             LOAD LENGTH OF PARAMETER
         LTR   R9,R9                Q. IS PARAMETER LENGTH ZERO
         BZ    NOPARAM                 Y. GO PROCESS 'SYSIN'
         CLI   2(R7),C'O'           Q. ONLINE REQUEST
         BNE   NOPARAM                 N. ISSUE TPUTS
         MVI   BATCH,C'N'           TURN BATCH FLAG OFF
NOPARAM  DS    0H
*---------------------------------------------------------------------*
         MODCB ACB=ACB1,DDNAME=(*,DDVSAM) SET DDNAME IN ACB
OPEN     DS    0H
         OPEN  ACB1                    OPEN VSAM DATASET
         LTR   R15,R15                 Q. ZERO RETURN CODE
         BZ    SHOWCB                     Y. GO SHOW CB
OPENERR1 DS    0H
         SHOWCB ACB=ACB1,AREA=ERROR,LENGTH=4,FIELDS=ERROR
         LA    R3,116
         C     R3,ERROR                Q. VERIFY NEEDED
         BE    VERIFY                     Y. GO VERIFY
         BNE   STMSGOPN                   N. PROCESS OTHER ERROR
         LA    R3,160
         C     R3,ERROR                Q. EMPTY DATASET
         BE    EMPTY                      Y. GO VERIFY
         B     STMSGOPN                PROCESS OTHER ERROR
VERIFY   DS    0H
         VERIFY RPL=RPLVER,ACTION=REFRESH
         LA    R3,1
         X     R3,OPNAGAIN             SET VERIFIED FLAG ON
         ST    R3,OPNAGAIN             LOAD REOPEN FALG
         BNZ   CLOSE                      N. GOTO CLOSE + RETRY OPEN
STMSGOPN DS    0H
         MVC   MESSAGE(21),=C'ERROR OPENING DATASET'
         B     CLOSE                   GO TO CLOSE ROUTINE
SHOWCB   DS    0H
         SHOWCB ACB=ACB1,AREA=NLOGR,LENGTH=16,                         X
               FIELDS=(NLOGR,LRECL,KEYLEN,RKP)
         L     R3,NLOGR                LOAD NO OF LOGICAL RECORDS
         LTR   R3,R3                   Q. EMPTY DATASET
         BNZ   NOTEMPTY                   N. CONTINUE PROCESSING
EMPTY    DS    0H
         MVC   MESSAGE(21),=C'EMPTY VSAM DATASET'
         B     CLOSE                   GO TO CLOSE ROUTINE
NOTEMPTY DS    0H
         TESTCB ACB=ACB1,ATRB=KSDS     TEST DATASET
         BE    GETRECD                    Y. GO TEST FOR SORT
NOTKSDS  DS    0H
         MVC   MESSAGE(21),=C'NOT KSDS VSAM DATASET'
         B     CLOSE                   GO TO CLOSE ROUTINE
GETRECD  DS    0H
         XR    R8,R8                   NO OF READS
         XR    R9,R9                   SUCESSFUL READS
         L     R2,NLOGR                LOAD MAXIMUM NUMBER OF RECORDS
         ST    R2,SEED                 SAVE AS SEED
GETRECD1 DS    0H
         LA    R8,1(R8)                UP READS
         BAL   R14,RANDOM              GET NEXT RANDOM NUMBER
         GET   RPL=RETRVE
         LTR   R15,R15
         BNZ   CONT#100
         LA    R9,1(R9)                UP HITS
         CLI   BATCH,C'Y'              Q. BATCH EXECUTION
         BNE   PDUMP1                     N. BYPASS
*        TPUT  RBA,70
*        LA    R5,WTODET1              LOAD ADDRESS OF WTO AREA
*        MVC   WTODET1+4(50),AREA1
*        WTO   MF=(E,(5))              ISSUE MSG
         B     CONT#100
PDUMP1   DS    0H
         TPUT  RBA,70
*        PDUMP MSG='RBA(4),DATA(REST)',RBA,REGS=N,QUIT=CLOSE
CONT#100 DS    0H
         BCT   R2,GETRECD1             INFINITE LOOP
*---------------------------------------------------------------------*
EODAD1   DS    0H
         MVC   MESSAGE(21),=C'NOT ENOUGH RECORDS'
         B     CLOSE
CLOSE    DS    0H
         CLOSE ACB1                    CLOSE VSAM DATA SET
         L     R3,OPNAGAIN             LOAD RE-OPEN FLAG
         LTR   R3,R3                   Q. ALREADY RETRIED
         BNZ   OPEN                       Y. RETRY OPEN
         CLI   BATCH,C'Y'              Q. BATCH EXECUTION
         BNE   EXITPR00                   N. BYPASS
         MVI   WTODET1+4,C' '
         MVC   WTODET1+5(80),WTODET1+1
         LA    R5,WTODET1              LOAD ADDRESS OF WTO AREA
         UNPACK R8,8,WTODET1+4
         MVC   WTODET1+14(13),=C'READ ATTEMPTS'
         WTO   MF=(E,(5))              ISSUE MSG
         UNPACK R9,8,WTODET1+4
         MVC   WTODET1+14(16),=C'SUCCESSFUL READS'
         WTO   MF=(E,(5))              ISSUE MSG
*
EXITPR00 DS    0H
         RACKOFF RC=(R15)              RETURN
                                                                SPACE 1
*---------------------------------------------------------------------*
RANDOM   DS    0H
         ST    R14,RSAVE               SAVE RETURN ADDRESS
         L     R5,=F'15625'
         XR    R4,R4                   INIT EVEN REG
         M     R4,SEED                 MULT EVEN REG PAIR BY SEED
         AL    R5,=F'22221'            ADD LOGICAL
         BC    14,CONT                 BYPASS IF NO CARRY
         LA    R4,1(R4)                MOVE CARRY TO EVEN REG
CONT     DS    0H
         D     R4,DIVISOR              DIVIDE BY 2 ** 31
         ST    R4,SEED                 REMAINDER (EVEN REG) NEW SEED
         C     R5,=F'0'                Q. IS R5 < 0 IE NEGATIVE
         BNL   ISPOS                      N. BYPASS
         MH    R5,=H'-1'               CONVERT TO POSITIVE
ISPOS    DS    0H
         ST    R5,RBA                  SAVE NEW RBA
         L     R14,RSAVE               RESTORE RETURN ADDRESS
         BR    R14                     RETURN TO CALLER
*---------------------------------------------------------------------*
*=====================================================================*
*    VSAM CONTROL BLOCKS                                              *
*=====================================================================*
ACB1     ACB   EXLST=EXLST1,                                           X
               MACRF=(ADR,DIR,IN),                                     X
               BUFND=5,                                                X
               BUFNI=2,                                                X
               RMODE31=ALL
                                                                SPACE 1
EXLST1   EXLST EODAD=(EODAD1,A)
                                                                SPACE 1
RETRVE   RPL   ACB=ACB1,                                               X
               AREA=AREA1,                                             X
               AREALEN=32760,                                          X
               ARG=RBA,                                                X
               OPTCD=(ADR,DIR,SYN,NUP,MVE)
                                                                SPACE 1
RPLVER   RPL   ACB=ACB1,                                               X
               AREA=AREA1,                                             X
               AREALEN=32760,                                          X
               OPTCD=CNV
                                                                SPACE 1
*=====================================================================*
*    WORK AREAS AND CONSTANTS                                         *
*=====================================================================*
         LTORG
RSAVE    DC    18F'0'               REGISTER SAVE AREA
*
DDVSAM   DC    CL8'READVSAM'
DIVISOR  DC    X'80000000'
*
OPNAGAIN DC   F'0'
MESSAGE  DC   CL79' '
NLOGR    DC   F'0'
LRECL    DC   F'4096'
KEYLEN   DC   F'0'
KEYPOS   DC   F'0'
ERROR    DC   F'0'
SEED     DC   F'0'
BATCH    DC   CL4'Y'
WTODET1  DS    0F
         WTO   '                                                       *
                                        ',                             *
               ROUTCDE=(11),DESC=(7),                                  *
               MF=L
RBA      DC   F'1'
AREA1    DS   CL32760
*---------------------------------------------------------------------*
         END
*=====================================================================*
