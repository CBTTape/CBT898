*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
**
**       TITLE:    'READ A VSAM DATASET'
**       SYSTEM:   N/A
**       MODULE:   RVSMDYNA
**       PROGRAM:  RVSMREAD
**       FUNCTION: DYNAMICALLY ALLOCATE/FREE OR TEST DATASET.
**       AUTHOR:   A. KARA.
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
RVSMDYNA KICKOFF (R13)
         LR    R12,R1               ADDRESS OF COMMON AREA
         USING RVSMCOMA,R12         ESTABLISH ADDRESABILITY
*---------------------------------------------------------------------*
         LA    R0,100               AMOUNT OF STORAGE REQUIRED
*                                      FOR OUR 3 TEXT UNITS
         GETMAIN R,LV=(0)           GET REQUIRED STORAGE
         LR    R10,R1               SAVE GETMAIN REGISTER
         USING S99RBP,R10           ESTABLISH ADDRESABILITY
         LA    R4,S99RBPTR+4        POINT PAST RBPTR
         USING S99RB,R4             EST ADDRESABILITY FOR "RB" DSECT
         ST    R4,S99RBPTR          MAKE "RBPTR" TO "RB"
         OI    S99RBPTR,S99RBPND    TURN ON HIGH ORDER BIT OF "RBPTR"
         XC    S99RB(RBLEN),S99RB   ZERO OUT "RB" ENTIRELY
         MVI   S99RBLN,RBLEN        PUT THE LEN OF "RB" IN ITS LEN FLD
         LA    R5,S99RB+RBLEN       POINT 20 BYTES PAST 'RB'
         USING S99TUPL,R5           EST ADDRESABILITY TO TEXT UNIT PTRS
         ST    R5,S99TXTPP          INIT TEXT POINTER IN 'RB'
TESTINFO DS    0H
         CLI   VSAMOPER,C'I'        Q. REQUEST FOR 'INFO'
         BNE   TESTALOC                N. NEXT TEST
         MVI   S99VERB,S99VRBIN     SET VERB FOR INFO RETRIEVAL
         LA    R6,S99TUPL+TUPTRSL2  POINT JUST PAST THE TEXT UNIT PTR
         USING S99TUNIT,R6          ADDRESABILITY FOR 1ST TEXT UNIT
         ST    R6,S99TUPTR          POINT 1ST TXT UNIT PTR TO 1ST TXT U
         BAL   R14,DDINFOR             Y. GO GET DSNAME
         B     WINDUP               GO WINDUP
TESTALOC DS    0H
         CLI   VSAMOPER,C'A'        Q. REQUEST FOR 'ALLOC'
         BNE   TESTFREE                N. NEXT TEST
         MVI   S99VERB,S99VRBAL     SET VERB FOR INFO RETRIEVAL
         LA    R6,S99TUPL+TUPTRSL3  POINT JUST PAST THE TEXT UNIT PTR
         USING S99TUNIT,R6          ADDRESABILITY FOR 1ST TEXT UNIT
         ST    R6,S99TUPTR          POINT 1ST TXT UNIT PTR TO 1ST TXT U
         BAL   R14,ALLOCR              Y. GO ALLOCATE
         B     WINDUP               GO WINDUP
TESTFREE DS    0H
         CLI   VSAMOPER,C'F'        Q. REQUEST FOR 'FREE'
         BNE   EXITPR08                N. INVALID ACTION SELECTED
         MVI   S99VERB,S99VRBUN     SET VERB FOR UNALLOCATION
         LA    R6,S99TUPL+TUPTRSL2  POINT JUST PAST THE TEXT UNIT PTR
         USING S99TUNIT,R6          ADDRESABILITY FOR 1ST TEXT UNIT
         ST    R6,S99TUPTR          POINT 1ST TXT UNIT PTR TO 1ST TXT U
         BAL   R14,FREEAR              Y. GO ALLOCATE
         B     WINDUP               GO WINDUP
*---------------------------------------------------------------------*
EXITPR08 DS    0H
         MVI   RCODE,X'08'          SET ERROR RETURN CODE
*---------------------------------------------------------------------*
WINDUP   DS    0H
         LA    R3,100               AMOUNT OF STORAGE REQUIRED
         FREEMAIN R,A=(10),LV=(3)   FREE REQUIRED STORAGE
         XR    R15,R15              INITIALISE RETURN CODE REGISTER
         IC    R15,RCODE            SET RETURN CODE
EXITPROG DS    0H
         RACKOFF RC=(R15)           EXIT WITH R15 ALREADY SET
*=====================================================================*
*---------------------------------------------------------------------*
* ROUTINE TO RETURN THE DSNAME ASSOCIATED WITH A DDNAME               *
*---------------------------------------------------------------------*
DDINFOR  DS    0H
         ST    R14,R14SAVER         SAVE RETURN REGISTER
         LA    R7,DINDDNAM          GET THE KEY FOR DDNAME
         STH   R7,S99TUKEY          PUT KEY IN TEXT UNIT KEY FLD
         LA    R7,1                 BECAUSE THE DSNAME KEY REQUIRES 1
         STH   R7,S99TUNUM            PARM, STORE 1 IN NUMBER FIELD
         LA    R7,8                 MAXIMUM LENGTH OF DATASET
         STH   R7,S99TULNG          STORE TEXT UNIT LENGTH
         BCTR  R7,0                 SUBTRACT 1 FROM LENGTH FOR REL MOVE
         EX    R7,MOVEDDN           MOVE DSNAME
         LA    R6,S99TUNIT          POINT JUST PAST THE 1ST TEXT UNIT
         LA    R6,6(R6)             POINT PAST KEY, # AND LENGTH
         LA    R7,1(R7)             BACK TO NORMAL LENGTH
         AR    R6,R7                POINT PAST DSNAME
         LA    R5,S99TUPL+4         POINT TO 2ND TEXT UNIT PTR IN LIST
         ST    R6,S99TUPTR          POINT 2ND TXT UNIT PTR TO 2ND T.U.
         OI    S99TUPTR,S99TUPLN    TURN ON HIGH ORDER BIT FOR LAST PTR
         LA    R7,DINRTDSN          GET KEY FOR RETURN DSNAME
         STH   R7,S99TUKEY          PUT THE KEY IN TEXT UNIT
         LA    R7,1                 BECAUSE THE DSNAME KEY REQUIRES 1
         STH   R7,S99TUNUM            PARM, STORE 1 IN NUMBER FIELD
         LA    R7,44                BECAUSE THE DSNAME KEY REQUIRES A
         STH   R7,S99TULNG            LENGTH OF 44
         LR    R1,R10               REQ BLK PTR ADR IN R1 FOR DYNALLOC
         DYNALLOC
         MVI   RCODE,X'00'          ZERO RETURN CODE
         LTR   R15,R15              Q. ZERO RETURN CODE
         BZ    RETMAIN1                Y. GO TO GOOD RETURN
         MVI   RCODE,X'04'          ERROR RETURN CODE
RETMAIN1 DS    0H
         L     R14,R14SAVER         RESTORE RETURN REGISTERS
         BR    R14                  RETURN
*---------------------------------------------------------------------*
* ROUTINE TO ALLOCATE A DSNAME TO SUPPLIED DDNAME                     *
*---------------------------------------------------------------------*
ALLOCR   DS    0H
         ST    R14,R14SAVER         STORE RETURN REGISTERS
         LA    R7,DALDSNAM          GET THE KEY FOR DSNAME
         STH   R7,S99TUKEY          PUT KEY IN TEXT UNIT KEY FLD
         LA    R7,1                 BECAUSE THE DSNAME KEY REQUIRES 1
         STH   R7,S99TUNUM            PARM, STORE 1 IN NUMBER FIELD
         LA    R7,44                MAXIMUM LENGTH OF DATASET
         STH   R7,S99TULNG          STORE TEXT UNIT LENGTH
         BCTR  R7,0                 SUBTRACT 1 FROM LENGTH FOR REL MOVE
         EX    R7,MOVEDSN           MOVE DSNAME
         LA    R6,S99TUNIT          POINT JUST PAST THE 1ST TEXT UNIT
         LA    R6,6(R6)             POINT PAST KEY, # AND LENGTH
         LA    R7,1(R7)             BACK TO NORMAL LENGTH
         AR    R6,R7                POINT PAST DSNAME
         LA    R5,S99TUPL+4         POINT TO 2ND TEXT UNIT PTR IN LIST
         ST    R6,S99TUPTR          POINT 2ND TXT UNIT PTR TO 2ND T.U.
         LA    R7,DALDDNAM          GET THE KEY FOR DSNAME
         STH   R7,S99TUKEY          PUT KEY IN TEXT UNIT KEY FLD
         LA    R7,1                 BECAUSE THE DSNAME KEY REQUIRES 1
         STH   R7,S99TUNUM            PARM, STORE 1 IN NUMBER FIELD
         LA    R7,8                 MAXIMUM LENGTH OF DATASET
         STH   R7,S99TULNG          STORE TEXT UNIT LENGTH
         BCTR  R7,0                 SUBTRACT 1 FROM LENGTH FOR REL MOVE
         EX    R7,MOVEDDN           MOVE DSNAME
         LA    R6,S99TUNIT          POINT JUST PAST THE 1ST TEXT UNIT
         LA    R6,6(R6)             POINT PAST KEY, # AND LENGTH
         LA    R7,1(R7)             BACK TO NORMAL LENGTH
         AR    R6,R7                POINT PAST DSNAME
         LA    R5,S99TUPL+4         POINT TO 3ND TEXT UNIT PTR IN LIST
         ST    R6,S99TUPTR          POINT 3ND TXT UNIT PTR TO 2ND T.U.
         OI    S99TUPTR,S99TUPLN    TURN ON HIGH ORDER BIT FOR LAST PTR
         LA    R7,DALSTATS          GET KEY FOR STATUS SPECIFICATIONS
         STH   R7,S99TUKEY          PUT THE KEY IN TEXT UNIT
         LA    R7,1                 BECAUSE THE DSNAME KEY REQUIRES 1
         STH   R7,S99TUNUM            PARM, STORE 1 IN NUMBER FIELD
         STH   R7,S99TULNG          SET STATUS PARM LENGTH ALSO TO 1
TSTOLDD  DS    0H
         CLI   PANELID,C'E'         Q. EDITING
         BNE   MUSTBSHR                N. MUST BE SHARE
         MVI   S99TUPAR,X'01'       OLD DISPOSITION
         B     CONT#30              BYPASS
MUSTBSHR DS    0H
         MVI   S99TUPAR,X'08'       SHR DISPOSITION
CONT#30  DS    0H
         LR    R1,R10               REQ BLK PTR ADR IN R1 FOR DYNALLOC
         DYNALLOC
         MVI   RCODE,X'00'          ZERO RETURN CODE
         LTR   R15,R15              Q. ZERO RETURN CODE
         BZ    RETMAIN2                Y. GO TO GOOD RETURN
         MVI   RCODE,X'04'          ERROR RETURN CODE
RETMAIN2 DS    0H
         L     R14,R14SAVER         RESTORE RETURN REGISTERS
         BR    R14                  RETURN
*---------------------------------------------------------------------*
* ROUTINE TO DEALLOCATE A DATASET USING DDNAME                        *
*---------------------------------------------------------------------*
FREEAR   DS    0H
         ST    R14,R14SAVER         STORE RETURN REGISTERS
         LA    R7,DUNDDNAM          GET THE KEY FOR DDNAME
         STH   R7,S99TUKEY          PUT KEY IN TEXT UNIT KEY FLD
         LA    R7,1                 BECAUSE THE DSNAME KEY REQUIRES 1
         STH   R7,S99TUNUM            PARM, STORE 1 IN NUMBER FIELD
         LA    R7,8                 MAXIMUM LENGTH OF DATASET
         STH   R7,S99TULNG          STORE TEXT UNIT LENGTH
         BCTR  R7,0                 SUBTRACT 1 FROM LENGTH FOR REL MOVE
         EX    R7,MOVEDDN           MOVE DSNAME
         LA    R6,S99TUNIT          POINT JUST PAST THE 1ST TEXT UNIT
         LA    R6,6(R6)             POINT PAST KEY, # AND LENGTH
         LA    R7,1(R7)             BACK TO NORMAL LENGTH
         AR    R6,R7                POINT PAST DSNAME
         LA    R5,S99TUPL+4         POINT TO 2ND TEXT UNIT PTR IN LIST
         ST    R6,S99TUPTR          POINT 2ND TXT UNIT PTR TO 2ND T.U.
         OI    S99TUPTR,S99TUPLN    TURN ON HIGH ORDER BIT FOR LAST PTR
         LA    R7,DUNUNALC          GET KEY FOR RETURN DSNAME
         STH   R7,S99TUKEY          PUT THE KEY IN TEXT UNIT
         LA    R7,0                 BECAUSE THE UNALLOC KEY REQUIRES 0
         STH   R7,S99TUNUM            PARM, STORE 0 IN NUMBER FIELD
         LR    R1,R10               REQ BLK PTR ADR IN R1 FOR DYNALLOC
         DYNALLOC
         MVI   RCODE,X'00'          ZERO RETURN CODE
         LTR   R15,R15              Q. ZERO RETURN CODE
         BZ    RETMAIN3                Y. GO TO GOOD RETURN
         MVI   RCODE,X'04'          ERROR RETURN CODE
RETMAIN3 DS    0H
         L     R14,R14SAVER         RESTORE RETURN REGISTERS
         BR    R14                  RETURN
*---------------------------------------------------------------------*
MOVEDSN  MVC   S99TUPAR(0),DSNAME
MOVEDDN  MVC   S99TUPAR(0),DDNAME
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
R14SAVER DS    F                   R14 (RETURN REGISTER) SAVE AREA
#TUPTRS2 EQU   2                   WE ARE USING 2 TEXT UNITS
TUPTRSL2 EQU   (#TUPTRS2*4)        TEXT UNIT PTR LENGTH
#TUPTRS3 EQU   3                   WE ARE USING 3 TEXT UNITS
TUPTRSL3 EQU   (#TUPTRS3*4)        TEXT UNIT PTR LENGTH
#TUPTRS4 EQU   4                   WE ARE USING 4 TEXT UNITS
TUPTRSL4 EQU   (#TUPTRS4*4)        TEXT UNIT PTR LENGTH
         IEFZB4D0
         IEFZB4D2
RBLEN    EQU   (S99RBEND-S99RB)
*---------------------------------------------------------------------*
RVSMCOMA DSECT
         COPY  RVSMCOMA
DUMPL    EQU   *-RVSMCOMA
*---------------------------------------------------------------------*
         END
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-==-=-=-*
