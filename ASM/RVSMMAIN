*   DSN=TECHAK.LOADLIB         <-------- FOR COMPRESSION
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
**
**       TITLE:    'READ A VSAM DATASET'
**       SYSTEM:   N/A
**       MODULE:   RVSMMAIN
**       PROGRAM:  RVSMMAIN
**       FUNCTION: MAIN DRIVER.
**       AUTHOR:   A. KARA.
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
RVSMMAIN KICKOFF (R12,R13)
*---------------------------------------------------------------------*
START    DS    0H
         LA    R1,RVSMCOMA             COMMON DATA AREA
         CALL  RVSMINIT                CALL INITIALISATION ROUTINE
         CLI   RCODE,C'0'              Q. ZERO RETURN CODE
         BNE   EXITPR08                   N. EXIT
         ISPINIT
*---------------------------------------------------------------------*
         CLC   NLOGR(4),=F'1'          Q. ANY RECORDS IN DATASET
         BH    READINI1                   Y. PROCESS NON EMPTY DATASET
         CLI   PANELID,C'E'            Q. EDIT
         BE    DISPSCRN                   Y. GO DISPLAY SCREEN
         MVI   EMPT1STI,C'N'           BROW SET EMPTY 1ST TIME FLAG OFF
         B     DISPSCRN                GO DISPLAY SCREEN
READINI1 DS    0H
         MVI   EMPT1STI,C'N'           SET EMPTY 1ST TIME FLAG OFF
READLOOP DS    0H
*---------------------------------------------------------------------*
         MVI   VSAMOPER,C'G'           SET OPEN FLAG
         LA    R1,RVSMCOMA             COMMON DATA AREA
         CALL  RVSMREAD                CALL OPEN ROUTINE
         CLI   RCODE,C'0'              Q. ZERO RETURN CODE
         BNE   CLOSFILE                   N. CLOSE FILE AND EXIT
DISPSCRN DS    0H
         LA    R1,RVSMCOMA             COMMON DATA AREA
         CALL  RVSMDISP                CALL OPEN ROUTINE
         CLI   RCODE,C'0'              Q. ZERO RETURN CODE
         BE    READLOOP                   Y. GO AND READ THE VSAM
CLOSFILE DS    0H
         MVI   VSAMOPER,C'C'           SET OPEN FLAG
         LA    R1,RVSMCOMA             COMMON DATA AREA
         CALL  RVSMREAD                CALL OPEN ROUTINE
*--------------------------------------------------------------------*
         MVI   VSAMOPER,C'F'           SET FREE FLAG
         LA    R1,RVSMCOMA             COMMON DATA AREA
         CALL  RVSMDYNA                CALL DYNAMIC ALLOCATE ROUTINE
         LTR   R15,R15                 Q. ZERO RETURN CODE FROM INFO
         BZ    UPDTPROF                   Y. DDNAME ALLOCATED
         MVC   GC011,SPACES            CLEAR MESSAGE AREA
         MVC   GC011(27),=C'DATASET COULD NOT BE FREED.'
         ISPEXEC 'SETMSG MSG(GC011)'
*--------------------------------------------------------------------*
UPDTPROF DS    0H
         MVC   DSNAME+44(28),SPACES    CLEAR TRAIL PART OF DSNAME
         LA    R1,44                   LOOP COUNTER
         LA    R2,DSNAME+43            LAST BYTE OF DSNAME
DSNLOOP1 DS    0H
         CLI   0(R2),C'-'              Q. IS CHARACTER A HYPHEN
         BNE   DSNLOP1E                   N. EXIT LOOP
         MVI   0(R2),C' '              MOVE SPACE
         BCTR  R2,0                    PREVIOUS CHAR
         BCT   R1,DSNLOOP1             LOOP TIL END
DSNLOP1E DS    0H
         ISPEXEC 'CONTROL ERRORS RETURN'
         ISPEXEC 'TBEND RVSMEDBR'
         ISPEXEC 'TBOPEN RVSMEDBR LIBRARY(ISPPROF)'
         ISPEXEC 'TBMOD RVSMEDBR'
         ISPEXEC 'TBCLOSE RVSMEDBR LIBRARY(ISPPROF)'
EXITPR00 DS    0H
         L     R1,ARRAY@               SET FREEMAIN ADDRESS
         FREEMAIN RC,LV=ARRAYL,A=(1) FREE THE WORK AREA
*---------------------------------------------------------------------*
         USING CHANGES,R10             ESTABLISH ADDRESABILITY
         L     R10,CHGFWD@             LOAD 1ST FORWARD CHAIN
FREELOOP DS    0H
         LTR   R10,R10                 Q. ANY CHANGED CHAINS
         BZ    EXFREELP                   N. EXIT FREEMAIN LOOP
*<=====  LH    R2,CHGDATAL             LOAD LENGTH OF AREA
         LH    R2,CHGDRECL             LOAD LENGTH OF AREA
         LA    R2,16(R2)               ADD LENGTH OF XTRA ADDRESSES
         LR    R3,R10                  LOAD 1ST FORWARD CHAIN
         L     R10,CFWDPTR             LOAD NEXT FORWARD POINTER
         HEXCHMAC (R3),GC011,39                  <======
         TPUT  GC011,78,EDIT,WAIT,,BREAKIN,HIGHP <=====
         FREEMAIN RC,LV=(R2),A=(R3) FREE THE WORK AREA
         B     FREELOOP                LOOP TILL END
EXFREELP DS    0H
*---------------------------------------------------------------------*
         RACKOFF RC=00
EXITPR08 RACKOFF RC=08
EXITPR20 RACKOFF RC=20
*=====================================================================*
*=====================================================================*
         LTORG
*---------------------------------------------------------------------*
RVSMCOMA DS    0D
         COPY  RVSMCOMA
         END
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-==-=-=-*
