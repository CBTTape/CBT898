*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*
**       TITLE   : 'Return related zone id'
**       SYSTEM  : N/A
**       MODULE  : RELZONE
**       PROGRAM : RELZONE
**       FUNCTION: Accept a SMP/E DLIB parameter and return its related
**                 target zone in ISPF variable 'DZONE' placed in the
**                 shared pool.
**
**                 The GLOBAL CSI will need to be allocated to DD name
**                 'SMPCSI'.  An ISPF variable 'DZONE' will need to be
**                 set to the value of the DLIB required and placed in
**                 the shared pool.
**
**                 The program processes the GLOBAL CSI and extracts
**                 the DLIB's DSN from it using the following key:
**                          X'00000300'
**                          C'GLOBAL  '
**                          X'20100000'
**                          CL8'dlibname'
**
**                 The extracted DSN is then dynamically allocated to
**                 DD name SMPCSIR with disp of SHR REUSE.
**
**                 The program processes the DLIB CSI and extracts the
**                 related target zone's name using the following
**                 search key set to length of 16:
**                          X'00010300'
**                          CL8'dlibname'
**                          X'05100000'
**
**                 The related target zone's name is then placed in
**                 variable 'RZONE' put in the shared pool and the
**                 program is terminated with a return code of 0.
**
**                 If a processing error is detected, an error message
**                 is set in variable 'MESSAGE' (79 bytes long) and the
**                 return code is set to 8.
**
**                 Program is not re-entrant.
**
**                 Register usage:
**                 R11,R12 - Base registers
**                 R13     - Register save area
**                 R10     - Register save area
**
**       AUTHOR  : A. Kara (ASYSTS P/L) for Vic. Roads...
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*        PRINT OFF
*        COPY  @MACROS                  COMMON MACROS USE BY PROGRAM
*        PRINT ON
*---------------------------------------------------------------------*
RELZONE  KICKOFF (R11,R12),AMODE=31,LIST=NO
         LA    R10,SVC99WK
*---------------------------------------------------------------------*
*  ESTABLISH ISPF ENVIRONMENT                                         *
*---------------------------------------------------------------------*
         ISPINIT MF=E,LIST=NO          INITIALISE ISPF ENVIRONMENT
         ISPVDEFX INM=(DZONE),ANM=DZONE,FORMAT=CHAR,LEN=8,MF=E,        X
               LIST=NO
         ISPVDEFX INM=(RZONE),ANM=RZONE,FORMAT=CHAR,LEN=8,MF=E,        X
               LIST=NO
         ISPVDEFX INM=(MESSAGE),ANM=MESSAGE,FORMAT=CHAR,LEN=79,MF=E,   X
               LIST=NO
         ISPEXEC 'VGET DZONE SHARED'
                                                                SPACE 1
*---------------------------------------------------------------------*
*  TEST ALLOACTION OF DDNAME 'SMPCSI'                                 *
*---------------------------------------------------------------------*
TESTALOC ADYN  ACTION=INFO,BASE=R10,DD=SMPCSI,DSN=AREA1,               X
               PARMT=PRARM
         LTR   R15,R15                  Q. DDNAME ALLOACTED
         BZ    OPEN1                       Y. OPEN DATASET
         MVC   MESSAGE(30),=C'DDNAME ''SMPCSI'' NOT ALLOCATED.'
         B     EXITPROG                EXIT PROGRAM
                                                                SPACE 1
OPEN1    DS    0H
         MVC   MESSAGE(35),=C'DISTRIBUTION ZONE ENTERED NOT FOUND'
         BAL   R14,OPEN                GO PROCESS GLOBAL CSI
         LTR   R15,R15                 Q. ZEROR RETURN CODE
         BNZ   EXITPROG                   N. EXIT PROGRAM
                                                                SPACE 1
*---------------------------------------------------------------------*
*  GOT THE DLIBS DSNAME. DYNAMICALLY ALLOACTE IT TO DDNAME 'SMPCSID'  *
*  (FREE FIRST IF ALREADY ALLOACTED).                                 *
*---------------------------------------------------------------------*
         XC    OPNAGAIN,OPNAGAIN       INITIALISE FOR SECOND PROCESS
         MVC   RCODE(2),=H'8'          INITIALISE FOR SECOND PROCESS
         L     R2,LRECL                LOAD RECORD LENGTH
         SH    R2,=H'33'               LESS LENGTH OF RECORD LEADER +1
MOVEDSN  MVC   DSNAME(0),AREA1+32      MOVE DSNAME
         EX    R2,MOVEDSN              ACTUAL MOVE
ALOCSMPT ADYN  ACTION=ALLOC,BASE=R10,DD=SMPCSIT,DSN=(DSNAME),          X
               DISP=SHR,REUSE=YES,PARMT=PARM
         LTR   R15,R15                 Q. TEST RETURN CODE FROM ADYN
         BZ    PROCDLIB                   Y. PROCESS DLIB
         MVC   MESSAGE(45),=C'ALLOCATION OF DLIB TO DDNAME ''SMPCSIT'' F
               FAILED'
         B     EXITPROG                EXIT PROGRAM
                                                                SPACE 1
*---------------------------------------------------------------------*
*  MODIFY ACB TO ACCEPT NEW DDNAME. CHANGE SEARCH KEY AND LENGTH TO   *
*  PROCESS DLIB DATASET                                               *
*---------------------------------------------------------------------*
PROCDLIB DS    0H
         MODCB ACB=ACB1,DDNAME=SMPCSIT SET DDNAME IN ACB
         MVC   SRCHKEY(4),=X'00010300' SET SEARCH KEY
         MVC   SRCHKEY+4(8),DZONE      SET SEARCH KEY
         MVC   SRCHKEY+12(4),=X'05100000' SET SEARCH KEY
         LA    R2,16                   SET KEY LENGTH
         MODCB RPL=RETRVE,KEYLEN=(R2)  CHANGE NEW KEY LENGTH
OPEN2    DS    0H
         MVC   MESSAGE(30),=C'TARGET ZONE DEDUCTED NOT FOUND'
         BAL   R14,OPEN                GO PROCESS GLOBAL CSI
FREESMPT ADYN  ACTION=FREE,BASE=R10,DD=SMPCSIT,                        X
               PARMT=PARM
EXITPROG DS    0H
         ISPEXEC 'VPUT (RZONE MESSAGE) SHARED' PUT VARS IN SHR POOL
         LH    R15,RCODE               SET RETURN CODE
         RACKOFF RC=(R15)              RETURN
                                                                SPACE 1
*---------------------------------------------------------------------*
*  OPEN ACB                                                           *
*---------------------------------------------------------------------*
OPEN     DS    0H
         ST    R14,R14SAVER            SAVE RETURN ADDRESS
         OPEN  ACB1                    OPEN VSAM DATASET
         LTR   R15,R15                 Q. ZERO RETURN CODE
         BZ    SHOWCB                     Y. GO SHOW CB
OPENERR1 DS    0H
         SHOWCB ACB=ACB1,AREA=ERROR,LENGTH=4,FIELDS=ERROR
         LA    R3,116
         C     R3,ERROR                Q. VERIFY NEEDED
         BE    VERIFY                     Y. GO VERIFY
         BNE   STMSGOPN                   N. PROCESS OTHER ERROR
         LA    R3,160
         C     R3,ERROR                Q. EMPTY DATASET
         BE    EMPTY                      Y. GO VERIFY
         B     STMSGOPN                PROCESS OTHER ERROR
VERIFY   DS    0H
         VERIFY RPL=RPLVER,ACTION=REFRESH
         LA    R3,1
         X     R3,OPNAGAIN             SET VERIFIED FLAG ON
         ST    R3,OPNAGAIN             LOAD REOPEN FALG
         BNZ   CLOSE                      N. GOTO CLOSE + RETRY OPEN
STMSGOPN DS    0H
         MVC   MESSAGE(21),=C'ERROR OPENING DATASET'
         B     CLOSE                   GO TO CLOSE ROUTINE
SHOWCB   DS    0H
         SHOWCB ACB=ACB1,AREA=NLOGR,LENGTH=16,                         X
               FIELDS=(NLOGR,LRECL,KEYLEN,RKP)
         L     R3,NLOGR                LOAD NO OF LOGICAL RECORDS
         LTR   R3,R3                   Q. EMPTY DATASET
         BNZ   NOTEMPTY                   N. CONTINUE PROCESSING
EMPTY    DS    0H
         MVC   MESSAGE(21),=C'EMPTY VSAM DATASET'
         B     CLOSE                   GO TO CLOSE ROUTINE
NOTEMPTY DS    0H
         TESTCB ACB=ACB1,ATRB=KSDS     TEST DATASET
         BE    GETRECD                    Y. GO TEST FOR SORT
NOTKSDS  DS    0H
         MVC   MESSAGE(21),=C'NOT KSDS VSAM DATASET'
         B     CLOSE                   GO TO CLOSE ROUTINE
*---------------------------------------------------------------------*
*  ACTUALLY GET RECORD ON KEY ACCESS                                  *
*---------------------------------------------------------------------*
GETRECD  DS    0H
         XC    RZONE,RZONE             INITIALISE RZONE
         GET   RPL=RETRVE              DUMMY 1ST.RECORD
         LTR   R15,R15                 Q. RECORD FOUND
         BNZ   NOTFOUND                   N. TELL OF ERROR
         SHOWCB RPL=RETRVE,AREA=LRECL,LENGTH=4,FIELDS=RECLEN
         MVC   RZONE(8),AREA1+16       MOVE R-ZONE NAME
         XC    RCODE,RCODE             SET ZERO RETURN CODE
         B     CLOSE                   GO CLOSE DATASET
NOTFOUND DS    0H
         B     CLOSE                   GO TO CLOSE ROUTINE
*---------------------------------------------------------------------*
*  SHOULD NEVER BE EXECUTED ON KEYED ACCESS                           *
*---------------------------------------------------------------------*
EODAD1   DS    0H
         MVC   MESSAGE(21),=C'NOT ENOUGH RECORDS'
         B     CLOSE
CLOSE    DS    0H
         CLOSE ACB1                    CLOSE VSAM DATA SET
         L     R3,OPNAGAIN             LOAD RE-OPEN FLAG
         LTR   R3,R3                   Q. ALREADY RETRIED
         BNZ   OPEN                       Y. RETRY OPEN
RETMAIN1 DS    0H
         L     R14,R14SAVER            SAVE RETURN ADDRESS
         LH    R15,RCODE               LOAD RETURN CODE
         BR    R14                     RETURN TO CALLER
                                                                SPACE 1
*=====================================================================*
*    VSAM CONTROL BLOCKS                                              *
*=====================================================================*
ACB1     ACB   EXLST=EXLST1,                                           X
               MACRF=(KEY,DIR,IN),                                     X
               DDNAME=SMPCSI,                                          X
               BUFND=5,                                                X
               BUFNI=2,                                                X
               RMODE31=ALL
EXLST1   EXLST EODAD=(EODAD1,A)
RETRVE   RPL   ACB=ACB1,                                               X
               AREA=AREA1,                                             X
               AREALEN=32760,                                          X
               ARG=SRCHKEY,                                            X
               KEYLEN=24,                                              X
               OPTCD=(KEY,DIR,GEN)
RPLVER   RPL   ACB=ACB1,                                               X
               AREA=AREA1,                                             X
               AREALEN=32760,                                          X
               OPTCD=CNV
                                                                SPACE 1
*=====================================================================*
*    WORK AREAS AND CONSTANTS                                         *
*=====================================================================*
         LTORG
RSAVE    DC    18F'0'               REGISTER SAVE AREA
*
R14SAVER DS    F                    R14 RETURN SAVE AREA
NLOGR    DC    F'0'                 NO OF RECORDS IN CSI
LRECL    DC    F'4096'              RECORD LENGTH
KEYLEN   DC    F'0'                 KEY LENGTH
KEYPOS   DC    F'0'                 KEY POSOITION
ERROR    DC    F'0'                 ERROR CODE
RCODE    DC    H'8'                 RETIURN CODE ON EXIT FROM PROGRAM
SRCHKEY  DS    0CL24                SEARCH KEY
         DC    X'00000300'
GZONE    DC    C'GLOBAL  '
         DC    X'20100000'
DZONE    DC    CL8' '
RZONE    DC    CL8' '
*
OPNAGAIN DC    F'0'                 RETRY DASTASET OPEN FLAG
         ISPINIT  MF=L              LIST FORMAT OF ISPINIT MACRO
         ISPVDEFX MF=L              LIST FORMAT OF ISPVDEFX MACRO
         ADYN     MF=L              LIST FORMAT OF ADYN MACRO
DSNAME   DC    CL44' '              DSNAME FOR DYNAMIC ALLOCATION
MESSAGE  DC    CL79' '              ERROR MESSAGE
SVC99WK  DS    CL400                DYNAMIC ALLOCATION WORK AREA
         DS    0F
AREA1    DS    CL200                DATA DESTINATION FOR VSAM READ
ADYNI    ADYN  DATA=IBM             IBM FOTRMAT OF ADYN MACRO
*---------------------------------------------------------------------*
         END   RELZONE
*=====================================================================*
