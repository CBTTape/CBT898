*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
S2       KICKOFF (R12)
         B CONT#100
TRACE    DC C'Y'                       P(RIORITY), R(OUTINE) N(ON-OP)
TARGET   DC C'R'                       P(RIORITY), R(OUTINE) N(ON-OP)
EIBDATE  DC    PL4'099365'             00CYYDDD
EIBTIME  DC    PL4'235000'             00HHMMSS
CONT#100 DS 0H
         LA    R4,TARGET               POINT TO INPUT DATA
         PDUMPR EIBDATE,MSG='Change date and time to your liking...',  +
               QUIT=EXITPROG,DISPLAY=Y,LIST=Y
         MVC   TIMEDATP,EIBDATE        DATE IN PACKED FORM
         B     CONVDATE
EXITPROG RACKOFF
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*
CONVDATE EQU   *
*---------------------------------------------------------------------*
         UNPK  TIMEDATE,TIMEDATP       CONVERT DATE TO YYMMDD FORMAT.
         MVC   CONVDAYS,TIMEDATP+2     DDD PORTION OF DATE
         PACK  CONVYEAR,TIMEDATE(3)    PACK CYY FOR LEAPYEAR TEST
         AP    CONVYEAR,=PL3'1900'     ADD 1900 TO GIVE FULL CC VALUE
TESTLEAP DS    0H
         MVC   WORKYEAR,CONVYEAR       BACKUP FULL LENGTH YEAR
         DP    CONVYEAR,=P'400'        DIVIDE BY 400
         CLC   CONVYEAR+2(2),=X'000F'  Q. DIVISABLE BY 400
         BNP   ISLEAPY                    Y. LEAP YEAR
         MVC   CONVYEAR,WORKYEAR       BACKUP FULL LENGTH YEAR
         DP    CONVYEAR,=P'100'        DIVIDE BY 100
         CLC   CONVYEAR+2(2),=X'000F'  Q. DIVISABLE BY 100
         BNP   NONLEAPY                   Y. NOT A LEAP YEAR
         MVC   CONVYEAR,WORKYEAR       BACKUP FULL LENGTH YEAR
         DP    CONVYEAR,=P'4'          DIVIDE BY 4
         CLI   CONVYEAR+3,X'0F'        Q. DIVISABLE BY 4
         BP    NONLEAPY                   N. NOT A LEAP YEAR
ISLEAPY  EQU   *
         LA    R15,LEAPDAMN            POINT TO LEAP YEAR VALUES
         B     CONVIMON
*
NONLEAPY EQU   *
         LA    R15,CONVDAMN            POINT TO NON LEAP YEAR VALUES
*
*---------------------------------------------------------------------*
CONVIMON EQU   *
    PDUMPR  24(R15),MSG='(2) : 365C=Non-leap, 366C=Leap',              +
               QUIT=EXITPROG,DISPLAY=Y,LIST=Y
         B     CONT#100
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
*        STATIC STORAGE FOR SWM100P                                   *
*---------------------------------------------------------------------*
*
CONVDAMN DC    PL2'0'                  DAYS IN MONTH - CUMULATIVE.
         DC    P'31',P'59',P'90'
         DC    P'120',P'151',P'181'
         DC    P'212',P'243',P'273'
         DC    P'304',P'334',P'365'
*
LEAPDAMN DC    PL2'0'                  DAYS IN MONTH - CUMULATIVE.
         DC    P'31',P'60',P'91'       LEAP YEAR
         DC    P'121',P'152',P'182'
         DC    P'213',P'244',P'274'
         DC    P'305',P'335',P'366'
*
*--------END OF STATIC STORAGE----------------------------------------*
*
*---------------------------------------------------------------------*
*        LITERAL POOL                                                 *
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
*        DYNAMIC STORAGE LAYOUT                                       *
*---------------------------------------------------------------------*
*
CONVDAYS DS    PL2                     USED BY CONVDATE ROUTINE
CONVMONS DS    PL2
CONVYEAR DS    PL4
WORKYEAR DS    PL4
*
TIMEDATE DS    CL6                     DATE IN CYYDDD FORMAT
TIMEDATP DS    PL4                     DITTO - PACKED
TIMENOW  DS    0CL6
TIMEHRS  DS    CL2
TIMEMIN  DS    CL2
TIMESEC  DS    CL2
TIMEPCK  DS    PL2
TIMEINC  DS    PL2                     MESSAGE MUST BE SENT BY.
*
*--------END OF RBSWMPTR MACRO----------------------------------------*
         END
