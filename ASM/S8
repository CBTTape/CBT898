S3       KICKOFF AMODE=31,RMODE=ANY
LOOP     PDUMPR DDNAME,MSG='Enter data under ? or /QUIT',QUIT=EXITPR00
         OC    DDNAME(15),=CL15' '     Convert to upper
*
         MVI   ERRMSG,C' '
         MVC   ERRMSG+1(L'ERRMSG-1),ERRMSG
*
*---------------------------------------------------------------------*
* Parameter format:                                                   *
* -----------------                                                   *
* Cols   Description                                                  *
* ------ ------------------------------------------------------------ *
*  01-08 DDname defined to CICS.                                      *
*  09-14 Time/Interval dependant on Time/Interval indicator (next     *
*        field).                                                      *
*        Time     - Time to execute the offload facility.             *
*        Interval - Time to elapse before next execution of the       *
*                   offload facility.                                 *
*  15-15 Time/Interval indicator. Literal 'T' or 'I'.                 *
*  16-16 Itarate indicator. Set to '*' to repeatedly execute the      *
*        offload at satisfaction of time/interval. Leave blank for a  *
*        once off execution.                                          *
*---------------------------------------------------------------------*
* Test if ddname is present. If not present, use default DD=MSGUSER   *
* cleared now.                                                        *
*---------------------------------------------------------------------*
         CLI   DDNAME,C' '             Q. Missing parameter(s)
         BE    EXECNOW                    Y. Execute now
*---------------------------------------------------------------------*
* Validate DDname for format.                                         *
*---------------------------------------------------------------------*
         LA    R6,7                    Length of DDNAME
         LA    R1,DDNAME+7             Point to end of field
DDBLOP   DS    0H
         CLI   0(R1),C' '              Q. Space
         BNE   TESTDD1                    N. Exit Loop
         BCTR  R1,0                    Back up one
         BCT   R6,DDBLOP               Back up length By 1
         CLI   0(R1),C' '              Q. Space
         BE    BADDDN                     Y. Invalid name (all spaces)
TESTDD1  DS    0H
         TRT   DDNAME(*-*),TRTTIME     Q. First byte numeric
         LA    R1,DDNAME                  Set 1st. byte as bad char
         BZ    BADDDN                     Y. Invalid name
         TRT   DDNAME(*-*),TRTNAME     Q. Valid characters
         EX    R6,*-6                     Execute test
         BNZ   BADDDN                     Y. Invalid name
*---------------------------------------------------------------------*
* Test if time is numeric, hh<=24, mm<=59 and ss<=59.                 *
*---------------------------------------------------------------------*
TESTTIM1 DS    0H
         LA    R6,5                    Length of Time (-1 for EX cmd)
TESTTIME DS    0H
         TRT   TIMEI(*-*),TRTTIME      Q. Time numeric
         EX    R6,TESTTIME             Q. Time numeric
         BNZ   BADTIME                    N. Signal time error
         PACK  DWORK8(8),HH(2)         Pack hours
         CVB   R1,DWORK8               Convert to binary
         CH    R1,=H'24'               Q. > 24 hours
         BH    BADTIME                    Y. Signal time error
         PACK  DWORK8(8),MM(2)         Pack minutes
         CVB   R1,DWORK8               Convert to binary
         CH    R1,=H'59'               Q. > 59 Minutes
         BH    BADTIME                    Y. Signal time error
         PACK  DWORK8(8),SS(2)         Pack seconds
         CVB   R1,DWORK8               Convert to binary
         CH    R1,=H'59'               Q. > 59 Minutes
         BH    BADTIME                    Y. Signal time error
*---------------------------------------------------------------------*
* Test Time/Interval indicator.                                       *
*---------------------------------------------------------------------*
TESTTYPE DS    0H
         CLI   TYPE,C'I'               Q. 'I'ncremental?
         BE    TESTTIM1                   Y. Go to next test
         CLI   TYPE,C'T'               Q. 'T'ime?
         BNE   BADTYPE                    N. Tell of error
*---------------------------------------------------------------------*
* Test iteration indicator.                                           *
*---------------------------------------------------------------------*
TESTITER DS    0H
         CLI   ITER,C' '               Q. Unspecified
         BE    VALIDPRM                   Y. Go to next test
         CLI   ITER,C'*'               Q. Iterate indicator
         BNE   BADITER                    N. Signal iretation error
VALIDPRM DS    0H
         MVC   ERRMSG(10),=C'Valid data'
         B     LOOP                    Repeat
BADDDN   DS    0H
         MVC   ERRMSG(34),=C'Invalid character "." at DDNAME(.).'
         MVC   ERRMSG+19(1),0(R1)      Move character to message
         LA    R6,DDNAME               Load start of ddname
         SR    R1,R6                   Point to offset
         LA    R1,1(R1)                Bump length
         O     R1,=X'000000F0'         Convert to displat
         STC   R1,ERRMSG+32            Store character
         B     LOOP                    Repeat
BADTIME  DS    0H
         MVC   ERRMSG(30),=C'Invalid time "......" entered.'
         MVC   ERRMSG+14(6),TIMEI      Move entered time value
         B     INVEXIT                 Exit with error message
BADTYPE  DS    0H
         MVC   ERRMSG(23),=C'Invalid TYPE=. entered.'
         MVC   ERRMSG+13(1),TYPE       Move invalid value to message
         B     INVEXIT                 Exit with error message
BADITER  DS    0H
         MVC   ERRMSG(23),=C'Invalid ITER=. entered.'
         MVC   ERRMSG+13(1),ITER       Move invalid value to message
         B     INVEXIT                 Exit with error message
EXECNOW  DS    0H
         MVC   ERRMSG(14),=C'Executing now.'
*
 B     LOOP    <====
*
INVEXIT  DS    0H
 B     LOOP                    Repeat
EXITPR00 RACKOFF
*
         LTORG
DDNAME   DC    CL8'MSGUSER'
TIMEI    DC    C'120000'               Format 'HHMMSS'
         ORG   TIMEI
HH       DS    CL2
MM       DS    CL2
SS       DS    CL2
         ORG
TYPE     DC    C'T'                    Format 'I' or 'T'
ITER     DC    C'*'                    Iteration indicator
         DC    CL32'Louie is a Jerk'
ERRMSG   DC    CL79' '
DWORK8   DS    D
*
TRTNAME  DS    0CL256
         DC    256AL1(*-TRTNAME)
         ORG   TRTNAME
         DC    01X'01'
         ORG   TRTNAME+C'$'
         DC    01X'00'
         ORG   TRTNAME+C'#'
         DC    02X'00'
         ORG   TRTNAME+C'@'
         DC    02X'00'
         ORG   TRTNAME+C'A'
         DC    09X'00'
         ORG   TRTNAME+C'J'
         DC    10X'00'
         ORG   TRTNAME+C'S'
         DC    09X'00'
         ORG   TRTNAME+C'0'
         DC    10X'00'
         ORG
TRTTIME  DS    0CL256
         DC    256AL1(*-TRTTIME)
         ORG   TRTTIME
         DC    01X'01'
         ORG   TRTTIME+C'0'
         DC    10X'00'
         ORG
*
         END
