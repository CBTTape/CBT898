* CSECT <----- TO PERMIT GOAK'ING
*
* PROGRAM TO LOAD A LOADMODULE AND DISPLAY IT TO ENSURE THAT THE
* VERSION BEING EXECUTED IS THE ONE YOU WANT.....
*
SHOWLOAD KICKOFF (R13,R12)
*---------------------------------------------------------------------*
START    DS    0H
         LR    R7,R1                   CPPL ADDR
         USING CPPL,R7                 ADDRESABILITY TO CPPL
RETGETM  DS    0H
         L     R6,CPPLCBUF             GET COMMAND BUFFER ADDR
         LH    R1,0(R6)                TOTAL COMMAND LENGTH
         SH    R1,=H'2'                SUBTRACT LENGTH OF ITSELF
         LH    R3,2(R6)                OPERAND LENGTH
         SR    R1,R3                   SUBTRACT OPERAND LENGTH
         SH    R1,=H'2'                SUBTRACT LENGTH OF OPERAND
         LTR   R1,R1                   Q. ANY PARAMETER LENGTH
         BZ    EXITPR12                   N. ERROR EXIT
         LA    R2,4(R6)                ADDR OF START OF COMMAND
         AR    R2,R3                   ADD DISPLACEMENT OF OPERAND
PROCESS  DS    0H
         BCTR  R1,0                    RELATIVE LENGTH
         EX    R1,MOVEPROG             MOVE PROGRAM NAME
         OC    PROGRAM,SPACES          CONVERT TO UPPER CASE
         LA    R3,PROGRAM              POINT TO PROGRAM NAME
         LOAD  EPLOC=(R3)              LOAD PROGRAM
         LTR   R15,R15                 Q. WAS LOAD SUCCESFUL
         BNZ   EXITPR16                   N. GO TO ERROR EXIT
         LR    R2,R0                   POINT TO ENTRY
         PDUMP (R2),MSG='POINTING TO PROGRAM',LIST=YES
         DELETE EPLOC=(R3)             DELETE PROGRAM
EXITPR00 RACKOFF RC=0
EXITPR12 DS    0H
         LA    R2,MSG1
         LA    R3,L'MSG1
         TPUT  (R2),(R3),EDIT,WAIT,,BREAKIN,HIGHP
         RACKOFF RC=0
EXITPR16 DS    0H
         LA    R2,MSG2
         LA    R3,L'MSG2
         TPUT  (R2),(R3),EDIT,WAIT,,BREAKIN,HIGHP
         RACKOFF RC=0
         LTORG
PROGRAM  DC    CL8' '
SPACES   DC    CL256' '
MSG1     DC    C'MISSING LOAD MODULE PARAMETER.'
MSG2     DC    C'LOAD OF PROGRAM REQUESTED UNSUCCESFUL.'
MOVEPROG MVC   PROGRAM(0),0(R2)        MOVE PROGRAM NAME
         IKJCPPL COMMAND           PROCESSOR PARAMETER LIST
         END
