* CSECT <----- To permit GOAK'ing
*
* Program to load a LOADMOD and display it to ensure that the
* version being executed is the one you want.....
*
SLMODR   KICKOFF (R13),LIST=Y
*---------------------------------------------------------------------*
         PDUMPR ,                      INITIALISE ENVIRONMENT
START    DS    0H
         LR    R7,R1                   CPPL ADDR
         USING CPPL,R7                 ADDRESABILITY TO CPPL
RETGETM  DS    0H
         L     R6,CPPLCBUF             GET COMMAND BUFFER ADDR
         LH    R1,0(R6)                TOTAL COMMAND LENGTH
         SH    R1,=H'2'                SUBTRACT LENGTH OF ITSELF
         LH    R3,2(R6)                OPERAND LENGTH
         SR    R1,R3                   SUBTRACT OPERAND LENGTH
         SH    R1,=H'2'                SUBTRACT LENGTH OF OPERAND
         LTR   R1,R1                   Q. ANY PARAMETER LENGTH
         BZ    EXITPR12                   N. ERROR EXIT
         LA    R2,4(R6)                ADDR OF START OF COMMAND
         AR    R2,R3                   ADD DISPLACEMENT OF OPERAND
PROCESS  DS    0H
         BCTR  R1,0                    RELATIVE LENGTH
MOVEPROG MVC   PROGRAM(0),0(R2)        MOVE PROGRAM NAME
         EX    R1,MOVEPROG             MOVE PROGRAM NAME
         OC    PROGRAM,=CL8' '         CONVERT TO UPPER CASE
         LA    R3,PROGRAM              POINT TO PROGRAM NAME
         LOAD  EPLOC=(R3)              LOAD PROGRAM
         LTR   R15,R15                 Q. WAS LOAD SUCCESFUL
         BNZ   EXITPR16                   N. GO TO ERROR EXIT
         LR    R2,R0                   POINT TO ENTRY
         ST    R2,PPTR                 STORE POINTER
         ICM   R1,B'1000',=X'00'       Clear High Byte
         SLL   R1,3                    Multiply by 8
         ST    R1,TARGET               Save length
         HEXCHMAC TARGET,PROGRAM+24,4  Conver to character
         PDUMPR (R2),MSG=MSGO(MSGOL)
         DELETE EPLOC=(R3)             DELETE PROGRAM
EXITPR00 DS    0H
         RACKOFF RC=0
EXITPR12 DS    0H
         PDUMPR MSG1
         B     EXITPR00
EXITPR16 DS    0H
         PDUMPR MSG2
         B     EXITPR00
*
         LTORG
MSGO     DC    C'Memory dump of load module '''
PROGRAM  DC    CL8' '
         DC    C''' for length of 12345678 bytes.'
MSGOL    EQU   *-MSGO
         DS    0F
PPTR     DS    F
         DC    CL12' '
MSG0     DC    CL80'Place cursor over 1st. address and hit <ENTER>.'
MSG1     DC    CL80'Missing load module parameter.'
MSG2     DC    CL80'Load of program requested unsuccesful.'
TARGET   DC    X'00000000'
         IKJCPPL COMMAND           PROCESSOR PARAMETER LIST
         END
