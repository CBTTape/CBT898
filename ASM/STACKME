STACKME  KICKOFF (R12),WORKAREA,WORKLEN
         LR    R2,R1                   STORE CPPL ADDRESS
         LA    R0,WORKCONS             ADDRESS OF STORAGE
         L     R1,=A(WORKCONL)         LENGTH OF STORAGE
         L     R14,=A(WORKCONS)        SOURCE
         LR    R15,R1
         MVCL  R0,R14                  MOVE DATA AREA INTO GOTTEN AREA
         ST    R2,CPPLSAVE             STORE REGISTER 1 LATER USE
         BAL   R14,GETMAIN1            GO AND GET WORK AREA
RETGETM  DS    0H
EXITPR00 DS    0H
         RACKOFF RC=0
*---------------------------------------------------------------------*
*=====================================================================*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*   GETMAIN WORK AREA  FOR LIST SOURCE DESCRIPTOR (LSD) AND IN-STORAGE*
*   LIST MUST BE ALLOCATED OUT OF SUBPOOL "78".                       *
*   LIST REQUEST IS MADE AS SEVERAL AREAS ARE REQUIRED.               *
*   NOTE - NOTE - NOTE - NOTE - NOTE - NOTE - NOTE - NOTE - NOTE      *
*    THIS AREA GETMAINED DOES NOT HAVE TO BE FREEMAINED. IN FACT AN   *
*    ATTEMPTED FREEMAIN WILL CAUSE AN ABEND.                          *
*---------------------------------------------------------------------*
GETMAIN1 DS    0H
         STM   R0,R14,REGSAVE1         SAVE REGISTERS
         LA    R2,GMLIST               LIST ADDRESS
         LA    R3,GMADRS               RETURN ADDRESSES
         GETMAIN LU,LA=(R2),A=(R3),SP=78,LOC=BELOW ISSUE GETMAIN
         LTR   R15,R15                 Q. ZERO RETURN CODE FROM GETMAIN
         BNZ   EXITPR00                   N. EXIT PROGRAM
         L     R5,GMADRS               LOAD THE 1ST. ADDRESS IN LIST
         MVC   0(16,R5),$LSD           MOVE LSD DETAILS
*
         L     R6,GMADRS+4             LOAD THE 2ND. ADDRESS IN LIST
         ST    R6,0(R5)                STORE ISL 1ST. POINTER
         ST    R6,8(R5)                ALSO USE AS LAST POINTER HERE
         MVC   0(240,R6),COMMANDS      MOVE THE THREE COMMANDS
*
         L     R7,CPPLSAVE             LOAD CPPL SAVED ADDRESS
         USING CPPL,R7                 ADDRESABILITY TO CPPL
         L     R3,CPPLUPT              LOAD UPT ADDRESS
         L     R4,CPPLECT              LOAD ECT ADDRESS
*
         STACK PARM=STCKLIST,UPT=(R3),ECT=(R4),ECB=STCKECB,            X
               STORAGE=((R5),SOURCE),MF=(E,IOPLADRS)
         LTR   R15,R15                 Q. ZERO RETURN CODE
         BZ    RETMAIN1                   Y. EXIT
RETMAIN1 DS    0H
         LM    R0,R14,REGSAVE1         RESTORE REGISTERS
         BR    R14                     RETURN TO CALLER
*---------------------------------------------------------------------*
*=====================================================================*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
         DS    0F
WORKAREA CSECT
REGSAVER DS    18F
WORKCONS DS    0D
REGSAVE1 DS    18F
CPPLSAVE DS    F
GMAIN@1  DS    F
$LSD     DS    A                    ADDRESS OF 1ST. IOL POINTER
         DC    H'80'                LENGTH OF EACH ENTRY
         DC    H'240'               TOTAL LENGTH
*        DC    H'160'
         DS    A                    ADDRESS OF LAST IOL POINTER
         DC    F'0'                 RESERVED
$LSDLEN  EQU   *-$LSD
COMMANDS DC    CL80'%CONFLICT'
         DC    CL80'LISTDS ''ZTSALK.ISPF.ISPPROF'''
         DC    CL80'%WHATHAS ZTSALK'
GMAINLEN EQU   *-$LSD
GMLIST   DC    F'16'                LENGTH OF 1ST. GETMAIN AREA
         DC    X'80'                END OF LIST INDICATOR
         DC    AL3(240)             LENGTH OF 2ND. GETMAIN AREA
GMADRS   DC    2F'0'                RETURN ADRRESES OF GETMAINS
STCKLIST STACK MF=L                 LIST FORMAT OF STACK MACRO
STCKECB  DC    F'0'                 STACKS ECB ADDRESS
IOPLADRS DC    4F'0'                INPUT/OUTPUT PARAMETER LIST
WORKCONL EQU   *-WORKCONS
WORKLEN  EQU   *-WORKAREA
         IKJCPPL                    DSECT FOR THE COMMAND PROCESSOR
*---------------------------------------------------------------------*
         END
