*
**       Title:    'WAIT FOR A SPECIFIED TIME'
**       Module:   WAITFOR
**       Program:  WAITFOR
**       Function: Accept input parameter to WAIT for specified
**                 period from either a command processor or single
**                 parameter list. The format of input will be HHMMSS
**                 (right justified).
**
**                 Any non numeric data will generate a return code
**                 of 08.
**
**                 The default, no parameter, is 1 second.
**
**       Author:   ASYSTS P/L (A. Kara).
*
*=====================================================================*
*        PRINT OFF
         COPY  @MACROS                 Common macros
*        PRINT ON
WAITFOR  KICKOFF (R11,R12),WORKAREA,WORKAREL,LIST=NO,AMODE=31,RMODE=24
*---------------------------------------------------------------------*
INIT     DS    0H
         LR    R7,R1                   CPPL address ??
         MVC   VTIME,CTIME             Move constant to DSECT
         TM    0(R7),X'80'             Q. Parameter type address
         BO    PARMPARM                   Y. Process as parameter type
CMDPARM  DS    0H
         USING CPPL,R7                 Addressability to CPPL
         L     R6,CPPLCBUF             Get command buffer addr
         LH    R1,0(R6)                Total command length
         SH    R1,=H'2'                Subtract length of itself
         LH    R3,2(R6)                Operand length
         SR    R1,R3                   Q. Any parameter length
         SH    R1,=H'2'                Subtract length of operand
         BZ    WAIT                       N. Use default wait
         LA    R2,4(R6)                Start of command buffer
         AR    R2,R3                   Add displacement of operand
         B     PROCPARM                Go process parameter
PARMPARM DS    0H
         L     R6,0(R7)                Load address of parameter
         LH    R1,0(R6)                Load it's length
         LTR   R1,R1                   Q. Is parm length zero
         BZ    WAIT                       Y. Go process default
         LA    R2,2(R6)                Point to parameter
PROCPARM DS    0H
         LR    R5,R1                   Save length
         BCTR  R5,0                    Prepare for 'EX' command
         EX    R5,VALIDNO              Q. Any invalid characters
         BNZ   EXITPR08                   Y. Exit with RC=8
         LA    R3,VTIME+5              Point to end of time field
         SR    R3,R5                   Back back length of time parm
         EX    R5,MOVETIME             Move time to WAIT
*---------------------------------------------------------------------*
WAIT     DS    0H
         STIMER WAIT,DINTVL=VTIME      Wait for specified time
EXITPR00 DS    0H
         RACKOFF RC=0,LIST=NO          Exit with RC=0
EXITPR08 DS    0H
         RACKOFF RC=8,LIST=NO          Exit with RC=8
*---------------------------------------------------------------------*
VALIDNO  TRT   0(0,R2),TRTABLE         Compare valid characters
MOVETIME MVC   0(0,R3),0(R2)           Move time to wait
*=====================================================================*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
SPACES   DC    CL256' '
*                hhmmsstt'                                            *
CTIME    DC    CL8'00000100'           Default constant time interval
TRTABLE  DC    256X'01'
         ORG   TRTABLE+X'F0'
         DC    X'00000000000000000000'
         ORG
*---------------------------------------------------------------------*
WORKAREA DSECT
REGSAVER DS    18F                     Don't F...in touch this
*                hhmmsstt'                                            *
VTIME    DC    D'00000000'             Variable time interval
WORKAREL EQU   *-WORKAREA
*---------------------------------------------------------------------*
         IKJCPPL COMMAND               PROCESSOR PARAMETER LIST
*---------------------------------------------------------------------*
         END   WAITFOR
