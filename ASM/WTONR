*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
**
**       TITLE:    'WTO MESSAGES TO CONSOLE.'
**       SYSTEM:   N/A
**       MODULE:   WTONR
**       PROGRAM:  WTONR
**       FUNCTION: WRITES A NUMBER OF 'WTO' MESSAGES TO THE CONSOLE.
**                 NO REPLY REQUIRED.
**
**                 PROGRAM IS NOT RE-ENTRANT.
**
**       AUTHOR:   A. KARA.
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
WTORMSG  KICKOFF (R13)
         BAL   R14,PROCESS             GO AND PROCESS
EXITPROG RACKOFF RC=(R15)
*
PROCESS  DS    0H
         ST    R14,SAVSUBR1            SAVE RETURN REGISTERS
*---------------------------------------------------------------------*
         LR    R11,R1              POINTER TO PARAMETER LIST
         L     R11,0(R11)          LOAD PARM LIST ADDRESS
TESTPARM DS    0H
         LH    R2,0(R11)               LENGTH TO R2
         LTR   R2,R2                   Q. PARM LENGTH = 0?
         BZ    OPENFILE                   Y. GET RECORD FROM SYSIN
         BCTR  R2,0                    RELATIVE LENGTH
         EX    R2,MOVEPARM             MOVE PARAMETER
         LA    R1,WTODET1              LOAD ADDRESS OF WTO AREA
         WTO   MF=(E,(1))              ISSUE MSG
         B     RETMAIN1                EXIT ROUTINE
*---------------------------------------------------------------------*
MOVEPARM MVC   WTODET1+4(0),2(R11)     MOVE BUFFER DETAILS
*---------------------------------------------------------------------*
OPENFILE DS    0H
         OPEN  (SYSIN,(INPUT))         OPEN INPUT FILE
         TM    SYSIN+48,X'10'          Q. DATASET OPEN
         BZ    RETMAIN1                   N. EXIT PROGRAM WITH ERROR
         LA    R2,200                  MAXIMUM NO OF ENTRIES
         LA    R3,BUFFER               ADDRESS OF BUFFER
LOOPDATA DS    0H
         GET   SYSIN,CARD              READ SYSIN INTO CARD
         MVC   0(80,R3),CARD           MOVE IT TO BUFFER
         LA    R3,80(R3)               POINT TO NEX BUFFER LINE
         BCT   R2,LOOPDATA             LOOP TILL END
         MVI   LINELIMT,C'Y'           SET LINE LIMIT PASSED FLAG
INFEND   DS    0H
         CLOSE SYSIN                   CLOSE INPUT FILE
REDISPL  DS    0H
         LA    R4,200                  MAXIMUM NO OF ENTRIES
         SR    R4,R2                   LOOP LENGTH
         LA    R3,BUFFER               START ADDRESS OF BUFFER
         LA    R5,WTODET1              LOAD ADDRESS OF WTO AREA
WTOLOOP  DS    0H
         MVC   WTODET1+4(80),0(R3)     MOVE BUFFER DETAILS
         WTO   MF=(E,(5))              ISSUE MSG
         LA    R3,80(R3)               POINT TO NEX BUFFER LINE
         BCT   R4,WTOLOOP              LOOP TILL END
WTOLOOPE DS    0H
         CLI   LINELIMT,C'Y'           Q. LIMIT PASSED FLAG SET
         BNE   DISPWTOR                   N. BYPASS
         LA    R5,WTODET2              LOAD ADDRESS OF WTO AREA
         WTO   MF=(E,(5))              ISSUE MSG
DISPWTOR DS    0H
         XR    R15,R15                 SET ZERO RETURN CODE
RETMAIN1 DS    0H
         L     R14,SAVSUBR1            RESTORE SAVED REGISTERS
         BR    R14                     RETURN TO CALLER
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
WTODET1  DS    0F
         WTO   '                                                       *
                                        ',                             *
               ROUTCDE=(11),DESC=(7),                                  *
               MF=L
*---------------------------------------------------------------------*
WTODET2  DS    0F
         WTO   '>>>> - MESSAGE IS > 200 LINE LIMIT. ADVISE USER ACCORDI*
               NGLY - <<<<',                                           *
               ROUTCDE=(11),DESC=(7),                                  *
               MF=L
*---------------------------------------------------------------------*
SAVSUBR1 DS    15F
DWORK8   DS    D
LINELIMT DC    CL1'N'
SPACES   DC    CL256' '
REPLY    DS    CL2
CARD     DS    CL80
REPLYECB DC    F'0'
SYSIN    DCB   DDNAME=SYSIN,                                           X
               DSORG=PS,                                               X
               MACRF=(GM),                                             X
               EODAD=INFEND
BUFFER   DS    200CL80
         END
