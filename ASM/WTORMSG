*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
**
**       TITLE:    'WTOR MESSAGES TO CONSOLE.'
**       SYSTEM:   N/A
**       MODULE:   WTORMSG
**       PROGRAM:  WTORMSG
**       FUNCTION: WRITES A NUMBER OF 'WTO' MESSAGES TO THE CONSOLE
**                 WITH THE LAST ONE BEING A WTOR PROMPT TO 'R' REPEAT,
**                 OR A 1 DIGIT RETURN CODE AS ADVISED. THE RETURN
**                 CODE WILL BE SET IN ACCORDANCE WITH THE REPLY.
**                 PARAMETERS ARE PASSED FROM A SYSIN DATASET OF 80
**                 CHARACTERS. THERE IS A LIMIT OF 200 LINES OF
**                 MESSAGES. (CONSIDERED SUFFICIENT FOR CONSOLE
**                 DISPLAY.) IF > 200 REQUIRED WRITE A SHORT MESSAGE
**                 WITH FURTHER REFERENCE ELSEWHERE.
**
**                 PROGRAM IS NOT RE-ENTRANT.
**
**       AUTHOR:   A. KARA.
*
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
WTORMSG  KICKOFF (R13)
         BAL   R14,PROCESS             GO AND PROCESS
EXITPROG RACKOFF RC=(R15)
*
PROCESS  DS    0H
         ST    R14,SAVSUBR1            SAVE RETURN REGISTERS
OPENFILE DS    0H
         OPEN  (SYSIN,(INPUT))         OPEN INPUT FILE
         TM    SYSIN+48,X'10'          Q. DATASET OPEN
         BZ    RETMAIN1                   N. EXIT PROGRAM WITH ERROR
         LA    R2,200                  MAXIMUM NO OF ENTRIES
         LA    R3,BUFFER               ADDRESS OF BUFFER
LOOPDATA DS    0H
         GET   SYSIN,CARD              READ SYSIN INTO CARD
         MVC   0(80,R3),CARD           MOVE IT TO BUFFER
         LA    R3,80(R3)               POINT TO NEX BUFFER LINE
         BCT   R2,LOOPDATA             LOOP TILL END
         MVI   LINELIMT,C'Y'           SET LINE LIMIT PASSED FLAG
INFEND   DS    0H
         CLOSE SYSIN                   CLOSE INPUT FILE
REDISPL  DS    0H
         LA    R4,200                  MAXIMUM NO OF ENTRIES
         SR    R4,R2                   LOOP LENGTH
         LA    R3,BUFFER               START ADDRESS OF BUFFER
         LA    R5,WTODET1              LOAD ADDRESS OF WTO AREA
WTOLOOP  DS    0H
         MVC   WTODET1+4(80),0(R3)     MOVE BUFFER DETAILS
         WTO   MF=(E,(5))              ISSUE MSG
         LA    R3,80(R3)               POINT TO NEX BUFFER LINE
         BCT   R4,WTOLOOP              LOOP TILL END
WTOLOOPE DS    0H
         CLI   LINELIMT,C'Y'           Q. LIMIT PASSED FLAG SET
         BNE   DISPWTOR                   N. BYPASS
         LA    R5,WTODET2              LOAD ADDRESS OF WTO AREA
         WTO   MF=(E,(5))              ISSUE MSG
DISPWTOR DS    0H
         XC    REPLYECB,REPLYECB       INITIALISE REPLY CONTROL BLOCK
         LA    R1,WTORDET1             ADDRESS OF WTOR MESSAGE
         WTOR  MF=(E,(1))              ISSUE MSG TO OPERATOR
         WAIT  ECB=REPLYECB            WAIT FOR RESPONSE
         OC    REPLY(2),=X'4040'       ENABLE UPPER CASE
         CLI   REPLY+1,C' '            Q. WAS 2ND. BYTE A SPACE
         BNE   DISPWTOR                   N. ERROR REPLY
         CLI   REPLY,C'0'              Q. WAS REPLY REPEAT
         BL    TESTREDS                   N. TEST FOR REPEAT
         PACK  DWORK8(8),REPLY(1)      PACK RETURN CODE
         CVB   R15,DWORK8              CONVERT TO BINARY
         B     RETMAIN1                GOTO COMMON RETURN
TESTREDS DS    0H
         CLI   REPLY,C'R'              Q. WAS REPLY REPEAT
         BE    REDISPL                    Y. GO REOPEN FILE + REPROCESS
*        CLC   REPLY(2),=CL2'C'        Q. WAS REPLY CONTINUE
*        BNE   DISPWTOR                   N. REDISPLAY WTOR PROMPT
         B     DISPWTOR                REDISPLAY WTOR PROMPT
         XR    R15,R15                 SET ZERO RETURN CODE
RETMAIN1 DS    0H
         L     R14,SAVSUBR1            RESTORE SAVED REGISTERS
         BR    R14                     RETURN TO CALLER
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
*---------------------------------------------------------------------*
         LTORG
*---------------------------------------------------------------------*
WTODET1  DS    0F
         WTO   '                                                       *
                                        ',                             *
               ROUTCDE=(2,12),DESC=(7),                                *
               MF=L
*---------------------------------------------------------------------*
WTODET2  DS    0F
         WTO   '>>>> - MESSAGE IS > 200 LINE LIMIT. ADVISE USER ACCORDI*
               NGLY - <<<<',                                           *
               ROUTCDE=(2,12),DESC=(7),                                *
               MF=L
*---------------------------------------------------------------------*
WTORDET1 DS    0F
         WTOR  'REPLY "R" TO REPEAT DISPLAY OR A 1 DIGIT REPLY AS ABOVE*
               .',                                                     *
               REPLY,              ... REPLY FIELD                     *
               2,                  ... REPLY LENGTH                    *
               REPLYECB,           ... WTOR EVENT FIELD                *
               MF=L
*---------------------------------------------------------------------*
SAVSUBR1 DS    15F
DWORK8   DS    D
LINELIMT DC    CL1'N'
SPACES   DC    CL256' '
REPLY    DS    CL2
CARD     DS    CL80
REPLYECB DC    F'0'
SYSIN    DCB   DDNAME=SYSIN,                                           X
               DSORG=PS,                                               X
               MACRF=(GM),                                             X
               EODAD=INFEND
BUFFER   DS    200CL80
         END
