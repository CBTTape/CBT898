***********************************************************************
*                                                                     *
*    Title:    "EXCLUDE DUPLICATE LINES"                              *
*    System:   N/A                                                    *
*    Module:   XDUP                                                   *
*    Program:  XDUP                                                   *
*    Function: The purpose of this program is to extract duplicate    *
*              lines from the edited file. It is assumed that a prior *
*              sort has been issued.                                  *
*    Parameters:                                                      *
*              sss - Up to a 3 digit start column < LRECL/256.        *
*              eee - Up to a 3 digit end column < LRECL/256 and must  *
*                    be > sss. For end column, 'sss' must be entered  *
*                    otherwise it will be treated as 'sss'.           *
*              B   - Batch job.                                       *
*              D   - Delete excluded lines.                           *
*              E   - Exit edit session on macro (program) termination.*
*              K   - Backup member using the 'BU P #' macro and       *
*                    parameters.                                      *
*              U   - File is unsorted.                                *
*              X   - Exclude the original line as well as it's        *
*                    duplicates.                                      *
*                                                                     *
*              Has it's own built in tutorial. Member XDPLH000....    *
*                                                                     *
*    Notes:    Program is re-entrant                                  *
*    Author:   ASYSTS Pty. Ltd. (Alex Kara)                           *
*                                                                     *
***********************************************************************
*         PRINT OFF
*         COPY  @MACROS                 COMMON MACROS
*         PRINT ON
***********************************************************************
                                                                SPACE 1
XDUP      KICKOFF (R11,R12),WORKAREA,WORKAREL
                                                                SPACE 1
*---------------------------------------------------------------------*
*   Initialise environment                                            *
*---------------------------------------------------------------------*
         ISPEXEC INIT=Y,MF=E           INIT ISPF INTERFACE
         ISPVDEFX LIST=YE,FORMAT=CHAR,MF=E,                            +
               INM=(STATUS),ANM=STATUS,LEN=4096
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(LINE#),ANM=LINE#,LEN=6
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(PARM1),ANM=STATUS,LEN=79
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(PARM2),ANM=STATUS+80,LEN=79
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(PARM3),ANM=STATUS+160,LEN=79
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(PARM4),ANM=STATUS+240,LEN=79
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(PARM5),ANM=STATUS+320,LEN=79
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(PARM6),ANM=STATUS+400,LEN=79
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(PARM7),ANM=STATUS+480,LEN=79
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(PARM8),ANM=STATUS+560,LEN=79
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(GC009),ANM=GC009,LEN=79
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(START),ANM=START,LEN=3
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(END),ANM=END,LEN=3
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(XST),ANM=XSTATUS,LEN=2
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(#EX),ANM=#EX,LEN=8
                                                                SPACE 1
         MVC   STATUS(256),SPACES      CLEAR FIELD
         MVC   STATUS+256(256),SPACES  CLEAR FIELD
         MVC   GC009,SPACES            CLEAR FIELD
         MVI   QSUB,C' '               CLEAR FIELD
                                                                SPACE 1
*        ISPEXEC 'CONTROL ERRORS RETURN'
         ISPEXEC 'CONTROL ERRORS CANCEL'
         ISPEXEC 'ISREDIT MACRO (PARM1,PARM2,PARM3,PARM4,PARM5,PARM6,PAR
               RM7 PARM8)'
#PARMS   EQU   8                       PERMISSABLE NO OF UNIQUE PARMS
         CLI   STATUS,C'?'             Q. HELP CALLED
         BNE   NOTHELP                    N. CONTINUE
         ISPEXEC 'SELECT PGM(ISPTUTOR) PARM(XDUPH000)'
         ISPEXEC 'CONTROL DISPLAY REFRESH' REFRESH SCREEN AFTER FULLSCR
         B     EXITPR00                EXIT PROGRAM
                                                                SPACE 1
NOTHELP  DS    0H
         BAL   R14,PARSPARM            GO PARSE PARAMETERS
                                                                SPACE 1
BACKITUP DS    0H
*---------------------------------------------------------------------*
* Backup member if requested                                          *
*---------------------------------------------------------------------*
         TM    FLAG,BACKUP             Q. BACKUP MEMBER
         BNO   NOBACKUP                   N. BYPASS
         ISPEXEC 'ISREDIT BU P #'      BACKUP DATASET
         LTR   R15,R15                 Q. BACKUP OK
         BZ    NOBACKUP                   Y. BYPASS
         MVC   GC009(22),=C'Error backing up file.'
         B     EXITPR08                ERROR EXIT
                                                                SPACE 1
NOBACKUP DS    0H
         ISPEXEC 'ISREDIT (STATUS) = USER_STATE'
         BAL   R14,CURSOR10            REPOSITION CURSOR
         BAL   R14,LASTLINE            FIND LAST LINE
         PACK  DWORK8(8),LAST(6)       PACK LAST LINE NUMBER
         CVB   R9,DWORK8                  AND USE AS LOOP COUNTER
         LA    R8,1                    INITIALISE LINE NUMBER POINTER
         CLEARV SLINE,PAD=00,MF=E      INITIALISE ORIGINAL LINE
         PACK  DWORK8(8),START(3)      PACK STARTING
         CVB   R4,DWORK8                  COLUMN
         BCTR  R4,0                       RELATIVE TO 0
         LA    R2,LINE                 POINT TO START OF LINE
         AR    R2,R4                   POINT TO STARTING
         PACK  DWORK8(8),END(3)        PACK ENDING COLUMN AND
         CVB   R3,DWORK8                CONVERT
         SR    R3,R4                   CALCULATE ACTUAL LENGTH
         UNPACK R3,3,LEN,MF=E          STORE LENGTH
         BCTR  R3,0                    PREPARE FOR 'EX' COMMAND
         OI    FLAG,NOTEXCL            SET ORIG FLAG NOT EXLUDED
         MVI   TRACE,C'Y'              ENABLE PDUMP TRACE ON  <------
         LA    R7,SLINE                POINT TO SAVE LINE
         ISPEXEC 'ISREDIT RESET'
                                                                SPACE 1
         TM    FLAG,UNSORT             Q. UNSORTED FILE
         BO    UNSORTED                   Y. PROCESS UNSORTED
                                                                SPACE 1
LOOP10   DS    0H
         UNPACK R8,6,LINE#,MF=E        CONVERT TO CHARACTER
         ISPEXEC 'ISREDIT (LINE) = LINE &&LINE#'
*        PDUMP (R2),MSG='R7=ORIGINAL',TRACE=TRACE,QUIT=EXITPR00
         EX    R3,COMPARE              Q. ARE THE LINES EQUAL
         BNE   NOTEQUA1                   N. GO BUMP POINTER
         ISPEXEC 'ISREDIT XSTATUS &&LINE# = X'
         TM    FLAG,XDUPL              Q. EXCLUDE DUPLICATES
         BNO   NEXTLIN1                   N. GO BUMP POINTER
         TM    FLAG,NOTEXCL            Q. FLAG NOT EXLUDED
         BZ    NEXTLIN1                   N. BYPASS EXCLUSION
         NI    FLAG,255-NOTEXCL        SET ORIG FLAG EXCLUDED
         BCTR  R8,0                    POINT TO PREVIOUS LINE
         UNPACK R8,6,LINE#,MF=E        CONVERT TO CHARACTER
         ISPEXEC 'ISREDIT XSTATUS &&LINE# = X'
         LA    R8,1(R8)                REPLACE THE -1 FOR 'EX' COMMAND
NOTEQUA1 DS    0H
         OI    FLAG,NOTEXCL            SET ORIG FLAG NOT EXLUDED
NEXTLIN1 DS    0H
         EX    R3,SAVELINE             SAVE LINE
         LA    R8,1(R8)                BUMP LINE NUMBER
         BCT   R9,LOOP10               LOOP TILL END
         B     FINISHUP                GO FINISH UP
                                                                SPACE 1
*---------------------------------------------------------------------*
* Unsorted file processing                                            *
*---------------------------------------------------------------------*
UNSORTED DS    0H
         MVI   DLM,C'"'                SET DEFAULT " DELIMITER
         CLI   QSUB,C' '               Q. QUOTE SUB NOT PROVIDED
         BE    NOQSUB                     Y. BYPASS
         TM    FLAG,DELX               Q. DELETE EXCLUDED
         BO    QSUBVDEF                   Y. CONTINUE
         MVC   GC009(74),=C'Ex lines will not be deleted. Restoration of
               f quote sub may reset ex lines.'
         B     COMMEXT1                GO TO COMMON ERROR EXIT
QSUBVDEF DS    0H
         ISPVDEFX LIST=YE,FORMAT=CHAR,MF=E,                            +
               INM=(QSUB),ANM=QSUB,LEN=1
         ISPEXEC 'ISREDIT FIND ''&&QSUB'' FIRST'
         LTR   R15,R15                 Q. QUOTE SUB FOUND
         BZ    QSUBERR1                   Y. TELL OF ERROR
         ISPEXEC 'ISREDIT CHANGE ''"'' ''&&QSUB'' ALL'
         B     HAVEDELM                PROCES USING " AS DELIMITER
QSUBERR1 DS    0H
         MVC   GC009(32),=C'''.'' temporary replacement found.'
         MVC   GC009+1(1),QSUB         MOVE IN QUOTE SUBSTITUTE
         B     COMMEXT1                GO TO COMMON ERROR EXIT
NOQSUB   DS    0H
         ISPEXEC 'ISREDIT FIND ''"'' FIRST'
         LTR   R15,R15                 Q. " FOUND
         BNZ   HAVEDELM                   N. TAKE DEFAULT
         MVI   DLM,C''''               SET DEFAULT ' DELIMITER
         ISPEXEC 'ISREDIT FIND "''" FIRST'
         LTR   R15,R15                 Q. ' FOUND
         BNZ   HAVEDELM                   N. TAKE DEFAULT
         MVC   GC009(82),=C''' and " found in file. Invoke tutorial usin
               ng ? as parameter for subsequent action.'
COMMEXT1 DS    0H
         BAL   R14,USTATERS            GO RESET USER_STATE
         B     EXITPR08                GO TO ERROR EXIT
HAVEDELM DS    0H
*---------------------------------------------------------------------*
         LA    R0,EXCLUDE1             LOAD ADDRESS OF DESTINATION
         LA    R1,EXCLUDEL             LOAD LENGTH OF MOVE
         LA    R14,EXCLUDE0            LOAD SOURCE ADDRESS
         LA    R15,EXCLUDEL            LOAD LENGTH OF MOVE
         ICM   R15,B'1000',=C' '       MOVE PAD CHARACTER
         MVCL  R0,R14                  SET AREA TO ALL SPACES
*---------------------------------------------------------------------*
         MVC   DL1(1),DLM              MOVE DEFAULT DELIMITERS
         LA    R7,FINDLINE             POINT TO SAVE LINE
         AR    R7,R3                   BUMP BY LENGTH OF MOVE
         LA    R7,1(R7)                +1 FOR RELATIVE LENGTH
         MVC   0(1,R7),DLM               MOVE LELIMITER
         LA    R7,FINDLINE             POINT TO SAVE LINE
         BAL   R14,CURSOR10            REPOSITION CURSOR
LOOP11   DS    0H
         UNPACK R8,6,LINE#,MF=E        CONVERT TO CHARACTER
         ISPEXEC 'ISREDIT (XST) = XSTATUS &&LINE#'
         CLI   XSTATUS,C'X'            Q. ALREADY EXCLUDED
         BE    NEXTLIN2                   Y. GET NEXT LINE
         ISPEXEC 'ISREDIT (LINE) = LINE &&LINE#'
         EX    R3,SAVELINE             SAVE LINE
         ISPEXEC EXCLUDE1,EXCLUDEL,LONG=Y  EXCLUDE ALL LINES
         ISPEXEC 'ISREDIT (#EX) = EXCLUDE_COUNTS'
*-----------------------------<======---------------------------------*
*        LA    R4,#EX
*        PDUMP EXCLUDE1,MSG='R4=#EX',TRACE=TRACE,QUIT=EXITPR00
*-----------------------------<======---------------------------------*
         CLC   =C'00000001',#EX        Q. ORIGINAL ONLY EXCLUDED
         BE    DONTEXC2                   Y. DON'T EXCLUDE IT
         TM    FLAG,XDUPL              Q. EXCLUDE DUPLICATES
         BO    NEXTLIN2                   Y. ALREADY EXCLUDED, GET NEXT
DONTEXC2 DS    0H
         ISPEXEC 'ISREDIT XSTATUS &&LINE# = NX'
NEXTLIN2 DS    0H
         LA    R8,1(R8)                BUMP LINE COUNTER
         BCT   R9,LOOP11               LOOP TILL END
                                                                SPACE 1
FINISHUP DS    0H
         TM    FLAG,DELX               Q. DELETE EXCLUDED LINES
         BNO   TESTXOE1                   N. GO TEST FOR EXIT
         ISPEXEC 'ISREDIT DELETE ALL X'
         PACK  DWORK8(8),LAST(6)       PACK ORIGINAL LAST
         CVB   R9,DWORK8                   LINE NUMBER
         BAL   R14,LASTLINE            FIND LAST LINE
         PACK  DWORK8(8),LAST(6)       PACK LAST LINE NUMBER
         CVB   R8,DWORK8                   LINE NUMBER
         SR    R9,R8                   SUBTRACT LAST NUMBER
         MVC   GC009,SPACES            INITIALISE FIELD
         UNPACK R9,6,GC009,MF=E        UNPACK LINES DELETED
         MVC   GC009+7(24),=C'duplicate lines deleted.'
         BAL   R14,SETMSG              GO AND SET MESSAGE
         CLI   QSUB,C' '               Q. QUOTE SUB NOT PROVIDED
         BE    TESTXOE1                   Y. BYPASS
         ISPEXEC 'ISREDIT CHANGE ''&&QSUB'' ''"'' ALL'
         ISPEXEC 'ISREDIT RESET'
TESTXOE1 DS    0H                                               SPACE 1
         BAL   R14,CURSOR10            REPOSITION CURSOR
         BAL   R14,USTATERS            GO RESET USER_STATE
         TM    FLAG,ENDEXIT            Q. EXIT ON END
         BNO   EXITPROG                   N. EXIT PROGRAM
         ISPEXEC 'ISREDIT END'
                                                                SPACE 1
EXITPR00 DS    0H
         XR    R15,R15                 SET RETURN CODE
         B     EXITPROG                GO TO COMMON EXIT
                                                                SPACE 1
EXITPR08 DS    0H
         BAL   R14,SETMSG              GO AND SET MESSAGE
         LA    R15,8                   SET ERROR EXIT CODE
         TM    FLAG,ENDEXIT            Q. EXIT ON END
         BNO   EXITPROG                   N. EXIT PROGRAM
         ISPEXEC 'ISREDIT END'
         LA    R15,8                   SET ERROR EXIT CODE
                                                                SPACE 1
EXITPROG DS    0H
         RACKOFF RC=(R15)              EXIT PROGRAM WITH 0 RETURN CODE
                                                                SPACE 1
*---------------------------------------------------------------------* ---*
COMPARE  CLC   0(0,R7),0(R2)           COMPARE WITH PREVIOUS LINE
COMPARES CLC   0(0,R2),SPACES          COMPARE WITH PREVIOUS LINE
SAVELINE MVC   0(0,R7),0(R2)           SAVE LINE FOR NEXT LINE COMPARE
*---------------------------------------------------------------------* ---*
                                                                SPACE 1
PARSPARM DS    0H
         ST    R14,R14SAVE1            SAVE RETURN REGISTER
         ISPEXEC 'ISREDIT (LINE#) = LRECL' GET RECORD LENGTH
         MVC   START,=C'001'           SET UP STARTING COLUMN
         MVC   END,LINE#+2             MOVE RECORD LENGTH
         PACK  DWORK8(8),END(3)        PACK ENDING COLUMN AND
         CVB   R3,DWORK8                 CONVERT FOR USE IN LINE LENGTH
         ISPVDEFX LIST=NO,FORMAT=CHAR,MF=E,                            +
               INM=(LINE),ANM=LINE,LEN=(R3)
                                                                SPACE 1
         CLI   STATUS,C' '             Q. ANY PARAMETER TO PROCESS
         BE    EXITRTN1                   N. EXIT ROUTINE
         LA    R9,#PARMS               # OF PARAMETERS TO PROCESS
         LA    R2,STATUS               POINT TO PARAMETER 1
         MVI   TARGET,C'X'             SET EXCLUDE ORIG MASK
         BAL   R14,TESTLITT            TEST FOR EXCLUDE ORIG PARAMETER
         LTR   R15,R15                 Q. HIT ON EXCLUDE
         BNZ   TESTBKUP                   N. NEXT TEST
         OI    FLAG,XDUPL              TURN EXCLUDE DUP ORIGINAL ON
                                                                SPACE 1
TESTBKUP DS    0H
         CLI   STATUS,C' '             Q. ANY PARAMETER TO PROCESS
         BE    EXITRTN1                   N. EXIT ROUTINE
         LA    R2,STATUS               POINT TO PARAMETER 1
         MVI   TARGET,C'K'             SET BACKUP MASK
         BAL   R14,TESTLITT            TEST FOR EXCLUDE ORIG PARAMETER
         LTR   R15,R15                 Q. HIT ON UNSORTED
         BNZ   TESTUSRT                   N. NEXT TEST
         OI    FLAG,BACKUP             TURN BACKUP FLAG ON
                                                                SPACE 1
TESTUSRT DS    0H
         CLI   STATUS,C' '             Q. ANY PARAMETER TO PROCESS
         BE    EXITRTN1                   N. EXIT ROUTINE
         LA    R2,STATUS               POINT TO PARAMETER 1
         MVI   TARGET,C'U'             SET UNSORTED MASK
         BAL   R14,TESTLITT            TEST FOR EXCLUDE ORIG PARAMETER
         LTR   R15,R15                 Q. HIT ON UNSORTED
         BNZ   TESTQSUB                   N. NEXT TEST
         OI    FLAG,UNSORT             TURN UNSORTED FLAG ON
                                                                SPACE 1
TESTQSUB DS    0H
         CLI   STATUS,C' '             Q. ANY PARAMETER TO PROCESS
         BE    EXITRTN1                   N. EXIT ROUTINE
         LA    R2,STATUS               POINT TO PARAMETER
         MVI   TARGET,C'Q'             SET QUOTE SUB MASK
         BAL   R14,TESTLITT            TEST FOR EXCLUDE ORIG PARAMETER
         LTR   R15,R15                 Q. HIT ON UNSORTED
         BNZ   TESTENDX                   N. NEXT TEST
         MVC   QSUB(1),SLINE+2         MOVE SUBSTITUTE PARAMETER
                                                                SPACE 1
TESTENDX DS    0H
         CLI   STATUS,C' '             Q. ANY PARAMETER TO PROCESS
         BE    EXITRTN1                   N. EXIT ROUTINE
         LA    R2,STATUS               POINT TO PARAMETER 1
         MVI   TARGET,C'E'             SET END IT MASK
         BAL   R14,TESTLITT            TEST FOR EXCLUDE ORIG PARAMETER
         LTR   R15,R15                 Q. HIT ON UNSORTED
         BNZ   TESTDELX                   N. NEXT TEST
         OI    FLAG,ENDEXIT            TURN END ON EXIT FLAG ON
                                                                SPACE 1
TESTDELX DS    0H
         CLI   STATUS,C' '             Q. ANY PARAMETER TO PROCESS
         BE    EXITRTN1                   N. EXIT ROUTINE
         LA    R2,STATUS               POINT TO PARAMETER 1
         MVI   TARGET,C'D'             SET DELETE MASK
         BAL   R14,TESTLITT            TEST FOR EXCLUDE ORIG PARAMETER
         LTR   R15,R15                 Q. HIT ON UNSORTED
         BNZ   TESTBATC                   N. NEXT TEST
         OI    FLAG,DELX               TURN DELETE X FLAG ON
                                                                SPACE 1
TESTBATC DS    0H
         CLI   STATUS,C' '             Q. ANY PARAMETER TO PROCESS
         BE    EXITRTN1                   N. EXIT ROUTINE
         LA    R2,STATUS               POINT TO PARAMETER 1
         MVI   TARGET,C'B'             SET BATCH MASK
         BAL   R14,TESTLITT            TEST FOR EXCLUDE ORIG PARAMETER
         LTR   R15,R15                 Q. HIT ON UNSORTED
         BNZ   TESTSTRT                   N. NEXT TEST
         OI    FLAG,BATCH              TURN BATCH FLAG ON
         OI    FLAG,ENDEXIT            TURN END ON EXIT FLAG ON
                                                                SPACE 1
TESTSTRT DS    0H
         CLI   STATUS,C' '             Q. ANY PARAMETER TO PROCESS
         BE    EXITRTN1                   N. EXIT ROUTINE
         LA    R2,STATUS               POINT TO PARAMETER 1
         MVI   TARGET,C'S'             SET START COLUMN INDICATOR
         BAL   R14,TESTCOLM            TEST FOR UN-SORTED PARAMETER
         MVC   START,SLINE             MOVE TO STARTING COLUMN
         CLC   START,END               Q. START > END
         BNH   TESTENDC                   N. TEST END PARAMETER
         MVC   GC009(39),=C'Start column cannot be > record length.'
         B     EXITPR08                GO TO ERROR EXIT
                                                                SPACE 1
TESTENDC DS    0H
         CLI   STATUS+80,C' '          Q. ANY PARAMETER TO PROCESS
         BE    EXITRTN1                   N. EXIT ROUTINE
         LA    R2,STATUS+80            POINT TO PARAMETER 1
         MVI   TARGET,C'E'             SET END COLUMN INDICATOR
         BAL   R14,TESTCOLM            TEST FOR UN-SORTED PARAMETER
         CLC   SLINE,END               Q. END COLUMN > RECORD LENGTH
         BNH   COMPWSTR                   N. COMPARE WITH START
         MVC   GC009(39),=C'End column must not be > record length.'
         B     EXITPR08                GO TO ERROR EXIT
COMPWSTR DS    0H
         MVC   END,SLINE               MOVE TO ENDING COLUMN
         CLC   START,END               Q. START > END
         BNH   EXITRTN1                   N. EXIT ROUTINE
         MVC   GC009(34),=C'Start column must be < end column.'
         B     EXITPR08                GO TO ERROR EXIT
                                                                SPACE 1
EXITRTN1 DS    0H
         L     R14,R14SAVE1            LOAD RETURN REGISTER
         BR    R14                     RETURN TO CALLER
                                                                SPACE 1
*---------------------------------------------------------------------* ---*
* TEST FOR PARAMETER OF 'B' INDICATING EXCLUSION OF 'BOTH' DUPLICATES
* AND ORIGINAL LINES.
*---------------------------------------------------------------------* ---*
TESTLITT DS    0H
         ST    R14,R14SAVE2            SAVE RETURN REGISTER
         LR    R8,R9                   LOOP COUNTER
LOOP21   DS    0H
         OC    0(1,R2),SPACES          CONVERT TO UPPER CASE
         CLC   0(1,R2),TARGET          Q. MATCH ON TARGET MASK
         BE    HAVEMTCH                   Y. EXIT LOOP
         LA    R2,80(R2)               NEXT PARAMETER
         BCT   R8,LOOP21               LOOP TILL END
         LA    R15,8                   SET ERROR RETURN CODE
         B     EXITRTN2                GO TO ROUTINE'S EXIT
HAVEMTCH DS    0H
         MVC   SLINE(10),0(R2)         SAVE ORIGINAL PARAMETER (Q=.)
         BAL   R14,SHUFFLE             GO PERCOLATE PARMS BACKWARDS
         XR    R15,R15                 SET ZERO RETURN CODE
EXITRTN2 DS    0H
         L     R14,R14SAVE2            LOAD RETURN REGISTER
         BR    R14                     RETURN TO CALLER
                                                                SPACE 1
*---------------------------------------------------------------------* ---*
* TEST FOR NUMERIC PARAMETER. OTHER PARAMETERS SHOULD HAVE BEEN
* REMOVED. FIRST PARAMETER IS STARTING COLUMN.
*---------------------------------------------------------------------* ---*
TESTCOLM DS    0H
         ST    R14,R14SAVE2            SAVE RETURN REGISTER
         LA    R3,2(R2)                POINT TO LAST POTENTIAL BYTE
         LA    R9,3                    MAX PARAMETER LENGTH
LOOP41   DS    0H
         CLI   0(R3),C' '              Q. SPACE CHARACTER
         BNE   LOOP41E                    N. EXIT LOOP
         BCTR  R3,0                    PREVIOUS PARAMETER
         BCT   R9,LOOP41               LOOP TILL END
LOOP41E  DS    0H
         BCTR  R9,0                    PREPARE FOR EX COMMAND
         MVC   SLINE(2),=C'00'         INITIALISE COLUMN COUNTER
         LA    R7,SLINE+2              POINT TO END OF PARAMETER
         SR    R7,R9                   MOVE BACK LENGTH OF PARAMETER
         EX    R9,SAVELINE             MOVE DATA
         CLI   SLINE,C'0'              Q. NUMERIC CHARACTER
         BL    INVCOL                     N. SIGNAL INVALID COLUMN
         CLI   SLINE+1,C'0'            Q. NUMERIC CHARACTER
         BL    INVCOL                     N. SIGNAL INVALID COLUMN
         CLI   SLINE+2,C'0'            Q. NUMERIC CHARACTER
         BL    INVCOL                     N. SIGNAL INVALID COLUMN
EXITRTN4 DS    0H
         L     R14,R14SAVE2            LOAD RETURN REGISTER
         BR    R14                     RETURN TO CALLER
                                                                SPACE 1
INVCOL   DS    0H
         LA    R2,GC009                POINT TO MESSAGE AREA
         MVC   0(79,R2),SPACES         INITIALISE MESSAGE
         MVC   0(07,R2),=C'Invalid'
         CLI   TARGET,C'S'             Q. START MASK
         BNE   ENDMASK                    N. SET END MASK
         MVC   08(05,R2),=C'start'
         LA    R2,14(R2)               POINT PAST CURRENT MESSAGE
         B     RESTMSG1                GO FINISH REST OF MESSAGE
ENDMASK  DS    0H
         MVC   08(03,R2),=C'end'
         LA    R2,12(R2)               POINT PAST CURRENT MESSAGE
RESTMSG1 DS    0H
         MVC   0(18,R2),=C'parameter entered.'
         B     EXITPR08                EXIT PROGRAM
                                                                SPACE 1
*---------------------------------------------------------------------* ---*
* ROUTINE TO SHUFFLE THE PARAMETERS BACKWARDS (ASSUMES THE 5TH.
* UNUSED PARAMETER IS SPACES).
*    IE. PARM1 <- PARM2
*         .......
*    IE. PARM4 <- ' '
*---------------------------------------------------------------------* ---*
SHUFFLE  DS    0H
         ST    R14,R14SAVE3            SAVE RETURN REGISTER
         BCTR  R9,0                    DECREASE #OF PARMS 2 PROCESS
LOOP91   DS    0H
         MVC   0(80,R2),80(R2)         MOVE NEXT INTO CURRENT
         CLI   80(R2),C' '             Q. NEXT PARAMETER BLANK
         BE    EXITRTN9                   Y. EXIT ROUTINE
         LA    R2,80(R2)               NEXT PARAMETER
         BCT   R8,LOOP91               LOOP TILL END
EXITRTN9 DS    0H
         L     R14,R14SAVE3            LOAD RETURN REGISTER
         BR    R14                     RETURN TO CALLER
                                                                SPACE 1
*---------------------------------------------------------------------* ---*
* Lastline
*---------------------------------------------------------------------* ---*
LASTLINE DS    0H
         ST    R14,R14SAVE3            SAVE RETURN REGISTER
         ISPEXEC 'ISREDIT (LINE#) = LINENUM .ZLAST'
         MVC   LAST,LINE#              MOVE TO APPROPRIATE FIELD
         L     R14,R14SAVE3            LOAD RETURN REGISTER
         BR    R14                     RETURN TO CALLER
                                                                SPACE 1
*---------------------------------------------------------------------* ---*
* Reposition cursor 1 0.
*---------------------------------------------------------------------* ---*
CURSOR10 DS    0H
         ST    R14,R14SAVE3            SAVE RETURN REGISTER
         ISPEXEC 'ISREDIT CURSOR = 1 0' TOP OF SCREEN
         L     R14,R14SAVE3            LOAD RETURN REGISTER
         BR    R14                     RETURN TO CALLER
                                                                SPACE 1
*---------------------------------------------------------------------* ---*
* Restore USER_STATE...
*---------------------------------------------------------------------* ---*
USTATERS DS    0H
         ST    R14,R14SAVE3            SAVE RETURN REGISTER
         ISPEXEC 'ISREDIT USER_STATE = (STATUS)'
         L     R14,R14SAVE3            LOAD RETURN REGISTER
         BR    R14                     RETURN TO CALLER
                                                                SPACE 1
*---------------------------------------------------------------------* ---*
* Output message to the appropriate destination
*---------------------------------------------------------------------* ---*
SETMSG   DS    0H
         ST    R14,R14SAVE3            SAVE RETURN REGISTER
         TM    FLAG,BATCH              Q. BATCH JOB
         BNO   SETMSGO                    N. USE SETMSG
                                                                SPACE 1
*---------------------------------------------------------------------*
* Output to SYSPRINT
*---------------------------------------------------------------------*
         MVC   TPUTWK1(TPUTCONL),TPUTCON1  MOVE CONSTANT TO DSECT
         TPUT  GC009,79,EDIT,NOWAIT,,BREAKIN,HIGHP,MF=(E,TPUTWK1)
                                                                SPACE 1
         MVC   OPENL(OPENSKLL),OPENSKL MOVE CONSTANT TO DSECT
         MVC   SYSPRINT(SYSPRINL),SYSPRINC MOVE CONSTANT TO DSECT
                                                                SPACE 1
         LA    R10,STATUS              SVC 99 PARAMATER LIST ADDRS
         ADYN  ACTION=ALLOC,DD=SYSPRINT,RECFM=FBA,LRECL=133,           X
               BLKSIZE=1330,SYSOUT=X,PARMT=PARM,DSORG=PS,              X
               MF=E,BASE=(R10),REUSE=YES,UCLOSE=YES
OPENFILE DS    0H
         LA    R2,SYSPRINT             DCB FOR SYSPRINT
         LA    R1,OPENL                OPEN LIST
         OPEN  ((R2),(OUTPUT)),MF=(E,(R1)) OPEN INTERNAL READER
         USING IHADCB,R2               AND TELL ASSEMBLER ABOUT IT.
         TM    DCBOFLGS,DCBOFOPN       Q. IS IT OPEN
         DROP  R2                      TELL ASSEMBLER TO FORGET IT.
         BNO   CLOSFILE                   N. THEN REPORT ERROR
         MVC   GC009+79(55),SPACES     CLEAR REST OF FIELD
         MVI   GC009-1,C' '            CLEAR REST OF FIELD
         PUT   (R2),GC009-1            PUT OPEN ERROR
CLOSFILE DS    0H
         LA    R1,OPENL                OPEN LIST
         CLOSE ((R2)),MF=(E,(R1))      CLOSE INTERNAL READER
         B     TESTXOE2                GO TO COMMON EXIT
*---------------------------------------------------------------------*
* Output to ISPF screen message facility
*---------------------------------------------------------------------*
SETMSGO  DS    0H
         ISPEXEC 'SETMSG MSG(GC009)'
TESTXOE2 DS    0H                                               SPACE 1
         L     R14,R14SAVE3            LOAD RETURN REGISTER
         BR    R14                     RETURN TO CALLER
                                                                SPACE 1
*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*
         LTORG
*---------------------------------------------------------------------* ---*
SPACES   DC    CL256' '
EXCLUDE0 DC    C'ISREDIT EX '
         DC    C'.ZCSR .ZLAST &&START &&END ALL '
         DC    C'"'
         DC    CL256' '
         DC    C' '
EXCLUDEL EQU   *-EXCLUDE0
                                                                SPACE 1
TPUTCON1 TPUT  MF=L
TPUTCONL EQU   *-TPUTCON1
                                                                SPACE 1
SYSPRINC DCB   DDNAME=SYSPRINT,                                        X
               DSORG=PS,                SEQUENTIAL                     X
               MACRF=(PM),                                             X
               LRECL=133,                                              X
               BLKSIZE=133,                                            X
               RECFM=FA
SYSPRINL EQU   *-SYSPRINC
                                                                SPACE 1
OPENSKL  OPEN  (),MF=L
OPENSKLL EQU   *-OPENSKL
                                                                SPACE 1
*---------------------------------------------------------------------* ---*
WORKAREA DSECT
         DS    18F                     REGISTER SAVE AREA ETC...
R14SAVE1 DS    F                       RETURN REGISTER SAVE AREA
R14SAVE2 DS    F                       RETURN REGISTER SAVE AREA
R14SAVE3 DS    F                       RETURN REGISTER SAVE AREA
DWORK8   DS    D                       DOUBLE WORD WORK FIELD
EXCLUDE1 DC    C'ISREDIT EX '
         DC    C'.ZCSR .ZLAST &&START &&END ALL '
DL1      DC    C'"'
FINDLINE DC    CL256' '
DL2      DC    C' '
                                                                SPACE 1
DLM      DC    C'"'
                                                                SPACE 1
TPUTWK1  TPUT  MF=L
         UNPACK   MF=L                 LIST FORMAT OF UNPACK
         ISPEXEC INIT=Y,MF=L           INIT ISPF INTERFACE
         ISPVDEFX MF=L,LIST=Y          LIST FORMAT OF ISPVDEFX
         CLEARV   MF=L                 LIST FORMAT OF CLEARV
                                                                SPACE 1
SYSPRINT DCB   DDNAME=SYSPRINT,                                        X
               DSORG=PS,                SEQUENTIAL                     X
               MACRF=(PM),                                             X
               LRECL=133,                                              X
               BLKSIZE=133,                                            X
               RECFM=FA
                                                                SPACE 1
OPENL    OPEN  (),MF=L
                                                                SPACE 1
         ADYN  MF=L
                                                                SPACE 1
TRACE    DS    CL1                     TARGET FLAG FOR PDUMP
TARGET   DS    CL1                     TARGET FLAG FOR PDUMP
FLAG     DS    X'00'                   FLAG
NOTEXCL  EQU   X'01'                   ORIGINAL LINE ALREADY EXCLUDED
XDUPL    EQU   X'02'                   EXCLUDE ORIGINAL
UNSORT   EQU   X'04'                   UNSORTED FILE
FREE1    EQU   X'08'                   UNUSED
ENDEXIT  EQU   X'10'                   EXIT ON ENDING MACRO
DELX     EQU   X'20'                   DELETE ALL EXCLUDED
BATCH    EQU   X'40'                   DELETE ALL EXCLUDED
BACKUP   EQU   X'80'                   DELETE ALL EXCLUDED
START    DC    CL3'001'                STARTING COLUMN
LEN      DC    CL3'256'                LENGTH OF COMPARE
END      DC    CL3'256'                LENGTH OF COMPARE
LINE#    DC    CL6' '                  LINE NUMBER
LAST     DC    CL6' '                  LAST LINE NUMBER
#EX      DS    D                       EXCLUDE COUNT
XSTATUS  DS    CL2                     EXCLUDE STATUS
QSUB     DS    CL1                     QUOTE SUBSTITUTION
GC009    DC    0CL256' '               LINE
LINE     DC    CL256' '                LINE
SLINE    DC    CL256' '                LINE SAVE AREA
STATUS   DS    CL4096                  REQUIRES 1380 BYTES
WORKAREL EQU   *-WORKAREA
*---------------------------------------------------------------------* ---*
         DCBD
IBMDSECT ADYN  DATA=IBM
*---------------------------------------------------------------------*
         END
