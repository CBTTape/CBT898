/******************************** REXX ********************************/
/*--------------------------------------------------------------------*/
/*  Process Check Data parameter entry                                */
/*--------------------------------------------------------------------*/
Arg oper1,utillib,templib,tempsdef,debug
    Address ISPEXEC

    cactn = "RETURN"
/*  cactn = "CANCEL"                                                  */
    "CONTROL ERRORS" cactn

    "VGET (chkdav,panelv,ssid,sqlid) SHARED"
    "VGET ("chkdav panelv" nexto) SHARED"
    table = 'CD'Time('s')
    genolist = 'OFF'

checkda_tss1:
    zcmd =
    "DISPLAY PANEL(&PANEL80) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,checkda_tss1)
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('chkdav');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal checkda_tss1;End

    If rc /= 0 Then Do
        "VPUT ("chkdav") SHARED"
        gc009 = oper1 "entry panel abandoned."
        "SETMSG MSG(GC009)"
        csr1=;csrp1=
        Return 9
    End
    If zcmd='?' Then Do
        x=helpdrvr(helptab '9 /OPEN')
        Signal checkda_tss1
    End

    ret_scr = 0
/*--------------------------------------------------------------------*/
/*  If tablespace has not been validated or has changed, do it.       */
/*--------------------------------------------------------------------*/
    If r_table_ts /= chkddbnm || chkdtspc || chkdpart Then,
        If R0valtsp('TS',chkddbnm,chkdtspc,chkdpart) Then Do
            csr1 = 'CHKDDBNM'
            Signal checkda_tss1
        End
    r_table_ts = chkddbnm || chkdtspc || chkdpart

    x = checkda_ver_common()
    If ret_scr Then Signal checkda_tss1

    tspace = chkddbnm'.'chkdtspc
    If chkdpart = '' Then partno =
    Else partno = 'PART' chkdpart +0

Return0:
    "VPUT ("chkdav" tspace partno genolist) SHARED"
    Return 0

/*--------------------------------------------------------------------*/
/*  Comon rebldinx field verifications:                               */
/*--------------------------------------------------------------------*/
checkda_ver_common:
    If chkddevt = '*' Then Do
        gc009 = 'Device type entry for Load operation.'
        "SETMSG MSG(GC009)"
        x = R0edtl('DASD',10,10,debug)
        If Datatype(x) = 'CHAR' | x > 20 Then chkddevt = x
        ret_scr=1
        Return 4
    End
    Else Do
        x = R0edtl('LIST',,,debug)
        If Wordpos(chkddevt,x) = 0 Then Do
            gc009 = 'Invalid devive type. Suggest enter "*" for selection list.'
            "SETMSG MSG(GC009)"
            csr1 = 'chkddevt'
            ret_scr=1
            Return 4
        End
    End

    If chkdeddn = '*' Then Do
        gc009 = 'Template processing for ERRORDDN Check Data.'
        "SETMSG MSG(GC009)"
        x = R0listm(table,tempsdef,'$TTY','ND')
        x = R0listm(table,templib,'$TTY',,'EXIST')
        If Datatype(x) = 'CHAR' Then Parse VAR x chkdeddn .
        ret_scr=1
    End
    Else If chkdeddn = '/' Then Do
             xmsg = 'ERRORDDN Check Data'
             x = R0templ(templib,tempsdef,xmsg,'chkdeddn')
             If Datatype(x) = 'CHAR' Then chkdeddn = x
             ret_scr=1
         End
    xmsg=Msg('Off')
    If Sysdsn("'"templib"("chkdeddn")'") /= 'OK' Then,
        If Sysdsn("'"tempsdef"("chkdeddn")'") /= 'OK' Then Do
            gc009 = 'Template not found.'
            "SETMSG MSG(GC009)"
            csr1  = 'CHKDEDDN'
            ret_scr=1
            xmsg=Msg(xmsg)
            Return 4
        End
    xmsg=Msg(xmsg)
    If chkdwddn = '*' Then Do
        gc009 = 'Template processing for WORKDDN Check Data.'
        "SETMSG MSG(GC009)"
        x = R0listm(table,tempsdef,'$TTY','ND')
        x = R0listm(table,templib,'$TTY',,'EXIST')
        If Datatype(x) = 'CHAR' Then Parse VAR x chkdwddn .
        ret_scr=1
    End
    Else If chkdwddn = '/' Then Do
             xmsg = 'WORKDDN Check Data'
             x = R0templ(templib,tempsdef,xmsg,'chkdwddn')
             If Datatype(x) = 'CHAR' Then chkdwddn = x
             ret_scr=1
         End
    xmsg=Msg('Off')
    If Sysdsn("'"templib"("chkdwddn")'") /= 'OK' Then,
        If Sysdsn("'"tempsdef"("chkdwddn")'") /= 'OK' Then Do
            gc009 = 'Template not found.'
            "SETMSG MSG(GC009)"
            csr1  = 'CHKDWDDN'
            ret_scr=1
            xmsg=Msg(xmsg)
            Return 4
        End
    xmsg=Msg(xmsg)

    Return 0
