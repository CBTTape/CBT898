/******************************** REXX ********************************/
/*--------------------------------------------------------------------*/
/*  Process Check Index parameter entry                               */
/*--------------------------------------------------------------------*/
Arg oper1,utillib,templib,tempsdef,debug
    Address ISPEXEC

    cactn = "RETURN"
/*  cactn = "CANCEL"                                                  */
    "CONTROL ERRORS" cactn

    "VGET (chkixv,panelv,ssid,sqlid) SHARED"
    "VGET ("chkixv panelv" nexto) SHARED"
    table = 'CI'Time('s')

checkix_scr1:
    zcmd =
    "DISPLAY PANEL(&PANEL130) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,checkix_scr1)
    If rc /= 0 Then Do
        "VPUT ("chkixv") SHARED"
        gc009 = oper1 "entry panel abandoned."
        "SETMSG MSG(GC009)"
        csr1=;csrp1=
        Return 9
    End
    csr1=;csrp1=
    If zcmd='?' Then Do
        x=helpdrvr(helptab '10  /OPEN')
        Signal checkix_scr1
    End
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('chkixv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal checkix_scr1;End

    genolist = 'OFF'
    opertp = 'CHECK INDEX'
    Parse VAR zcmd cmd parm1
    Select
        When cils /= '' Then Do
            cils =
            ulact = 'LS'
            x = checkix_lss1()
            If x = 0 Then Signal Return0
        End
        When cits /= '' Then Do
            cits =
            ulact = 'TS'
            x = checkix_tss1()
            opertp = opertp '(ALL) TABLESPACE'
            If x = 0 Then Signal Return0
        End
        When ciix /= '' Then Do
            ciix =
            ulact = 'IX'
            x = checkix_ixs1()
            If x = 0 Then Signal Return0
        End
        Otherwise Signal checkix_scr1
    End
    Signal checkix_scr1

Return0:
    If chkisrtn /= '' Then sortnum = 'SORTNUM' chkisrtn
    Else                   sortnum =
    "VPUT ("chkixv" tspace partno nexto genolist ulact sortnum",
           "opertp) SHARED"
    Return 0

/*--------------------------------------------------------------------*/
/* Rebuild index from LiStdef                                         */
/*--------------------------------------------------------------------*/
checkix_lss1:
    zcmd =
    "DISPLAY PANEL(&PANEL131) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,checkix_lss1)
    csr1=;csrp1=
    If rc /= 0 Then Do
        gc009 = oper1 "FROM TABLE name entry panel abandoned."
         "SETMSG MSG(GC009)"
        Return 9
    End
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('chkixv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal checkix_lss1;End

    If zcmd='?' Then Do
        x=helpdrvr(helptab '10A /OPEN')
        Signal checkix_lss1
    End

    ret_scr = 0
    Parse VAR zcmd cmd parm1

    If chkildef = '*' Then Do
        gc009 = 'Listdef processing for Check Index.'
        "SETMSG MSG(GC009)"
        x = R0listm(table,utillib)
        If Datatype(x) = 'CHAR' Then chkildef = x
        Signal checkix_lss1
    End
    Else If chkildef = '/' Then Do
             gc009 = 'Listdef processing for Check Index.'
             "SETMSG MSG(GC009)"
             x = r0ldef(utillib,'chkildef')
             If Datatype(x) = 'CHAR' Then chkildef = x
             Signal checkix_lss1
         End
    If Sysdsn("'"utillib"("chkildef")'") /= 'OK' Then Do
        gc009 = Sysdsn("'"utillib"("chkildef")'")
        csr1 = 'chkildef'
        "SETMSG MSG(GC009)"
        Signal checkix_lss1
    End
    xx = R0vrldef(utillib'('chkildef')','IX')
    If xx /= 0 Then Do
        gc009 = 'List contains mixture of TABLESPACE and INDEXSPACE.',
                'Can only have INDEXSPACEs. Please amend the list or',
                'select an alternative list.'
        csr1 = 'chkildef'
        "SETMSG MSG(GC009)"
        Signal checkix_lss1
    End
    x = checkix_ver_common()
    If ret_scr Then Signal checkix_lss1
    genolist = 'ON'
    nexto    = 'JCL(JC,J10,J20,J120)'

    Return 0

/*--------------------------------------------------------------------*/
/* Rebuild index from Table Space                                     */
/*--------------------------------------------------------------------*/
checkix_tss1:
    zcmd =
    "DISPLAY PANEL(&PANEL132) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,checkix_tss1)
    csr1=;csrp1=
    If rc /= 0 Then Do
        gc009 = oper1 "FROM TABLE name entry panel abandoned."
         "SETMSG MSG(GC009)"
        Return 9
    End
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('chkixv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal checkix_tss1;End

    If zcmd='?' Then Do
        x=helpdrvr(helptab '10B /OPEN')
        Signal checkix_tss1
    End

    ret_scr = 0
    Parse VAR zcmd cmd parm1

/*--------------------------------------------------------------------*/
/*  If tablespace has not been validated or has changed, do it.       */
/*--------------------------------------------------------------------*/
    If r_table_ts /= chkidbnm || chkispce || chkipart Then,
        If R0valtsp('TS',chkidbnm,chkispce,chkipart) Then Do
            csr1 = 'chkidbnm'
            Signal checkix_tss1
        End
    r_table_ts = chkidbnm || chkispce || chkipart

    x = checkix_ver_common()
    If ret_scr Then Signal checkix_tss1

    tspace = chkidbnm'.'chkispce
    If chkipart = '' Then partno =
    Else partno = 'PART' chkipart +0

    Return 0

/*--------------------------------------------------------------------*/
/* Rebuild index from Index Name                                      */
/*--------------------------------------------------------------------*/
checkix_ixs1:
    zcmd =
    "DISPLAY PANEL(&PANEL133) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,checkix_ixs1)
    csr1=;csrp1=
    If rc /= 0 Then Do
        gc009 = oper1 "FROM TABLE name entry panel abandoned."
         "SETMSG MSG(GC009)"
        Return 9
    End
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('chkixv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal checkix_ixs1;End

    If zcmd='?' Then Do
        x=helpdrvr(helptab '10C /OPEN')
        Signal checkix_ixs1
    End

    ret_scr = 0
    Parse VAR zcmd cmd parm1

/*--------------------------------------------------------------------*/
/*  If tablespace has not been validated or has changed, do it.       */
/*--------------------------------------------------------------------*/
    If r_table_ix /= chkicrea || chkindex || chkiptix Then Do
        If R0valtsp('RB',chkicrea,chkindex,chkiptix) Then Do
            csr1 = 'CHKICREA'
            Signal checkix_ixs1
        End
        r_table_ix = chkicrea || chkindex || chkiptix
    End

    x = checkix_ver_common()
    If ret_scr Then Signal checkix_ixs1

    tspace = chkicrea'.'chkindex
    If chkiptix = '' Then partno =
    Else                  partno = 'PART' chkiptix +0

    Return 0

/*--------------------------------------------------------------------*/
/*  Comon rebldinx field verifications:                               */
/*--------------------------------------------------------------------*/
checkix_ver_common:
/*--------------------------------------------------------------------*/
/*  Break up supplied WHEN parameters in 72 character lots without    */
/*  splitting words.                                                  */
/*--------------------------------------------------------------------*/
    If chkiwddn = '*' Then Do
        gc009 = 'Template processing for WORKDDN Check Index.'
        "SETMSG MSG(GC009)"
        x = R0listm(table,tempsdef,'$TTY','ND')
        x = R0listm(table,templib,'$TTY',,'EXIST')
        If Datatype(x) = 'CHAR' Then Parse VAR x chkiwddn .
        ret_scr = 1
    End
    Else If chkiwddn = '/' Then Do
             xmsg = 'WORKDDN Check Index'
             x = R0templ(templib,tempsdef,xmsg,'chkiwddn')
             If Datatype(x) = 'CHAR' Then chkiwddn = x
             ret_scr = 1
         End
    xmsg=Msg('Off')
    If Sysdsn("'"templib"("chkiwddn")'") /= 'OK' Then,
        If Sysdsn("'"tempsdef"("chkiwddn")'") /= 'OK' Then Do
            gc009 = 'Template not found.'
            "SETMSG MSG(GC009)"
            csr1  = 'CHKIWDDN'
            ret_scr=1
            xmsg=Msg(xmsg)
            Return 4
        End
    xmsg=Msg(xmsg)
    If chkidevt = '*' Then Do
        gc009 = 'Device type entry for Load operation.'
        "SETMSG MSG(GC009)"
        x = R0edtl('DASD',10,10,debug)
        If Datatype(x) = 'CHAR' | x > 20 Then chkidevt = x
        ret_scr=1
        Return 4
    End
    Else Do
        x = R0edtl('LIST',,,debug)
        If Wordpos(chkidevt,x) = 0 Then Do
            gc009 = 'Invalid devive type. Suggest enter "*" for selection list.'
            "SETMSG MSG(GC009)"
            csr1 = 'chkidevt'
            ret_scr=1
            Return 4
        End
    End

    Return 0
