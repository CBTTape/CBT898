/******************************** REXX ********************************/
/*--------------------------------------------------------------------*/
/*  Process Jobcard                                                   */
/*--------------------------------------------------------------------*/
Arg oper1,utillib,templib,tempsdef,debug
    Address ISPEXEC

    cactn = "RETURN"
/*  cactn = "CANCEL"                                                  */
    "CONTROL ERRORS" cactn

    "VGET (copyv,panelv,ssid,sqlid) SHARED"
    "VGET ("copyv panelv") SHARED"
    table = 'CO'Time('s')

/*--------------------------------------------------------------------*/
/*  Process COPY LISTDEF parameter entry                              */
/*--------------------------------------------------------------------*/
copy_scr1:
    zcmd =
    "DISPLAY PANEL(&PANEL20) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,copy_scr1)
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('copyv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal copy_scr1;End

    csr1=;csrp1=
    If rc /= 0 Then Do
        gc009 = oper1 "entry panel abandoned."
        "SETMSG MSG(GC009)"
        Return 9
    End
    If zcmd='?' Then Do
        x=helpdrvr(helptab '2 /OPEN')
        Signal copy_scr1
    End
    ret_scr = 0
    If copyldef = '*' Then Do
        gc009 = 'Listdef processing for COPY.'
        "SETMSG MSG(GC009)"
        x = R0listm(table,utillib)
        If Datatype(x) = 'CHAR' Then copyldef = x
        ret_scr = 1
    End
    Else If copyldef = '/' Then Do
             gc009 = 'Listdef processing for COPY.'
             "SETMSG MSG(GC009)"
             x = r0ldef(utillib,'copyldef')
             If Datatype(x) = 'CHAR' Then copyldef = x
             ret_scr = 1
         End
    xmsg = Msg('Off')
    If Sysdsn("'"utillib"("copyldef")'") /= 'OK' Then Do
        gc009 = Sysdsn("'"utillib"("copyldef")'")
        csr1 = 'copyldef'
        "SETMSG MSG(GC009)"
        xmsg = Msg(xmsg)
        Signal copy_scr1
    End
    If copycddn = '*' Then Do
        gc009 = 'Template processing for LOCAL copy'
        "SETMSG MSG(GC009)"
        x = R0listm(table,tempsdef,'$TTY','ND')
        x = R0listm(table,templib,'$TTY',,'EXIST','')
        If Datatype(x) = 'CHAR' Then Parse VAR x copycddn .
        ret_scr = 1
    End
    Else If copycddn = '/' Then Do
             xmsg = 'LOCAL copy'
             x = R0templ(templib,tempsdef,xmsg,'copycddn')
             If Datatype(x) = 'CHAR' Then copycddn = x
             ret_scr = 1
         End
    If copyrddn = '*' Then Do
        gc009 = 'Template selection for REMOTE copy'
        "SETMSG MSG(GC009)"
        x = R0listm(table,tempsdef,'$TTY','ND')
        x = R0listm(table,templib,'$TTY',,'EXIST','')
        If Datatype(x) = 'CHAR' Then Parse VAR x copyrddn .
        ret_scr = 1
    End
    Else If copyrddn = '/' Then Do
             xmsg = 'REMOTE copy'
             x = R0templ(templib,tempsdef,xmsg,'copyrddn')
             If Datatype(x) = 'CHAR' Then copyrddn = x
             ret_scr = 1
         End
    If ret_scr Then Signal copy_scr1

    If Sysdsn("'"utillib"("copyldef")'") /= 'OK' Then Do
        gc009 = Sysdsn("'"utillib"("copyldef")'")
        csr1 = 'COPYLDEF'
        "SETMSG MSG(GC009)"
        xmsg = Msg(xmsg)
        Signal copy_scr1
    End
    If copycddn /= '' Then,
        If Sysdsn("'"templib"("copycddn")'") /= 'OK' Then,
            If Sysdsn("'"tempsdef"("copycddn")'") /= 'OK' Then Do
                gc009 = Sysdsn("'"templib"("copycddn")'")
                csr1 = 'copycddn'
                "SETMSG MSG(GC009)"
                xmsg = Msg(xmsg)
                Signal copy_scr1
            End
    If copyrddn /= '' Then,
        If Sysdsn("'"templib"("copyrddn")'") /= 'OK' Then,
            If Sysdsn("'"tempsdef"("copyrddn")'") /= 'OK' Then Do
                gc009 = Sysdsn("'"templib"("copyrddn")'")
                csr1 = 'copyrddn'
                "SETMSG MSG(GC009)"
                xmsg = Msg(xmsg)
                Signal copy_scr1
            End

    "VPUT ("copyv") SHARED"
    xmsg = Msg(xmsg)
    Return 0

