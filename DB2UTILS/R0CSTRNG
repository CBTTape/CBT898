/******************************** REXX ********************************/
/**********************************************************************/
/* TRACE r                                                            */
/*--------------------------ooo000OOOO000ooo--------------------------*/
/*                                                                    */
/* Function to change occurances of a string in a string to another   */
/* string.                                                            */
/*                                                                    */
/* Format:                                                            */
/*     CSTRING(string,from_string,to_string,occurances)               */
/*                                                                    */
/*     where:                                                         */
/*                                                                    */
/*          string      - Original string                             */
/*          from_string - Character(string) in original string        */
/*          to_string   - New value, character(string)                */
/*          occurances  - Occurances to change. Default is 1, '*'     */
/*                        changes all occurances.                     */
/*          start       - Starting column. Default is 1.              */
/*                                                                    */
/*--------------------------------------------------------------------*/
    Parse ARG string,fstrg,tstrg,rep,start,trace
    Numeric Digits 10
    If rep = '*' Then rep = 9999999999
    If Datatype(rep) /= 'NUM' | Index(rep,'.') > 0 Then rep = 1
    If Datatype(start) /= 'NUM' | Index(start,'.') > 0 Then start = 1
    fl = Length(fstrg)
    tl = Length(tstrg)
    Do While rep > 0
        start = Index(string,fstrg,start)
        If start = 0 Then Leave
        string = Delstr(string,start,fl)
        string = Insert(tstrg,string,start-1,tl)
        rep = rep -1
        start = start + tl
    End
    Return string
