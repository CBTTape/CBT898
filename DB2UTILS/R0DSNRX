/******************************** REXX ********************************/
/*--------------------------------------------------------------------*/
/*  Check DB2 REXX API interface and set on/off according to argument.*/
/*--------------------------------------------------------------------*/
Arg arg1,arg2,debug
    If debug /= '' Then Trace r
    Select
        When arg1 = 'EX' Then Do
            Address ISPEXEC "VGET (ssid,sqlstmt,sqlretf) SHARED"
            Address DSNREXX "EXECSQL PREPARE S1 FROM :sqlstmt"
            Address DSNREXX "EXECSQL DECLARE C1 CURSOR FOR S1"
            Address DSNREXX "EXECSQL OPEN C1"
            sqlcode = 0
            flds    = Translate(sqlretf,'  ',':,')
            sqlretv =
            Do While sqlcode = 0
                Address DSNREXX "EXECSQL FETCH C1 INTO" sqlretf
                If sqlcode /= 0 Then Leave
                Interpret 'sqlretv = sqlretv' flds
                If Arg(2) = 1 Then Leave
            End
            rc_r = sqlcode
            Address DSNREXX "EXECSQL CLOSE C1"
            Address ISPEXEC "VPUT (sqlretv) SHARED"
            Return rc_r
        End
        When arg1 = 'ON' Then Do
            Address ISPEXEC "VGET (ssid) SHARED"
            Address TSO 'SUBCOM DSNREXX'     /* Host cmd mode availe? */
            If rc Then x = RXSUBCOM('ADD','DSNREXX','DSNREXX')
            Address DSNREXX "CONNECT" ssid
        End
        Otherwise Do
            Address DSNREXX "DISCONNECT"
        End
    End
    Return 0
