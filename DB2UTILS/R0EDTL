/******************************** REXX ********************************/
/*--------------------------ooo000OOOO000ooo--------------------------*/
/*                                                                    */
/* Program - R0EDTL                                                   */
/*                                                                    */
/* Purpose - To force user into a valid device type through a series  */
/*           of panels.                                               */
/*                                                                    */
/* Parameters:                                                        */
/*    DASD - Bring up the DASD list.                                  */
/*    TAPE - Bring up the TAPE list.                                  */
/*    LIST - Return a list of DAS and TAPEs to the caller.            */
/*         - (blank) prompt for DASD/TAPE selection.                  */
/*                                                                    */
/* Members required to package this feature:                          */
/*   REXX:                                                            */
/*         R0EDTL       - This driver                                 */
/*                                                                    */
/*   Panel:                                                           */
/*         R0142P       - POP up panel for device TYPE selection.     */
/*         R0143P       - POP up panel for device selection ( 1-16).  */
/*         R0144P       - POP up panel for device selection (17-32).  */
/*         R0145P       - POP up panel for device selection (33-48).  */
/*                                                                    */
/*   Messages:                                                        */
/*         GC00         - Generic message member                      */
/*                                                                    */
/*   Programs:                                                        */
/*         EDTLIST      - Assembler program to provide a list of      */
/*                        generic device names for a device type in a */
/*                        REXX variable.                              */
/*                                                                    */
/*--------------------------------------------------------------------*/
    Arg edt,row,col,debug
    If debug /= '' Then Trace r
    Address ISPEXEC
    If row = '' Then row = 5
    If col = '' Then col = 5
    If edt /= '' Then Signal Call_edtlist
pop_up_2:
    zwinttl  = 'Device Types'
    "ADDPOP ROW("row") COLUMN("col")"
    "DISPLAY PANEL(R0142P)"
    rc1 = rc
    "REMPOP"
    If rc1 /= 0 Then Do
        gc009 = 'Device type selection abandoned.'
        "SETMSG MSG(GC009)"
        Return '?'
    End
Call_edtlist:
    If edt = 'LIST' Then Do
        list = expand_edt_list('DASD')
        Return list expand_edt_list('TAPE')
    End
    Call edtlist
    edtocr = c2d(Substr(edtlist,5,4))
    edtlist= Substr(edtlist,9)
    If Word(edtlist,1) = 'Error' Then Do
        gc009 = 'Error found while looking for device type "'EDT'".',
                'Please refer to dialogue developer.'
        "SETMSG MSG(GC009)"
        Return 8
    End
    Do i=1 To edtocr
        Interpret 'edtt'i' = Substr(edtlist,(i-1)*8+1,8);T'i'='i
    End
pop_up_3:
    zwinttl  = 'Device Selection'
    "ADDPOP ROW("row") COLUMN("col")"
    Select
        When edtocr < 17 Then "DISPLAY PANEL(R0143P)"
        When edtocr < 33 Then "DISPLAY PANEL(R0144P)"
        Otherwise             "DISPLAY PANEL(R0145P)"
    End
    rc1 = rc
    "REMPOP"
    If rc1 /= 0 Then Do
        gc009 = 'Device type selection abandoned.'
        "SETMSG MSG(GC009)"
        Signal pop_up_2
    End
    Interpret 'Return Strip(edtt'tt')'

expand_edt_list:
    edt = Arg(1)
    Call edtlist
    edtocr = c2d(Substr(edtlist,5,4))
    edtlist= Substr(edtlist,9)
    xx=
    Do i=1 To edtocr
        xx=xx Strip(Substr(edtlist,(i-1)*8+1,8))
    End
    Return xx
