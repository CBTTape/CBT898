/******************************** REXX ********************************/
/*--------------------------------------------------------------------*/
/*  Process LISTDEF data                                              */
/*--------------------------------------------------------------------*/
Arg utillib
    Address ISPEXEC

    cactn = "RETURN"
/*  cactn = "CANCEL"                                                  */
    "CONTROL ERRORS" cactn
    "VGET (panelv) SHARED";"VGET ("panelv") SHARED"
    table    = 'LD'Time('s')
    listname = ''

    Call set_vars_1

listdef_scr1:
    zcmd = ''
    "DISPLAY PANEL(&panel12) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,listdef_scr1)
    csr1=;csrp1=
    If rc /= 0 Then Do
        "VPUT (listname) PROFILE"
        If Arg(2) = '' Then Do
            gc009 = "LISTDEF entry panel completed."
            "SETMSG MSG(GC009)"
            Return 9
        End
        Else Return listname
    End
    If zcmd='?' Then Do
        x=helpdrvr(helptab '13 /OPEN')
        Signal listdef_scr1
    End
    Parse VAR zcmd cmd parm1
/*--------------------------------------------------------------------*/
/*  Build LISTDEF example values for testing                          */
/*--------------------------------------------------------------------*/
    IF Substr(cmd,1,1) = 'B' Then Do;Call set_vars_2;Signal listdef_scr1;End
/*--------------------------------------------------------------------*/
    If  listname = '*' Then Do
        listname =
        x = R0listm(table,utillib)
        If Datatype(x) = 'CHAR' Then Do
            listname = x
            x = set_vars_2()
        End
        Signal listdef_scr1
    End
    If listact /= '' Then Do
        If Sysdsn("'"utillib"'") /= 'OK' Then Do
            Address TSO "ALLOC DA('"utillib"') TRACKS NEW CATALOG",
                "RECFM(F B) LRECL(80) BLKSIZE(0) SPACE(45,15)",
                "DIR(20) UNIT(SYSALLDA) DSORG(PO)"
            Address TSO "FREE  DA('"utillib"')"
        End
    End

/*--------------------------------------------------------------------*/
/*  Parse the dynamic field and verify names and build member contents*/
/*--------------------------------------------------------------------*/
    xxx=da1;i=0;j=0
    Drop linet.
    Do while xxx /= ''
        Parse Var xxx,
          '[' ts1 ']' '[' ts2 ']' '[' type ']' '[' ie ']' '[' ri ']' '[' pl ']',
          xxx
        j = j +1
        ts = ts1'.'ts2
        If ts /= '.' Then Do
            If name_ver('TS') > 0 Then Signal listdef_scr1
            If type = 'IX' Then type = INDEXSPACE
            Else                type = TABLESPACE
            If Strip(ri) /= '' Then ri = 'RI'
            Else                    ri = '  '
            If pl /= '' Then pl = 'PARTLEVEL'
            If Substr(ie,1,1) = 'E' Then ie = EXCLUDE
            Else                         ie = INCLUDE
            i = i +1
            linet.i = '    'ie type Substr(ts,1,18) || ri pl
        End
    End
    linet.0 = i

/*--------------------------------------------------------------------*/
/*  ACtion member update options                                      */
/*--------------------------------------------------------------------*/
    edit = ''
    dd = 'UL'Time('s')
    Select
        When listact = 'C' Then Do
            Address TSO "ALLOC F("dd") DA('"utillib"("listname")')",
                "SHR REU"
            Address TSO "NEWSTACK"
            Queue '  LISTDEF' listname
            Queue ''
            Address TSO "EXECIO * DISKW "dd" (OPEN"
            Address TSO "DELSTACK"
            Address TSO "EXECIO * DISKW "dd" (STEM linet. FINIS"
            Address TSO "FREE  F("dd")"
            x = R0stats(utillib"("listname")")
            gc009 = 'Member "'listname'" updated.'
            edit = '/'
        End
        When listact = 'A' Then Do
            Drop line.
            Address TSO "ALLOC F("dd") DA('"utillib"("listname")')",
                "SHR REU"
            If Sysdsn("'"utillib"("listname")'") /= 'OK' Then Do
                line.1 = '  LISTDEF' listname
                line.0 = 1
            End
            Else Address TSO "EXECIO * DISKR "dd" (STEM line. FINIS)"
            Address TSO "EXECIO * DISKW "dd" (STEM line. OPEN)"
            Address TSO "EXECIO * DISKW "dd" (STEM linet. FINIS"
            Address TSO "FREE  F("dd")"
            x = R0stats(utillib"("listname")")
            gc009 = 'Details appended to existin member "'listname'".'
            edit = '/'
        End
        Otherwise NOP
    End
    listact = ''

    If edit = '/' Then Do
        "EDIT DATASET('&UTILLIB(&LISTNAME)')"
        "SETMSG MSG(GC009)"
        listedt = ''
    End
    Signal listdef_scr1

/*--------------------------------------------------------------------*/
/*  Verify name format of table/index spaces provided on LISTDEF      */
/*  parameter entry screen. Allows wildcirding using * and %.         */
/*--------------------------------------------------------------------*/
name_ver: Arg fld
    Interpret "val = "fld
    Parse VAR val v.1 '.' v.2
    v.1 = Strip(v.1);v.2 = Strip(v.2)
    If v.2 = '' Then v.2 = '*'
    Do k=1 To 2
        of2 = Verify(Substr(v.k,1,1),alpha)
        If of2 /= 0 Then Signal name_err
        of2 = Verify(v.k,alpha||numbs)
        If of2 /= 0 Then Signal name_err
    End
    Interpret fld "= v.1'.'v.2"
    Return 0
name_err:
    gc009 = 'Invalid name format "'v.k'". Re-enter please.'
    "SETMSG MSG(GC009)"
    csr1 = 'DA1'
    csrp1 = 13
    If k = 1 Then of1 = 1
    Else          of1 = 12
    csrp1 = csrp1 + (j-1)*80 + of1 + (of2-1)
    Return 4


set_vars_1:
    xxx   = '[        ].[        ] [  ]  [       ] [  ] [ ]',
            Substr(' ',1,33)
    da1   = '            'Copies(xxx,33)
    alpha = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ@#$%*'
    numbs = '0123456789'

    Return 0

set_vars_2:
    If Sysdsn("'"utillib"("listname")'") /= 'OK' Then Do
        gc009 = Sysdsn("'"utillib"("listname")'")
        "SETMSG MSG(GC009)"
        Return 0
    End
    dd = 'UL'Time('s')
    Address TSO "ALLOC F("dd") DA('"utillib"("listname")')",
        "SHR REU"
    Address TSO "EXECIO * DISKR "dd" (STEM linei. FINIS"
    Address TSO "FREE  F("dd")"
    If linei.0 > 34 Then linei.0 = 34
    da1 = '            '
    Do i=2 To linei.0
        Parse VAR linei.i ie tt db '.' sp .
        If Index(linei.i,' RI ') > 0 Then ri = 'RI'
        Else                              ri = '  '
        If Index(linei.i,' PARTLEVEL ') > 0 Then pl = '/'
        Else                                     pl = ' '
        If Substr(tt,1,1) = 'T' Then tt = 'TS'
        Else                         tt = 'IX'
        db = Substr(db,1,8);sp = Substr(sp,1,8)
        da1 = da1 ||,
              '['db'].['sp'] ['tt']  ['ie'] ['ri'] ['pl']',
              Substr(' ',1,33)
    End
    i = i-1
    If i < 33 Then Do
        xxx = '[        ].[        ] [  ]  [       ] [  ] [ ]',
              Substr(' ',1,33)
        da1 = da1 || Copies(xxx,34-i)
    End

    Return 0

