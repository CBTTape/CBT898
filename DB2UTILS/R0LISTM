/******************************** REXX ********************************/
/*--------------------------------------------------------------------*/
/*  Members selection panel.                                          */
/*                                                                    */
/*  Build a member list ISPF table and present under table display    */
/*  panel CSGCHPCO to copy members from the backup to staging         */
/*  datasets.                                                         */
/*  Parameters (positional comma seperated):                          */
/*    Table name.                                                     */
/*    Input file.                                                     */
/*    Member name of template definitions (only for template list)    */
/*    Action - any non blank value will RETURN table                  */
/*    Table exist indicator - any non blank value will not CREATE     */
/*                            table                                   */
/*--------------------------------------------------------------------*/
Arg table,ifile,imemb,iact,existab,debug
    Address ISPEXEC

    cactn = "RETURN"
/*  cactn = "CANCEL"                                                  */
    "CONTROL ERRORS" cactn
    If debug/='' Then Trace r

    panel90 = 'R090P'                       /* Member list table      */
    resname1 =' ',                          /* Reserved names list:-  */
             '$TTY',                        /* Template type XREF     */
             '#$TTY',                       /* $TTY backup            */
             ' '
    resname2 =' ',                          /* Reserved names list:-  */
             '$$$INDEX',                    /* Index                  */
             ' '

    If existab /= '' Then Signal list_check_ifile
    "TBCREATE &TABLE KEYS(MEMBER) NAMES(LMDATE LMTIME",
         "ZLCDATE ZLMDATE ZLMTIME SELCTD VERSMOD",
         "ZLCNORC ZLINORC ZLMNORC ZLUSER TTY IFILE)",
         "REPLACE NOWRITE"

list_check_ifile:
    If Index(ifile,'.TEMPLATE') > 0 Then pmsg = 'Template Type'
    Else                                 pmsg =

    If Sysdsn("'"ifile"'") /= 'OK' Then Do
/*      gc009 = "Dataset '"ifile"' not found."                        */
/*      "SETMSG MSG(GC009)"                                           */
        If iact /= '' Then Return 4
        Signal dis1
    End
    "LMINIT DATAID(DATA1) DATASET('&IFILE') ENQ(SHR)"
    "LMOPEN DATAID(&DATA1) OPTION(INPUT)"
    member =
    tty    =
    Do Forever
        "LMMLIST DATAID(&DATA1) OPTION(LIST) MEMBER(MEMBER)",
                "STATS(YES)"
        If rc /= 0 Then Leave
        If zlvers /= '' Then versmod = zlvers'.'zlmod
        Else                 versmod = ''
        member = Strip(member)
        If Index(resname1,' 'member' ') = 0 Then "TBADD &TABLE"
    End
    "LMMLIST DATAID(&DATA1) OPTION(FREE)"
    "LMCLOSE DATAID(&DATA1)"
    "LMFREE  DATAID(&DATA1)"

    If imemb /= '' Then x = get_temp_type(ifile,imemb)
    "TBSORT &TABLE FIELDS(MEMBER,C,A)"
    "TBTOP &TABLE"
    If iact /= '' Then Return 0

dis1:
    sel =
    "TBDISPL &TABLE PANEL(&PANEL90)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,dis1)
    If rc > 4 Then Do
        "TBEND &TABLE"
        Return ?
    End
    Parse var zcmd cmd parm1 parm2
    If cmd = 'L' Then Do
        If parm1 = '' Then Do
            gc009 = 'Missing Locate parameter.'
            "SETMSG MSG(gc009)"
            Signal dis1
        End
        ISPEXEC "TBTOP &TABLE"
        If Index(parm1,'*') > 0 Then
             member = Substr(parm1,1,Index(parm1,'*')-1)
        member = parm1'*'
        ISPEXEC "TBSCAN &TABLE ARGLIST(MEMBER) CONDLIST(GE)"
        Signal dis1
    End

    If sel = '' Then Signal dis1

    msub = ztdsels
    Signal con1

mus1:
    "TBDISPL &TABLE"

con1:
    act =
    Select
        When (sel = 'B') Then act = 'BROWSE'
        When (sel = 'V') Then act = 'VIEW'
        When (sel = 'E') Then Do
            If ifile = tempsdef Then Do
                act = 'VIEW'
                gc009 = 'Edit changed to view for site default member.'
                "SETMSG MSG(GC009)"
            End
            Else act = 'EDIT'
        End
        When (sel = 'S') Then Do
            If Index(resname2,' 'member' ') = 0 Then Do
                "TBEND &TABLE"
                If imemb /= '' Then Return member tty
                Else                Return member
            End
            Else Do
                act = 'BROWSE'
                gc009 = 'Cannot select this member. It is not a LIST',
                        'definition member.'
                "SETMSG MSG(GC009)"
            End
        End
        Otherwise NOP
    End
    If act = '' Then Signal dis1
    "CONTROL DISPLAY SAVE"
    "&ACT DATASET('&IFILE(&MEMBER)')"
    "CONTROL DISPLAY RESTORE"

gnx1:
    msub = msub - 1
    If msub < 1 Then  Signal dis1
    Else              Signal mus1

/*--------------------------------------------------------------------*/
/*  Read member $TTY to establish template type.                      */
/*--------------------------------------------------------------------*/
get_temp_type: Arg ifile,imemb
    "TBTOP &TABLE"
    If Sysdsn("'"ifile"("imemb")'") /= 'OK' Then Do
        gc009 = "Member '"imemb"' not found. Cannot expand template type."
        "ISPEXEC SETMSG MSG(GC009)"
        Return 4
    End
    dd = 'LM'Time('s')
    Address TSO "ALLOC F("dd") DA('"ifile"("imemb")') SHR REU"
    Address TSO "EXECIO * DISKR "dd" (STEM line. FINIS"
    Do i=1 To line.0
        If Substr(line.i,1,1) = '*' Then Iterate
        Parse VAR line.i member tty1 .
        "TBSCAN &TABLE ARGLIST(MEMBER)"
        If rc = 0 Then Do
            tty = Strip(tty1)
            "TBMOD &TABLE"
        End
    End
    Address TSO "FREE  F("dd")"

    Return 0

