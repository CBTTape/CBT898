/******************************** REXX ********************************/
/*--------------------------------------------------------------------*/
/*  Validate and if valid list Table/indexspace copies.               */
/*--------------------------------------------------------------------*/
Parse Arg sqlstmt,sqlretf,table,perfdblk,panel,fname1,fname2,debug

    If debug /= '' Then Trace r
    If panel = '' Then Do                   /* Dataset table display  */
        paneltb = 'R140TP'
        selall = '*'
    End
    Else Do
        paneltb = panel
        selall =
    End
    Address ISPEXEC
    x = R0dsnrx('On')

    "VPUT (sqlstmt,sqlretf) SHARED"
    xx = R0dsnrx('Ex','*')
    x  = R0dsnrx('Off')
    "VGET (sqlretv,ssid) SHARED"

    If sqlretv = '' Then Do
        If perfdblk /= '' Then xtra = 'for like "'perfdblk'" '
        Else                   xtra =
        gc009 = 'No entries found 'xtra'on "'ssid'", RC = 'xx'.'
        "SETMSG MSG(GC009)"
        Return 1
    End

    seq#  = 0
    "TBCREATE &TABLE KEYS(SEQ#,DBNAME,NAME) NAMES(SEL,T1,T2)",
              "REPLACE NOWRITE"
    "TBSORT &TABLE FIELDS(DBNAME,C,D)"

    flds = Translate(sqlretf,'  ',':,')
    Do While sqlretv /= ''
        Interpret 'Parse VAR sqlretv' flds 'sqlretv'
        seq#=seq#+1
        "TBMOD  &TABLE"
    End
    "TBTOP &TABLE"

dis0:
    "TBDISPL &TABLE PANEL(&PANELTB)"
    If rc > 4 Then Do
        "TBEND &TABLE"
        gc009 = 'PERFQ table/index selection abandoned.'
        "SETMSG MSG(GC009)"
        Return 1
    End
    If zcmd='DEBUG' Then Interpret r0debug(debug,dis0)
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('perfqv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal dis0;End

    If zcmd='?' Then Do
        x=helpdrvr(helptab '16 /OPEN')
        Signal dis0
    End
    "VPUT (perfid) SHARED"
    If selall='*' Then Do
        "TBTOP &TABLE"
        Do Forever
            "TBSKIP &TABLE";If rc /= 0 Then Leave
            sel = 'S'
            "TBMOD &TABLE"
        End
        Return 0
    End

    If ztdsels = 0 Then Do
        "TBTOP &TABLE"
        "TBVCLEAR &TABLE"
        sel = 'S'
        "TBSARG &TABLE NAMECOND(SEL,EQ)"
        "TBSCAN &TABLE"
        If rc /= 0 Then Do
            gc009 = 'No entries have been selected. Make selection(s)',
                    'or use END PF key to abandon processing.'
            "SETMSG MSG(GC009)"
            Signal dis0
        End
        Return 0
    End
    msub = ztdsels
    Signal con0
mus0:
    "TBDISPL &TABLE"
con0:
    If sel /= '' Then sel = 'S'
    "TBMOD &TABLE"
gnx0:
    msub = msub - 1
    If msub < 1 Then Do
        gc009 = 'Hit [ENTER] to continue processing, make further',
                'selections or use END PF key to abandon processing.'
        "SETMSG MSG(GC009)"
        Signal dis0
    End
    Else Signal mus0

