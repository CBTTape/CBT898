/******************************** REXX ********************************/
/*--------------------------------------------------------------------*/
/*  Process REBUILD INDEX parameter entry                             */
/*--------------------------------------------------------------------*/
Arg oper1,utillib,templib,tempsdef,debug
    Address ISPEXEC

    cactn = "RETURN"
/*  cactn = "CANCEL"                                                  */
    "CONTROL ERRORS" cactn

    "VGET (rebixv,panelv,ssid,sqlid) SHARED"
    "VGET ("rebixv panelv" nexto) SHARED"
    table = 'RI'Time('s')

rebldinx_scr1:
    zcmd =
    "DISPLAY PANEL(&PANEL120) CURSOR(&csr1) CSRPOS(&csrp1)"
    If rc /= 0 Then Do
        "VPUT ("rebixv") SHARED"
        gc009 = oper1 "entry panel abandoned."
        "SETMSG MSG(GC009)"
        csr1=;csrp1=
        Return 9
    End
    If zcmd='DEBUG' Then Interpret r0debug(debug,rebldinx_scr1)
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('rebixv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal rebldinx_scr1;End

    If zcmd='?' Then Do
        x=helpdrvr(helptab '11  /OPEN')
        Signal rebldinx_scr1
    End
    csr1=;csrp1=
    genolist = 'OFF'
    opertp = 'REBUILD INDEX'
    Parse VAR zcmd cmd parm1
    Select
        When rbls /= '' Then Do
            rbls =
            ulact = 'LS'
            x = rebldinx_lss1()
            If x = 0 Then Signal Return0
        End
        When rbts /= '' Then Do
            rbts =
            ulact = 'TS'
            x = rebldinx_tss1()
            opertp = opertp '(ALL) TABLESPACE'
            If x = 0 Then Signal Return0
        End
        When rbix /= '' Then Do
            rbix =
            ulact = 'IX'
            x = rebldinx_ixs1()
            If x = 0 Then Signal Return0
        End
        Otherwise Signal rebldinx_scr1
    End
    Signal rebldinx_scr1

Return0:
    If rebisrtn /= '' Then sortnum = 'SORTNUM' rebisrtn' '
    Else                   sortnum =
    If rebisrtk /= '' Then sortnum = sortnum || 'SORTKEYS'
    "VPUT ("rebixv" tspace partno nexto genolist ulact sortnum",
           "opertp) SHARED"
    Return 0

/*--------------------------------------------------------------------*/
/* Rebuild index from LiStdef                                         */
/*--------------------------------------------------------------------*/
rebldinx_lss1:
    zcmd =
    "DISPLAY PANEL(&PANEL121) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,rebldinx_lss1)
    csr1=;csrp1=
    If rc /= 0 Then Do
        gc009 = oper1 "FROM TABLE name entry panel abandoned."
         "SETMSG MSG(GC009)"
        Return 9
    End
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('rebixv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal rebldinx_lss1;End

    If zcmd='?' Then Do
        x=helpdrvr(helptab '11A /OPEN')
        Signal rebldinx_lss1
    End

    ret_scr = 0
    Parse VAR zcmd cmd parm1

    If rebildef = '*' Then Do
        gc009 = 'Listdef processing for Rebuild Index.'
        "SETMSG MSG(GC009)"
        x = R0listm(table,utillib)
        If Datatype(x) = 'CHAR' Then rebildef = x
        ret_scr = 1
    End
    Else If rebildef = '/' Then Do
             gc009 = 'Listdef processing for Rebuild Index.'
             "SETMSG MSG(GC009)"
             x = r0ldef(utillib,'rebildef')
             If Datatype(x) = 'CHAR' Then rebildef = x
             ret_scr = 1
         End
    xmsg=Msg('Off')
    If Sysdsn("'"utillib"("rebildef")'") /= 'OK' Then Do
        gc009 = Sysdsn("'"utillib"("rebildef")'")
        csr1 = 'rebildef'
        "SETMSG MSG(GC009)"
        xmsg=Msg(xmsg)
        Signal rebldinx_lss1
    End
    xmsg=Msg(xmsg)
    xx = R0vrldef(utillib'('rebildef')','1ST')
    If xx /= 0 Then Do
        gc009 = 'List contains mixture of TABLESPACE and INDEXSPACE.',
                'Can only have "'xx'"s. Please amend the list or',
                'select an alternative list.'
        csr1 = 'rebildef'
        "SETMSG MSG(GC009)"
        Signal rebldinx_lss1
    End
    x = rebldinx_ver_common()
    If ret_scr Then Signal rebldinx_lss1
    genolist = 'ON'
    nexto    = 'JCL(JC,J10,J20,J120)'

    Return 0

/*--------------------------------------------------------------------*/
/* Rebuild index from Table Space                                     */
/*--------------------------------------------------------------------*/
rebldinx_tss1:
    zcmd =
    "DISPLAY PANEL(&PANEL122) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,rebldinx_tss1)
    csr1=;csrp1=
    If rc /= 0 Then Do
        gc009 = oper1 "FROM TABLE name entry panel abandoned."
         "SETMSG MSG(GC009)"
        Return 9
    End
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('rebixv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal rebldinx_tss1;End

    If zcmd='?' Then Do
        x=helpdrvr(helptab '11B /OPEN')
        Signal rebldinx_tss1
    End

    ret_scr = 0
    Parse VAR zcmd cmd parm1
    If ret_scr Then Signal rebldinx_tss1

/*--------------------------------------------------------------------*/
/*  If tablespace has not been validated or has changed, do it.       */
/*--------------------------------------------------------------------*/
    If r_table_ts /= rebidbnm || rebispce || rebipart Then,
        If R0valtsp('TS',rebidbnm,rebispce,rebipart) Then Do
            csr1 = 'rebidbnm'
            Signal rebldinx_tss1
        End
    r_table_ts = rebidbnm || rebispce || rebipart

    x = rebldinx_ver_common()
    If ret_scr Then Signal rebldinx_tss1

    tspace = rebidbnm'.'rebispce
    If rebipart = '' Then partno =
    Else partno = 'PART' rebipart +0 'FORCEROLLUP YES'

    Return 0

/*--------------------------------------------------------------------*/
/* Rebuild index from Index Name                                      */
/*--------------------------------------------------------------------*/
rebldinx_ixs1:
    zcmd =
    "DISPLAY PANEL(&PANEL123) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,rebldinx_ixs1)
    csr1=;csrp1=
    If rc /= 0 Then Do
        gc009 = oper1 "FROM TABLE name entry panel abandoned."
         "SETMSG MSG(GC009)"
        Return 9
    End
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('rebixv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal rebldinx_ixs1;End

    If zcmd='?' Then Do
        x=helpdrvr(helptab '11C /OPEN')
        Signal rebldinx_ixs1
    End

    ret_scr = 0
    Parse VAR zcmd cmd parm1

/*--------------------------------------------------------------------*/
/*  If tablespace has not been validated or has changed, do it.       */
/*--------------------------------------------------------------------*/
    If r_table_ix /= rebicrea || rebindex || rebiptix Then Do
        If R0valtsp('RB',rebicrea,rebindex,rebiptix) Then Do
            csr1 = 'REBICREA'
            Signal rebldinx_ixs1
        End
        r_table_ix = rebicrea || rebindex || rebiptix
    End

    x = rebldinx_ver_common()
    If ret_scr Then Signal rebldinx_ixs1

    tspace = rebicrea'.'rebindex
    If rebiptix = '' Then partno =
    Else                  partno = 'PART' rebiptix +0

    Return 0

/*--------------------------------------------------------------------*/
/*  Comon rebldinx field verifications:                               */
/*--------------------------------------------------------------------*/
rebldinx_ver_common:
/*--------------------------------------------------------------------*/
/*  Break up supplied WHEN parameters in 72 character lots without    */
/*  splitting words.                                                  */
/*--------------------------------------------------------------------*/
    If rebiwddn = '*' Then Do
        gc009 = 'Template processing for WORKDDN Rebuild Index.'
        "SETMSG MSG(GC009)"
        x = R0listm(table,tempsdef,'$TTY','ND')
        x = R0listm(table,templib,'$TTY',,'EXIST')
        If Datatype(x) = 'CHAR' Then Parse VAR x rebiwddn .
        ret_scr = 1
    End
    Else If rebiwddn = '/' Then Do
             xmsg = 'WORKDDN Rebuild Index'
             x = R0templ(templib,tempsdef,xmsg,'rebiwddn')
             If Datatype(x) = 'CHAR' Then rebiwddn = x
             ret_scr = 1
         End
    If Sysdsn("'"templib"("rebiwddn")'") /= 'OK' Then,
        If Sysdsn("'"tempsdef"("rebiwddn")'") /= 'OK' Then Do
            gc009 = 'Template not found.'
            "SETMSG MSG(GC009)"
            csr1  = 'REBIWDDN'
            ret_scr=1
            Return 4
        End
    If rebidevt = '*' Then Do
        gc009 = 'Device type entry for Rebuild Index.'
        "SETMSG MSG(GC009)"
        x = R0edtl('DASD',10,10,debug)
        If Datatype(x) = 'CHAR' | x > 20 Then rebidevt = x
        ret_scr = 1
    End
    Else Do
        x = R0edtl('LIST',,,debug)
        If Wordpos(rebidevt,x) = 0 Then Do
            gc009 = 'Invalid devive type. Suggest enter "*" for selection list.'
            "SETMSG MSG(GC009)"
            csr1 = 'rebidevt'
            ret_scr = 1
        End
    End

    Return 0
