/******************************** REXX ********************************/
/*--------------------------------------------------------------------*/
/*  Process UNLOAD LISTDEF parameter entry                            */
/*--------------------------------------------------------------------*/
Arg oper1,utillib,templib,tempsdef,debug
    Address ISPEXEC

    cactn = "RETURN"
/*  cactn = "CANCEL"                                                  */
    "CONTROL ERRORS" cactn

    "VGET (recovv,panelv,ssid,sqlid) SHARED"
    "VGET ("recovv panelv" nexto) SHARED"
    table  = 'RC'Time('s')

recover_scr1:
    genolist = 'OFF'
    rctype   =
    zcmd     =
    "DISPLAY PANEL(&PANEL100) CURSOR(&csr1) CSRPOS(&csrp1)"
    If rc /= 0 Then Do
        "VPUT ("recovv") SHARED"
        gc009 = oper1 "entry panel abandoned."
        "SETMSG MSG(GC009)"
        Return 9
    End
    If zcmd='DEBUG' Then Interpret r0debug(debug,recover_scr1)
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('recovv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal recover_scr1;End

    If zcmd='?' Then Do
        x=helpdrvr(helptab '12  /OPEN')
        Signal recover_scr1
    End
    csr1=;csrp1=
    Parse VAR zcmd cmd parm1
    Select
        When ulls /= '' Then Do
            ulls  =
            ulact = 'LS'
            x = recover_lss1()
            If x = 0 Then Signal Return0
        End
        When ults /= '' Then Do
            ults  =
            ulact = 'TS'
            x = recover_tss1()
            If x = 0 Then Signal Return0
        End
        When ulix /= '' Then Do
            ulix  =
            ulact = 'IX'
            x = recover_ixs1()
            If x = 0 Then Signal Return0
        End
        Otherwise Signal recover_scr1
    End
    Signal recover_scr1

Return0:
    "VPUT ("recovv" nexto genolist ulact tspace partno dsnx",
           "tocopy rctype) SHARED"
    Return 0

/*--------------------------------------------------------------------*/
/*  Recover LiStdef                                                   */
/*--------------------------------------------------------------------*/
recover_lss1:
    zcmd =
    "DISPLAY PANEL(&PANEL101) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,recover_lss1)
    csr1=;csrp1=
    If rc /= 0 Then Do
        gc009 = oper1 "LISTDEF name entry panel abandoned."
         "SETMSG MSG(GC009)"
        Return 9
    End
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('recovv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal recover_lss1;End

    If zcmd='?' Then Do
        x=helpdrvr(helptab '12A /OPEN')
        Signal recover_lss1
    End

    ret_scr = 0
    Parse VAR zcmd cmd parm1

    If recoldef = '*' Then Do
        x = R0listm(table,utillib)
        If Datatype(x) = 'CHAR' Then recoldef = x
        ret_scr = 1
    End
    Else If recoldef = '/' Then Do
             x = r0ldef(utillib,'recoldef')
             If Datatype(x) = 'CHAR' Then recoldef = x
             ret_scr = 1
         End
    If Sysdsn("'"utillib"("recoldef")'") /= 'OK' Then Do
        gc009 = Sysdsn("'"utillib"("recoldef")'")
        csr1 = 'recoldef'
        "SETMSG MSG(GC009)"
        ret_scr = 1
    End
    If ret_scr Then Signal recover_lss1

    genolist = 'ON'
    nexto    = 'JCL(JC,J10,J20,J100)'

    Return 0

/*--------------------------------------------------------------------*/
/*  Recover Table Space                                               */
/*--------------------------------------------------------------------*/
recover_tss1:
    zcmd =
    "DISPLAY PANEL(&PANEL102) CURSOR(&csr1) CSRPOS(&csrp1)"
    csr1=;csrp1=
    If rc /= 0 Then Do
        gc009 = oper1 "DB2 Object name entry panel abandoned."
         "SETMSG MSG(GC009)"
        Return 9
    End
    If zcmd='DEBUG' Then Interpret r0debug(debug,recover_tss1)
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('recovv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal recover_tss1;End

    If zcmd='?' Then Do
        x=helpdrvr(helptab '12B /OPEN')
        Signal recover_tss1
    End

    ret_scr = 0
    Parse VAR zcmd cmd parm1

    tocopy = recotcpy
/*--------------------------------------------------------------------*/
/*  If tablespace has not been validated or has changed, do it.       */
/*--------------------------------------------------------------------*/
    dbname = recodbnm
    tspace = recospce
    If recobjtp = 'IX' Then objtyp = 'RI'
    Else                    objtyp = recobjtp
    If r_table_ts /= dbname || tspace || recopart || objtyp Then,
        If R0valtsp(objtyp,recodbnm,recospce,recopart,debug) Then Do
            csr1 = 'recodbnm'
            Signal recover_tss1
        End
    r_table_ts = dbname || tspace || recopart || objtyp

    If recotcpy = 'TOCOPY' Then Do
        If r_table_fc /= dbname || tspace Then Do
            If R0valcop(dbname,tspace,recopart) Then Do
                csr1 = 'recodbnm'
                Signal recover_tss1
            End
            "VGET (dsnx) SHARED"
            r_table_fc = dbname || tspace
        tocopy = 'TOCOPY' dsnx
        End
    End
    If recobjtp = 'TS' Then tspace = 'TABLESPACE' recodbnm'.'recospce
    Else                    tspace = 'INDEXSPACE' recodbnm'.'recospce
    If recopart = '' Then partno =
    Else                  partno = 'DSNUM' recopart +0
    rctype = recoopt1

    Return 0

/*--------------------------------------------------------------------*/
/*  Recover Index                                                     */
/*--------------------------------------------------------------------*/
recover_ixs1:
    zcmd =
    "DISPLAY PANEL(&PANEL103) CURSOR(&csr1) CSRPOS(&csrp1)"
    csr1=;csrp1=
    If rc /= 0 Then Do
        gc009 = oper1 "DB2 Object name entry panel abandoned."
         "SETMSG MSG(GC009)"
        Return 9
    End
    If zcmd='DEBUG' Then Interpret r0debug(debug,recover_ixs1)
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('recovv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal recover_ixs1;End

    If zcmd='?' Then Do
        x=helpdrvr(helptab '12C /OPEN')
        Signal recover_ixs1
    End

    ret_scr = 0
    Parse VAR zcmd cmd parm1

    tocopy = recotcpy
/*--------------------------------------------------------------------*/
/*  If tablespace has not been validated or has changed, do it.       */
/*--------------------------------------------------------------------*/
    If r_table_ix /= recocrea || recoindx || recoptix Then Do
        dbname = recocrea
        tspace = recoindx
        If R0valtsp('OW',dbname,tspace,recoptix) Then Do
            csr1 = 'recocrea'
            Signal recover_ixs1
        End
        "VGET (sqlretv) SHARED"
        Parse VAR sqlretv dbname tspace .
        If R0valtsp('RI',dbname,tspace,recoptix) Then Do
            csr1 = 'recocrea'
            Signal recover_ixs1
        End
        r_table_ix = recocrea || recoindx || recoptix
    End

    If recotcpy = 'TOCOPY' Then Do
        If R0valcop(dbname,tspace) Then Do
            csr1 = 'recocrea'
            Signal recover_ixs1
        End
        "VGET (dsnx) SHARED"
        tocopy = 'TOCOPY' dsnx
    End
    tspace = 'INDEX' recocrea'.'recoindx
    If recoptix = '' Then partno =
    Else                  partno = 'DSNUM' recoptix +0
    rctype = recoopt1

    Return 0
