/******************************** REXX ********************************/
/*--------------------------------------------------------------------*/
/*  Process UNLOAD LISTDEF parameter entry                            */
/*--------------------------------------------------------------------*/
Arg oper1,utillib,templib,tempsdef,debug
    Address ISPEXEC

    cactn = "RETURN"
/*  cactn = "CANCEL"                                                  */
    "CONTROL ERRORS" cactn

    "VGET (uloadv,panelv,ssid,sqlid) SHARED"
    "VGET ("uloadv panelv" nexto) SHARED"
    table = 'UL'Time('s')

    Call temp_var1
    x = Msg('Off')

unload_scr1:
    genolist='OFF';zcmd=
    "DISPLAY PANEL(&PANEL50) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,unload_scr1)
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('uloadv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal unload_scr1;End

    csr1=;csrp1=
    If rc /= 0 Then Do
        "VPUT ("uloadv") SHARED"
        gc009 = oper1 "entry panel abandoned."
        "SETMSG MSG(GC009)"
        Return 9
    End
    If zcmd='?' Then Do
        x=helpdrvr(helptab '6  /OPEN')
        Signal unload_scr1
    End
    Parse VAR zcmd cmd parm1
    Select
        When ulft /= '' Then Do
            ulft =
            ulact = 'FT'
            x = unload_fts1()
            If x = 0 Then Signal Return0
        End
        When ulls /= '' Then Do
            ulls =
            ulact = 'LS'
            x = unload_lss1()
            If x = 0 Then Signal Return0
        End
        When ults /= '' Then Do
            ults =
            ulact = 'TS'
            x = unload_tss1()
            If x = 0 Then Signal Return0
        End
        When ulfc /= '' Then Do
            ulfc =
            ulact = 'FC'
            x = unload_fcs1()
            If x = 0 Then Signal Return0
        End
        Otherwise Signal unload_scr1
    End
    Signal unload_scr1

Return0:
    "VPUT ("uloadv" nexto genolist ulact",
          "when1 when2 when3 when4 when5 when6 when7 when8"
          ") SHARED"
    Return 0

/*--------------------------------------------------------------------*/
/*  Unload Table (Name)                                               */
/*--------------------------------------------------------------------*/
unload_fts1:
    zcmd =
    "DISPLAY PANEL(&PANEL51) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,unload_fts1)
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('uloadv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal unload_fts1;End

    csr1=;csrp1=
    If rc /= 0 Then Do
        gc009 = oper1 "FROM TABLE name entry panel abandoned."
         "SETMSG MSG(GC009)"
        Return 9
    End
    If zcmd='?' Then Do
        x=helpdrvr(helptab '6A /OPEN')
        Signal unload_fts1
    End

    ret_scr = 0
    Parse VAR zcmd cmd parm1

/*--------------------------------------------------------------------*/
/*  If table name has not been validated or has changed, do it.       */
/*--------------------------------------------------------------------*/
    If u_table_ft /= unldownr || unldtbnm || ssid Then Do
        x = R0dsnrx('On')

        sqlstmt = "SELECT DBNAME,TSNAME",
                 "FROM SYSIBM.SYSTABLES",
                 "WHERE CREATOR = '"unldownr"'",
                 "AND   NAME   = '"unldtbnm"'"
        sqlretf = ':database,:tspace'
        "VPUT (sqlstmt,sqlretf) SHARED"
        xx = R0dsnrx('Ex',1,debug)
        x  = R0dsnrx('Off')
        If xx /= 0 Then Do
            gc009 = '"'unldownr'.'unldtbnm'" table not found on "',
                    ssid'", RC = 'xx'.'
             "SETMSG MSG(GC009)"
            csr1 = 'unldownr'
            Signal unload_fts1
        End
        Address ISPEXEC "VGET (sqlretv) SHARED"
        flds = Translate(sqlretf,'  ',':,')
        Do While sqlretv /= ''
            Interpret 'Parse VAR sqlretv' flds 'sqlretv'
        End
        adr2 = Address()
        Address TSO
        "NEWSTACK"
        Queue '-DISPLAY DB('database') SPACE('tspace') LIMIT(*)'
        Queue 'END'
        dbsum = R0dbsum()
        "DELSTACK"
        Address(adr2)
        If dbsum /= 0 Then Do
            gc009 = '"'unldownr'.'unldtbnm'" table not available',
                    'on "'ssid'".'
             "SETMSG MSG(GC009)"
            csr1 = 'unldownr'
            Signal unload_fts1
        End

        u_table_ft = unldownr || unldtbnm || ssid
    End

    ulwhen = unldwhn1 || unldwhn2 || unldwhn3
    x = unload_ver_common()
    If ret_scr Then Signal unload_fts1

    Return 0

/*--------------------------------------------------------------------*/
/*  Unload LiStdef                                                    */
/*--------------------------------------------------------------------*/
unload_lss1:
    zcmd =
    "DISPLAY PANEL(&PANEL52) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,unload_lss1)
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('uloadv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal unload_lss1;End

    csr1=;csrp1=
    If rc /= 0 Then Do
        gc009 = oper1 "FROM TABLE name entry panel abandoned."
         "SETMSG MSG(GC009)"
        Return 9
    End
    If zcmd='?' Then Do
        x=helpdrvr(helptab '6B /OPEN')
        Signal unload_lss1
    End

    ret_scr = 0
    Parse VAR zcmd cmd parm1

    If unldldef = '*' Then Do
        x = R0listm(table,utillib)
        If Datatype(x) = 'CHAR' Then unldldef = x
        ret_scr = 1
    End
    Else If unldldef = '/' Then Do
             x = r0ldef(utillib,'unldldef')
             If Datatype(x) = 'CHAR' Then unldldef = x
             ret_scr = 1
         End
    If Sysdsn("'"utillib"("unldldef")'") /= 'OK' Then Do
        gc009 = Sysdsn("'"utillib"("unldldef")'")
        csr1 = 'unldldef'
        "SETMSG MSG(GC009)"
        Signal unload_lss1
    End
    ulwhen =
    x = unload_ver_common()
    If ret_scr Then Signal unload_lss1
    genolist = 'ON'
    nexto    = 'JCL(JC,J10,J20,J50)'

    Return 0

/*--------------------------------------------------------------------*/
/*  Unload Table Space                                                */
/*--------------------------------------------------------------------*/
unload_tss1:
    zcmd =
    "DISPLAY PANEL(&PANEL53) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,unload_tss1)
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('uloadv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal unload_tss1;End

    csr1=;csrp1=
    If rc /= 0 Then Do
        gc009 = oper1 "FROM TABLE name entry panel abandoned."
         "SETMSG MSG(GC009)"
        Return 9
    End
    If zcmd='?' Then Do
        x=helpdrvr(helptab '6C /OPEN')
        Signal unload_tss1
    End

    ret_scr = 0
    Parse VAR zcmd cmd parm1

/*--------------------------------------------------------------------*/
/*  If tablespace has not been validated or has changed, do it.       */
/*--------------------------------------------------------------------*/
    If u_table_ts /= unlddbnm || unldtspc || unldpart || 'TS' Then,
        If R0valtsp('TS',unlddbnm,unldtspc,unldpart) Then Do
            csr1 = 'unlddbnm'
            Signal unload_tss1
        End
    u_table_ts = unlddbnm || unldtspc || unldpart || 'TS'

    ulwhen =
    x = unload_ver_common()
    If ret_scr Then Signal unload_tss1

    Return 0

/*--------------------------------------------------------------------*/
/*  Unload From Copy                                                  */
/*--------------------------------------------------------------------*/
unload_fcs1:
    zcmd =
    "DISPLAY PANEL(&PANEL54) CURSOR(&csr1) CSRPOS(&csrp1)"
    If zcmd='DEBUG' Then Interpret r0debug(debug,unload_fcs1)
    If Substr(zcmd,1,3)='DEF' Then Do
        x=R0site$('uloadv');"VGET (defvars) SHARED";"VGET ("defvars") SHARED"
        Signal unload_fcs1;End

    csr1=;csrp1=
    If rc /= 0 Then Do
        gc009 = oper1 "FROM TABLE name entry panel abandoned."
         "SETMSG MSG(GC009)"
        Return 9
    End
    If zcmd='?' Then Do
        x=helpdrvr(helptab '6D /OPEN')
        Signal unload_fcs1
    End

    ret_scr = 0
    Parse VAR zcmd cmd parm1

    ulwhen =
    x = unload_ver_common()
    If ret_scr Then Signal unload_fcs1

/*--------------------------------------------------------------------*/
/*  If tablespace has not been validated or has changed, do it.       */
/*--------------------------------------------------------------------*/
    If u_table_fc /= unlddbnm || unldtspc Then Do
        If R0valcop(unlddbnm,unldtspc) Then Do
            csr1 = 'unlddbnm'
            Signal unload_fcs1
        End
        "VGET (dsnx) SHARED"
    u_table_fc = unlddbnm || unldtspc

    Return 0

/*--------------------------------------------------------------------*/
/*  Comon unload field verifications:                                 */
/*--------------------------------------------------------------------*/
unload_ver_common:
/*--------------------------------------------------------------------*/
/*  Break up supplied WHEN parameters in 72 character lots without    */
/*  splitting words.                                                  */
/*--------------------------------------------------------------------*/
    when1=;when2=;when3=;when4=;when5=;when6=;when7=;when8=
    If ulwhen /= '' Then Do
        when1 = 'WHEN ('
        i=2
        Do While ulwhen /= ''
            Interpret 'when'i '= "      "Wordat(ulwhen,64,"L")'
            ulwhen = Wordat(ulwhen,60,"T")
            i=i+1
        End
        Interpret "when"i "= '     )'"
    End

    If unldpddn = '*' Then Do
        gc009 = 'Template processing for Unload PUNCHDDN.'
        "SETMSG MSG(GC009)"
        x = R0listm(table,tempsdef,'$TTY','ND')
        x = R0listm(table,templib,'$TTY',,'EXIST')
        If Datatype(x) = 'CHAR' Then Parse VAR x unldpddn .
        ret_scr = 1
    End
    Else If unldpddn = '/' Then Do
             xmsg = 'unload PUNCHDDN'
             x = R0templ(templib,tempsdef,xmsg,'unldpddn')
             If Datatype(x) = 'CHAR' Then unldpddn = x
             ret_scr = 1
         End
    If Sysdsn("'"templib"("unldpddn")'") /= 'OK' Then,
        If Sysdsn("'"tempsdef"("unldpddn")'") /= 'OK' Then Do
            gc009 = 'Template not found.'
            "SETMSG MSG(GC009)"
            csr1  = 'unldpddn'
            ret_scr=1
            Return 4
        End
    If unlduddn = '*' Then Do
        gc009 = 'Template selection for Unload UNLDDN.'
        "SETMSG MSG(GC009)"
        x = R0listm(table,tempsdef,'$TTY','ND')
        x = R0listm(table,templib,'$TTY',,'EXIST')
        If Datatype(x) = 'CHAR' Then Parse VAR x unlduddn .
        ret_scr = 1
    End
    Else If unlduddn = '/' Then Do
             xmsg = 'unload UNLDDN'
             x = R0templ(templib,tempsdef,xmsg,'unlduddn')
             If Datatype(x) = 'CHAR' Then unlduddn = x
             ret_scr = 1
         End
    If Sysdsn("'"templib"("unlduddn")'") /= 'OK' Then,
        If Sysdsn("'"tempsdef"("unlduddn")'") /= 'OK' Then Do
            gc009 = 'Template not found.'
            "SETMSG MSG(GC009)"
            csr1  = 'unlduddn'
            ret_scr=1
            Return 4
        End

    Return 0

/*--------------------------------------------------------------------*/
/* Temp vars for testing                                              */
/*    JOBCARD    - Obvious.                                           */
/*--------------------------------------------------------------------*/
temp_var1:
ulwhen = 'A2345678',
         'B2345678',
         'C2345678',
         'D2345678',
         'E2345678',
         'F2345678',
         'G2345678',
         'H2345678',
         'I2345678',
         'J2345678',
         'K2345678',
         'L2345678',
         'M2345678',
         'N2345678',
         'O2345678',
         ''
Parse VAR ulwhen 1 unldwhn1 +67 .,
                68 unldwhn2 +67 .,
               135 unldwhn3 +67 .
unlddbnm = DSN8D71A
unldpart = 7
unldtspc = DSN8S71Z /*  0  */
unldtspc = DSN8S71P /*  1  */
unldtspc = DSN8S71E /*  4  */
unldownr = PUBLIC
unldtbnm = TRUTH

Return 0

