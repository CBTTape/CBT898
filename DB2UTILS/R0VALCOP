/******************************** REXX ********************************/
/*--------------------------------------------------------------------*/
/*  Validate and if valid list Table/indexspace copies.               */
/*--------------------------------------------------------------------*/
Arg dbname,tspace,p_no,debug
    If debug /= '' Then Trace r
    panel93 = 'R093P'                       /* Dataset table display  */
    Address ISPEXEC
    x = R0dsnrx('On')

    If p_no = '' Then p_no = 0
    sqlstmt = "SELECT DSNAME,ICDATE,ICTIME",
             "FROM SYSIBM.SYSCOPY",
             "WHERE DBNAME = '"dbname"'",
             "AND   TSNAME = '"tspace"'",
             "AND   DSNUM  = "p_no,
             "AND   ICTYPE = '"F"'",
             "AND   ICBACKUP = ' '",
             "ORDER BY ICDATE DESC,ICTIME DESC"
    sqlretf = ':dsnx,:datex,:timex'
    "VPUT (sqlstmt,sqlretf) SHARED"
    xx = R0dsnrx('Ex','*')
    x  = R0dsnrx('Off')
    "VGET (sqlretv) SHARED"
    If sqlretv = '' Then Do
        gc009 = 'No COPY found for tablespace',
                '"'dbname'.'tspace'" on "'ssid'", RC = 'xx'.'
        "SETMSG MSG(GC009)"
        Return 1
    End

    table = 'VC'Time('s')
    "TBCREATE &TABLE KEYS(DSNX) NAMES(DATEX TIMEX) REPLACE NOWRITE"
    "TBSORT   &TABLE FIELDS(DATEX,C,D,TIMEX,C,D)"

    flds = Translate(sqlretf,'  ',':,')
    Do While sqlretv /= ''
        Interpret 'Parse VAR sqlretv' flds 'sqlretv'
        Parse VAR datex yy +2 mm +2 dd
        datex = yy'/'mm'/'dd
        Parse VAR timex hh +2 mm +2 ss
        timex = hh':'mm':'ss
        "TBMOD  &TABLE"
    End
    "TBTOP &TABLE"

val_sel_copy_dis1:
    sel =
    "TBDISPL &TABLE PANEL(&PANEL93)"
    If rc > 4 Then Do
        "TBEND &TABLE"
        gc009 = 'COPY selection abandoned.'
        "SETMSG MSG(GC009)"
        Return 1
    End
    If sel = '' Then Signal val_sel_copy_dis1
    "TBEND &TABLE"
    "VPUT (dsnx) SHARED"

    Return 0

