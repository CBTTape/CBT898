/******************************** REXX ********************************/
/*--------------------------------------------------------------------*/
/*  Validate Table/indexspace.                                        */
/*--------------------------------------------------------------------*/
Arg type,dbname,tspace,partno,debug
    Address ISPEXEC
    x = R0dsnrx('On')
    If partno /= '' Then sqlx = "AND   PARTITION   = "partno
    Else                 sqlx =
    Select
        When type = 'TS' Then Do            /* TableSpace             */
            sqlstmt = "SELECT COUNT(*)",
                      "FROM SYSIBM.SYSTABLEPART",
                      "WHERE DBNAME = '"dbname"'",
                      "AND TSNAME = '"tspace"'" sqlx
            sqlretf = ':exist'
            mtype   = 'Tablespace'
        End
        When type = 'RI' Then Do            /* Recover index          */
            sqlstmt = "SELECT COUNT(*)",
                      "FROM SYSIBM.SYSINDEXES",
                      "WHERE DBNAME = '"dbname"'",
                      "AND INDEXSPACE = '"tspace"'" sqlx
            sqlretf = ':exist'
            mtype   = 'Indexspace'
        End
        When type = 'RB' Then Do            /* Rebuild/Reorg index    */
            sqlstmt = "SELECT COUNT(*)",
                      "FROM SYSIBM.SYSINDEXPART",
                      "WHERE IXCREATOR = '"dbname"'",
                      "AND IXNAME = '"tspace"'" sqlx
            sqlretf = ':exist'
            mtype   = 'Indexspace'
        End
        When type = 'OW' Then Do            /* Owner                  */
            sqlstmt = "SELECT DBNAME,INDEXSPACE",
                      "FROM SYSIBM.SYSINDEXES",
                      "WHERE CREATOR = '"dbname"'",
                      "AND NAME = '"tspace"'" sqlx
            sqlretf = ':ixdbname,:ixname'
            mtype   = 'Index'
            exist   = 1                     /* Stop err-msg for Index */
        End
        Otherwise Do
            gc009 = 'Invalid "'type'" passed to R0VALTSP. Refer',
                    'problem to dialogue developer...'
            "SETMSG MSG(GC009)"
            Return 1
        End
    End
If debug /= '' Then Do;Say sqlstmt;Trace r;End
    "VPUT (sqlstmt,sqlretf) SHARED"
    xx = R0dsnrx('Ex',1,debug)
    x  = R0dsnrx('Off')
    If tspace = '' Then tspace = dbname
    Else                tspace = dbname'.'tspace
    If partno = '' Then partno =
    Else                partno = ' PART' partno
    "VPUT (tspace,partno) SHARED"
    If xx = 100 Then Do
        gc009 = mtype '"'tspace || partno'" not found on "',
                ssid'", RC =' xx'.'
        "SETMSG MSG(GC009)"
        Return 1
    End
    If xx /= 0 Then Do
        gc009 = mtype '"'tspace || partno'" completed with RC='xx'.'
        "SETMSG MSG(GC009)"
        Return 1
    End
    "VGET (sqlretv) SHARED"
    flds = Translate(sqlretf,'  ',':,')
    Do While sqlretv /= ''
        Interpret 'Parse VAR sqlretv' flds 'sqlretv'
    End
    If exist = 0 Then Do
        "VGET (ssid) SHARED"
        gc009 = mtype '"'tspace || partno'" not found on "',
                ssid'" (count=0).'
        "SETMSG MSG(GC009)"
        Return 1
    End

    Return 0

