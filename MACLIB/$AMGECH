*---------------------------------------------------------------------*
         MACRO
&NAME    $AMGECH &MODE,&REG,&STMODE=
         AGO .A$HELPE
*---------------------------------------------------------------------*
*                                                                     *
* CHANGE ADDRESSING MODE.                                             *
* -----------------------                                             *
* The purpose of this macro is to change addressing modes (AMODE)     *
* during execution of a program.                                      *
*                                                                     *
* PARAMETERS:                                                         *
*    MODE        - New addressing mode. This is a positional          *
*                  parameter and must have value of 24, 31 or         *
*                  O(riginal).                                        *
*                                                                     *
*                    eg. LABEL1  AMODECHG 31                          *
*                        LABEL2  AMODECHG 24                          *
*                                                                     *
*    REG         - WORK REGISTER (DEFAULT IS 15).                     *
*    STMODE=cccc - Address to store current addressing mode for       *
*                  subsequent reversal.                               *
*                                                                     *
* NOTE:                                                               *
*    1. The same format of this macro can be used in any type of      *
*       programs, (ie. can be used in re-entrant programs).           *
*    2. This macro WILL change the contents of register 15 if no      *
*       register parameter is passed.                                 *
*                                                                     *
*---------------------------------------------------------------------*
.A$HELPE ANOP
         LCLC  &LABEL11,&LABEL21
         GBLB  &AMODE31,&AMODE24
         LCLC  &WREG1,&WREG2,&LABEL1
         AIF   (T'&NAME EQ 'O').A$YY
&NAME    DS    0H
.A$YY    ANOP
.*--------------------------------------------------------------------*
         AIF   (T'&MODE EQ 'O').ERR1
&WREG1   SETC  '15'                 Set work register
         AIF   (T'&REG EQ 'O').WREGST1 Bypass if register omitted
&WREG1   SETC  '&REG'               Set work register
.WREGST1 ANOP
         AIF   ('&MODE' EQ '24').A$24
         AIF   ('&MODE' EQ '31').A$31
.*--------------------------------------------------------------------*
.A$OR    ANOP
*---------------------------------------------------------------------*
*  ===>  CHANGE TO ORIGINAL ADDRESSING MODE IN STREAM                 *
*---------------------------------------------------------------------*
&LABEL1  SETC  'AOR'.'&SYSNDX'.'1'
         LA    &WREG1,&LABEL1      SET BSM ADDRESS
         AIF   (T'&STMODE EQ 'O').ERR2
         O     &WREG1,&STMODE      HIT WITH ORIGINAL MODE
         BSM   &WREG1,&WREG1       CHANGE MODE TO ORIGINAL MODE
&LABEL1  EQU   *
         AGO   .MEXIT
.*--------------------------------------------------------------------*
.A$24    ANOP
*---------------------------------------------------------------------*
*  ===>  CHANGE TO 24 BIT ADDRESSING MODE IN STREAM                   *
*---------------------------------------------------------------------*
&LABEL1  SETC  'A24'.'&SYSNDX'.'1'
         LA    &WREG1,&LABEL1      SET BSM ADDRESS
         BSM   &WREG1,&WREG1       CHANGE MODE TO 24 BIT
&LABEL1  EQU   *
         AGO   .MEXIT
.*--------------------------------------------------------------------*
.A$31    ANOP
*---------------------------------------------------------------------*
*  ===>  CHANGE TO 31 BIT ADDRESSING MODE IN STREAM                   *
*---------------------------------------------------------------------*
&LABEL1  SETC  'A31'.'&SYSNDX'.'1'
         LA    &WREG1,&LABEL1      SET BSM ADDRESS
         O     &WREG1,=X'80000000' SET 32ND BIT FOR SWAP TO 31 BIT MODE
         AIF   (T'&STMODE EQ 'O').A31001
         BASSM &WREG1,&WREG1           CHANGE TO 31 AND SAVE CURRENT
         AGO   .A31002
.A31001  ANOP
         BSM   &WREG1,&WREG1           CHANGE MODE TO 31 BIT
.A31002  ANOP
&LABEL1  EQU   *
         AIF   (T'&STMODE EQ 'O').MEXIT
         N     &WREG1,=X'80000000'     ORIGINAL AMODE
         ST    &WREG1,&STMODE          STORE IT
         AGO   .MEXIT
.*--------------------------------------------------------------------*
.*       ERROR MESSAGES                                               *
.*--------------------------------------------------------------------*
.ERR1    MNOTE 8,'MISSING ADDRESSING MODE PARAMETER (24 OR 31).'
         AGO   .MEXIT
.ERR2    MNOTE 8,'ORIGINAL RESTORE MODE REQUIRES A "STMODE=" PARAM.'
         AGO   .MEXIT
.*--------------------------------------------------------------------*
.MEXIT   ANOP
         MEXIT
.*--------------------------------------------------------------------*
         MEND
