         MACRO                                                 @BD910JY
&NAME    DFHTUTEN &C=,             COMPARE CODE      FOR T=2           *
               &T=(2,E),           TYPE OF OPERATION FOR T=1           *
               &CP=CLC,                                                *
               &OFF=,                                                  *
               &TB=,               TABLE TYPE        FOR T=2           *
               &TBR=,              TABLE RETURN TYPE FOR T=1           *
               &K=,                OUTPUT KEYWORD    FOR BOTH          *
               &E=,                EXIT              FOR BOTH          *
               &NX=SRT,                                                *
               &DUMMY=
.*  $MAC(DFHTUTEN),COMP(TRACE),PROD(CICS/VS) :                        *
.*  $L1=G00    160 810918 HDXDRP : ABGV CHANGE FIELD OFFSETS          *
         GBLB  &GENEXIT            GLOBAL FOR EXITS
.*                                                                    *
.*       BUILD TRACE ENTRY IN TABLE                                   *
.*                                                                    *
         LCLA  &A1                                          @EG00X @L1A
         LCLC  &C1,&C2,&C4,&C5                                 @BD910JY
         LCLC  &E1,&E2             EXIT ADDRESSES              @BD910JY
         LCLC  &TBD                TABLE RETURN ID             @BBAI20Y
.*
&C1      SETC  '&C(1)'            SET IST ENTRY                @BD910JY
&C2      SETC  '00'                                            @BD910JY
         AIF   (N'&C LT 2).TUEN010                             @BD910JY
         AIF   ('&C(2)' EQ '').TUEN010                         @BD910JY
&C2      SETC  '&C(2)'                                         @BD910JY
.TUEN010 ANOP                                                  @BD910JY
.*
&E1      SETC  'SRT' SET DEFAULTS                              @BD910JY
&E2      SETC  'SRT' SET DEFAULTS                              @BD910JY
         AIF   ('&E' EQ '').TUEN020                            @BD910JY
&E1      SETC  '&E(1)'             SET EXIT 1 OFFSET           @BD910JY
         AIF   (N'&E LT 2).TUEN020                             @BD910JY
&E2      SETC  '&E(2)'             SET EXIT 2 OFFSET           @BD910JY
.TUEN020 ANOP                                                  @BD910JY
.*
.*       LEVEL TWO TABLE TO PRODUCE NOW
.*
         AIF   ('&T' EQ '1').TUEN200 IF TABLE 1 BRANCH         @BD910JY
         AIF   ('&T(2)' EQ 'I').TUENI10
&NAME    DC    X'&C1'              CODE BYTE ONE               @BD910JY
         DC    X'&C2'              CODE BYTE TWO               @BD910JY
         AIF   (NOT &GENEXIT).TUENG10 IF NO GEN
         DC    AL2(TUTB2&NX-TUTB2SRT) GEN NEXT ENTRY
.TUENG10 ANOP
.*
         AGO   .TUEN500            GO OUTPUT KEYWORD           @BD910JY
.TUENI10 ANOP
&GENEXIT SETB  1                   SET NO EXIT REQ
&C4      SETC  '00'
         AIF   ('&CP' EQ 'TM').TUENI25
&C4      SETC  '02'
         AIF   ('&CP' EQ 'CLI').TUENI20
&C4      SETC  '04'                                            @BM10519
         AIF   ('&CP' EQ 'CLC').TUENI20                        @BM10519
.*                                                             @BM10519
&C4      SETC  '06'                                            @BM10519
         AIF   ('&CP' EQ '2BIN').TUENI20                       @BM10519
&C4      SETC  '08'                                            @BM10519
         AIF   ('&CP' EQ '1BIN').TUENI20                       @BM10519
&C4      SETC  '10'                                            @BM10519
         AIF   ('&CP' EQ 'RBIN').TUENI20                       @BM10519
&C4      SETC  '12'                                            @BM10519
         AIF   ('&CP' EQ 'LBIN').TUENI20                       @BM10519
&C4      SETC  '14'                                            @BM10519
         AIF   ('&CP' EQ '1DEC').TUENI20                       @BM10519
&C4      SETC  '16'                                            @BM10519
         AIF   ('&CP' EQ '2DEC').TUENI20                       @BM10519
&C4      SETC  '18'                                            @BM10519
         AIF   ('&CP' EQ '3DEC').TUENI20                       @BM10519
.*                                                             @BM10519
         MNOTE 12,'WRONG CP OPERAND'
.TUENI20 ANOP
&GENEXIT SETB  1                   SET EXIT REQUIRED
.TUENI25 ANOP
&C5      SETC  'ID'                                         @EG00X @L1C
         AIF   ('&OFF' EQ 'ID').TUENI30                        @BM10519
&C5      SETC  'RETAD'                                      @EG00X @L1C
         AIF   ('&OFF' EQ 'ADDR').TUENI30                      @BM10519
&C5      SETC  'TR'                                         @EG00X @L1C
         AIF   ('&OFF' EQ 'R0').TUENI30
&C5      SETC  'TR+1'                                       @EG00X @L1C
         AIF   ('&OFF' EQ 'R1').TUENI30
&C5      SETC  'TCAID'                                      @EG00X @L1C
         AIF   ('&OFF' EQ 'TASKNO').TUENI30                    @BM10519
&C5      SETC  'DATA1'                                      @EG00X @L1C
         AIF   ('&OFF' EQ 'A0').TUENI30
&C5      SETC  'DATA1+1'                                    @EG00X @L1C
         AIF   ('&OFF' EQ 'A1').TUENI30
&C5      SETC  'DATA1+2'                                    @EG00X @L1C
         AIF   ('&OFF' EQ 'A2').TUENI30
&C5      SETC  'DATA1+3'                                    @EG00X @L1C
         AIF   ('&OFF' EQ 'A3').TUENI30
&C5      SETC  'DATA2'                                      @EG00X @L1C
         AIF   ('&OFF' EQ 'B0').TUENI30
&C5      SETC  'DATA2+1'                                    @EG00X @L1C
         AIF   ('&OFF' EQ 'B1').TUENI30
&C5      SETC  'DATA2+2'                                    @EG00X @L1C
         AIF   ('&OFF' EQ 'B2').TUENI30
&C5      SETC  'DATA2+3'                                    @EG00X @L1C
         AIF   ('&OFF' EQ 'B3').TUENI30
         MNOTE 12,'WRONG OFF COMAND'
.TUENI30 ANOP
         DC    AL1(&C4)            OPERATION TYPE              @BM10519
.*
         DC    AL1(ZTR&C5-ZTRENTRY)   FIELD OFFSET          @EG00X @L1C
         DC    AL2(TUTE1&E1-TUTE1SRT) EXIT OFFSET
         DC    AL2(TUTB2&NX-TUTB2SRT) NEXT OFFSET
         AGO   .TUEN550
.*        LEVEL ONE TABLE TO PRODUCE NOW
.*
.TUEN200 ANOP                                                  @BD910JY
&TBD     SETC  '&TB'               SET TO DEFAULT              @BBAI20Y
         AIF   ('&TBR' EQ '').TUEN210                          @BBAI20Y
&TBD     SETC  '&TBR'              SET TO REAL                 @BBAI20Y
.TUEN210 ANOP                                                  @BBAI20Y
.* GENERATE TABLE ENTRY                                        @BBAI20Y
&NAME    DC  AL2(TUTB2&TB-TUTB2SRT) TABLE OFFSET               @BD910JY
         DC  AL2(TUTB2&TBD-TUTB2SRT) TABLE OFFSET FOR RETURN   @BBAI20Y
.*       DC  AL2(TUTE1&E2-TUTE1SRT) EXIT 2 OFFSET
.TUEN500 ANOP                                                  @BD910JY
         AIF   ('&K' NE '').TUEN520 IF NOT NULL BRANCH         @BD910JY
         DC    AL1(0),C' '         NULL STRING                 @BD910JY
         AGO   .TUEN550                                        @BD910JY
.TUEN520 ANOP                                                  @BD910JY
&A1      SETA  K'&K                                         @EG00X @L1C
         AIF   ('&K'(1,1) EQ '''').TUEN540                     @BD910JY
         DC    AL1(&A1-1),CL&A1'&K'                         @EG00X @L1C
         AGO   .TUEN550                                        @BD910JY
.TUEN540 ANOP                                                  @BD910JY
&A1      SETA  &A1-2                                        @EG00X @L1A
         DC    AL1(&A1-1),CL&A1&K                           @EG00X @L1C
.TUEN550 ANOP                                                  @BD910JY
         MEXIT                                                 @BD910JY
.*       TRACE TABLE SETUP MACRO.
.*
.*       STATUS CHANGE LEVEL 00.
.*
.*       FUNCTION.
.*           TO PROVIDE TABLES FOR DECODING THE TRACE TABLE.
.*           TWO TYPES OF TABLE ARE SETUP.
.*           TYPE 1 (T=1) LEVEL ONE TABLE CONTAINING
.*           OFFSET OF EXIT
.*           OFFSET OF LEVEL TWO TABLE
.*           CHARACTER STRING TO OUTPUT
.*
.*           TYPE 2 (T=2) LEVEL TWO TABLE CONTAINING
.*           CHAR TO COMPARE 1
.*           CHAR TO COMPARE 2
.*           CHAR STRING TO OUTPUT IF COMPARES ARE TRUE.
.*       OUTPUT.
.*           FOR EACH INVOCATION OF THE MACRO ONE ENTRY IS OUTPUT.
.*           T=1
.*             E=EXIT REQUIRED
.*             TB=TABLE TWO REQUIRED
.*             K=CHAR STING TO OUTPUT
.*           T=(2,E)
.*             C=HEX (2,2) DIGITS TO COMPARE
.*             K=CHAR STRING TO OUTPUT
.*                INTERNAL USE BY TRACE ROUTINES ONLY
.*           T=(2,I)
.*             CP=COMPARE TYPE
.*             OFF=OFFSET IN MINOR TABLE
.*             K=CHAR STRING TO OUTPUT
.*             E=EXIT REQUIRED
.*
.*       METHOD
.*          THIS MACRO FIRST SETS LOCAL VALUES FOR THE OPTIONS
.*          AND THEN OUTPUTS THE ENTRY.
.*       EXTERNAL FIELDS
.*          NONE
.*
.*       ERROR MSGS
.*           NONE
.*
.*       NOTES
.*          NONE.
.*
.*
         MEND                                                  @BD910JY
