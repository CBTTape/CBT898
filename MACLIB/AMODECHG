         MACRO
&NAME    AMODECHG &MODE,&REG,&ST=
         AGO .A$HELPE
*---------------------------------------------------------------------*
*                                                                     *
* CHANGE ADDRESSING MODE.                                             *
* -----------------------                                             *
* The purpose of this macro is to change addressing modes (AMODE)     *
* during execution of a program or to return the addressing mode for  *
* subsequent testing.                                                 *
*                                                                     *
* Parameters:                                                         *
*    MODE        - New addressing mode. This is a positional          *
*                  parameter and must have value of 24, 31,           *
*                  O(riginal) or Q(uery).                             *
*                  Under 'O' mode, ST= parameter must be passed.      *
*                  Under 'Q' mode, the register (specified or defautl)*
*                  will be set to Hex zeros if amode is 24 and        *
*                  non-zero (X'80000000') if 31 bit addressing. You   *
*                  may also store mode under query.                   *
*                                                                     *
*                    eg. LABEL1  AMODECHG 31,ST=AMODEIN               *
*                        LABEL2  AMODECHG 24                          *
*                        LABEL3  AMODECHG O,ST=AMODEIN                *
*                        LABEL4  AMODECHG Q                           *
*                        LTR     15,15              Q. 24 BIT         *
*                        BZ      A24BIT                Y. PROCESS 24  *
*                                                                     *
*    REG         - Work register (default is 15).                     *
*    ST=cccc     - Address to store current addressing mode for       *
*                  subsequent reversal.                               *
*                                                                     *
* Notes:                                                              *
*    1. The same format of this macro can be used in any type of      *
*       programs, (ie. can be used in re-entrant programs).           *
*    2. This macro WILL change the contents of register 15 if no      *
*       register parameter is passed.                                 *
*                                                                     *
*---------------------------------------------------------------------*
.A$HELPE ANOP
         LCLC  &LABEL11,&LABEL21,&ST#
         GBLB  &AMODE31,&AMODE24
         LCLC  &WREG1,&WREG2,&LABEL1
         AIF   (T'&NAME EQ 'O').A$YY
&NAME    DS    0H
.A$YY    ANOP
.*--------------------------------------------------------------------*
         AIF   (T'&MODE EQ 'O').ERR1
&WREG1   SETC  '15'                    Set work register
         AIF   (T'&REG EQ 'O').WREGST1 Bypass if register omitted
&WREG1   SETC  '&REG'                  Set work register
.WREGST1 ANOP
         AIF   ('&MODE' EQ 'Q').A$LQR10
         AIF   ('&MODE' NE 'O').A$LXX10
.*--------------------------------------------------------------------*
.A$OR    ANOP
*---------------------------------------------------------------------*
*  ===>  Change to original addressing mode in stream                 *
*---------------------------------------------------------------------*
&LABEL1  SETC  'AOR'.'&SYSNDX'.'1'
         LA    &WREG1,&LABEL1          Set BSM address
         AIF   (T'&ST EQ 'O').ERR2
         O     &WREG1,&ST              Hit with original mode
         BSM   &WREG1,&WREG1           Change mode to original mode
&LABEL1  EQU   *
         AGO   .MEXIT
.*--------------------------------------------------------------------*
.A$LXX10 ANOP
*---------------------------------------------------------------------*
         AIF   ('&MODE' EQ '31').A$L3110      Set 31 comment
*---------------------------------------------------------------------*
*  ===>  Change to 24 bit addressing mode in stream                   *
*---------------------------------------------------------------------*
&LABEL1  SETC  'A24'.'&SYSNDX'.'1'
         AGO   .A$LXX20                       Bypass
.A$L3110 ANOP
*---------------------------------------------------------------------*
*  ===>  Change to 31 bit addressing mode in stream                   *
*---------------------------------------------------------------------*
&LABEL1  SETC  'A31'.'&SYSNDX'.'1'
.A$LXX20 ANOP
*---------------------------------------------------------------------*
         LA    &WREG1,&LABEL1          Set BSM address
         AIF   ('&MODE' EQ '31').A$LXX25      Bypass for 31 bits
         N     &WREG1,=X'7FFFFFFF'     Set 32nd bit for swap to 24 bit
         AGO   .A$LXX35
.A$LXX25 ANOP
         O     &WREG1,=X'80000000'     Set 32nd bit for swap to 31 bit
.A$LXX30 ANOP
         AIF   (T'&ST EQ 'O').A$LXX35
         BASSM &WREG1,&WREG1           Change mode and save current
         AGO   .A$LXX40
.A$LXX35 ANOP
         BSM   &WREG1,&WREG1           Change mode
.A$LXX40 ANOP
&LABEL1  EQU   *
         AIF   (T'&ST EQ 'O').MEXIT
         N     &WREG1,=X'80000000'     Original AMODE
         ST    &WREG1,&ST              Store it
         AGO   .MEXIT
.*
.*--------------------------------------------------------------------*
.*
.A$LQR10 ANOP
*---------------------------------------------------------------------*
* Query addressing mode                                               *
*---------------------------------------------------------------------*
&LABEL1  SETC  'AQR'.'&SYSNDX'.'1'
         BAS   &WREG1,&LABEL1          Dranch and set addressing mode
&LABEL1  EQU   *
         N     &WREG1,=X'80000000'     Set reg zero/24, non-zero/31
         AIF   (T'&ST EQ 'O').MEXIT
         ST    &WREG1,&ST              Store it
         AGO   .MEXIT
.*--------------------------------------------------------------------*
.*       ERROR MESSAGES                                               *
.*--------------------------------------------------------------------*
.ERR1    MNOTE 8,'Missing addressing mode parameter (24, 31, O or Q).'
         AGO   .MEXIT
.ERR2    MNOTE 8,'Original restore mode requires a "ST=" parameter.'
         AGO   .MEXIT
.*--------------------------------------------------------------------*
.MEXIT   ANOP
         MEXIT
.*--------------------------------------------------------------------*
         MEND
