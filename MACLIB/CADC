         MACRO
&NAME    CADC &TEXTL=,&TEXTC=,&TEXTH=,&REP=1,&LIST=YES,&HELP=NO,       X
               &RESET=YES
         PUSH  PRINT
         AIF   ('&LIST'(1,1) NE 'N').A$XX
         PRINT NOGEN
.A$XX    ANOP
.*--------------------------------------------------------------------*
         GBLB  &CADC1ST
         GBLC  &CADCCPT,&CADCHPT
         AIF   (&CADC1ST).A$HELPE      IF NOT 1ST. TIME THRU
&CADC1ST SETB  1
         AIF   ('&HELP'(1,1) EQ 'Y').A$HELP
*---------------------------------------------------------------------*
* For a more comprehensive description of the 'CADC' parameter and    *
* it's use, pass parameter 'HELP=Yes'.                                *
.*--------------------------------------------------------------------*
         AGO .A$HELPE
.A$HELP  ANOP
*---------------------------------------------------------------------*
*                                                                     *
* Character Attribute Define Constant (CADC).                         *
* -------------------------------------------                         *
* Macro used  to expand a text to include character attributes to     *
* permit each character to be a diffrent color with different         *
* attributes.                                                         *
*                                                                     *
* Parameters:                                                         *
*   1. TEXTL=   - Text literal to have character attributes inserted. *
*                 If text contain spaces, enclose field in quotes,    *
*                 eg. TEXTL='TEXTL HAS BLANKS'                        *
*                 If the text contains quotes "'" or ampersand "&"    *
*                 they should be doubled up. (This should not be      *
*                 necessary but is required to keep syntax in line    *
*                 with assembler requirements.)                       *
*   2. TEXTC=   - A color prefix letter corresponding to each         *
*                 character in the TEXTL= parameter, where:           *
*                 B - Blue                                            *
*                 G - Green                                           *
*                 O - OFF (USE DEFAULT COLOR)                         *
*                 P - Pink                                            *
*                 R - Red                                             *
*                 T - Turqoise                                        *
*                 W - White                                           *
*                 Y - Yellow                                          *
*                 The character color may be left blank if you wish   *
*                 to retain the color of the previous character.      *
*                 Both upper an lower case characters are acceptable. *
*   3. TEXTH=   - An hilite letter corresponding to each character    *
*                 in the TEXTL= parameter, where:                     *
*                 B - Blink                                           *
*                 O - Off (no special hilite)                         *
*                 R - Reverse                                         *
*                 U - Underscore                                      *
*                 The character hilite may be left blank if you wish  *
*                 retain the color of the previous character.         *
*                 Both upper an lower case characters are acceptable. *
*   4. REP=     - Repetition number. Number of times to repeat the    *
*                 string. The default is 1.                           *
*   5. LIST=    - List the macro expansion. Default is NO.            *
*   6. HELP=    - Provide this help listing. Default is NO.           *
*                                                                     *
*   7. RESET=   - Reset character attributes to default values.       *
*                 Refer to Note 4 below. Valid values are YES/NO with *
*                 NO being the default.                               *
*                                                                     *
* NOTE:                                                               *
*      1. There is no seperate EXEC or LIST formats.                  *
*                                                                     *
*      2. If you omit the TEXTL= parameter, the values supplied on    *
*         TEXTC= and TEXTH= parameters will be used as defaults for   *
*         subsequent macro processing if either of the two parameters *
*         are not supplied on the macro.  The default values may be   *
*         turned off by passing the parameter(s) without values,      *
*          eg. TEXTC=,TEXTL=                                          *
*                                                                     *
*      3. If you omit the TEXTC= and/or TEXTH= parameters, the values *
*         will be taken from the paramater values from the last       *
*         processed macro without the TEXTL= parameter.               *
*         If you resort to using this define default facility, the    *
*         length of the supplied default string must be equal to or   *
*         longer than the TEXTL= value's length.                      *
*                                                                     *
*      4. Once you have introduced character attributes (either by    *
*         using this macro on any other macro), the last character    *
*         attribute used is retained UNLESS you turn it off by either:*
*                                                                     *
*         a) Using the RESET=YES parameter either as a complementary  *
*            or sole (default) parameter, ie code the CADC macro      *
*            without parameters.                                      *
*         b) setting the last character as a space and setting its    *
*            attribute(s) to 'O' (Off).                               *
*         c) include the following HEX code in your data              *
*            stream:    DC   X'284200'     turns on default color     *
*                       DC   X'284100'     turns on default hilite    *
*                                                                     *
.*--------------------------------------------------------------------*
.A$HELPE ANOP
         LCLA  &TLEN,&CLEN,&HLEN,&#REP,&LCTR
         LCLC  &CCOL,&CHAR,&CHIL,&TCOL,&THIL,&#TEXTL,&#TEXTC,&#TEXTH
.*--------------------------------------------------------------------*
         AIF   (T'&TEXTL NE 'O').STD0       BYPASS IF TEXT NOT MISSING
         AIF   (T'&TEXTC NE 'O').SETDFL0    SET DEF IF COLOR PRESENT
         AIF   (T'&TEXTH NE 'O').SETDFL0    SET DEF IF COLOR PRESENT
.*--------------------------------------------------------------------*
.*    ALL MAJOR PARAMETERS MISSING                                    *
*---------------------------------------------------------------------*
         AIF   (T'&RESET EQ 'O').E0         NO PARAMETERS ???
         AIF   ('&RESET'(1,1) NE 'Y').E0         NO PARAMETERS ???
         DC   X'284200284100'          TURN ON DEFAULT COLOR AND HILITE
         AGO   .EXIT
.*--------------------------------------------------------------------*
.SETDFL0 ANOP
*---------------------------------------------------------------------*
&CADCCPT SETC  '&TEXTC'                     SET GLOBAL DEFAULT COLOR
&CADCHPT SETC  '&TEXTH'                     SET GLOBAL DEFAULT HILITE
         MNOTE 0,'COLOR AND HILITE DEFAULTS STORED'
         AGO   .EXIT
.*--------------------------------------------------------------------*
.STD0    ANOP
&#REP    SETA  &REP
&#TEXTC  SETC  '&TEXTC'
&#TEXTH  SETC  '&TEXTH'
         AIF   (T'&TEXTC NE 'O').STD1       BYPASS IF COLOR SUPPLIED
&#TEXTC  SETC  '&CADCCPT'                   SET GLOBAL DEFAULT COLOR
.STD1    ANOP
         AIF   (T'&TEXTH NE 'O').QT0        BYPASS IF HILITE SUPPLIED
&#TEXTH  SETC  '&CADCHPT'                   SET GLOBAL DEFAULT HILITE
.QT0     ANOP                               UNQUOTE TEXT
         AIF   ('&TEXTL'(1,1) EQ '''').QT2  IF QUOTED GO UNQUOTE
.QT1     ANOP                               UNQUOTE TEXT
&TLEN    SETA  K'&TEXTL(1)
&#TEXTL  SETC  '&TEXTL'
         AGO   .QC0
.QT2     ANOP                               UNQUOTE TEXT
&TLEN    SETA  K'&TEXTL(1)-2
&#TEXTL  SETC  '&TEXTL'(2,&TLEN)
.QC0     ANOP
         AIF   (T'&#TEXTC EQ 'O').BP1       BYPASS IF COLOR IS MISSING
         AIF   ('&#TEXTC'(1,1) EQ '''').QC2 IF QUOTED GO UNQUOTE
.QC1     ANOP                               UNQUOTE TEXT
&CLEN    SETA  K'&#TEXTC
&#TEXTC  SETC  '&#TEXTC'
         AGO   .QC3
.QC2     ANOP                               UNQUOTE TEXT
&CLEN    SETA  K'&#TEXTC-2
&#TEXTC  SETC  '&#TEXTC'(2,&CLEN)
.QC3     ANOP
         AIF   (&TLEN GT &CLEN).E1          ERRO IF LENGTH < TEXT LEN
.BP1     ANOP
.QH0     ANOP
         AIF   (T'&#TEXTH EQ 'O').BP2       BYPASS IF COLOR IS MISSING
         AIF   ('&#TEXTH'(1,1) EQ '''').QH2 IF QUOTED GO UNQUOTE
.QH1     ANOP                               UNQUOTE TEXT
&HLEN    SETA  K'&#TEXTH
&#TEXTH  SETC  '&#TEXTH'
         AGO   .QH3
.QH2     ANOP                               UNQUOTE TEXT
&HLEN    SETA  K'&#TEXTH-2
&#TEXTH  SETC  '&#TEXTH'(2,&HLEN)
.QH3     ANOP
         AIF   (&TLEN GT &HLEN).E2          ERRO IF LENGTH < TEXT LEN
.BP2     ANOP
&#NAME   SETC '&NAME'
.*--------------------------------------------------------------------*
.LOOP1   AIF   (&#REP LT 1).LOOP1E
*---------------------------------------------------------------------*
&LCTR    SETA  1                            LOOP COUNTER
.LOOP2   ANOP
         AIF   (&LCTR GT &TLEN).LOOP2E
.*--------------------------------------------------------------------*
.TSTCO1  AIF   (T'&#TEXTC EQ 'O').TSTHI1    TEST HILITE IF NO COLOR
&CCOL    SETC  '&#TEXTC'(&LCTR,1)
         AIF   ('&CCOL' EQ ' ').TSTHI1 KEEP LAST IF OMITTED
&#NAME   DC   X'2842'                  COLOR CAHARACTER ATTRIBUTE
&#NAME   SETC ' '
         AIF   ('&CCOL' EQ 'B').B1
         AIF   ('&CCOL' EQ 'G').G1
         AIF   ('&CCOL' EQ 'O').O1
         AIF   ('&CCOL' EQ 'P').P1
         AIF   ('&CCOL' EQ 'R').R1
         AIF   ('&CCOL' EQ 'T').T1
         AIF   ('&CCOL' EQ 'W').W1
         AIF   ('&CCOL' EQ 'Y').Y1
         AIF   ('&CCOL' EQ 'b').B1
         AIF   ('&CCOL' EQ 'g').G1
         AIF   ('&CCOL' EQ 'o').O1
         AIF   ('&CCOL' EQ 'p').P1
         AIF   ('&CCOL' EQ 'r').R1
         AIF   ('&CCOL' EQ 't').T1
         AIF   ('&CCOL' EQ 'w').W1
         AIF   ('&CCOL' EQ 'y').Y1
         AGO   .E3
.B1      ANOP
&CCOL    SETC  'F1'
         DC   X'&CCOL'                 COLOR = BLUE
         AGO   .TSTHI1
.G1      ANOP
&CCOL    SETC  'F4'
         DC   X'&CCOL'                 COLOR = GREEN
         AGO   .TSTHI1
.O1      ANOP
&CCOL    SETC  '00'
         DC   X'&CCOL'                 COLOR = OFF
         AGO   .TSTHI1
.P1      ANOP
&CCOL    SETC  'F3'
         DC   X'&CCOL'                 COLOR = PINK
         AGO   .TSTHI1
.R1      ANOP
&CCOL    SETC  'F2'
         DC   X'&CCOL'                 COLOR = RED
         AGO   .TSTHI1
.T1      ANOP
&CCOL    SETC  'F5'
         DC   X'&CCOL'                 COLOR = TURQUOISE
         AGO   .TSTHI1
.W1      ANOP
&CCOL    SETC  'F7'
         DC   X'&CCOL'                 COLOR = WHITE
         AGO   .TSTHI1
.Y1      ANOP
&CCOL    SETC  'F6'
         DC   X'&CCOL'                 COLOR = YELLOW
.*--------------------------------------------------------------------*
.TSTHI1  AIF   (T'&#TEXTH EQ 'O').SETCH
&CHIL    SETC  '&#TEXTH'(&LCTR,1)           SET HILITE CHARACTER
         AIF   ('&CHIL' EQ ' ').SETCH  KEEP LAST IF OMITTED
         DC   X'2841'                  HILITE CHARACTER ATTRIBUTE
         AIF   ('&CHIL' EQ 'B').B2
         AIF   ('&CHIL' EQ 'O').O2
         AIF   ('&CHIL' EQ 'R').R2
         AIF   ('&CHIL' EQ 'U').U2
         AIF   ('&CHIL' EQ 'b').B2
         AIF   ('&CHIL' EQ 'o').O2
         AIF   ('&CHIL' EQ 'r').R2
         AIF   ('&CHIL' EQ 'u').U2
         AGO   .E4
.B2      ANOP
&CHIL    SETC  'F1'
         DC   X'&CHIL'                 HIGHLIHT = BLINK
         AGO   .SETCH
.O2      ANOP
&CHIL    SETC  '00'
         DC   X'&CHIL'                 HIGHLIHT = OFF
         AGO   .SETCH
.R2      ANOP
&CHIL    SETC  'F2'
         DC   X'&CHIL'                 HIGHLIHT = REVERSE
         AGO   .SETCH
.U2      ANOP
&CHIL    SETC  'F4'
         DC   X'&CHIL'                 HIGHLIHT = UNDERSCORE
.*
.*--------------------------------------------------------------------*
.*
.SETCH   ANOP
&CHAR    SETC  '&#TEXTL'(&LCTR,1)      SET HILITE CHARACTER
         AIF  ('&CHAR' EQ '''').DBLCH  REMOVE DOUBLE CHARACTER
         AIF  ('&CHAR.&CHAR' NE '&&').DEFCH REMOVE DOUBLE CHARACTER
.DBLCH   ANOP
&CHAR    SETC  '&CHAR.&CHAR'
&LCTR    SETA  &LCTR+1
.DEFCH   ANOP
&CHAR    SETC  '''&CHAR''            &CHAR'
         DC   C&CHAR.                  CHARACTER
&LCTR    SETA  &LCTR+1
         AGO   .LOOP2
.LOOP2E  ANOP
&#REP    SETA &#REP-1
         AGO   .LOOP1
*---------------------------------------------------------------------*
.LOOP1E  ANOP
         AIF   (T'&RESET EQ 'O').EXIT       EXIT IF RESET NOT REQUESTED
         AIF   ('&RESET'(1,1) EQ 'N').EXIT  EXIT IF RESET NOT REQUESTED
.TSTCO2  AIF   (T'&#TEXTC EQ 'O').TSTHI2    TEST HILITE IF NO COLOR
         DC   X'284200'                TURN ON DEFAULT COLOR
.TSTHI2  AIF   (T'&#TEXTH EQ 'O').EXIT      EXIT IF NO HILITE
         DC   X'284100'                TURN ON DEFAULT HILITE
         AGO   .EXIT
.E0      MNOTE 12,'MISSING PARAMETERS ????'
         AGO   .EXIT
.E1      MNOTE 12,'TEXTC PARAMETER DOES NOT CORESPOND WITH TEXT LENGTH'
         AGO   .EXIT
.E2      MNOTE 12,'TEXTH PARAMETER DOES NOT CORESPOND WITH TEXT LENGTH'
         AGO   .EXIT
.E3      MNOTE 12,'INVALID COLOR ATTRIBUTE CHARACTER ''&CCOL'''
         AGO   .EXIT
.E4      MNOTE 12,'INVALID HILITE ATTRIBUTE CHARACTER ''&CHIL'''
         AGO   .EXIT
.EXIT    ANOP
         POP   PRINT
         MEXIT
         MEND
