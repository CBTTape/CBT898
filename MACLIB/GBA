         MACRO
&NAME    GBA   &REG,&ROW,&COL
*---------------------------------------------------------------------*
* MACRO TO DETERMINE IF THE FIRST TWO BYTES OF A VARIABLE (POINTED TO *
* BY A REGISTER) ARE EQUAL TO "ROW" AND "COL" ARGUMENTS PASSED.       *
*                                                                     *
*   PARAMETERS:                                                       *
*       1. REGISTER POINTING TO AN INPUT VARIABLE                     *
*       2. ROW NUMBER (DECIMAL)                                       *
*       3. COL NUMBER (DECIMAL)                                       *
*                                                                     *
*   FORMULAE:  ((R-1)*80+(C-1))/64   QOUTIENT  => ROW DISP IN TABLE   *
*                                    REMAINDER => COL DISP IN TABLE   *
*---------------------------------------------------------------------*
         LCLC  &C1,&C2,&C3,&C4,&TABLE,&CROW,&CCOL,&CFAB
         LCLC  &CCURSOR
         LCLA  &AROW,&ACOL,&AREM,&A1,&A2,&DISP,&LEN,&CSFE
         AIF   (T'&REG EQ 'O').E1                 ROW NOT ENTERED
         AIF   (T'&ROW EQ 'O').E2                 ROW NOT ENTERED
         AIF   (T'&COL EQ 'O').E3                 COL NOT ENTERED
.CALSDA  ANOP
&AROW    SETA  &ROW
&ACOL    SETA  &COL
         AIF   (&AROW GT 43).E4
         AIF   (&AROW LT 1).E4
         AIF   (&ACOL GT 80).E5
         AIF   (&ACOL LT 1).E5
&C1      SETC  '40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'  1 TO 16 ( 1-33)
&C2      SETC  '50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F' 17 TO 32 (34-65)
&C3      SETC  '6061E2E3E4E5E6E7E8E96A6B6C6D6E6F' 33 TO 48 (66-97)
&C4      SETC  'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F' 49 TO 64 (98-129)
&TABLE   SETC  '&C1&C2&C3&C4'
&DISP    SETA  &AROW-1
&DISP    SETA  &DISP*80                 DECREMENT ROW # BY 1
&DISP    SETA  &DISP+&ACOL-1
&AROW    SETA  &DISP/64
&ACOL    SETA  &AROW*64
&ACOL    SETA  &DISP-&ACOL
&ACOL    SETA  &ACOL*2
&ACOL    SETA  &ACOL+1
&AROW    SETA  &AROW*2
&AROW    SETA  &AROW+1
&CROW    SETC  '&TABLE'(&AROW,2)
&CCOL    SETC  '&TABLE'(&ACOL,2)
&NAME    CLC   =XL3'11&CROW&CCOL',0(&REG)
         MEXIT
.E1      MNOTE 12,'GBA REQUIRES A REGISTER POINTING TO A VARIABLE.'
.E2      MNOTE 12,'GBA REQUIRES A LINE NUMBER.'
.E3      MNOTE 12,'GBA REQUIRES A COLUMN NUMBER.'
.E4      MNOTE 12,'GBA REQUIRES LINE IN RANGE 1 TO 43.'
.E5      MNOTE 12,'GBA REQUIRES COLUMN RANGE 1 TO 80.'
         MEND
