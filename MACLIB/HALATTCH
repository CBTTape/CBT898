         MACRO
&NAME    HALATTCH &ENTRY,&ECB,&ERROR,&RSAVER,&MF=,&LIST=YES
         PUSH  PRINT
         AIF   ('&LIST' NE 'NO').A$XX
         PRINT NOGEN
.A$XX    ANOP
*---------------------------------------------------------------------*
* MACRO TO CONVERT HEX DATA TO CHARACTER DISPLAYABLE DATA.            *
*                                                                     *
*  PARAMETERS:                                                        *
*      1. ENTRY POINT NAME (VSE VARIABLE NAME OF ENTRY ADDRESS)       *
*      2. ECB NAME                                                    *
*      3. ERROR ROUTINE ON RETURN                                     *
*      4. MACRO FORMAT FOR RE-ENTRANT PROGRAMS MF=E OR MF=L           *
*      5. OPTION TO GENERATE MACRO EXPANSION OR NOT LIST=YES/NO       *
*  NOTE:                                                              *
*      PARAMETERS MAY BE PASSED IN REGISTERS USING THE (RN)           *
*      NOTATION (NOTE CANNOT HAVE DIGITS).                            *
*---------------------------------------------------------------------*
         GBLC  &SYSTEM
         GBLC  &ALABEL,&AVSEST,&AVSEPR,&AVSES1,&AVSES2
         GBLB  &AT1STE,&AT1STL,&AT1NAME
         LCLC  &DNAM,&REM,&ERR
         LCLC  &#ENTRY,&#ECB,&#ERROR
         LCLA  &NAML
.*--------------------------------------------------------------------*
&NAML    SETA  K'&NAME
&ERR     SETC  'BNZ'                          SET ERROR BRANCH COMMAND
&DNAM    SETC  '&NAME'
.*--------------------------------------------------------------------*
         AIF   ('&ENTRY'(1,1) NE '(').NOREGEN
&#ENTRY  SETC  '0&ENTRY'                      TRUNCATE NAME TO 7 CHARS
         AGO   .TREGEC                        GO TEST REG FORMAT ECB
.NOREGEN ANOP
&#ENTRY  SETC  '&ENTRY'
.TREGEC  ANOP
.*--------------------------------------------------------------------*
         AIF   ('&ECB'(1,1) NE '(').NOREGEC
&#ECB    SETC  '0&ECB'                        TRUNCATE TO REGISTER
         AGO   .TREGER                        GO TEST REG FORMAT ECB
.NOREGEC ANOP
&#ECB    SETC  '&ECB'
.TREGER  ANOP
.*--------------------------------------------------------------------*
         AIF   ('&ERROR'(1,1) NE '(').NOREGER
&#ERROR  SETC  '&ERROR(1)'                    TRUNCATE TO REGISTER
&ERR     SETC  'BNZR'                         SET ERROR BRANCH COMMAND
         AGO   .A$01                          GO TEST REG FORMAT ECB
.NOREGER ANOP
&#ERROR  SETC  '&ERROR'
.*--------------------------------------------------------------------*
.*--------------------------------------------------------------------*
.A$01    ANOP
         AIF   (&NAML LT 8).NOTR
&DNAM    SETC  '&NAME'(1,7)                   TRUNCATE NAME TO 7 CHARS
         MNOTE 4,'&NAME TRUNCATED TO &DNAM'
.NOTR    ANOP
         AIF   (&AT1NAME).A$00
&ATNAM   SETC  'AT'.'&SYSNDX'
&ALABEL1 SETC  '&ATNAM'.'10'
&ALABEL2 SETC  '&ATNAM'.'20'
&ALABEL4 SETC  '&ATNAM'.'30'
&ALABEL5 SETC  '&ATNAM'.'40'
&AVSEPR  SETC  '&ATNAM'.'50'
&AVSES1  SETC  '&ATNAM'.'60'
&AVSES2  SETC  '&ATNAM'.'70'
&AVSEEC  SETC  '&ATNAM'.'80'
&AVSEDL  SETC  '&ATNAM'.'90'
&AT1NAME SETB  1
.*--------------------------------------------------------------------*
.A$00    ANOP
         AIF   (T'&MF EQ 'O').E5       IF PARM 4 NOT PASSED ERROR
         AIF   ('&MF' EQ 'L').LIST1    IF MF=L PARAMETER PASSED BYPASS
         AIF   (T'&ENTRY EQ 'O').E1    IF PARM 1 NOT PASSED ERROR
         AIF   (T'&RSAVER EQ 'O').E3   IF PARM 1 NOT PASSED ERROR
         AIF   (T'&ERROR EQ 'O').E4    IF PARM 4 NOT PASSED ERROR
         AIF   ('&SYSTEM' NE 'VSE').TESTVME
.*------------------------- VSE MF=E ---------------------------------*
.VSEMFE  ANOP
         STM   R2,R14,&RSAVER          STORE REGISTERS
         LA    R0,&AVSEDL              LENGTH OF GETVIS AREA FOR VSE
         GETVIS ADDRESS=11             GETMAIN AREA
         USING &DNAM.L,R11             ADDRESABILITY TO GETMAINED DSECT
         L     R1,&#ENTRY              LOAD POINTER TO PROGRAM ENTRY
         LA    R3,&AVSEEC              ADDRESS OF ECB
         LA    R4,&AVSES1              ADDRESS OF DYNAMIC SAVE 1
         LA    R5,&AVSES2              ADDRESS OF DYNAMIC SAVE 2
&DNAM.E  ATTACHX (R1),ECB=(R3),SAVE=(R4),ABSAVE=(R5),MFG=(R11)
         LM    R2,R14,&RSAVER          RESTORE REGISTERS
         LTR   R1,R1                   Q. ZERO RETURN CODE
         &ERR  &#ERROR                    N. GO TO ERROR ADDRESS
         AGO   .EXIT                   EXIT MACRO
.*--------------------------------------------------------------------*
.TESTVME ANOP
         AIF   (T'&ECB EQ 'O').E2      IF PARM 2 NOT PASSED ERROR
         AIF   ('&SYSTEM' NE 'VM').MVSMFE
.*-------------------------- VM MF=E ---------------------------------*
.VMMFE   ANOP
         LA    R1,&#ECB                ADDRESS OF ECB
         LA    R14,&DNAM.L             ADDRESS OF DYNAMIC SAVE 1
&DNAM.E  ATTACHX EP=&ENTRY,ECB=(R1),SZERO=YES,JSTCB=YES,SF=(E,(R14))
         LTR   R15,R15                 Q. ZERO RETURN CODE
         &ERR  &#ERROR                    N. GO TO ERROR ADDRESS
         AGO   .EXIT                   EXIT MACRO
.*------------------------- MVS MF=E ---------------------------------*
.MVSMFE  ANOP
         LA    R1,&#ECB                ADDRESS OF ECB
         LA    R14,&DNAM.L             ADDRESS OF DYNAMIC SAVE 1
&DNAM.E  ATTACHX EP=&ENTRY,ECB=(R1),SF=(E,(R14))
         LTR   R15,R15                 Q. ZERO RETURN CODE
         &ERR  &#ERROR                    N. GO TO ERROR ADDRESS
         AGO   .EXIT                   EXIT MACRO
.*--------------------------------------------------------------------*
.LIST1   ANOP
         AIF   ('&SYSTEM' NE 'VSE').TESTVML
.*------------------------- VSE MF=L ---------------------------------*
.VSEMFL  ANOP
&DNAM.L  DSECT
&AVSEPR  DS    CL64
&AVSES1  DS    CL128
&AVSES2  DS    CL72
&AVSEEC  DS    F
&AVSEDL  EQU   *-&DNAM.L
         AGO   .EXIT                   EXIT MACRO
.*--------------------------------------------------------------------*
.TESTVML ANOP
         AIF   (T'&ENTRY EQ 'O').E1    IF PARM 1 NOT PASSED ERROR
         AIF   ('&SYSTEM' NE 'VSE').MVSMFL
.*-------------------------- VM MF=L ---------------------------------*
.VMMFL   ANOP
&DNAM.L  ATTACHX EP=&ENTRY,SZERO=YES,JSTCB=YES,SM=SUPV,SF=L
         AGO   .EXIT                   EXIT MACRO
.*------------------------- MVS MF=L ---------------------------------*
.MVSMFL  ANOP
&DNAM.L  ATTACHX EP=&ENTRY,SZERO=YES,SVAREA=YES,SF=L
&DNAM.L  ATTACHX EP=&ENTRY,SZERO=YES,SVAREA=YES,SF=L
         AGO   .EXIT                   EXIT MACRO
.*--------------------------------------------------------------------*
.EXIT    ANOP
         POP   PRINT
         MEXIT
.*--------------------------------------------------------------------*
.*       ERROR MESSAGES                                               *
.*--------------------------------------------------------------------*
.E1      MNOTE 8,'PROGRAM ENTRY NAME/REGISTER NOT PASSED'
         MEXIT
.*--------------------------------------------------------------------*
.E2      MNOTE 8,'ECB NAME/REGISTER NOT PASSED'
         MEXIT
.*--------------------------------------------------------------------*
.E3      MNOTE 8,'REGISTER SAVE AREA NOT PASSED'
         MEXIT
.*--------------------------------------------------------------------*
.E4      MNOTE 8,'ERROR ROUTINE ADDRESS/REGISTER NOT PASSED'
         MEXIT
.*--------------------------------------------------------------------*
         MEND
