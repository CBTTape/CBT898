         MACRO
&NAME    ISPVDEFX &INM=,&ANM=,&FORMAT=,&LEN=,&OPT=,&MF=,&LIST=NO,      x
               &TYPE=VAR,&HELP=NO
         PUSH  PRINT
         AIF   ('&LIST' NE 'NO').A$XX
         PRINT NOGEN
.A$XX    ANOP
         AIF   ('&HELP' EQ 'YES').A$HELP
*---------------------------------------------------------------------*
* For a more comprehensive description of the 'ISPVDEFX' parameter    *
* and it's use, pass parameter 'HELP=Yes'.                            *
*---------------------------------------------------------------------*
         AGO .A$HELPE
.A$HELP  ANOP
*---------------------------------------------------------------------*
*                                                                     *
* This macro is used to define addressability between a program and   *
* ISPF variables.                                                     *
*                                                                     *
* In the normal form:                                                 *
*   It is designed to create common variables the first time thru and *
*   re-use them everytime the macro is invoked.                       *
* In re-entrant form:                                                 *
*   Use the MF=L and MF=E options.                                    *
*                                                                     *
* Parameters (for further details, refer to ISPF DMS manuals):        *
*   1. INM=..... - ISPF variable name, enclosed in brackets ().       *
*   2. ANM=..... - Internal (assembler) varaiable name.               *
*   3. FORMAT=.. - Variable format, valid values are:                 *
*                        CHAR FIXED BIT HEX DBCS USER                 *
*   4. LEN=..... - Length of the variable in numeric bytes or         *
*                  register (2) - (13). If register is used, the      *
*                  length must be loaded ito the register.            *
*   5. OPT=..... - Options, valid values are:                         *
*                        COPY NOBSCAN LIST                            *
*   6. MF=...... - Macro format. Omit for non re-entrant programs.    *
*                  For re-entrant programs:                           *
*                    E - EXEC format of macro                         *
*                    L - LIST format of macro.                        *
*   7. TYPE=.... - VARiable/CONstant for MF=L. For re-entrant programs*
*                  to copy constant into GETMAINed variable.          *
*   8. LIST=..... - Generate or suppress macro expansion, (YES/NO).   *
*                                                                     *
* Example of input:                                                   *
*      ISPVDEFX INM=(ISPFname),ANM=int.name,FORMAT=CHAR,LEN=20        *
*          - OR -                                                     *
*      EXEC format:                                                   *
*        ISPVDEFX INM=(ISPFname),ANM=int.name,FORMAT=CHAR,LEN=20,MF=E *
*      LIST format:                                                   *
*        ISPVDEFX MF=L           (default ,TYPE=VAR)                  *
*          - or -                                                     *
*        ISPVDEFX MF=L,TYPE=CONS (and move CONS to VAR)               *
*                                                                     *
*---------------------------------------------------------------------*
.A$HELPE ANOP
&NAME    DS    0H
         GBLC  &VNAME
         GBLB  &VDFX1ST,&VMFE1ST,&VNMF1ST
         LCLA  &LL,&VLL
         LCLC  &REGL
&LL      SETA  K'&LEN
&VLL     SETA  K'&ANM
         AIF   (&VDFX1ST).A$00         IF NOT 1ST. BYPASS
&VDFX1ST SETB  1                       SET 1ST. TIME THRU FLAG
&VNAME   SETC  'IVD'.'&SYSNDX'         SET "INDEX"
.A$00    ANOP
         AIF   ('&MF' EQ 'L').A$11     BYPASS IF RE-ENT
         AIF   ('&MF' EQ 'E').A$33     BYPASS IF RE-ENT
         AIF   (T'&MF NE 'O').E1       INVALID FORMAT OF MF PARM
         AIF   (&VNMF1ST).A$55         BYPASS IF NOT 1ST NON-R
&VNMF1ST SETB  1                       SET 1ST. TIME THRU FLAG
         B     &VNAME.L                BYPASS DATA DEFS AFTER 1ST. THRU
         AGO  .A$111
.A$11    ANOP
.*--------------------------------------------------------------------*
.*   LIST form of macro 'ISPVDEFX'                                    *
.*--------------------------------------------------------------------*
.A$111   ANOP
*---------------------------------------------------------------------*
*    LIST form of macro 'ISPVDEFX' (constant).                        *
*---------------------------------------------------------------------*
         AIF   ('&TYPE'(1,1) NE 'C').A$112
         DS    F                       VDEFINE
         DS    F                       ISPF NAME
         DS    F                       INTERNAL NAME
         DS    F                       VAR TYPE
         DS    F                       VAR LENGTH
         DS    F                       OPTION
         DC    C'VDEFINE '             VDEF LITERAL
         DC    CL10'          '        ISPF DEST (ALLOW FOR "()")
         DC    C' '                    SPACER
         DC    C'CHAR    '             VDEF FORMAT TYPE
         DC    C' '                    SPACER
         DS    CL2                     LENGTH SOURCE PRIOR CONVERSION
         DC    C' '                    SPACER
         DS    F                       R2 SAVEAREA FOR LEN CONVERSION
         DS    D                       DBL WD LEN CONVERSION WK FLD
         DS    F                       LENGTH CONVERTED TO FULL WD BIN
         DC    C'&OPT    '             OPTIONS (NOT USED BY THIS RTN)
         DS    F                       INTERNAL NAME ADDRESS STORE
         AGO   .A$113
.*--------------------------------------------------------------------*
.A$112   ANOP
*---------------------------------------------------------------------*
*    LIST form of macro 'ISPVDEFX' (modifiable).                      *
*---------------------------------------------------------------------*
&VNAME.1 DS    F                       VDEFINE
         DS    F                       ISPF NAME
         DS    F                       INTERNAL NAME
         DS    F                       VAR TYPE
         DS    F                       VAR LENGTH
         DS    F                       OPTION
&VNAME.2 DC    C'VDEFINE '             VDEF LITERAL
&VNAME.3 DC    CL10'          '        ISPF DEST (ALLOW FOR "()")
         DC    C' '                    SPACER
&VNAME.4 DC    C'CHAR    '             VDEF FORMAT TYPE
         DC    C' '                    SPACER
&VNAME.5 DS    CL2                     LENGTH SOURCE PRIOR CONVERSION
         DC    C' '                    SPACER
&VNAME.6 DS    F                       R2 SAVEAREA FOR LEN CONVERSION
&VNAME.7 DS    D                       DBL WD LEN CONVERSION WK FLD
&VNAME.8 DS    F                       LENGTH CONVERTED TO FULL WD BIN
&VNAME.9 DC    C'&OPT    '             OPTIONS (NOT USED BY THIS RTN)
&VNAME.V DS    F                       INTERNAL NAME ADDRESS STORE
.A$113   ANOP
*-----------------------------------------------------------------*
         AIF   ('&MF' NE 'L').A$22     BYPASS IF NOT MF=L
         POP   PRINT
         MEXIT                         EXIT IF MF=L
.A$22    ANOP
&VNAME.L DS    0H
         AIF   (T'&MF EQ 'O').A$44     BYPASS IF RE-ENT
.A$33    ANOP
*---------------------------------------------------------------------*
*    EXEC form of macro 'ISPVDEFX'                                    *
*---------------------------------------------------------------------*
.EXMFE   ANOP
         AIF   (&VMFE1ST).A$55          IF NOT 1ST. BYPASS
&VMFE1ST SETB  1                        SET 1ST. TIME THRU FLAG
*    Unique code                                                      *
*---------------------------------------------------------------------*
         MVI   &VNAME.2,C' '            SPACE FILL DATA
         MVC   &VNAME.2+1(55),&VNAME.2  USING RIPPLE MOVE
         MVC   &VNAME.2(7),=C'VDEFINE'  MOVE LITERAL
         AGO   .A$441
.A$44    ANOP
*    Unique code                                                      *
*---------------------------------------------------------------------*
.A$441   ANOP
         ST    R2,&VNAME.6             SAVE REG2
         LA    R2,&VNAME.1             R1 DEST VALUE FOR "CALL"
         LA    R1,&VNAME.2             ADDRESS OF PARAMETER
         ST    R1,0(R2)                STORE ADDRESS
         LA    R2,4(R2)                POINT TO NEXT ADDRESS POS
         LA    R1,&VNAME.3             ETC...
         ST    R1,0(R2)                    "
         LA    R2,4(R2)                    "
         LA    R2,4(R2)                <-- " (ALLOW FOR 'INT NAME @')
         LA    R1,&VNAME.4                 "
         ST    R1,0(R2)                    "
         LA    R2,4(R2)                    "
         LA    R1,&VNAME.8                 "
         ST    R1,0(R2)                    "
         LA    R2,4(R2)                    "
         LA    R1,&VNAME.9                 "
         ST    R1,0(R2)                    "
         MVI   0(R2),X'80'                 "
         L     R2,&VNAME.6                RESET R2
.A$55    ANOP
*-----------------------------------------------------------------*
*    Common code                                                      *
*---------------------------------------------------------------------*
         AIF   (T'&MF EQ 'O').A$66
         MVC   &VNAME.4(8),=CL8'&FORMAT' SET UP "(ISPF-FORM)" VALUE
.A$66    ANOP
         MVC   &VNAME.3(10),=CL10'&INM'   SET UP "(ISPF-NAME)" VALUE
.*--------------------------------------------------------------------*
.* Test if length is supplied in a register
.*--------------------------------------------------------------------*
         AIF ('&LEN'(1,1) NE '(').NREGL
&PL      SETA  K'&LEN
&REGL    SETC  '&LEN'(2,&PL-2)            EXTRACT REGISTER
         ST    &REGL.,&VNAME.8            SAVE REGISTER PASSED LENGTH
         AGO   .FINLEN                    .BYPASS NUMERIC LENGTH
.NREGL   ANOP
         MVC   &VNAME.5(&LL),=CL&LL'&LEN' SET UP LENGTH
         PACK  &VNAME.7,&VNAME.5(&LL)     PACK LENGTH FOR CONVERSION
         CVB   R1,&VNAME.7                CONV LEN TO FULL WD BIN
         ST    R1,&VNAME.8                SAVE CONVERTED  LENGTH
.FINLEN  ANOP
.*--------------------------------------------------------------------*
         LA    R1,&ANM                    ADDRESS OF INTERNAL NAME
         ST    R1,&VNAME.1+8              SAVE INT NAME @
         LA    R1,&VNAME.1                LOAD PARAMETER LIST ADDRESS
         L     15,ISPLINK                 LOAD "ISPLINK" ADDRESS
.*       L     15,=V(ISPLINK)             LOAD "ISPLINK" ADDRESS
         BALR  14,15                      BRANCH AND LINK
         POP   PRINT
         MEXIT
.*--------------------------------------------------------------------*
.E1      MNOTE 8,'INVALID "MF" PARAMETER PASSED'
.*--------------------------------------------------------------------*
         POP   PRINT
         MEND
