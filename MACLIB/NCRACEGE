         MACRO
&NAME    $TRACEGE &TYPE,           Macro type, START/FAIL              X
               &FORMAT=CWATERM,    Test format                         X
               &FAIL=,             Label if miss                       X
               &CWA=CWA+X'130',    CWA+X'130' access                   X
               &TCTTETI=TCTTETI,   Term id    access                   X
               &TCTTECIA=TCTTECIA, Term user area access               X
               &LIST=Y,&HELP=N
.*
         AIF   ('&HELP'(1,1) EQ 'Y').A$HELP
*---------------------------------------------------------------------*
* For a more comprehensive description of the '$TRACEGE' parameters   *
* and it's use, pass parameter of 'HELP=Yes'.                         *
*---------------------------------------------------------------------*
         AGO .A$HELPE
.A$HELP  ANOP
*---------------------------------------------------------------------*
* Macro to generate code to test for trace condition based on the     *
* format supplied.                                                    *
*                                                                     *
* PARAMETERS:                                                         *
* 1. Type             - Type of macro. It can be either a START or    *
*                       FAIL. The should be paired and should not be  *
*                       mixed unless the FAIL= option is used. The    *
*                       default is FAIL as only a label is generated. *
* 2. FAIL=nnnn        - Number suffix used to control generation of   *
*                       the miss match                                *
*                       labels. Without this parameter, the internal  *
*                       index is used.                                *
* 3. FORMAT=cccccccc  - The format of the test to be used.            *
*                       The following options are available.          *
*                       a) CWATERM - test for termid matching any non *
*                                    X'00' at X'130' offset from the  *
*                                    CWA. Up to 30 terminals can be   *
*                                    defined and use LZAP to ZAP the  *
*                                    CWA. This is the default.        *
*                                    Requires addressability to       *
*                                     . CWA                           *
*                                     . TCTTE                         *
*                                    or you may specify your own      *
*                                    access to                        *
*    CWA=expression   - any expression to access CWA+X'130' (default) *
*                       eg. CWAX130 where the CWA is ORG'd            *
*    TCTTETI=expr     - any expression to access the TCTTETI,         *
*                       eg. TCTTETI-DFHTCTTE(5) where R5 -> TCTTE     *
*                       b) CWAUSER - test for userid extracted from   *
*                                    the TCTUA matching any non       *
*                                    X'00' at X'130' offset from the  *
*                                    CWA. Up to 10 users can be       *
*                                    defined and use LZAP to ZAP the  *
*                                    CWA. Each userid should be 8     *
*                                    bytes long with trailing spaces. *
*                                    Requires addressability to       *
*                                     . CWA                           *
*                                     . TCTTE                         *
*                                    or you may specify your own      *
*                                    access to                        *
*    CWA=expression   - any expression to access CWA+X'130' (default) *
*                       eg. CWAX130 where the CWA is ORG'd            *
*    TCTTECIA=expr    - any expression to access the TCTTECIA,        *
*                       eg. TCTTECIA-DFHTCTTE(5) where R5 -> TCTTE    *
*                                    Requires the following DSECTs    *
*                                     . TCTUA (KPW0908)               *
*                                                                     *
* NOTES - Registers 14 and 15 will be changed.                        *
*                                                                     *
*---------------------------------------------------------------------*
.A$HELPE ANOP
         PUSH  PRINT
         AIF   ('&LIST'(1,1) NE 'N').A$XX
         PRINT NOGEN
.A$XX    ANOP
         GBLC  &T$MISS
         LCLC  &T$NAME,&T$LOOP,&T$HIT
         LCLA  &PL
.*
         AIF   (T'&FAIL EQ 'O').NAMES      Use defults
&T$MISS  SETC  '&FAIL'
.*
.NAMES   ANOP
         AIF   (T'&TYPE EQ 'O').ENDTR      FAIL by default
         AIF   ('&TYPE'(1,1) NE 'S').ENDTR FAIL specified
&T$NAME  SETC  'T$'.'&SYSNDX'
&T$LOOP  SETC  '&T$NAME'.'01'
&T$HIT   SETC  '&T$NAME'.'02'
&T$SAVE  SETC  '&T$NAME'.'03'
         AIF   (T'&FAIL NE 'O').STARTTR    Use supplied label
&T$MISS  SETC  '&T$NAME'.'09'
.*--------------------------------------------------------------------*
.* Start Trace condition testing                                      *
.*--------------------------------------------------------------------*
.STARTTR ANOP
         AIF   (T'&FORMAT EQ 'O').CWATERM  'CWATERM' by default
         AIF   ('&FORMAT' EQ 'CWATERM').CWATERM  'CWATERM'
         AIF   ('&FORMAT' EQ 'CWAUSER').CWAUSER  'CWAUSER'
         AGO   .ERR1
.CWATERM ANOP
         L     15,X'21C'(,0)           OLD TCB POINTER (FROM PSA)
         L     15,X'D0'(,15)           TCB EXTENTION
         L     15,X'14'(,15)           AFCB
         L     15,X'08'(,15)           CSA 3.3 OR HIGHER
         L     14,X'4C'(,15)           TCA (USER)
         L     14,X'08'(,14)           TCTTE@
         MVC   &T$SAVE(4),0(14)        MOVE TERMID
         L     15,X'E4'(,15)           CWA
         LA    15,X'130'(15)           POINT TO CWA FILLER ZAPED
         LA    14,30                   LOOP LIMIT
         B     &T$LOOP                 BYPASS SAVE AREA
&T$SAVE  DC    CL4'                    '               DATA SAVE AREA
&T$LOOP  DS    0H
         CLC   =X'00000000',0(15)      ZAPPED
         BE    &T$MISS                 NO, BYPASS TRACING
         CLC   0(4,15),&T$SAVE         MATCH TERMID
         BE    &T$HIT                  REGISTER
         LA    15,4(15)                NEXT FIELD
         BCT   14,&T$LOOP              LOOP TILL END
         B     &T$MISS                 REACHED LOOP LIMIT
&T$HIT   DS    0H
         AGO   .MEXIT1
.*
.*--------------------------------------------------------------------*
.CWAUSER ANOP
         B     &T$SAVE+L'&T$SAVE       BYPASS SAVE AREA
         DS    0H                      HALF WORD ALINGMENT
&T$SAVE  DC    CL8' '                  DATA SAVE AREA
         L     15,&TCTTECIA            TERMINAL USER AREA
         MVC   &T$SAVE.(7),TUASIGN-TCTUA(15) SAVE USERID (KPW0908)
         LA    15,&CWA                 POINT TO CWA FILLER ZAPED
         LA    14,10                   LOOP LIMIT
&T$LOOP  DS    0H
         CLC   =X'00000000',0(15)      ZAPPED
         BE    &T$MISS                 NO, BYPASS TRACING
         CLC   0(7,15),&T$SAVE         MATCH USERID
         BE    &T$HIT                  REGISTER
         LA    15,8(15)                NEXT FIELD
         BCT   14,&T$LOOP              LOOP TILL END
         B     &T$MISS                 REACHED LOOP LIMIT
&T$HIT   DS    0H
         AGO   .MEXIT1
.*
.*--------------------------------------------------------------------*
.*
.*--------------------------------------------------------------------*
.*
.ENDTR   ANOP
&T$MISS  DS    0H
         AGO   .MEXIT1
.*--------------------------------------------------------------------*
.ERR1    MNOTE 8,'FORMAT &FORMAT NOT VALID'
         AGO   .MEXIT1
.*--------------------------------------------------------------------*
.MEXIT1  ANOP
*---------------------------------------------------------------------*
         POP PRINT
         MEXIT
         MEND
