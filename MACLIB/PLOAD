         MACRO
&NAME    PLOAD &PROG,&LIST=NO,&HELP=NO,&R15=
         AIF   ('&HELP'(1,1) EQ 'Y').A$HELP
*---------------------------------------------------------------------*
* For a more comprehensive description of the 'PLOAD' parameter and   *
* it's use, pass parameter 'HELP=Yes'.
*---------------------------------------------------------------------*
         AGO .A$HELPE
.A$HELP  ANOP
*---------------------------------------------------------------------*
*  Macro to load an external program, execute it and then delete it.  *
*                                                                     *
*  PARAMETERS:                                                        *
*    1. Program name.                                                 *
*       This may be any literal program name (unquoted) or a register *
*       pointing to an 8 byte variable containing the program name.   *
*    2. R15=... Where to store return code from PLOAD program.        *
*       This may be a variable name or a register pointing to a 4     *
*        byte variable.                                               *
*    3. LIST=No/Yes. Used to generate macro expansion code.           *
*             Default is 'NO'                                         *
*    4. HELP=No/Yes. Used to display this help box.                   *
*             Default is 'NO'                                         *
*                                                                     *
*---------------------------------------------------------------------*
.A$HELPE ANOP
         PUSH  PRINT
         AIF   ('&LIST'(1,1) EQ 'Y').A$XX
         PRINT NOGEN
.A$XX    ANOP
         AIF   (T'&NAME EQ 'O').A$00   BYPASS IF NO LABEL PASSED
&NAME    DS    0H
.A$00    ANOP
         AIF (T'&PROG EQ 'O').E0       ERROR IF PROGNAME OMITTED
.*--------------------------------------------------------------------*
         LCLC  &REGSV15,&LTYPE
         LCLA  &PL
         AIF (T'&R15 EQ 'O').LOAD      BYPASS IF OMITTED
         AIF   ('&R15'(1,1) EQ '(').A$03
&REGSV15 SETC   '&R15'
         AGO   .LOAD
.A$03    ANOP
&PL      SETA  K'&R15-2                     PARAMETER LENGTH
&REGSV15 SETC   '&R15'(2,&PL)
&REGSV15 SETC   '0(&REGSV15)'
.*--------------------------------------------------------------------*
.LOAD    ANOP
         AIF   ('&PROG'(1,1) EQ '(').LOAD1 LEAVE LTYPE IF PROG ¬= REG
         LOAD  EP=&PROG                LOAD PROGRAM DUMP
         AGO   .LOADX
.LOAD1   ANOP
         LOAD  EPLOC=&PROG             LOAD PROGRAM DUMP
.LOADX   ANOP
         LR    R15,R0                  LOAD ENTRY POINT OF PROGRAM
         BALR  R14,R15                 CALL 'PROGDUMP'
         AIF (T'&R15 EQ 'O').DELT      BYPASS IF OMITTED
         ST    R15,&REGSV15            SAVE RETURN CODE
.DELT    ANOP
         AIF   ('&PROG'(1,1) EQ '(').DELT1 LEAVE LTYPE IF PROG ¬= REG
         DELETE EP=&PROG               LOAD PROGRAM DUMP
         AGO   .DELTX
.DELT1   ANOP
         DELETE EPLOC=&PROG            LOAD PROGRAM DUMP
.DELTX   ANOP
.*--------------------------------------------------------------------*
         AGO   .MEXIT
.*--------------------------------------------------------------------*
.E0      MNOTE 12,'''PLOAD'' REQUIRES A PROGRAM NAME OR POINTER'
         AGO   .MEXIT
.*--------------------------------------------------------------------*
.MEXIT   ANOP
         POP   PRINT
         MEXIT
         MEND
