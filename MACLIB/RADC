         MACRO
&NAME    RADC  &SROW,&SCOL,&FA,&SCOLOR,&HILITE,&EROW,&ECOL,&RC,&CONT,  +
               &HELP=NO,&LIST=YES,&DEF=YES
         PUSH  PRINT
         AIF   ('&LIST'(1,1) NE 'N').A$XX
         PRINT NOGEN
.A$XX    ANOP
.*--------------------------------------------------------------------*
         GBLB  &RADC1ST
         AIF   (&RADC1ST).A$HELPE      IF NOT 1ST. TIME THRU
&RADC1ST SETB  1
         AIF   ('&HELP'(1,1) EQ 'Y').A$HELP
*---------------------------------------------------------------------*
* For a more comprehensive description of the 'RADC' parameter and    *
* it's use, pass parameter 'HELP=Yes'.                                *
*---------------------------------------------------------------------*
         AGO .A$HELPE
.A$HELP  ANOP
*---------------------------------------------------------------------*
*   Repeat to Address ==> Define Constant (RADC)                      *
*                                                                     *
*   Parameters: (for more details refer to "SBADC" macro)             *
*       1. Starting row                                               *
*       2. Starting col                                               *
*       3. Field attribute (prot(P/U).intens(H/L).MDT(Y/N)            *
*       4. Color  ('M' = monochrome, 'C' = color)                     *
*       5. Hilite (default 'O' = "off")                               *
*       6. End row                                                    *
*       7. End col                                                    *
*       8. Fill character                                             *
*       9. Continuation identifier from a prev SBA. ie.               *
*          CONTINUE FROM WHERE IT LEFT OFF (FOR BUFFISPF)             *
*      10. DEF=...    - Use default parameters indicator. Valid values*
*                       are YES/NO with YES as the default.           *
*                       If this parameter is set to NO, default       *
*                       settings are omitted.                         *
*                       You may need to issue a stand alone CADC      *
*                       macro to restore default attributes if you    *
*                       find they are behaving strangely after using  *
*                       DEF=NO and not passing parameter #3.          *
*      11. HELP=...   - Display this help description. Valid values   *
*                       are YES/NO with NO as the default.            *
*      12. LIST=...   - List macro expansion. Valid values are YES/NO *
*                       with YES as the default.                      *
*                                                                     *
*   Formulae:  ((R-1)*80+(C-1))/64   qoutient  => row disp in table   *
*                                    remainder => col disp in table   *
*---------------------------------------------------------------------*
.A$HELPE ANOP
         LCLC  &C1,&C2,&C3,&C4,&TABLE,&CSROW,&CSCOL,&CFAB
         LCLC  &CSCOLOR,&CHILITE,&CEROW,&CECOL,&CRC
         LCLA  &ASROW,&ASCOL,&AEROW,&AECOL,&RCL
         LCLA  &AREM,&A1,&A2,&DISP,&LEN,&CSFE
         GBLC  &COL#MON                           COLOR INDICATOR
         AIF   (T'&COL#MON  NE 'O').A$COLD        BYPASS IF COLOR DEFD
&COL#MON SETC  'COLOR'                            DEFAULT COLOR
.A$COLD  ANOP
         AIF   (T'&SROW EQ 'O').E21              START ROW NOT ENTERED
         AIF   (T'&SCOL EQ 'O').E31              START COL NOT ENTERED
         AIF   (T'&EROW EQ 'O').E22              END   ROW NOT ENTERED
         AIF   (T'&ECOL EQ 'O').E32              END   COL NOT ENTERED
         AIF   (T'&RC EQ 'O').RSPACE             REPEAT CHAR OMITTED
&RCL     SETA  K'&RC
&AMPER   SETC  '&&'
         AIF   ('&RC'(2,2) EQ '&AMPER').RAMPER
         AIF   ('&RC'(2,1) EQ '''').RQUOTE
         AIF   (&RCL NE 3).E1
&CRC     SETC  '&RC'(2,1)
         AGO   .RANOK
.RSPACE  ANOP
&CRC     SETC  ' '                               SET TO SPACE
         AGO   .RANOK
.RAMPER  ANOP
&CRC     SETC  '&&'                              SET TO AMPERSAND
         AGO   .RANOK
.RQUOTE  ANOP
&CRC     SETC  ''''''                            SET TO QUOTE
         AGO   .RANOK
.RANOK   ANOP
&CSFE    SETA  1
         AIF   (T'&FA EQ 'O').FADEF               FA OMITTED PLN DEF'T
&LEN     SETA  K'&FA                              FA LENGTH
         AIF   (&LEN NE 3).E6                     NOT 3 FA
         AIF   ('&FA' EQ 'UHY').FAUHY
         AIF   ('&FA' EQ 'UHN').FAUHN
         AIF   ('&FA' EQ 'ULY').FAULY
         AIF   ('&FA' EQ 'ULN').FAULN
         AIF   ('&FA' EQ 'PHY').FAPHY
         AIF   ('&FA' EQ 'PHN').FAPHN
         AIF   ('&FA' EQ 'PLY').FAPLY
         AIF   ('&FA' EQ 'PLN').FAPLN
         AGO   .E7
.FADEF   ANOP
         AIF   (T'&DEF EQ 'O').DEFCOL
         AIF   ('&DEF'(1,1) EQ 'N').DEFCOL
&CFAB    SETC  '60'
         AGO   .DEFCOL
.FAUHY   ANOP
&CFAB    SETC  'C9'
         AGO   .DEFCOL
.FAUHN   ANOP
&CFAB    SETC  'C8'
         AGO   .DEFCOL
.FAULY   ANOP
&CFAB    SETC  'D5'
         AGO   .DEFCOL
.FAULN   ANOP
&CFAB    SETC  'D4'
         AGO   .DEFCOL
.FAPHY   ANOP
&CFAB    SETC  'F9'
         AGO   .DEFCOL
.FAPHN   ANOP
&CFAB    SETC  'F8'
         AGO   .DEFCOL
.FAPLY   ANOP
&CFAB    SETC  'F5'
         AGO   .DEFCOL
.FAPLN   ANOP
&CFAB    SETC  'F4'
.DEFCOL  ANOP
         AIF   (T'&SCOLOR EQ 'O').DEFHILI            FA OMITTED
&CSFE    SETA  &CSFE+1
         AIF   ('&COL#MON'(1,1) EQ 'M').CALSDA
         AIF   ('&SCOLOR'(1,1) EQ 'B').COLORB
         AIF   ('&SCOLOR'(1,1) EQ 'R').COLORR
         AIF   ('&SCOLOR'(1,1) EQ 'P').COLORP
         AIF   ('&SCOLOR'(1,1) EQ 'G').COLORG
         AIF   ('&SCOLOR'(1,1) EQ 'T').COLORT
         AIF   ('&SCOLOR'(1,1) EQ 'Y').COLORY
         AIF   ('&SCOLOR'(1,1) EQ 'W').COLORW
         AGO   .E8
.COLORB  ANOP
&CSCOLOR SETC  '42F1'
         AGO   .DEFHILI
.COLORR  ANOP
&CSCOLOR SETC  '42F2'
         AGO   .DEFHILI
.COLORP  ANOP
&CSCOLOR SETC  '42F3'
         AGO   .DEFHILI
.COLORG  ANOP
&CSCOLOR SETC  '42F4'
         AGO   .DEFHILI
.COLORT  ANOP
&CSCOLOR SETC  '42F5'
         AGO   .DEFHILI
.COLORY  ANOP
&CSCOLOR SETC  '42F6'
         AGO   .DEFHILI
.COLORW  ANOP
&CSCOLOR SETC  '42F7'
.DEFHILI ANOP
         AIF   (T'&HILITE EQ 'O').CALSDA
         AIF   ('&HILITE'(1,1) EQ 'O').CALSDA
&CSFE    SETA  &CSFE+1
         AIF   ('&HILITE'(1,1) EQ 'B').HILITEB
         AIF   ('&HILITE'(1,1) EQ 'R').HILITER
         AIF   ('&HILITE'(1,1) EQ 'U').HILITEU
         AGO   .E9
.HILITEB ANOP
&CHILITE SETC  '41F1'
         AGO   .CALSDA
.HILITER ANOP
&CHILITE SETC  '41F2'
         AGO   .CALSDA
.HILITEU ANOP
&CHILITE SETC  '41F4'
.CALSDA  ANOP
&ASROW   SETA  &SROW
&ASCOL   SETA  &SCOL
&AEROW   SETA  &EROW
&AECOL   SETA  &ECOL
         AIF   (&AEROW LT 44).CONT1      IS LINE < 44 OK
         AIF   (&AEROW GT 44).E42        CANNOT BE GREATER THAN 44
         AIF   (&AECOL GT 1).E42         IF 25, COLUMN MUST BE 1
.CONT1   ANOP
         AIF   (&ASROW GT 43).E41
         AIF   (&ASROW LT 1).E41
         AIF   (&ASCOL GT 80).E51
         AIF   (&ASCOL LT 1).E51
         AIF   (&AEROW LT 1).E42
         AIF   (&AECOL GT 80).E52
         AIF   (&AECOL LT 1).E52
&C1      SETC  '40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'  1 TO 16 ( 1-33)
&C2      SETC  '50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F' 17 TO 32 (34-65)
&C3      SETC  '6061E2E3E4E5E6E7E8E96A6B6C6D6E6F' 33 TO 48 (66-97)
&C4      SETC  'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F' 49 TO 64 (98-129)
&TABLE   SETC  '&C1&C2&C3&C4'
&DISP    SETA  &ASROW-1
&DISP    SETA  &DISP*80                 DECREMENT ROW # BY 1
&DISP    SETA  &DISP+&ASCOL-1
&ASROW   SETA  &DISP/64
&ASCOL   SETA  &ASROW*64
&ASCOL   SETA  &DISP-&ASCOL
&ASCOL   SETA  &ASCOL*2
&ASCOL   SETA  &ASCOL+1
&ASROW   SETA  &ASROW*2
&ASROW   SETA  &ASROW+1
&CSROW   SETC  '&TABLE'(&ASROW,2)
&CSCOL   SETC  '&TABLE'(&ASCOL,2)
&DISP    SETA  &AEROW-1
&DISP    SETA  &DISP*80                 DECREMENT ROW # BY 1
&DISP    SETA  &DISP+&AECOL-1
&AEROW   SETA  &DISP/64
&AECOL   SETA  &AEROW*64
&AECOL   SETA  &DISP-&AECOL
&AECOL   SETA  &AECOL*2
&AECOL   SETA  &AECOL+1
&AEROW   SETA  &AEROW*2
&AEROW   SETA  &AEROW+1
&CEROW   SETC  '&TABLE'(&AEROW,2)
&CECOL   SETC  '&TABLE'(&AECOL,2)
         AIF   ('&COL#MON'(1,1) EQ 'M').MONOCHR
         AIF   (T'&CONT EQ 'O').PRADC1C
** "RADC" ENTRY OMMITTED. IMMEDIATE FOLLOW-ON FROM PREVIOUS VALUE. **
         AGO   .PRADC3M
.PRADC1C ANOP
         AIF   (T'&CFAB EQ 'O').PRADC2C
&NAME    DC    X'11&CSROW&CSCOL.290&CSFE.C0&CFAB&CSCOLOR&CHILITE'
         AGO   .PRADC3M
.PRADC2C ANOP
         AIF   (T'&CSCOLOR EQ 'O').DEFHIL9
&CSCOLOR SETC  '28&CSCOLOR'
.DEFHIL9 ANOP
         AIF   (T'&CHILITE EQ 'O').PUTIT9
&CHILITE SETC  '28&CHILITE'
.PUTIT9  ANOP
&NAME    DC    X'11&CSROW&CSCOL.&CSCOLOR.&CHILITE'
         AGO   .PRADC3M
.MONOCHR ANOP
         AIF   (T'&CONT EQ 'O').PRADC1M
** "RADC" ENTRY OMMITTED. IMMEDIATE FOLLOW-ON FROM PREVIOUS VALUE. **
         AGO   .PRADC3M
.PRADC1M ANOP
         AIF   (T'&CFAB EQ 'O').PRADC2M
&NAME    DC    X'11&CSROW&CSCOL.1D&CFAB'
         AGO   .PRADC3M
.PRADC2M ANOP
&NAME    DC    X'11&CSROW&CSCOL'
.PRADC3M ANOP
         DC    X'3C&CEROW&CECOL'
         DC    C'&CRC'
         MEXIT
.E1      MNOTE 12,'RADC REPEAT CHARACTER MUST BE ''1'' CHAR (QUOTED).'
         MEXIT
.E20     MNOTE 12,'RADC REQUIRES A REPEAT CHARACTER.'
         MEXIT
.E21     MNOTE 12,'RADC REQUIRES A START LINE NUMBER.'
         MEXIT
.E31     MNOTE 12,'RADC REQUIRES A START COLUMN NUMBER.'
         MEXIT
.E41     MNOTE 12,'RADC REQUIRES START LINE IN RANGE 1 TO 23.'
         MEXIT
.E51     MNOTE 12,'RADC REQUIRES START COLUMN RANGE 1 TO 80.'
         MEXIT
.E22     MNOTE 12,'RADC REQUIRES END A LINE NUMBER.'
         MEXIT
.E32     MNOTE 12,'RADC REQUIRES END A COLUMN NUMBER.'
         MEXIT
.E42     MNOTE 12,'RADC REQUIRES END LINE IN RANGE 1 TO 23.'
         MEXIT
.E52     MNOTE 12,'RADC REQUIRES END COLUMN RANGE 1 TO 80.'
         MEXIT
.E6      MNOTE 12,'FA PASSED MUST BE THREE CHARS (PROT+INTENS+MTD).'
         MEXIT
.E7      MNOTE 12,'INVALID 3 CHAR FA PASSED.'
         MEXIT
.E8      MNOTE 12,'INVALID COLOR PASSED.'
         MEXIT
.E9      MNOTE 12,'INVALID HILITE PASSED.'
         MEND
