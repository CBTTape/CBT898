         MACRO
&NAME    SBADC &ROW,&COL,&FA,&COLOR,&HILITE,&CONT,&LIST=YES,&HELP=NO,  +
               &DEF=YES
         PUSH  PRINT
         AIF   ('&LIST'(1,1) NE 'N').A$XX
         PRINT NOGEN
.A$XX    ANOP
.*--------------------------------------------------------------------*
         GBLB  &SBADC1ST
         AIF   (&SBADC1ST).A$HELPE     IF NOT 1ST. TIME THRU
&SBADC1ST SETB 1
         AIF   ('&HELP'(1,1) EQ 'Y').A$HELP
*---------------------------------------------------------------------*
* For a more comprehensive description of the 'SBADC' parameter and   *
* it's use, pass parameter 'HELP=Yes'.                                *
*---------------------------------------------------------------------*
         AGO .A$HELPE
.A$HELP  ANOP
*---------------------------------------------------------------------*
* Macro to generate the "define constant" ("DC") code for SBA and     *
* related attribute bytes.                                            *
* The first 3 bytes are "SBA" ID and the HEX address of the row and   *
* column from the decimal row/col values. The following bytes are the *
* extended attruibute byte (X'29') followed by the pairings.          *
* (Generally 11 bytes are returned).                                  *
*                                                                     *
*   Input parameters:                                                 *
*       1. Row number                                                 *
*       2. Collumn                                                    *
*       3. 3 character Attribute Character Description:               *
*          1st. char : Protection  - U(nprotected), P(rotected)       *
*                                    (for protected fields, autoskip  *
*                                    is turned on.)                   *
*          2nd. char : Intensity   - H(igh), L(ow), N(on)             *
*          3nd. char : MDT on flag - Y(es), N(o)                      *
*          All character positions must be supplied or none.          *
*       4. 1st. byte of color - W(hite), P(ink), T(urquoise), R(ed),  *
*                               B(lue), Y(ellow), G(reen)             *
*                               M(monochrome - no extended attributes)*
*       5. 1st. byte of hilite- U(nderscore), R(everse), B(link)      *
*                               O(off , same as  blank)               *
*       6. non blank text     - Indicates that literal will follow on *
*                               immediately. No need to set new buffer*
*                               address.                              *
*       7. DEF=...    - Use default parameters indicator. Valid values*
*                       are YES/NO with YES as the default.           *
*                       If this parameter is set to NO, default       *
*                       settings are omitted.                         *
*                       You may need to issue a stand alone CADC      *
*                       macro to restore default attributes if you    *
*                       find they are behaving strangely after using  *
*                       DEF=NO and not passing parameter #3.          *
*       8. HELP=...   - Display this help description. Valid values   *
*                       are YES/NO with NO as the default.            *
*       9. LIST=...   - List macro expansion. Valid values are YES/NO *
*                       with YES as the default.                      *
*                                                                     *
*   Formulae:  ((R-1)*80+(C-1))/64   quotient  => row disp in table   *
*                                    remainder => col disp in table   *
*---------------------------------------------------------------------*
.A$HELPE ANOP
         LCLC  &C1,&C2,&C3,&C4,&TABLE,&CROW,&CCOL,&CFAB
         LCLC  &CCOLOR,&CHILITE
         LCLA  &AROW,&ACOL,&AREM,&A1,&A2,&DISP,&LEN,&CSFE
         GBLC  &COL#MON                           COLOR INDICATOR
         AIF   (T'&COL#MON  NE 'O').A$COLD        BYPASS IF COLOR DEFD
&COL#MON SETC  'COLOR'                            DEFAULT COLOR
.A$COLD  ANOP
         AIF   (T'&ROW EQ 'O').E2                 ROW NOT ENTERED
         AIF   (T'&COL EQ 'O').E3                 COL NOT ENTERED
&CSFE    SETA  1
         AIF   (T'&FA EQ 'O').FADEF               FA OMITTED
&LEN     SETA  K'&FA                              FA LENGTH
         AIF   (&LEN NE 3).E6                     NOT 3 FA
         AIF   ('&FA' EQ 'UHY').FAUHY
         AIF   ('&FA' EQ 'UHN').FAUHN
         AIF   ('&FA' EQ 'ULY').FAULY
         AIF   ('&FA' EQ 'UNN').FAUNN
         AIF   ('&FA' EQ 'UNY').FAUNY
         AIF   ('&FA' EQ 'ULN').FAULN
         AIF   ('&FA' EQ 'PHY').FAPHY
         AIF   ('&FA' EQ 'PHN').FAPHN
         AIF   ('&FA' EQ 'PLY').FAPLY
         AIF   ('&FA' EQ 'PLN').FAPLN
         AGO   .E7
.FADEF   ANOP
         AIF   (T'&DEF EQ 'O').DEFCOL
         AIF   ('&DEF'(1,1) EQ 'N').DEFCOL
&CFAB    SETC  '60'
         AGO   .DEFCOL
.FAUHY   ANOP
&CFAB    SETC  'C9'
         AGO   .DEFCOL
.FAUHN   ANOP
&CFAB    SETC  'C8'
         AGO   .DEFCOL
.FAULY   ANOP
&CFAB    SETC  'C5'
         AGO   .DEFCOL
.FAULN   ANOP
&CFAB    SETC  'C4'
         AGO   .DEFCOL
.FAUNY   ANOP
&CFAB    SETC  '4D'
         AGO   .DEFCOL
.FAUNN   ANOP
&CFAB    SETC  '4C'
         AGO   .DEFCOL
.FAPHY   ANOP
&CFAB    SETC  'F9'
         AGO   .DEFCOL
.FAPHN   ANOP
&CFAB    SETC  'F8'
         AGO   .DEFCOL
.FAPLY   ANOP
&CFAB    SETC  'F5'
         AGO   .DEFCOL
.FAPLN   ANOP
&CFAB    SETC  'F4'
.DEFCOL  ANOP
         AIF   (T'&COLOR EQ 'O').DEFHILI             FA OMITTED
&CSFE    SETA  &CSFE+1
         AIF   ('&COL#MON'(1,1) EQ 'M').CALSDA
         AIF   ('&COLOR'(1,1) EQ 'B').COLORB
         AIF   ('&COLOR'(1,1) EQ 'R').COLORR
         AIF   ('&COLOR'(1,1) EQ 'P').COLORP
         AIF   ('&COLOR'(1,1) EQ 'G').COLORG
         AIF   ('&COLOR'(1,1) EQ 'T').COLORT
         AIF   ('&COLOR'(1,1) EQ 'Y').COLORY
         AIF   ('&COLOR'(1,1) EQ 'W').COLORW
         AGO   .E8
.COLORB  ANOP
&CCOLOR  SETC  '42F1'
         AGO   .DEFHILI
.COLORR  ANOP
&CCOLOR  SETC  '42F2'
         AGO   .DEFHILI
.COLORP  ANOP
&CCOLOR  SETC  '42F3'
         AGO   .DEFHILI
.COLORG  ANOP
&CCOLOR  SETC  '42F4'
         AGO   .DEFHILI
.COLORT  ANOP
&CCOLOR  SETC  '42F5'
         AGO   .DEFHILI
.COLORY  ANOP
&CCOLOR  SETC  '42F6'
         AGO   .DEFHILI
.COLORW  ANOP
&CCOLOR  SETC  '42F7'
.DEFHILI ANOP
         AIF   (T'&HILITE EQ 'O').CALSDA
         AIF   ('&HILITE'(1,1) EQ 'O').CALSDA
&CSFE    SETA  &CSFE+1
         AIF   ('&HILITE'(1,1) EQ 'B').HILITEB
         AIF   ('&HILITE'(1,1) EQ 'R').HILITER
         AIF   ('&HILITE'(1,1) EQ 'U').HILITEU
         AGO   .E9
.HILITEB ANOP
&CHILITE SETC  '41F1'
         AGO   .CALSDA
.HILITER ANOP
&CHILITE SETC  '41F2'
         AGO   .CALSDA
.HILITEU ANOP
&CHILITE SETC  '41F4'
.CALSDA  ANOP
&AROW    SETA  &ROW
&ACOL    SETA  &COL
         AIF   (&AROW GT 43).E4
         AIF   (&AROW LT 1).E4
         AIF   (&ACOL GT 80).E5
         AIF   (&ACOL LT 1).E5
&C1      SETC  '40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'  1 TO 16 ( 1-33)
&C2      SETC  '50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F' 17 TO 32 (34-65)
&C3      SETC  '6061E2E3E4E5E6E7E8E96A6B6C6D6E6F' 33 TO 48 (66-97)
&C4      SETC  'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F' 49 TO 64 (98-129)
&TABLE   SETC  '&C1&C2&C3&C4'
&DISP    SETA  &AROW-1
&DISP    SETA  &DISP*80                 DECREMENT ROW # BY 1
&DISP    SETA  &DISP+&ACOL-1
&AROW    SETA  &DISP/64
&ACOL    SETA  &AROW*64
&ACOL    SETA  &DISP-&ACOL
&ACOL    SETA  &ACOL*2
&ACOL    SETA  &ACOL+1
&AROW    SETA  &AROW*2
&AROW    SETA  &AROW+1
&CROW    SETC  '&TABLE'(&AROW,2)
&CCOL    SETC  '&TABLE'(&ACOL,2)
         AIF   ('&COL#MON'(1,1) EQ 'M').MONOCHR
         AIF   (T'&CONT EQ 'O').PRADC1C
** "SBADC" ENTRY OMITTED. IMMEDIATE FOLLOW-ON FROM PREVIOUS VALUE. **
         AGO   .PRADC3C
.PRADC1C ANOP
         AIF   (T'&CFAB EQ 'O').PRADC2C
&NAME    DC    X'11&CROW&CCOL.290&CSFE.C0&CFAB&CCOLOR&CHILITE'
         AGO   .PRADC3C
.PRADC2C ANOP
         AIF   (T'&CCOLOR EQ 'O').DEFHIL9
&CCOLOR  SETC  '28&CCOLOR'
.DEFHIL9 ANOP
         AIF   (T'&CHILITE EQ 'O').PUTIT9
&CHILITE SETC  '28&CHILITE'
.PUTIT9  ANOP
&NAME    DC    X'11&CROW&CCOL.&CCOLOR.&CHILITE'
.PRADC3C ANOP
         AGO .MEXIT
.MONOCHR ANOP
         AIF   (T'&CONT EQ 'O').PRADC1M
** "SBADC" ENTRY OMITTED. IMMEDIATE FOLLOW-ON FROM PREVIOUS VALUE. **
         AGO   .PRADC3M
.PRADC1M ANOP
         AIF   (T'&CFAB EQ 'O').PRADC2M
&NAME    DC    X'11&CROW&CCOL.1D&CFAB'
         AGO   .PRADC3M
.PRADC2M ANOP
&NAME    DC    X'11&CROW&CCOL'
.PRADC3M ANOP
         AGO .MEXIT
.E2      MNOTE 12,'SBA REQUIRES A LINE NUMBER.'
         AGO .MEXIT
.E3      MNOTE 12,'SBA REQUIRES A COLUMN NUMBER.'
         AGO .MEXIT
.E4      MNOTE 12,'SBA REQUIRES LINE IN RANGE 1 TO 43.'
         AGO .MEXIT
.E5      MNOTE 12,'SBA REQUIRES COLUMN RANGE 1 TO 80.'
         AGO .MEXIT
.E6      MNOTE 12,'FA PASSED MUST BE THREE CHARS (PROT+INTENS+MTD).'
         AGO .MEXIT
.E7      MNOTE 12,'INVALID 3 CHAR FA PASSED.'
         AGO .MEXIT
.E8      MNOTE 12,'INVALID COLOR PASSED.'
         AGO .MEXIT
.E9      MNOTE 12,'INVALID HILITE PASSED.'
.MEXIT   ANOP
         POP   PRINT
         MEXIT
         MEND
