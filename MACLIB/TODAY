         MACRO
&NAME    TODAY &OUT,&LIST=YES,&HELP=NO
         PUSH  PRINT
         AIF   ('&LIST'(1,1) NE 'N').A$XX
         PRINT NOGEN
.A$XX    ANOP
.*--------------------------------------------------------------------*
         GBLB  &TODY1ST
         AIF   (&TODY1ST).A$HELPE      IF NOT 1ST. TIME THRU
&TODY1ST SETB  1
         AIF   ('&HELP'(1,1) EQ 'Y').A$HELP
*---------------------------------------------------------------------*
* For a more comprehensive description of the 'TODAY' parameter and   *
* it's use, pass parameter 'HELP=Yes'.                                *
.*--------------------------------------------------------------------*
         AGO .A$HELPE
.A$HELP  ANOP
*---------------------------------------------------------------------*
*   Returns todays date                                               *
*                                                                     *
*   Parameters:                                                       *
*       1. Destination fiels. Will output date in format DD/MM/YY so  *
*          ensure dest is at least 8 bytes long.                      *
*       2. LIST=    - List the macro expansion. Default is NO.        *
*       3. HELP=    - Provide this help listing. Default is NO.       *
*---------------------------------------------------------------------*
.A$HELPE ANOP
         AIF   (T'&OUT EQ 'O').E1
.E0      GBLC  &TDBAL
.*--------------------------------------------------------------------*
         GBLC  &REGSAVR,&REGSMCR
         GBLB  &REGSFLG,&REGDFLG
         AIF   (&REGSFLG).A$RS
&REGSAVR SETC  'GB'.'&SYSNDX'.'#'
&REGSMCR SETC  'TODAY'
&REGSFLG SETB  1
.A$RS    ANOP
.*--------------------------------------------------------------------*
         GBLB  &TDFIRST
         AIF   (&TDFIRST).A$99
&TDFIRST SETB  1
         LCLC  &TNAME
&TNAME   SETC  'XC'.'&SYSNDX'
&TDBAL   SETC  '&TNAME'.'1'
&TDBYPAS SETC  '&TNAME'.'2'
&TDLOOP1 SETC  '&TNAME'.'3'
&TDLAB1  SETC  '&TNAME'.'4'
&TDXLOP1 SETC  '&TNAME'.'5'
&TDWORK  SETC  '&TNAME'.'A'
&TDDAY   SETC  '&TNAME'.'B'
&TDDAYR  SETC  '&TNAME'.'C'
&TDIDATE SETC  '&TNAME'.'D'
&TDRM    SETC  '&TNAME'.'E'
&TDYEAR  SETC  '&TNAME'.'F'
&TDYEARR SETC  '&TNAME'.'G'
&TDMMM   SETC  '&TNAME'.'H'
&TDMM    SETC  '&TNAME'.'I'
&TDDDD   SETC  '&TNAME'.'J'
&TDD     SETC  '&TNAME'.'K'
&TDYY    SETC  '&TNAME'.'L'
&TDDATE1 SETC  '&TNAME'.'M'
&TDTABLE SETC  '&TNAME'.'N'
         B     &TDBYPAS
.*--------------------------------------------------------------------*
.*    IF GLOBAL REGISTER SAVE AREA DEFINED, BYPASS                    *
.*--------------------------------------------------------------------*
         AIF   (&REGDFLG).A$RD
&REGDFLG SETB  1
&REGSAVR DS    16F                     REGISTER SAVE AREA
.A$RD    ANOP
.*--------------------------------------------------------------------*
&TDWORK  DS    PL8
&TDDAY   DC    PL2'0'
&TDDAYR  DC    PL2'0'
&TDIDATE DC    CL5' '
&TDRM    DC    CL1' '
&TDYEAR  DS    0CL3
         DS    CL2
&TDYEARR DS    CL1
&TDMMM   DS    0CL3
         DS    CL1
&TDMM    DS    CL2
&TDDDD   DS    0CL3
         DS    CL1
&TDD     DS    CL2
&TDYY    DS    CL2
&TDDATE1 DS    0CL8
         DS    CL2
         DC    CL1'/'
         DS    CL2
         DC    CL1'/'
         DS    CL2
&TDTABLE DS    0CL24              DAYS OF MONTH IN YEAR TABLE
         DC    P'31'              J
         DC    P'28'              F
         DC    P'31'              M
         DC    P'30'              A
         DC    P'31'              M
         DC    P'30'              J
         DC    P'31'              J
         DC    P'31'              A
         DC    P'30'              S
         DC    P'31'              O
         DC    P'30'              N
         DC    P'31'              D
*---------------------------------------------------------------------*
&TDBAL   DS    0H
         TIME
         ST    R1,&TDWORK+4
         UNPK  &TDIDATE.(5),&TDWORK+5(3)  UNPACK DATE
         PACK  &TDYEAR,&TDIDATE.(2)       GET YEAR
         MVC   &TDDATE1+6(2),&TDIDATE    SET UP YEAR
         DP    &TDYEAR,=P'4'             DIVIDE YEAR BY 4
         CP    &TDYEARR,=P'0'            Q. REMAINDER 0
         BNE   &TDLAB1                      N. NOT LEAP YEAR
         MVC   &TDTABLE+2(2),=X'029C'       Y. FEB = 29 DAYS
&TDLAB1  DS     0H
         LA    R9,1(R0)                  SET MONTH COUNTER TO 1
         LA    R8,&TDTABLE               SET REG 8 AS MONTH POINTER
         PACK  &TDDAYR.(2),&TDIDATE+2(3) PACK DAYS INTO DAYS REMAING
&TDLOOP1 DS    0H
         MVC   &TDDAY.(2),&TDDAYR        SAVE DAYS REMAINING
         SP    &TDDAYR.(2),0(2,R8)       SUB DAY IN MONTH FROM DAYS
         CP    &TDDAYR.(2),=P'0'         Q. COMPARE IF NEGATIVE
         BL    &TDXLOP1                     Y. EXIT LOOP
         CP    &TDDAYR.(2),=P'0'         Q. COMPARE IF ZERO
         BZ    &TDXLOP1                     Y. EXIT LOOP
         LA    R8,2(R8)                  POINT TO NEXT MONTH
         LA    R9,1(R9)                  INCREMENT MONTH COUNTER
         B     &TDLOOP1                  RECALCULATE
&TDXLOP1 DS    0H
         UNPK  &TDDDD.(3),&TDDAY.(2)     SAVE CURRENT DAYS
         OI    &TDD+1,X'F0'              CHANGE SIGN OF LAST DEC BYTE
         MVC   &TDDATE1.(2),&TDD         MOVE 2 BYTES OF DAY TO OUT FLD
         CVD   R9,&TDWORK                CONVERT MONTH TO DEC/PACKED
         UNPK  &TDMMM.(3),&TDWORK+6(2)   UNPACK MONTH
         OI    &TDMM+1,X'F0'             CHG SIGN OF LAST BYTE OF MTH
         MVC   &TDDATE1+3(2),&TDMM       MOVE MONTH TO OUT FIELD
         MVC   &OUT.(8),&TDDATE1         MOVE TO DESTINATION
         BR    R14                       RETURN
*=====================================================================*
*---------------------------------------------------------------------*
&TDBYPAS DS    0H
.A$99    ANOP
         STM   R14,R12,&REGSAVR         SAVE REGISTERS
         BAL   R14,&TDBAL               GO TO GET DATE ROUTINE
         LM    R14,R12,&REGSAVR         RESTORE REGISTERS
         AGO   .MEXIT
.E1      MNOTE 8,'TODAY REQUIRES AN OUTPUT FIELD'
.MEXIT   ANOP
         MEXIT
         MEND
