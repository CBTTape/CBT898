         MACRO
&NAME    TRACED &OUT,&PL,&FL
         AIF   (T'&OUT EQ 'O').E1
         AIF   (T'&PL EQ 'O').E2
         LCLA  &PLL
&PLL     SETA  L'&PL
         AIF   (T'&FL EQ 'O').E0
&PLL     SETA  &FL
.E0      GBLC  &TRDATA
         GBLB  &TDFIRST
         LCLC  &#PL
.*--------------------------------------------------------------------*
         AIF   ('&PL'(1,1) EQ '(').A$01
&#PL     SETC  '&PL'
         AGO   .A$02
.A$01    ANOP
&#PL     SETC  '0(&PL(1))'
.A$02    ANOP
.*--------------------------------------------------------------------*
         GBLC  &REGSAVR,&REGSMCR
         GBLB  &REGSFLG,&REGDFLG
         AIF   (&REGSFLG).A$RS
&REGSAVR SETC  'GB'.'&SYSNDX'.'#'
&REGSMCR SETC  'TRACED'
&REGSFLG SETB  1
.A$RS    ANOP
.*--------------------------------------------------------------------*
         AIF   (&TDFIRST).A$99
&TDFIRST SETB  1
         LCLC  &TNAME
&TNAME   SETC  'TR'.'&SYSNDX'
&TRDATA  SETC  '&TNAME'.'1'
         B     &TNAME.2
&TNAME.1 DS    CL133
.*--------------------------------------------------------------------*
.*    IF GLOBAL REGISTER SAVE AREA DEFINED, BYPASS                    *
.*--------------------------------------------------------------------*
         AIF   (&REGDFLG).A$RD
&REGDFLG SETB  1
&REGSAVR DS    16F                     REGISTER SAVE AREA
.A$RD    ANOP
.*--------------------------------------------------------------------*
&TNAME.2 DS    0H
.A$99    ANOP
         STM   R14,R12,&REGSAVR
         MVI   &TRDATA,X'40'
         MVC   &TRDATA+1(132),&TRDATA
         MVC   &TRDATA+1(&PLL),&#PL
         PUT   &OUT,&TRDATA
         LM    R14,R12,&REGSAVR
         MEXIT
.E1      MNOTE 12,'TRACE REQUIRES AN OUT DATASET'
.E2      MNOTE 12,'TRACE REQUIRES A FUNCTION PARAMETER LIST'
         MEND
