         MACRO
&NAME    TRACEL &OUT,&PL
         AIF   (T'&OUT EQ ' ').E1
         AIF   (T'&PL EQ ' ').E2
         LCLA  &PLL
&PLL     SETA  K'&PL-2
         GBLC  &TRDATA
         GBLB  &TLFIRST
.*--------------------------------------------------------------------*
         GBLC  &REGSAVR,&REGSMCR
         GBLB  &REGSFLG,&REGDFLG
         AIF   (&REGSFLG).A$RS
&REGSAVR SETC  'GB'.'&SYSNDX'.'#'
&REGSMCR SETC  'TRACEL'
&REGSFLG SETB  1
.A$RS    ANOP
.*--------------------------------------------------------------------*
         AIF   (&TLFIRST).A$99
&TLFIRST SETB  1
         LCLC  &TNAME
&TNAME   SETC  'TR'.'&SYSNDX'
&TRDATA  SETC  '&TNAME'.'1'
         B     &TNAME.2
&TNAME.1 DS    CL133
.*--------------------------------------------------------------------*
.*    IF GLOBAL REGISTER SAVE AREA DEFINED, BYPASS                    *
.*--------------------------------------------------------------------*
         AIF   (&REGDFLG).A$RD
&REGDFLG SETB  1
&REGSAVR DS    16F                     REGISTER SAVE AREA
.A$RD    ANOP
.*--------------------------------------------------------------------*
&TNAME.2 DS    0H
.A$99    ANOP
         STM   R0,R15,&REGSAVR
         MVI   &TRDATA,X'40'
         MVC   &TRDATA+1(132),&TRDATA
         MVC   &TRDATA+1(&PLL),=C&PL
         PUT   &OUT,&TRDATA
         LM    R0,R15,&REGSAVR
         MEXIT
.E1      MNOTE 12,'TRACE REQUIRES AN OUT DATASET'
.E2      MNOTE 12,'TRACE REQUIRES A FUNCTION PARAMETER LIST'
         MEND
