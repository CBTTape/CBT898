         MACRO
&NAME    WCCDC &WRITE,&AKBMDT,&COLOR,&LIST=YES,&HELP=NO
         PUSH  PRINT
         AIF   ('&LIST'(1,1) NE 'N').A$XX
         PRINT NOGEN
.A$XX    ANOP
.*--------------------------------------------------------------------*
         GBLB  &WCCDC1ST
         AIF   (&WCCDC1ST).A$HELPE     IF NOT 1ST. TIME THRU
&WCCDC1ST SETB 1
         AIF   ('&HELP'(1,1) EQ 'Y').A$HELP
*---------------------------------------------------------------------*
* For a more comprehensive description of the 'WCCDC' parameter and   *
* it's use, pass parameter 'HELP=Yes'.                                *
*---------------------------------------------------------------------*
         AGO .A$HELPE
.A$HELP  ANOP
*---------------------------------------------------------------------*
*                                                                     *
* THIS MACRO FORCES MONOCHROME (NON-EXTENDED ATTRIBUTE) DEFINITIONS.. *
*                                                                     *
* Macro to set the start of "SBA" output attributes.                  *
* These are the "Write Control Characters" (WCC).                     *
*                                                                     *
*   Parameters:                                                       *
*       1. WRITE (W/EW/EWA)                                           *
*                 (default 'W')                                       *
*       2. AKBMDT (Y/N for alarm+restore keyboard+reset MDT)          *
*                 (default 'NYY')                                     *
*       3. COLOR/MONO indicator                                       *
*---------------------------------------------------------------------*
.A$HELPE ANOP
         GBLC  &COL#MON
&COL#MON SETC  '&COLOR'
&COL#MON SETC  'MONO'
         LCLC  &WCC
         AIF   (T'&WRITE EQ 'O').WRW            WRITE OMITTED
         AIF   ('&WRITE' EQ 'W').WRW            WRITE = WRITE
         AIF   ('&WRITE' EQ 'EW').WRE           WRITE = ERASE/WRITE
         AIF   ('&WRITE' EQ 'EWA').WRA          WRITE = ERASE/WRITE ALT
         AGO   .E1
.WRW     ANOP
&WCC     SETC  'F1'
         AGO   .DEFREST
.WRE     ANOP
&WCC     SETC  'F5'
         AGO   .DEFREST
.WRA     ANOP
&WCC     SETC  '7E'
.DEFREST ANOP
         AIF   (T'&AKBMDT EQ 'O').WCCNYN
         AIF   ('&AKBMDT' EQ 'YYY').WCCYYY
         AIF   ('&AKBMDT' EQ 'YYN').WCCYYN
         AIF   ('&AKBMDT' EQ 'YNY').WCCYNY
         AIF   ('&AKBMDT' EQ 'YNN').WCCYNN
         AIF   ('&AKBMDT' EQ 'NYY').WCCNYY
         AIF   ('&AKBMDT' EQ 'NYN').WCCNYN
         AIF   ('&AKBMDT' EQ 'NNY').WCCNNY
         AIF   ('&AKBMDT' EQ 'NNN').WCCNNN
         AGO   .E2
.WCCYYY  ANOP
&WCC     SETC  '&WCC.C7'
         AGO   .COLORT
.WCCYYN  ANOP
&WCC     SETC  '&WCC.C6'
         AGO   .COLORT
.WCCYNY  ANOP
&WCC     SETC  '&WCC.C5'
         AGO   .COLORT
.WCCYNN  ANOP
&WCC     SETC  '&WCC.C4'
         AGO   .COLORT
.WCCNYY  ANOP
&WCC     SETC  '&WCC.C3'
         AGO   .COLORT
.WCCNYN  ANOP
&WCC     SETC  '&WCC.C2'
         AGO   .COLORT
.WCCNNY  ANOP
&WCC     SETC  '&WCC.C1'
         AGO   .COLORT
.WCCNNN  ANOP
&WCC     SETC  '&WCC.40'
.COLORT  ANOP
         AIF   (T'&COL#MON EQ 'O').DEFTCOL
         AGO   .TSTCOLR
.DEFTCOL ANOP
&COL#MON SETC  'COLOR'
.TSTCOLR ANOP
         AIF   ('&COL#MON'(1,1) EQ 'C').PUTWCC
         AIF   ('&COL#MON'(1,1) EQ 'M').PUTWCC
         AGO   .E3
.PUTWCC  ANOP
&NAME    DC    X'&WCC'
         MEXIT
.E1      MNOTE 8,'VALID WRITES ARE "W/EW/EWA.'
.E2      MNOTE 8,'WCC NEEDS "Y/N" FOR ALARM, RESTORE KB + RESET MTD.'
.E3      MNOTE 8,'INVALID COLOR INDICATOR. VALID VALUES "Y/N"'
         MEND
